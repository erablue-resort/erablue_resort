
@COMNAME351
#FUNCTIONS
TSTR:コマンド名受渡 = 連れ出す
IF CFLAG:TARGET:同行
	IF TALENT:TARGET:恋慕
		TSTR:コマンド名受渡 = 恋人繋ぎ
	ELSE
		TSTR:コマンド名受渡 = 手を繋ぐ
	ENDIF
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_351
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_351
LOCALS = <br>■連れ出す<br>
LOCALS += "一定値以上の好感度レベルを持つ相手を、自身に同行させるコマンド。<br>同時に連れ歩けるのは二人まで。<br>同行してくれる時間は６０分。ただし、一緒に隠密中・うふふ中は同行が解除されない。<br>同行中は[手を繋ぐ]、更に[恋慕]を持つ場合[恋人繋ぎ]に変化。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM351
#DIM 同行者保存
#DIM 同行数値記録
#DIM 連れ出し時間
同行者保存 = 0
同行数値記録 = 0

CALLFORMF COMNAME351
SELECTCOM_NAME '= TSTR:コマンド名受渡

IF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100
	PRINTFORMW %CALLNAME:TARGET%は今日一日%CALLNAME:(ABS(CFLAG:TARGET:デート) - 100)%とデートしているため連れ出せません
	RETURN -1
ENDIF

IF CFLAG:TARGET:同行
	RFLAG:コマンド結果受渡し変数 = 1
	IF TALENT:TARGET:恋慕
		PRINTFORMW %CALLNAME:TARGET%と恋人繋ぎで手を繋ぎます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%と手を繋ぎます。
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:TARGET%を連れ出します。
ENDIF
;一度に連れ歩けるのは二人まで
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	;TARGETに関してカウントする必要なし
	SIF TARGET:LOCAL == TARGET
		CONTINUE
	IF CFLAG:(TARGET:LOCAL):同行
		IF 同行数値記録
			;既に同行者が2人いるパターン
			IF 同行数値記録 > CFLAG:(TARGET:LOCAL):同行
				CFLAG:(TARGET:LOCAL):同行 = 0
				CFLAG:(TARGET:LOCAL):デート = 0
			ELSE
				CFLAG:同行者保存:同行 = 0
				CFLAG:同行者保存:デート = 0
				同行数値記録 = CFLAG:(TARGET:LOCAL):同行
				同行者保存 = TARGET:LOCAL
			ENDIF
		ELSE
			同行数値記録 = CFLAG:(TARGET:LOCAL):同行
			同行者保存 = TARGET:LOCAL
		ENDIF
	ENDIF
NEXT
連れ出し時間 = 60 + (CFLAG:好感度レベル - 関係閾値:2) * 5
SIF CFLAG:TARGET:同行 < 連れ出し時間
	CFLAG:TARGET:同行 = 連れ出し時間
TFLAG:TARGET変更用 = TARGET
TIME += 1

;SOURCEまで回さないのでここで口上を表示させる
RESULT = 0
RESULTS =
IF FLAG:口上表示切り替えオプション > 0
	TRYCALLFORM KOJO_COM_{NO:TARGET}_351(TARGET)
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE351
;連れ出す実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(351)
	RETURN RESULT
;TARGETの好感度レベル
SIF CFLAG:好感度レベル < 1 + 関係閾値:2
	RETURN 0
;他人とデート中はダメ
SIF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100 && TCVAR:TARGET:デートちょっかい値 <= 0
	RETURN 0
SIF CFLAG:PLAYER:デート
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
