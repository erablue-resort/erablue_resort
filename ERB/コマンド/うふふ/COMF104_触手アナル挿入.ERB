;-------------------------------------------------
;触手アナル挿入
;触手系コマンド、レベル2
;-------------------------------------------------
@COMNAME104
#FUNCTIONS
TSTR:コマンド名受渡 = 触手アナル挿入
IF MODE_確認("触手アナル挿入", TARGET)
	TSTR:コマンド名受渡 = 触手アナル挿入解除
ENDIF
;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_104
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = V
同時モード許可 = 1


@COM_TOOLTIP_104
LOCALS = <br>■触手アナル挿入<br>
LOCALS += "触手をアナルに挿入するコマンド。<br>Ａ挿入系モードとなり、モードを変更するか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ａ・接触・露出・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)・Ａ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM104
#DIM 快楽基礎値

IF MODE_確認("触手アナル挿入", TARGET)
	CALL MODE_DELETE("触手アナル挿入")
ELSE
	CALL MODE_CHANGE("触手アナル挿入")
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 80
DOWNBASE:気力 += 100
RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 快楽基礎値 * 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 30)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 快楽基礎値 * 40)

;苦痛
LOCAL = 800 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:触手操作経験 ++
RETURN 1


@CAN_COM104(ARG)
#DIM 実行値
#DIM ＋表示フラグ

;モード実行中なら問答無用で許可
SIF MODE_確認("触手アナル挿入", TARGET)
	RETURN 1

SKIPDISP ARG

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
実行値 = RESULT
	＋表示フラグ = S

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 3
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	＋表示フラグ = 1
ENDIF
;ABL:Ａ感覚
IF ABL:Ａ感覚
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:Ａ感覚 * 2
	PRINT Ａ感覚
	PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
	＋表示フラグ = 1
ENDIF
;快楽刻印
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 3
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	＋表示フラグ = 1
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	PRINT  - 
	実行値 -= 5
	PRINT 潤滑不足
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情,5) * 3
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 2
	PRINT 羞恥心
	PRINTV '(,2,')
	＋表示フラグ = 1
ENDIF
;快感の否定
IF TALENT:快感応答 < 0 && !TALENT:恋慕
	PRINT  - 
	実行値 -= 5
	PRINT 快感の否定
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	実行値 -= 12
	PRINT 男嫌い
	PRINTV '(,12,')
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 恋慕
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF
;Ａ経験
IF EXP:Ａ経験 < EXPLV:2
	PRINT  - 
	実行値 -= 15
	PRINT Ａ経験
	PRINT 不足
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF
;触手経験
IF EXP:触手快楽経験 + EXP:触手絶頂経験 < EXPLV:2
	PRINT  - 
	実行値 -= 15
	PRINT 触手経験
	PRINT 不足
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 媚薬
	PRINTV '(,6,')
	＋表示フラグ = 1
ENDIF

;発情期
IF CFLAG:発情期フラグ < 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 4
	PRINT 発情中
	PRINTV '(,4,')
	＋表示フラグ = 1
ENDIF

;合計を表示(45以上で実行)
PRINT  = 
PRINTV 実行値

SIF 実行値 < 45
	PRINT  < 
SIF 実行値 == 45
	PRINT  = 
SIF 実行値 > 45
	PRINT  > 
PRINT 実行値
PRINTV 45

WAIT

;実行できない
SIF 実行値 < 45
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE104
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(104)
	RETURN RESULT
;モード実行中なら問答無用で許可
SIF MODE_確認("触手アナル挿入", TARGET)
	RETURN 1
;アナルバイブ挿入中はダメ
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0
;アナルを何かに使ってるならダメ
SIF TEQUIP:TARGET:アナル == 1
	RETURN 0
;操作術レベル不足
SIF あなた特殊能力:触手変化術 < 2
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM104

IF MODE_確認("触手アナル挿入", TARGET)
	;メッセージ表示タイミングはモード付加後なのでモードがあれば＝吸い付いたタイミング
	PRINTFORM %CALLNAME:PLAYER%は自らの触手を%CALLNAME:TARGET%の
	IF TEQUIP:下腹部前鎧あり || TEQUIP:下腹部後鎧あり
		PRINT 鎧の隙間から
		IF TEQUIP:下タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:下半身服あり
		PRINT 服の隙間から
		IF TEQUIP:下タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:下半身下着あり
		IF TEQUIP:下タイツあり
			PRINT タイツの下から
		ENDIF
		PRINT 下着に潜り込ませ、
	ENDIF
	
	IF EXP:Ａ経験 <= EXPLV:2
		PRINT まだ固く閉じられた
	ELSEIF EXP:Ａ経験 >= EXPLV:4
		PRINT すっかり柔らかく淫靡になった
	ENDIF
	PRINTFORML アナルへと挿入した
ELSE
	;メッセージ表示タイミングはモード付加後なのでモードが無いなら＝解除したタイミング
	PRINTFORM %CALLNAME:PLAYER%は触手を%CALLNAME:TARGET%のアナルから引き抜いた…
ENDIF

