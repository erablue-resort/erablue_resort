;-------------------------------------------------
;セックス系コマンド
;-------------------------------------------------
@COMNAME62
#FUNCTIONS
TSTR:コマンド名受渡 = Ａ挿入する
SIF MODE_存在判定_順不同("Ａ挿入系", PLAYER, TARGET)
	TSTR:コマンド名受渡 = Ａ体位を変える

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = Ａ挿入する

SIF 派生名フラグ
	TSTR:コマンド名受渡 = Ａ挿入

;許可時の特別文章はここに記入
IF TFLAG:オートコマンドフラグ >= 4000
	;反転時、つまりあなたが入れる方
	許可取り文章 = %CALLNAME:TARGET%は%CALLNAME:PLAYER%に[Ａ挿入]して欲しいようだ…
ELSE
	許可取り文章 = %CALLNAME:PLAYER%は%CALLNAME:TARGET%に[Ａ挿入]したいようだ…
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_62
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ａ系")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
;弱点コマンドカテゴリ = A
同時モード許可 = 3
同時モード拡張許可 = 1
オートコマンド許可 = 3
反転オート許可 = 3

@COM_TOOLTIP_62
LOCALS = <br>■Ａ挿入する<br>
LOCALS += "相手の尻穴に挿入し、アナルセックスを行うコマンド。<br>Ａ挿入系モード時、[Ａ体位を変える]に変化する。<br>Ａ挿入系モードとなり、モードを変更するか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：Ａ系<br>取得ソース：快Ａ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：Ｃ性交経験_Ａ挿入(PLAYER)・Ａ性交経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM62
#DIM 強さ初期値記録
#DIM 速さ初期値記録
#DIM ランダム候補, 20

IF TFLAG:オートコマンドフラグ
	;オートで実行する場合
	;ランダムで体位を決定して実行する
	VARSET ランダム候補, -1
	LOCAL:1 = 0
	FOR LOCAL, 0, 12
		TRYCALLFORM COM_ABLE62_{LOCAL}
		IF RESULT
			ランダム候補:(LOCAL:1) = LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
	LOCAL = ランダム候補:(RAND:(LOCAL:1))
	TRYCALLFORM COM62_{LOCAL}
	;メッセージ用に体位の種類を保存
	RFLAG:コマンド結果受渡し変数 = LOCAL
	RETURN 1
ELSEIF FLAG:モード管理 == 2
	IF RFLAG:コマンド結果受渡し変数 == 12
		TRYCALLFORM COM62_12(強さ初期値記録, 速さ初期値記録)
	ELSE
		強さ初期値記録 = 0
		速さ初期値記録 = 0
		$体位_LOOP2
		DRAWLINE
		PRINTL ■速さ・強さの初期値を選んでください
		PRINT 　　Ａ挿入強さ：
		PRINTBUTTON "[-]", 899
		FOR LOCAL, 0, 6
			IF 強さ初期値記録 >= LOCAL
				PRINTBUTTON "■", 900 + LOCAL
			ELSE
				PRINTBUTTON "□", 900 + LOCAL
			ENDIF
		NEXT
		PRINTBUTTON "[+]", "906"
		PRINT 　　
		PRINT Ａ挿入速さ：
		PRINTBUTTON "[-]", "909"
		FOR LOCAL, 0, 6
			IF 速さ初期値記録 >= LOCAL
				PRINTBUTTON "■", 910 + LOCAL
			ELSE
				PRINTBUTTON "□", 910 + LOCAL
			ENDIF
		NEXT
		PRINTBUTTON "[+]", 916
		PRINTL
		PRINTL
		PRINTBUTTONLC "[998]決定", 998
		PRINTBUTTONLC "[999]戻る", 999

		BINPUT

		LOCAL = RESULT
		SELECTCASE LOCAL
			CASE 900 TO 905
				強さ初期値記録 = LOCAL - 900
				GOTO 体位_LOOP2
			CASE 910 TO 915
				速さ初期値記録 = LOCAL - 910
				GOTO 体位_LOOP2
			CASE 899
				強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
				GOTO 体位_LOOP2
			CASE 906
				強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
				GOTO 体位_LOOP2
			CASE 909
				速さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
				GOTO 体位_LOOP2
			CASE 916
				速さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
				GOTO 体位_LOOP2
			CASE 999
				RETURN -1
		ENDSELECT
		LOCAL:10 = TARGET
		FOR LOCAL, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:LOCAL < 0
				BREAK
			RESULT = 0
			TARGET = 同時モード_選択キャラ:LOCAL
			TRYCALLFORM COM_ABLE62_12
			IF RESULT == 0
				TARGET = LOCAL:10
				RETURN -1
			ENDIF
			RESULT = 1
			TRYCALLFORM CAN_Ａ体位12(1)
			IF !RESULT
				TRYCALLFORM CAN_Ａ体位12(0)
				RETURN -1
			ENDIF
		NEXT
		TARGET = LOCAL:10

		TRYCALLFORM COM62_12(強さ初期値記録, 速さ初期値記録)
		;メッセージ用に体位の種類を保存
		RFLAG:コマンド結果受渡し変数 = 12
	ENDIF
	RETURN 1
ELSE
	IF MODE_存在判定_完全一致("Ａ挿入系", PLAYER, TARGET)
		強さ初期値記録 = A挿入強さ
		速さ初期値記録 = A挿入速さ
	ELSE
		強さ初期値記録 = 0
		速さ初期値記録 = 0
	ENDIF
	$体位_LOOP
	DRAWLINE
	PRINTL ■体位を選んでください
	PRINTL
	FOR LOCAL, 0, 20
		TRYCCALLFORM COM_ABLE62_{LOCAL}
			IF RESULT
				SELECTCASE LOCAL
					CASE 0
						PRINTBUTTONLC "[0]Ａ正常位", 0
					CASE 1
						PRINTBUTTONLC "[1]Ａ後背位", 1
					CASE 2
						PRINTBUTTONLC "[2]Ａ寝バック", 2
					CASE 3
						PRINTBUTTONLC "[3]Ａ対面座位", 3
					CASE 4
						PRINTBUTTONLC "[4]Ａ背面座位", 4
					CASE 5
						PRINTBUTTONLC "[5]Ａ騎乗位", 5
					CASE 6
						PRINTBUTTONLC "[6]Ａ側位", 6
					CASE 7
						PRINTBUTTONLC "[7]Ａ屈曲位", 7
					CASE 8
						PRINTBUTTONLC "[8]Ａ駅弁", 8
					CASE 9
						PRINTBUTTONLC "[9]Ａ背面駅弁", 9
					CASE 10
						PRINTBUTTONLC "[10]Ａ対面立位", 10
					CASE 11
						PRINTBUTTONLC "[11]Ａ立ちバック", 11
				ENDSELECT
			ENDIF
		CATCH
			CONTINUE
		ENDCATCH
	NEXT

	PRINTL
	PRINTL
	PRINTL ■速さ・強さの初期値を選んでください
	PRINT 　　Ａ挿入強さ：
	PRINTBUTTON "[-]", 899
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", 900 + LOCAL
		ELSE
			PRINTBUTTON "□", 900 + LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "906"
	PRINT 　　
	PRINT Ａ挿入速さ：
	PRINTBUTTON "[-]", "909"
	FOR LOCAL, 0, 6
		IF 速さ初期値記録 >= LOCAL
			PRINTBUTTON "■", 910 + LOCAL
		ELSE
			PRINTBUTTON "□", 910 + LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", 916
	PRINTL
	PRINTL
	PRINTBUTTONLC "[999]戻る", 999

	$INPUT_LOOP
	INPUT

	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 11
			TRYCALLFORM COM_ABLE62_{LOCAL}
			IF RESULT
				IF TARGET == MASTER
					TARGET = PLAYER
					PLAYER = MASTER
					RFLAG:指示モード対象あなたフラグ = 1
					TRYCALLFORM CAN_Ａ体位{LOCAL}(1)
					IF !RESULT
						TRYCALLFORM CAN_Ａ体位{LOCAL}(0)
						GOTO 体位_LOOP
					ENDIF
					RFLAG:指示モード対象あなたフラグ = 0
					PLAYER = TARGET
					TARGET = MASTER
				ELSE
					TRYCALLFORM CAN_Ａ体位{LOCAL}(1)
					IF !RESULT
						TRYCALLFORM CAN_Ａ体位{LOCAL}(0)
						GOTO 体位_LOOP
					ENDIF
				ENDIF
				TRYCALLFORM COM62_{LOCAL}(強さ初期値記録, 速さ初期値記録)
				;メッセージ用に体位の種類を保存
				RFLAG:コマンド結果受渡し変数 = LOCAL
				RETURN 1
			ENDIF
			GOTO INPUT_LOOP
		CASE 900 TO 905
			強さ初期値記録 = LOCAL - 900
			GOTO 体位_LOOP
		CASE 910 TO 915
			速さ初期値記録 = LOCAL - 910
			GOTO 体位_LOOP
		CASE 899
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 体位_LOOP
		CASE 906
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 体位_LOOP
		CASE 909
			速さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 体位_LOOP
		CASE 916
			速さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 体位_LOOP
		CASE 999
			A挿入強さ = 強さ初期値記録
			A挿入速さ = 速さ初期値記録
			RETURN -1
		CASEELSE
			GOTO INPUT_LOOP
	ENDSELECT
ENDIF

@COM62_0(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;引数は前から快楽量、露出量、接触量
CALL Ａ体位COM(400, 50, 150)
CALL MODE_CHANGE("Ａ正常位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_1(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;後背位
CALL Ａ体位COM(400, 70, 100)
CALL MODE_CHANGE("Ａ後背位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_2(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;寝バック
CALL Ａ体位COM(600, 50, 150)
CALL MODE_CHANGE("Ａ寝バック", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_3(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;対面座位
CALL Ａ体位COM(550, 70, 200)
CALL MODE_CHANGE("Ａ対面座位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_4(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;背面座位
CALL Ａ体位COM(450, 90, 120)
CALL MODE_CHANGE("Ａ背面座位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_5(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;騎乗位
CALL Ａ体位COM(500, 120, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)
CALL MODE_CHANGE("Ａ騎乗位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_6(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;側位
CALL Ａ体位COM(500, 70, 100)
CALL MODE_CHANGE("Ａ側位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_7(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;屈曲位
CALL Ａ体位COM(550, 70, 100)
CALL MODE_CHANGE("Ａ屈曲位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_8(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;駅弁
CALL Ａ体位COM(450, 70, 200)
CALL MODE_CHANGE("Ａ駅弁", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_9(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;背面駅弁
CALL Ａ体位COM(400, 110, 120)
CALL MODE_CHANGE("Ａ背面駅弁", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_10(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;対面立位
CALL Ａ体位COM(400, 70, 150)
CALL MODE_CHANGE("Ａ対面立位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_11(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;立ちバック
CALL Ａ体位COM(450, 90, 100)
CALL MODE_CHANGE("Ａ立ちバック", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
RETURN RESULT

@COM62_12(強さ初期値記録, 速さ初期値記録)
#DIM 強さ初期値記録
#DIM 速さ初期値記録
;同時挿入
CALL Ａ体位COM(350, 70, 90)
IF BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	CALL MODE_CHANGE("Ａ後背位", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
ELSE
	CALL MODE_CHANGE("Ａ立ちバック", PLAYER, TARGET, , , 強さ初期値記録, 速さ初期値記録)
ENDIF
RETURN RESULT



@Ａ体位COM(ARG, ARG:1, ARG:2)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, ARG)
CALL SOURCE_CALC_露出(TARGET, PLAYER, ARG:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, ARG:2)

IF !MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, ARG)
ELSE
	CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, ARG / 3)
ENDIF

;苦痛
LOCAL = 800 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF EXP:Ａ性交経験 == 0
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%のＡ処女を奪った</font><img src='えっちハート'>","うふふ")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%にＡ処女を捧げた</font><img src='えっちハート'>","うふふ")
ENDIF
EXP:PLAYER:Ｃ性交経験_Ａ挿入 ++
EXP:Ａ性交経験 ++
;A処女喪失
CALL LOST_A_VIRGIN(TARGET, PLAYER, 1)
CALL LOST_MAN_A_VIRGIN(PLAYER, TARGET)
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4


;愛情経験
EXP:愛情経験 += TALENT:恋慕

RETURN 1

@CAN_Ａ体位0(ARG)
;正常位
;実行に必要な基準値を投げる
SKIPDISP ARG
CALL CAN_Ａ体位(30)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位1(ARG)
;後背位
SKIPDISP ARG
CALL CAN_Ａ体位(32)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位2(ARG)
;寝バック
SKIPDISP ARG
CALL CAN_Ａ体位(32)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位3(ARG)
;対面座位
SKIPDISP ARG
CALL CAN_Ａ体位(40)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位4(ARG)
;背面座位
SKIPDISP ARG
CALL CAN_Ａ体位(42)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位5(ARG)
;騎乗位
SKIPDISP ARG
CALL CAN_Ａ体位(40)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位6(ARG)
;側位
SKIPDISP ARG
CALL CAN_Ａ体位(40)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位7(ARG)
;屈曲位
SKIPDISP ARG
CALL CAN_Ａ体位(45)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位8(ARG)
;駅弁
SKIPDISP ARG
CALL CAN_Ａ体位(47)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位9(ARG)
;背面駅弁
SKIPDISP ARG
CALL CAN_Ａ体位(49)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位10(ARG)
;対面立位
SKIPDISP ARG
CALL CAN_Ａ体位(30)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位11(ARG)
;立ちバック
SKIPDISP ARG
CALL CAN_Ａ体位(32)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL

@CAN_Ａ体位12(ARG)
;同時挿入
SKIPDISP ARG
CALL CAN_Ａ体位(40)
LOCAL = RESULT
SKIPDISP 0
RETURN LOCAL


@CAN_Ａ体位(ARG)
#DIM 実行値
#DIM ＋表示フラグ

SIF CFLAG:TARGET:睡眠
	RETURN 1

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
;IF TEQUIP:縄 == 0
	実行値 = 0
	＋表示フラグ = 0
	;すべての命令に共通の要素を考慮
	
	CALL COM_ORDER
	実行値 = RESULT
	＋表示フラグ = S

	;ABL:欲望
	IF ABL:欲望
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += ABL:欲望 * 3
		PRINT 欲望
		PRINTFORM LV{ABL:欲望}({ABL:欲望 * 3})
		＋表示フラグ = 1
	ENDIF
	IF RFLAG:指示モード対象あなたフラグ
		;ABL:Ｃ感覚
		IF ABL:Ｃ感覚
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += ABL:Ｃ感覚 * 3
			PRINT Ｃ感覚
			PRINTFORM LV{ABL:Ｃ感覚}({ABL:Ｃ感覚 * 3})
			＋表示フラグ = 1
		ENDIF
	ELSE
		;ABL:Ａ感覚
		IF ABL:Ａ感覚
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += ABL:Ａ感覚 * 2
			PRINT Ａ感覚
			PRINTFORM LV{ABL:Ａ感覚}({ABL:Ａ感覚 * 2})
			＋表示フラグ = 1
		ENDIF
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += MARK:快楽刻印 * 3
		PRINT 快楽刻印
		PRINTFORM LV{MARK:快楽刻印}({MARK:快楽刻印 * 3})
		＋表示フラグ = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF RFLAG:指示モード対象あなたフラグ
		LOCAL = PALAM:MASTER:潤滑
	ELSE
		LOCAL = PALAM:潤滑
	ENDIF
	IF LOCAL < PALAMLV:3
		PRINT  - 
		実行値 -= 5
		PRINT 潤滑不足
		PRINT (5)
		＋表示フラグ = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTFORM LV{GETPALAMLV(PALAM:欲情,5)}({GETPALAMLV(PALAM:欲情,5) * 3})
		＋表示フラグ = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		実行値 -= 2
		PRINT 羞恥心
		PRINT (2)
		＋表示フラグ = 1
	ENDIF
	;快感の否定
	IF TALENT:快感応答 < 0 && !TALENT:恋慕
		PRINT  - 
		実行値 -= 5
		PRINT 快感の否定
		PRINT (5)
		＋表示フラグ = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += 15
		PRINT 恋慕
		PRINT (15)
		＋表示フラグ = 1
	ELSE
		;男嫌い（恋慕で無効）
		IF TALENT:性別嗜好 & 1 && TALENT:PLAYER:性別 == 2
			PRINT  - 
			実行値 -= 12
			PRINT 男嫌い
			PRINT (12)
			＋表示フラグ = 1
		ENDIF
		;女嫌い（恋慕で無効）
		IF TALENT:性別嗜好 & 2 && TALENT:PLAYER:性別 == 1
			PRINT  - 
			実行値 -= 12
			PRINT 女嫌い
			PRINT (12)
			＋表示フラグ = 1
		ENDIF
	ENDIF
	;Ａ経験
	IF RFLAG:指示モード対象あなたフラグ
		;C経験
		IF EXP:Ｃ経験 < EXPLV:2
			PRINT  - 
			実行値 -= 5
			PRINT Ｃ経験
			PRINT 不足
			PRINT (5)
			＋表示フラグ = 1
		ENDIF
	ELSE
		IF EXP:Ａ経験 < EXPLV:2
			PRINT  - 
			実行値 -= 15
			PRINT Ａ経験
			PRINT 不足
			PRINT (15)
			＋表示フラグ = 1
		ENDIF
	ENDIF

	;調教者がふたなり
	IF (TALENT:PLAYER:性別 & 3) == 3
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += 4
		PRINT ふたなり
		PRINT (4)
		＋表示フラグ = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += 6
		PRINT 媚薬
		PRINT (6)
		＋表示フラグ = 1
	ENDIF

	;発情期
	IF CFLAG:発情期フラグ < 0
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += 4
		PRINT 発情中
		PRINT (4)
		＋表示フラグ = 1
	ENDIF

	;言いなり
	IF TCVAR:言いなり
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += 999
		PRINT あなたの言いなり
		PRINT (999)
		＋表示フラグ = 1
	ENDIF

	;合計を表示(ARG以上で実行)
	PRINT  = 
	PRINTV 実行値

	SIF 実行値 < ARG
		PRINT  < 
	SIF 実行値 == ARG
		PRINT  = 
	SIF 実行値 > ARG
		PRINT  > 
	PRINT 実行値
	PRINTV ARG

	WAIT

	;実行できない
	SIF 実行値 < ARG
		RETURN 0
;ENDIF
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE62
;Ａ挿入共通実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(62)
	RETURN RESULT
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;調教者がオトコ、半人半妖、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:性別 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;一定のＡ経験が必要
SIF EXP:2 < EXPLV:3
	RETURN 0
;処女でなくとも潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0

;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:性別 & 2) && !MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0

IF !MODE_存在判定_順不同("Ａ挿入系", PLAYER, TARGET)
	;ペニスを何かに使ってるなら無理（Ａ挿入中は許可）
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
	;アナルを何かに使ってるなら無理（Ａ挿入中は許可）
	SIF TEQUIP:TARGET:アナル
		RETURN 0
ENDIF

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF EXP:PLAYER:Ａ経験 == 0 || EXP:TARGET:Ａ経験 == 0
		RETURN 0
	SIF MODE_存在判定_順不同("Ａ挿入系", PLAYER, TARGET)
		RETURN 0
ENDIF
;睡姦の制限
IF CFLAG:TARGET:睡眠
	;陥落のみ
	SIF 陥落チェック(TARGET) == 0
		RETURN 0
	;初ウフフずみ
	SIF 初体験日:TARGET:初うふふ < 1
		RETURN 0
	;熟知や経験必要
	SIF EXP:TARGET:肉体熟知 < 500
		RETURN 0
	SIF EXP:PLAYER:Ｃ性交経験_Ａ挿入 + EXP:TARGET:肉体熟知 < 1500
		RETURN 0
ENDIF

RETURN 1

@COM_ABLE62_0
;正常位
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF

RETURN 1

@COM_ABLE62_1
;後背位
;だいたいいつでもOK

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

@COM_ABLE62_2
;寝バック
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_3
;対面座位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;お風呂場プレイ中はOK
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 1
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_4
;背面座位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;お風呂場プレイ中はOK
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 1
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_5
;騎乗位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_6
;側位
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_7
;屈曲位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ELSE
	;ベッドがないとダメ
	SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE62_8
;駅弁

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;TARGETのほうが3サイズ大きいと抱えきれない
SIF 体格差計算(PLAYER, TARGET) >= 3
	RETURN 0

RETURN 1

@COM_ABLE62_9
;背面駅弁

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;TARGETのほうが3サイズ大きいと抱えきれない
SIF 体格差計算(PLAYER, TARGET) >= 3
	RETURN 0

RETURN 1

@COM_ABLE62_10
;対面立位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;TARGETのほうが4サイズ大きいと届かない
SIF 体格差計算(PLAYER, TARGET) >= 4
	RETURN 0

RETURN 1

@COM_ABLE62_11
;立ちバック

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;TARGETのほうが4サイズ大きいと届かない
SIF 体格差計算(PLAYER, TARGET) >= 4
	RETURN 0

RETURN 1

@COM_ABLE62_12
;同時挿入

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0
;TARGETのほうが4サイズ大きいならプレイマットかベッドが必要
IF 体格差計算(PLAYER, TARGET) >= 4
	IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		SIF ITEM:20 == 0 && NOITEM == 0
			RETURN 0
	ELSE
		SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
			RETURN 0
	ENDIF
ENDIF

RETURN 1



;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM62
#DIM 同時キャラ番号


IF !MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
	LOCALS = 硬くそそり立つ陰茎
ELSE
	LOCALS = ペニスバンド
ENDIF

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 0
		;正常位
		PRINTFORML %CALLNAME:PLAYER%は仰向けに寝転んだ%CALLNAME:TARGET%のアナルに%LOCALS%を挿入した
	CASE 1
		;後背位
		PRINTFORML %CALLNAME:PLAYER%は四つん這いになった%CALLNAME:TARGET%のアナルに%LOCALS%を挿入した
	CASE 2
		;寝バック
		PRINTFORML %CALLNAME:PLAYER%はうつ伏せに寝転んだ%CALLNAME:TARGET%のアナルに%LOCALS%を挿入した
	CASE 3
		;対面座位
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と向かい合うように座り、そのままアナルへ%LOCALS%を挿入した
	CASE 4
		;背面座位
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を自身の上に座らせ、そのままアナルへ%LOCALS%を挿入した
	CASE 5
		;騎乗位
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に自らアナルへ%LOCALS%を挿入させた
	CASE 6
		;側位
		PRINTFORML %CALLNAME:PLAYER%は横向きに寝転んだ%CALLNAME:TARGET%のアナルに%LOCALS%を挿入した
	CASE 7
		;屈曲位
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の脚を揃えて上げさせ、そのままアナルへ%LOCALS%を挿入した
	CASE 8
		;駅弁
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を向かい合ったまま抱え上げ、そのままアナルへ%LOCALS%を挿入した
	CASE 9
		;背面駅弁
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を後ろ向きに抱え上げ、そのままアナルへ%LOCALS%を挿入した
	CASE 10
		;対面立位
		PRINTFORML %CALLNAME:PLAYER%は立ったまま%CALLNAME:TARGET%と向かい合い、アナルへと%LOCALS%を挿入した
	CASE 11
		;立ちバック
		PRINTFORML %CALLNAME:PLAYER%は後ろ向きに尻を突き出した%CALLNAME:TARGET%のアナルへと%LOCALS%を挿入した
	CASE 12
		;同時導入
		PRINTFORM %CALLNAME:PLAYER%は
		FOR 同時キャラ番号, 0, 同時モード_選択数
			SIF 同時キャラ番号 > 0
				PRINT 、
			PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
		NEXT
		PRINTFORML を並べると、アナルへと順番に%LOCALS%を挿入した
ENDSELECT

IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:TARGET%は小さく声を漏らしながらも、まだ夢の中にいる…
ENDIF
