;-------------------------------------------------
;ブラ脱衣
;-------------------------------------------------
@COMNAME206
#FUNCTIONS
IF TEQUIP:上半身下着あり == 2
	TSTR:コマンド名受渡 = ニプレスを剥がす
ELSEIF TEQUIP:上半身下着あり == 3
	TSTR:コマンド名受渡 = さらしから開放する
ELSEIF TEQUIP:上半身下着あり == 4
	TSTR:コマンド名受渡 = 胸のリボンを解く
ELSEIF TEQUIP:上半身下着あり == -3
	TSTR:コマンド名受渡 = 乳首の名札を外す
ELSEIF TEQUIP:上半身下着あり == -2
	TSTR:コマンド名受渡 = ニップルピアスを外す
ELSE
	TSTR:コマンド名受渡 = ブラを脱がす
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_206
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_206
LOCALS = <br>■ブラを脱がす<br>
LOCALS += "相手の上半身の下着を脱がせるコマンド。<br>上半身の下着の種類に応じて、名称が変化。<br>上半身下着抜き取り可の場合、上半身の服があっても実行出来る。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM206
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上半身下着あり = 0
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM206(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 上半身下着脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 5
		PRINT 野外露出(5)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE206
;上半身下着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(206)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上半身下着あり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0
;抜き取れない服があるとダメ
IF TEQUIP:上半身服あり && !TEQUIP:上半身下着抜き取り可
	;ニプレスなら差し込みorはだけ可でOK
	SIF TEQUIP:上半身下着あり != 2 || (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
		RETURN 0
ENDIF
;タイツもダメ
SIF TEQUIP:上タイツあり
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM206
PRINTFORM %CALLNAME:PLAYER%は
IF TEQUIP:上半身下着あり == 2
	PRINTFORML %CALLNAME:TARGET%のニプレスを剥がした
ELSEIF TEQUIP:上半身下着あり == 3
	PRINTFORML %CALLNAME:TARGET%の胸を締め付けていたさらしを解いた
ELSEIF TEQUIP:上半身下着あり == 4
	PRINTFORML %CALLNAME:TARGET%の胸をデコレートしているリボンをほどいた
ELSEIF TEQUIP:上半身下着あり == -3
	PRINTFORML %CALLNAME:TARGET%の乳首を貫いている名札つき安全ピンを外した
ELSEIF TEQUIP:上半身下着あり == -2
	PRINTFORML %CALLNAME:TARGET%の乳首を飾り立てるニップルピアスを外した
ELSE
	PRINTFORML %CALLNAME:TARGET%のブラを脱がせた
ENDIF
IF TALENT:TARGET:バストサイズ == 3
	PRINT どたぷんっ
	PRINT_IMG "えっちハート"
	PRINT と、豊満な乳房が大きく揺れながらまろびでた
ELSEIF TALENT:TARGET:バストサイズ == 2
	PRINT たっぷん、と大きな乳房がまろびでた
ENDIF


IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 人目のあるところで脱いだことで、%CALLNAME:TARGET%はどこか落ち着かないようだ…
ENDIF


