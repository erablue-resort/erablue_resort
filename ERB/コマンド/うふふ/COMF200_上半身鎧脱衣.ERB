;-------------------------------------------------
;上半身脱衣
;-------------------------------------------------
@COMNAME200
#FUNCTIONS
TSTR:コマンド名受渡 = 上鎧を脱がす

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_200
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_200
LOCALS = <br>■上鎧を脱がす<br>
LOCALS += "相手の上半身鎧を脱がせるコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM200
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上半身鎧あり = 0
ENDIF

RETURN 1


@CAN_COM200(ARG)
#DIM CONST 必要実行値 = 12
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 上半身鎧脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 10
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 10})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(12以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE200
;上半身鎧脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(200)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上半身鎧あり
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM200
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の上鎧を脱がせた…


