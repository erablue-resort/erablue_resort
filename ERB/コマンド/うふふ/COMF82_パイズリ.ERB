;-------------------------------------------------
;パイズリ
;奉仕系コマンド、レベル3
;-------------------------------------------------
@COMNAME82
#FUNCTIONS
IF MODE_存在判定_完全一致("フェラチオ", TARGET, PLAYER)
	TSTR:コマンド名受渡 = パイズリフェラ
ELSEIF CFLAG:睡眠 == 1
	TSTR:コマンド名受渡 = 胸を犯す
ELSE
	TSTR:コマンド名受渡 = パイズリ
ENDIF


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_82
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
同時モード許可 = 3
同時モード拡張許可 = 0
オートコマンド許可 = 3
反転オート許可 = 3
ダウン中許可_PLAYER = 1


@COM_TOOLTIP_82
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■パイズリする<br>
	LOCALS += "自分が胸で相手のペニスを愛撫するコマンド。<br>パイズリモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。<br>フェラチオモード時、[パイズリフェラ]に変化。"
ELSE
	LOCALS = <br>■パイズリ<br>
	LOCALS += "相手に胸で自分のペニスを愛撫してもらうコマンド。<br>パイズリモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。<br>フェラチオモード時、[パイズリフェラ]に変化。"
ENDIF
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｂ・露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：パイズリ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM82
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
IF MODE_存在判定_完全一致("フェラチオ", TARGET, PLAYER)
	RFLAG:同時モード体位変化フラグ = 1
	CALL MODE_DELETE_プレイヤー側("フェラチオ", TARGET)
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSEIF RFLAG:同時モード体位変化フラグ
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSE
	CALL MODE_CHANGE("パイズリ", TARGET, PLAYER)
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------




CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 160)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 80)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 400)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 200)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:パイズリ経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 7
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 7

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 ++

RETURN 1

@CAN_COM82(ARG)
#DIM CONST 必要実行値 = 30
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML パイズリ(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 3
	PRINTFORM 精液中毒LV{ABL:精液中毒}({ABL:精液中毒 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 3
	PRINT 恥じらい(3)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 汚臭鈍感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 汚臭敏感(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身的(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 3
	PRINT 快感の否定(3)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 男嫌い(12)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 親愛(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT ふたなり(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;特殊言いなり
IF TCVAR:特殊言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINTFORM %特殊言いなり分岐(TARGET)%(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(30以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE82
;パイズリ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE(82)
	RETURN RESULT
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;相手に胸がないならダメ
SIF TALENT:TARGET:性別 == 2
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0
;上半身着衣(TARGET)
;鎧、はだけない服、タイツがあると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:はだけ可 && !TEQUIP:パイズリ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
;下半身着衣(PLAYER)
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
IF !MODE_存在判定_完全一致("フェラチオ", TARGET, PLAYER)
	;ペニスを何かに使ってるなら無理
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
ENDIF
;胸を何かに使ってるなら無理
SIF TEQUIP:TARGET:胸
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM82
#DIM 同時キャラ番号
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
		PRINTFORML は乳房を寄せ合いながら%CALLNAME:PLAYER%のペニスに舌を這わせた…
	ELSE
		PRINTFORML は協力して%CALLNAME:PLAYER%のペニスを乳で挟み込んでいる…
	ENDIF
	RETURN 0
ENDIF

IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
	PRINTFORM %CALLNAME:TARGET%は
	IF 知識素質:性知識 < 0
		PRINTFORM %CALLNAME:PLAYER%に言われた通り
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%のペニスを咥えたまま
	IF TALENT:バストサイズ <= -1
		PRINT 薄い胸で懸命にペニスを
	;巨乳
	ELSEIF TALENT:バストサイズ == 1
		PRINT 豊満な胸でペニスを包み込んで
	;爆乳
	ELSEIF TALENT:バストサイズ == 2
		PRINT 豊満すぎる程の胸でペニスを包み隠すようにして
	;魔乳
	ELSEIF TALENT:バストサイズ >= 3
		PRINT 、魔性の大きさと呼べるほどにまで育ちきった胸でペニスを包み隠して
	ELSE
		PRINT 胸でペニスをはさんで
	ENDIF
	PRINTL 擦りあげ始めた…
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	IF 知識素質:性知識 < 0
		PRINTFORM %CALLNAME:PLAYER%のお願いを受けて
	ENDIF
	;恋慕か親愛
	SIF TEQUIP:上半身服あり
		PRINT 服を着たまま、
	;貧乳
	IF TALENT:バストサイズ <= -1
		PRINTFORM 薄い胸で懸命に%CALLNAME:PLAYER%のペニスを
	;巨乳
	ELSEIF TALENT:バストサイズ == 1
		PRINTFORM 豊満な胸で%CALLNAME:PLAYER%のペニスを包み込んで
	;爆乳
	ELSEIF TALENT:バストサイズ == 2
		PRINTFORM 豊満すぎる程の胸で%CALLNAME:PLAYER%のペニスを包み隠すようにして
	;魔乳
	ELSEIF TALENT:バストサイズ >= 3
		PRINTFORM 、魔性の大きさと呼べるほどにまで育ちきった胸で%CALLNAME:PLAYER%のペニスを包み隠して
	ELSE
		PRINTFORM 胸で%CALLNAME:PLAYER%のペニスをはさんで
	ENDIF
	PRINTL 擦りあげている…
ENDIF


