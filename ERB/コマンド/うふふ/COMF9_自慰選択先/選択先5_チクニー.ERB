
@COMNAME_S9_5
#FUNCTIONS
TSTR:コマンド名受渡 = チクニー

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_5
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = B
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_5
LOCALS = <br>■チクニー<br>
LOCALS += "相手が見ている前で乳首を弄り自慰を行うコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：露出・快Ｂ・歓楽(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：自慰経験(自慰実行者)、調教自慰経験(自慰実行者)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_5
;共通処理
CALL Ｂ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("チクニー", MASTER, (自慰ターゲット保存))
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_5

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_チクニー
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_チクニー
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_5")
	RETURN RESULT
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):上半身鎧あり
	RETURN 0
;服駄目
SIF TEQUIP:(自慰ターゲット保存):上半身服あり && !TEQUIP:(自慰ターゲット保存):はだけ可 && !TEQUIP:(自慰ターゲット保存):手差し込み可
	RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):上タイツあり && !TEQUIP:(自慰ターゲット保存):上タイツ破れ
	RETURN 0
;ニプレス駄目
SIF TEQUIP:(自慰ターゲット保存):上半身下着あり == 2
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｂ感度 == -2
	RETURN 0
;男の場合はABL必要
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 0) == 0 && ABL:(自慰ターゲット保存):Ｂ感覚 < 2
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):胸
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 5

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_5

PRINTFORM %CALLNAME:(自慰ターゲット保存)%は
IF TEQUIP:(自慰ターゲット保存):上半身服あり
	PRINT 服に手を差し込んで
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり
    PRINT 下着の中に手を差し込んで
ENDIF
PRINTL 乳首を指で転がし、刺激し始めた…


@MESSAGE_COM_S9_5_非口上時
SIF (自慰ターゲット保存)== MASTER
    RETURN 0
SIF TFLAG:オートコマンドフラグ < 1
    RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
    PRINTFORML 誰かに見られながらの自慰には慣れていないようで、%CALLNAME:(自慰ターゲット保存)%は顔を赤くして%CALLNAME:MASTER%を睨んでいる…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
    PRINTFORML 少し慣れてきたようだが、まだ%CALLNAME:MASTER%の視線が気になるようだ…
ELSE
    PRINTFORML %CALLNAME:(自慰ターゲット保存)%は%CALLNAME:MASTER%に見られながらの自慰に、妙な高揚感を覚えている…
ENDIF
