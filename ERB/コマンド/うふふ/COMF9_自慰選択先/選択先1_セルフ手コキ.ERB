
@COMNAME_S9_1
#FUNCTIONS
TSTR:コマンド名受渡 = セルフ手コキ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_1
LOCALS = <br>■セルフ手コキ<br>
LOCALS += "相手が見ている前で陰茎を手で擦り自慰を行うコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：露出・快Ｃ・歓楽(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：自慰経験(自慰実行者)、調教自慰経験(自慰実行者)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_1
;共通処理
CALL Ｃ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("セルフ手コキ", MASTER, (自慰ターゲット保存))

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_1
IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_セルフ手コキ
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_セルフ手コキ
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_1")
	RETURN RESULT
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):下腹部前鎧あり
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｃ感度 == -2
	RETURN 0
;ペニス必要
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 1) == 0
    RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):下タイツあり && !TEQUIP:(自慰ターゲット保存):下タイツ破れ
    RETURN 0
;前張り駄目
SIF TEQUIP:(自慰ターゲット保存):下半身下着あり == 2
    RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):ペニス
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_1

PRINTFORM %CALLNAME:(自慰ターゲット保存)%は自らのペニスを
IF TEQUIP:(自慰ターゲット保存):下半身服あり && !TEQUIP:(自慰ターゲット保存):スカートめくり可
    PRINT 服の隙間から出して、
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり
    PRINT 下着の隙間から出して、
ENDIF
PRINTL 手で上下に扱き始めた…


@MESSAGE_COM_S9_1_非口上時
SIF (自慰ターゲット保存)== MASTER
    RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
    PRINTFORML 誰かに見られながらの自慰には慣れていないようで、%CALLNAME:(自慰ターゲット保存)%は顔を赤くして%CALLNAME:MASTER%を睨んでいる…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
    PRINTFORML 少し慣れてきたようだが、まだ%CALLNAME:MASTER%の視線が気になるようだ…
ELSE
    PRINTFORML %CALLNAME:(自慰ターゲット保存)%は%CALLNAME:MASTER%に見られながらの自慰に、妙な高揚感を覚えている…
ENDIF
