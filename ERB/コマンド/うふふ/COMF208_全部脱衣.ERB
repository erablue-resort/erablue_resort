;-------------------------------------------------
;全部脱衣
;-------------------------------------------------
@COMNAME208
#FUNCTIONS
TSTR:コマンド名受渡 = 全裸にする
SIF FLAG:モード管理 == 2
	TSTR:コマンド名受渡 = まとめて全裸にする



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_208
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
同時モード許可 = 999
オートコマンド許可 = 3

@COM_TOOLTIP_208
LOCALS = <br>■全裸にする<br>
LOCALS += "相手の全部脱がせるコマンド。<br>脱衣系コマンドで唯一同時モードで実行可能"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM208
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:TARGET:上半身鎧あり = 0
	TEQUIP:TARGET:上半身服あり = 0
	TEQUIP:TARGET:脇出し = 0
	TEQUIP:TARGET:へそ出し = 0
	TEQUIP:TARGET:はだけ可 = 0
	TEQUIP:TARGET:手差し込み可 = 0
	TEQUIP:TARGET:パイズリ可 = 0
	TEQUIP:TARGET:上タイツあり = 0
	TEQUIP:TARGET:上半身下着あり = 0
	TEQUIP:TARGET:上半身下着抜き取り可 = 0
	TEQUIP:TARGET:下腹部後鎧あり = 0
	TEQUIP:TARGET:下腹部前鎧あり = 0
	TEQUIP:TARGET:下半身服あり = 0
	TEQUIP:TARGET:スカートめくり可 = 0
	TEQUIP:TARGET:ずらし可 = 0
	TEQUIP:TARGET:下タイツあり = 0
	TEQUIP:TARGET:下半身下着あり = 0
	TEQUIP:TARGET:下半身下着抜き取り可 = 0
	TEQUIP:TARGET:上下一体服 = 0
	TEQUIP:TARGET:上下一体タイツ = 0
	TEQUIP:TARGET:上タイツ破れ = 0
	TEQUIP:TARGET:下タイツ破れ = 0
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM208(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 全部脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1 
		PRINT  - 
		実行値 -= 15
		PRINT 野外露出(15)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE208
;全部脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(208)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0

;既に全裸だったらダメ
LOCAL:1 = 0

SIF TEQUIP:TARGET:上半身鎧あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:上半身服あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:上タイツあり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:上半身下着あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:下腹部後鎧あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:下腹部前鎧あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:下半身服あり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:下タイツあり != 0
	LOCAL:1 = 1
SIF TEQUIP:TARGET:下半身下着あり != 0
	LOCAL:1 = 1

SIF LOCAL:1 == 0
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM208
#DIM 同時キャラ番号

IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINTL の衣服をすべて脱がせた…
ELSE
	PRINTFORML %CALLNAME:TARGET%の衣服をすべて脱がせた…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 人目のあるところで脱いだことで、%CALLNAME:TARGET%はどこか落ち着かないようだ…
ENDIF

