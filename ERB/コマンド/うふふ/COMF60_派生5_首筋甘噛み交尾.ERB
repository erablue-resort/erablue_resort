
@COMNAME_S60_6
#FUNCTIONS
TSTR:コマンド名受渡 = 首筋甘噛み交尾

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_60_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
弱点コマンドカテゴリ = V
オートコマンド許可 = 1

@COM_TOOLTIP_S60_6
LOCALS = <br>■首筋甘噛み交尾<br>
LOCALS += "首筋に噛みつき、歯をたてながら腰を振るコマンド。<br>ドラフ、エルーン及びマゾっ気持ちに効果大。"
LOCALS += "<br>体格差がない場合の正常位、後背位、対面座位、寝バック、立ちバック時のみ実行可能。"
LOCALS += "<br>COMタイプ：Ｖ系<br>取得ソース：征服・快Ｖ・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ｖ性交経験(TARGET)・Ｃ性交経験_Ｖ挿入(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S60_6
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


CALL SOURCE_CALC_征服(PLAYER, TARGET, 150)
CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 400)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 400)

;苦痛
LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 180)
RCVAR:TARGET:苦痛コマンドフラグ = 1
RCVAR:PLAYER:嗜虐コマンドフラグ = 1

;マゾに効果大。ただし苦痛の量が増すのではなくそのほかが増える。
;痛みで締まるので快Ｖも盛っちゃお
IF ABL:TARGET:マゾ性感 > 2
	CALL SOURCE_CALC_征服(PLAYER, TARGET, 80)
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 80)
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)
	EXP:TARGET:苦痛快楽経験 += 1
ELSE
	CALL SOURCE_CALC_反感(TARGET, PLAYER, 60)
ENDIF

;エルーンとドラフにはさらに効果が増す。マゾエルーンが重複でマシマシ効果になるようにこの調整。
;猫は痛みで排卵するって言うし、噛まれて無意識に腰へこへこしちゃうマゾユエルは捗るのでこれでヨシ！
IF TALENT:TARGET:種族 == 2 || TALENT:TARGET:種族 == 3
	CALL SOURCE_CALC_征服(PLAYER, TARGET, 80)
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 80)
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 120)
	EXP:TARGET:苦痛快楽経験 += 1
ENDIF

;サドは嗜虐快楽
SIF ABL:サド性感
	EXP:嗜虐快楽経験 ++


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:Ｃ性交経験_Ｖ挿入 ++
EXP:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S60_6
;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("60_6")
	RETURN RESULT
;噛む側が口を何かに使ってるなら無理
SIF TEQUIP:PLAYER:口
	RETURN 0


;体格問題
SIF TALENT:PLAYER:体格 == 体格_ハーヴィン並 && TALENT:TARGET:体格 > 体格_子供
	RETURN 0
SIF TALENT:PLAYER:体格 < TALENT:TARGET:体格 - 2
	RETURN 0
;組み敷く系や密着系モードじゃない時はアウト。背面座位とかでもできないことはないんだけど……シチュ的にどうだろ？
SIF MODE_確認_完全一致("正常位", PLAYER, TARGET) < 0 && MODE_確認_完全一致("対面座位", PLAYER, TARGET) < 0 && MODE_確認_完全一致("後背位", PLAYER, TARGET) < 0 && MODE_確認_完全一致("立ちバック", PLAYER, TARGET) < 0 && MODE_確認_完全一致("寝バック", PLAYER, TARGET) < 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S60_6
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
PRINTFORML %CALLNAME:TARGET%の首筋に%TEXTR("噛みつき/歯をたて")%ながら%TEXTR("強く/激しく")%腰を打ち付けた

IF ABL:TARGET:マゾ性感 > 2 || TALENT:TARGET:種族 == 2 || TALENT:TARGET:種族 == 3
	PRINTFORM 痛みに嬌声をあげる%CALLNAME:TARGET%のカラダは%TEXTR("被支配のマゾ性感に打ち震え、/誰が『ご主人様』かを従順に理解し、/この場における上下関係を本能的に理解して、")%
	PRINTFORML %TEXTR("膣肉の締め付けを強くして/反射的に腰を擦りつけるようにして/無意識にヘコヘコと腰を振って/ペニスを深くまで咥えこみ、子宮を捧げて")%媚びている
ENDIF
