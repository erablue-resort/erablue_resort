;-------------------------------------------------
;呼び出す
;-------------------------------------------------
@COMNAME210
#FUNCTIONS
TSTR:コマンド名受渡 = 呼び出す

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_210
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("特殊プレイ")

@COM_TOOLTIP_210
LOCALS = <br>■呼び出す<br>
LOCALS += "性交渉中、他のキャラをこの場に呼び出すコマンド。<br>現在対象、及び呼び出す対象は[恋慕]か[セフレ]を所持していなければならない。"
LOCALS += "<br>COMタイプ：特殊プレイ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM210
#DIM 表示候補, 5000
#DIM 現在ページ
#DIM 呼び出し人数
#DIM 対象キャラ
#DIM DYNAMIC 初回実行フラグ = 1
#DIM リスタートフラグ
#DIM 実行結果
#DIM 入力内容
#DIM REDRAW_TEMP
#DIM 描画開始行
#DIM CONST 表示人数 = 15

IF 初回実行フラグ
	初回実行フラグ = 0
	; 呼び出しキャラ情報を初期化
	VARSET 表示候補, -1
	現在ページ = 0
	VARSET 呼び出し対象
	呼び出し人数 = 0
	CALL 呼び出す候補キャラ登録(表示候補)
	; 描画周りの初期状態を記憶
	描画開始行 =LINECOUNT
	REDRAW_TEMP = CURRENTREDRAW()
	REDRAW 0
ENDIF

PRINTL
DRAWLINE
PRINTL 誰を呼び出しますか？
DRAWLINE
LOCAL:1 = 0
;キャラの表示
FOR LOCAL, (現在ページ * 表示人数), ((現在ページ * 表示人数) + 表示人数)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		BREAK
	ENDIF
	;選択中のキャラの色を変更
	SIF MATCH(呼び出し対象, 表示候補:LOCAL)
		SETCOLOR カラーパレット("黄")
	PRINTBUTTONLC @"[{LOCAL, 2}] %ADD_STR_SPACE(NAME表示省略(NAME表示(表示候補:LOCAL)), "]"), 41, LEFT%", LOCAL
	RESETCOLOR
	; 体力の状態に合わせて体力の色を変更
	IF MAXBASE:(表示候補:LOCAL):体力 == BASE:(表示候補:LOCAL):体力
		SETCOLOR 0x50B050
	ELSEIF MAXBASE:(表示候補:LOCAL):体力 < BASE:(表示候補:LOCAL):体力 / 3
		SETCOLOR カラーパレット("赤")
	ENDIF
	PRINTFORM 体力：{BASE:(表示候補:LOCAL):体力, 4} / {MAXBASE:(表示候補:LOCAL):体力, 4}　
	RESETCOLOR
	; 性欲の状態に合わせて性欲の色を変更
	IF BASE:(表示候補:LOCAL):性欲 > 2000
		SETCOLOR カラーパレット("ピンク")
	ELSEIF BASE:(表示候補:LOCAL):性欲 > 1000
		SETCOLOR カラーパレット("薄ピンク")
	ENDIF
	PRINTFORM 性欲：{BASE:(表示候補:LOCAL):性欲, 4}　
	RESETCOLOR
	; 発情期表示
	IF CFLAG:(表示候補:LOCAL):発情期フラグ < 0
		SETCOLOR カラーパレット("えっちな色")
		PRINT 発情期
		PRINT_IMG "えっちハート"
		PRINT 　
		RESETCOLOR
	ELSE
		PRINT 　　　　　
	ENDIF
	; 陥落状態表示
	IF TALENT:(表示候補:LOCAL):恋慕
		SETCOLOR カラーパレット("えっちな色")
		PRINT 恋慕　
		RESETCOLOR
	ELSEIF TALENT:(表示候補:LOCAL):セフレ
		PRINT セフレ
	ELSE
		PRINT 　　　
	ENDIF
	PRINTFORM 　　画像数：{CHAR_GRAPHIC_COUNT(表示候補:LOCAL, 0), 4}　　
	PRINTBUTTON "[画像詳細]", 10000 + LOCAL
	PRINTL
NEXT

DRAWLINE
;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900] 前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF 表示候補:((現在ページ * 表示人数) + 表示人数) > 0
	PRINTBUTTONC "[901] 次ページへ", 901
ENDIF
PRINTL 
;まとめて選択系ボタン
PRINTBUTTONLC @"[995] 全員選択", 995
PRINTBUTTONLC @"[994] 恋慕キャラ選択", 994
PRINTBUTTONLC @"[993] セフレキャラ選択", 993
PRINTBUTTONLC @"[992] セフレキャラ選択(恋慕除く)", 992
PRINTL
;確定と選択解除ボタン
IF 呼び出し人数
	PRINTBUTTONLC @"[998] 以上{呼び出し人数}人を呼び出す", 998
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTFORMLC [---] 以上{呼び出し人数}人を呼び出す
	RESETCOLOR
ENDIF
PRINTBUTTONLC @"[996] 選択全解除", 996
;PRINTBUTTONLC @"[997] 呼び出し可能キャラを全員呼び出す", 997
PRINTL 
PRINTBUTTONLC "[999] やめる", 999

BINPUT
入力内容 = RESULT
リスタートフラグ = 0
実行結果 = 0
SELECTCASE 入力内容
	CASE IS >= 10000
		CALL 所持画像詳細表示(表示候補:(入力内容 - 10000))
		リスタートフラグ = 1
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		リスタートフラグ = 1
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		リスタートフラグ = 1
	CASE 999
		実行結果 = -1
	CASE 998
		; 呼び出し対象確定
		FOR LOCAL, 0, VARSIZE("呼び出し対象")
			対象キャラ = 呼び出し対象:LOCAL
			SIF 対象キャラ < 1
				CONTINUE
			CFLAG:対象キャラ:現在位置 = CFLAG:PLAYER:現在位置
			CFLAG:対象キャラ:現在マップ種別 = CFLAG:PLAYER:現在マップ種別
			CALL うふふ開始キャラ処理(対象キャラ, "呼び出す")
			;野外オナニー時に備えて隠密を消す
			CALL 野外オナニー途中終了(対象キャラ)
			CFLAG:対象キャラ:隠密 = 0
			;先に同室フラグを建てることで発覚口上をスキップする
			CFLAG:対象キャラ:同室 = 1
			SIF RFLAG:コマンド結果受渡し変数 < 1
				RFLAG:コマンド結果受渡し変数 = 対象キャラ
		NEXT
		SIF 呼び出し人数 > 1
			RFLAG:コマンド結果受渡し変数２ = 1
		;モード管理フラグを初期化
		FLAG:モード管理 = 0
		実行結果 = 0
	CASE 996
		; 選択全解除
		VARSET 呼び出し対象
		呼び出し人数 = 0
		リスタートフラグ = 1
	CASE 992 TO 995
		; まとめて選択系処理
		VARSET 呼び出し対象
		呼び出し人数 = 0
		FOR LOCAL, 0, VARSIZE("表示候補")
			対象キャラ = 表示候補:LOCAL
			;登録が終わったら表示終了
			SIF 対象キャラ == -1
				BREAK
			; 恋慕キャラを選択
			; → 恋慕なしはNG
			SIF 入力内容 == 994 && !TALENT:対象キャラ:恋慕
				CONTINUE
			; セフレキャラを選択(恋慕＋セフレのキャラもOK)
			; → セフレなしはNG
			SIF 入力内容 == 993 && !TALENT:対象キャラ:セフレ
				CONTINUE
			; セフレキャラを選択(恋慕＋セフレのキャラはNG)
			; → セフレなし or 恋慕ありはNG
			SIF 入力内容 == 992 && (!TALENT:対象キャラ:セフレ || TALENT:対象キャラ:恋慕)
				CONTINUE
			呼び出し対象:呼び出し人数 = 対象キャラ
			呼び出し人数 ++
		NEXT
		リスタートフラグ = 1
;	CASE 997
;		; まとめて呼び出す
;		呼び出し人数 = 0
;		FOR LOCAL, 0, VARSIZE("表示候補")
;			;登録が終わったら表示終了
;			IF 表示候補:LOCAL == -1
;				BREAK
;			ENDIF
;			CFLAG:(表示候補:LOCAL):現在位置 = CFLAG:PLAYER:現在位置
;			CFLAG:(表示候補:LOCAL):現在マップ種別 = CFLAG:PLAYER:現在マップ種別
;			CALL うふふ開始キャラ処理(表示候補:LOCAL)
;			;野外オナニー時に備えて隠密を消す
;			CALL 野外オナニー途中終了(表示候補:LOCAL)
;			CFLAG:(表示候補:LOCAL):隠密 = 0
;			;先に同室フラグを建てることで発覚口上をスキップする
;			CFLAG:(表示候補:LOCAL):同室 = 1
;			SIF RFLAG:コマンド結果受渡し変数 < 1
;				RFLAG:コマンド結果受渡し変数 = 表示候補:LOCAL
;			呼び出し人数 ++
;		NEXT
;		SIF 呼び出し人数 > 1
;			RFLAG:コマンド結果受渡し変数２ = 1
;		;モード管理フラグを初期化
;		FLAG:モード管理 = 0
;		実行結果 = 0
	CASEELSE
		対象キャラ = 表示候補:入力内容
		IF MATCH(呼び出し対象, 対象キャラ)
			FOR LOCAL, 0, VARSIZE("呼び出し対象")
				IF 呼び出し対象:LOCAL == 対象キャラ
					呼び出し対象:LOCAL = 0
					呼び出し人数 --
					BREAK
				ENDIF
			NEXT
		ELSE
			FOR LOCAL, 0, VARSIZE("呼び出し対象")
				IF 呼び出し対象:LOCAL < 1
					呼び出し対象:LOCAL = 対象キャラ
					呼び出し人数 ++
					BREAK
				ENDIF
			NEXT
		ENDIF
		リスタートフラグ = 1
ENDSELECT
CLEARLINE LINECOUNT - 描画開始行
SIF リスタートフラグ == 1
	RESTART
REDRAW REDRAW_TEMP
RETURN 実行結果

;-------------------------------------------------
;呼び出す候補キャラ登録
;-------------------------------------------------
@呼び出す候補キャラ登録(表示候補)
#DIM REF 表示候補
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;滞在してないキャラは出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;セフレでも恋慕でもないキャラは出さない
	SIF !TALENT:LOCAL:恋慕 && !TALENT:LOCAL:セフレ
		CONTINUE
	;睡眠中はアウト
	SIF CFLAG:LOCAL:睡眠
		CONTINUE
	;既に同室者はアウト
	SIF MATCH(TARGET, LOCAL)
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

RETURN LOCAL:1

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
@CAN_COM210(ARG)
#DIM CONST 必要実行値 = 40
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 他のキャラを呼び出す(%CALLNAME:TARGET%)

;体力限界時はオート許可
SIF CFLAG:体力限界
	RETURN 1

実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 5
	PRINT 恥じらい(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 7
	PRINT セフレ(7)
	＋表示フラグ = 1
ENDIF
;ニーア
IF TARGET == 64
	PRINT  - 
	実行値 -= 999
	PRINT ニーア(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(40以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE210
;呼び出す
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;複数人プレイに入るためのコマンドのため、一括管理はせずここで全部記述する
SIF TFLAG:調教モード != 2
	RETURN 0
SIF TARGET:1 <= 0 || CFLAG:睡眠 == 1 || CFLAG:隠密
	RETURN 0
;隠密プレイ時は除外
SIF CFLAG:PLAYER:隠密 || CFLAG:TARGET:隠密
	RETURN 0
;相手が恋慕、あるいはセフレでないときアウト
SIF !TALENT:恋慕 && !TALENT:セフレ
	RETURN 0
;指示モード時はだめ
SIF PLAYER != MASTER
	RETURN 0
;寝取らせ夜這い中はダメ
SIF TCVAR:言いなり
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM210

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(RFLAG:コマンド結果受渡し変数)%
SIF RFLAG:コマンド結果受渡し変数２
	PRINT たち
PRINTL を呼び出した

