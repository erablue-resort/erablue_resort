;-------------------------------------------------
;上半身タイツ脱衣
;------------------------------------------------
@COMNAME202
#FUNCTIONS
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 破く
ELSE
	LOCALS = 脱がす
ENDIF

IF TEQUIP:上下一体タイツ
	TSTR:コマンド名受渡 = タイツを%LOCALS%
ELSE
	TSTR:コマンド名受渡 = インナーを%LOCALS%
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_202
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_202
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS:1 = 破く
	LOCALS:2 = 破く
ELSE
	LOCALS:1 = 脱がす
	LOCALS:2 = 脱がせる
ENDIF
LOCALS = <br>■インナーを%LOCALS:1%<br>
LOCALS += @"相手の上半身のインナーを%LOCALS:2%コマンド。<br>上下一体タイツの場合、[タイツを%LOCALS:1%]に合流する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM202
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
		TEQUIP:上タイツ破れ = 1
		SIF TEQUIP:上下一体タイツ
			TEQUIP:下タイツ破れ = 1
	ELSE
		TEQUIP:上タイツあり = 0
		TEQUIP:上タイツ破れ = 0
		;上下一体の場合ここで下も脱がす
		IF TEQUIP:上下一体タイツ
			TEQUIP:下タイツあり = 0
			TEQUIP:下タイツ破れ = 0
			TEQUIP:上下一体タイツ = 0
		ENDIF
	ENDIF
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM202(ARG)
#DIM CONST 必要実行値 = 18
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 破き
ELSE
	LOCALS = 脱衣
ENDIF
IF TEQUIP:上下一体タイツ
	PRINTFORML タイツ%LOCALS%(%CALLNAME:TARGET%)
ELSE
	PRINTFORML インナー%LOCALS%(%CALLNAME:TARGET%)
ENDIF
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 10
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 10})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 5
		PRINT 野外露出(5)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;特殊言いなり
IF TCVAR:特殊言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	IF TCVAR:特殊言いなり == 1
		PRINT 『シンデレラ』(999)
	ELSEIF TCVAR:特殊言いなり == 2
		PRINT 「服従させる魔法」(999)
	ENDIF
	＋表示フラグ = 1
ENDIF

;合計を表示(18以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE202
#DIM 下半身側脱衣フラグ, 1

;上半身タイツ脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(202)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上タイツあり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0

下半身側脱衣フラグ = 0
IF TEQUIP:上下一体タイツ
	TFLAG:コマンド条件制御用 = 1
	CALL COM_ABLE205
	TFLAG:コマンド条件制御用 = 0
	下半身側脱衣フラグ = RESULT
ENDIF

;破く判定
IF TEQUIP:上半身服あり
	SIF (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可 && !TEQUIP:パイズリ可 && !下半身側脱衣フラグ) || TEQUIP:上タイツ破れ
		RETURN 0
;脱がせる判定
ELSE
	;指を何かに使ってるなら無理…と思ったけど腕部分ありなしどちらもあるかもなのでスルーに
	;SIF TEQUIP:TARGET:指２
	;	RETURN 0
	;胸をニプルキャップ以外で使ってると駄目
	SIF TEQUIP:TARGET:胸 && !MODE_存在判定_ターゲット側("ニプルキャップ", TARGET)
		RETURN 0
	;上下一体だと下半身も見る
	IF TEQUIP:上下一体タイツ
		SIF !下半身側脱衣フラグ
			RETURN 0
	ELSE
		;口を何かに使ってるなら無理
		SIF TEQUIP:TARGET:口
			RETURN 0
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM202
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 破いた
	LOCALS:1 = 破かれた
ELSE
	LOCALS = 脱がせた
	LOCALS:1 = 脱いだ
ENDIF
PRINTFORM %CALLNAME:PLAYER%は
IF TEQUIP:上下一体タイツ
	PRINTFORML %CALLNAME:TARGET%のタイツを%LOCALS%…
ELSE
	PRINTFORML %CALLNAME:TARGET%のインナーを%LOCALS%…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 人目のあるところで%LOCALS:1%ことで、%CALLNAME:TARGET%はどこか落ち着かないようだ…
ENDIF


