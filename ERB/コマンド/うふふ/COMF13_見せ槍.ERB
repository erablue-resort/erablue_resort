;-------------------------------------------------
;見せ槍
;-------------------------------------------------
@COMNAME13
#FUNCTIONS
TSTR:コマンド名受渡 = 見せ槍

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_13
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
オートコマンド許可 = 1

@COM_TOOLTIP_13
LOCALS = <br>■見せ槍<br>
LOCALS += "相手に陰茎を見せつけて、性欲を煽るコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：欲情・屈従・征服"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM13
#DIM 比較値
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;無知の場合、何も起こらない
IF 知識素質:TARGET:性知識 == -2
	RETURN 1
ENDIF

;基礎
CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
CALL SOURCE_CALC_屈従(TARGET, PLAYER, 60)

IF GETBIT(TALENT:TARGET:性別, 1)
	比較値 = TALENT:PLAYER:男性器サイズ - TALENT:TARGET:男性器サイズ
	;-2:こどもちんこ、-1:短小、0:平凡、1:巨根、2:規格外
	IF 比較値 > 0
		CALL SOURCE_CALC_屈従(TARGET, PLAYER, 100)
	ELSEIF 比較値 < 0
		CALL SOURCE_CALC_征服(TARGET, PLAYER, 100)
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 50)
	ELSE
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
	ENDIF
ELSE
	比較値 = TALENT:PLAYER:男性器サイズ - MAX(TALENT:TARGET:体格, -2)
	;-3:ハーヴィン並、-2:子供並、-1:小柄、0:普通、1:長身、2:男ドラフ並
	IF 比較値 > 0
		CALL SOURCE_CALC_屈従(TARGET, PLAYER, 100)
	ELSEIF 比較値 < 0
		CALL SOURCE_CALC_征服(TARGET, PLAYER, 100)
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 50)
	ELSE
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE13
;見せ槍
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(13)
	RETURN RESULT
;ちんこがない場合駄目
SIF GETBIT(TALENT:PLAYER:性別, 1) == 0 || TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;ちんこを何かに使ってるなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
;勃起必須
SIF BASE:PLAYER:勃起 < 1000
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM13
#DIM 発情素養

発情素養 = 0

SIF TALENT:性欲 != -2 && TALENT:自制心 != 2 && TALENT:無関心 != 1 && TALENT:Ｃ感度 != -2 && TALENT:Ｖ感度 != -2
	発情素養 = 1

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の顔に勃起した
SELECTCASE TALENT:PLAYER:男性器サイズ
	CASE -2
		PRINT 可愛らしい、子どものような
	CASE -1
		PRINT 小さめの
	CASE 0
	CASE 1
		PRINT 人並み以上の
	CASE 2
		PRINT 規格外に巨大な
ENDSELECT

PRINTL 陰茎を近づけ、見せつけた……


IF (TALENT:PLAYER:男性器サイズ >= 1 && TALENT:汚臭耐性 < 0 && 発情素養 == 1) || (TALENT:PLAYER:男性器サイズ >= 1 && TALENT:性欲 > 0)
;なんとなくデカチンポ前提にしてみた。ZP素質やらフィールドやらで制御しようってなった場合はここらへん弄るとよき。その場合は地の文もちょっと弄って
	IF 知識素質:性知識 < 0 && TALENT:性別 == 1 && CFLAG:TARGET:発情期フラグ >= 0 && RAND:5 > 0
		PRINTL 
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%が何をしているのかわからず、きょとんとした顔をしているが、
		PRINTFORML その目は不思議と%CALLNAME:PLAYER%のペニスに惹きつけられ、蕩けた顔でにおいを嗅いではもじもじと太腿を擦り合わせている……
		PRINTW
		PRINTFORML %CALLNAME:PLAYER%のペニスの匂いと熱を間近に感じたことで、
		PRINTFORM %CALLNAME:TARGET%はメスの本能が呼び覚まされ、
		SETCOLOR カラーパレット("えっちな色")
		PRINT [発情]
		RESETCOLOR
		PRINTFORML してしまったようだ……
		TCVAR:強制発情フラグ = 1
		CFLAG:TARGET:発情期フラグ = -1
	ELSEIF CFLAG:TARGET:発情期フラグ >= 0
		PRINTFORM 肉棒の猛りを眼前に突き付けられた%CALLNAME:TARGET%は
		IF 陥落チェック(TARGET) == 0
			IF TALENT:プライド > 1 || TALENT:ツンデレ
				PRINTFORML そのプライドからか必死に目を逸らそうとしている様子だったが、
				PRINTFORML チラチラと横目で盗み見ては無意識にもじもじと太腿を擦り合わせている……
			ELSEIF 固有素質チェック(TARGET, "メスガキ") || (TALENT:応答 == 1 && TALENT:TARGET:年齢層 == 10) || (TALENT:応答 == 1 && BASE:TARGET:年齢 <= 14 && BASE:TARGET:年齢 != 0) || TALENT:態度 == 1
			;メスガキ、生意気な子供、反抗的
				PRINTFORML 途端に息を呑み、生意気ばかり言う口をつぐんで喉を鳴らし、
				PRINTFORML 怯えるような、あるいは媚びへつらうような表情でペニスを見つめてはもじもじと太腿を擦り合わせている……
			ELSEIF TALENT:度胸 == 1 || TALENT:応答 == -1
			;大人しい、臆病
				PRINTFORML 怯えるように息を呑み、
				PRINTFORML 肩を竦ませながらも媚びへつらうような表情でペニスを見つめてはもじもじと太腿を擦り合わせている……
			ELSE
				PRINTFORML その威容にごくりと喉を鳴らし、もじもじと太腿を擦り合わせている……
			ENDIF
		ELSEIF TALENT:経験豊富 || TALENT:淫乱
			PRINTFORML その威容に惚れ惚れと息をつき、
			PRINTFORML 期待と熱の籠った熱視線を浴びせながらもじもじと太腿を擦り合わせている……
		ELSE
			PRINTFORML その威容にごくりと喉を鳴らし、もじもじと太腿を擦り合わせている……
		ENDIF
		PRINTW
		PRINTFORML %CALLNAME:PLAYER%のペニスの匂いと熱を間近に感じたことで、
		PRINTFORM %CALLNAME:TARGET%はメスの本能が呼び覚まされ、
		SETCOLOR カラーパレット("えっちな色")
		PRINT [発情]
		RESETCOLOR
		PRINTFORML してしまったようだ……
		TCVAR:強制発情フラグ = 1
		CFLAG:TARGET:発情期フラグ = -1
	ELSE
	;発情してる
		PRINTFORML すっかり発情した%CALLNAME:TARGET%は頬を上気させ、%TEXTR("熱の籠った/湿った")%吐息を荒くつきながら
		PRINTFORML %CALLNAME:PLAYER%のペニス%TEXTR("を食い入るように見つめて/から目を離せないで")%いる……。
	ENDIF
ELSEIF 知識素質:性知識 < 0 
	PRINTL 
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%が何をしているのかわからず、きょとんとした顔をしている
ENDIF
