;-------------------------------------------------
;触手挿入
;触手系コマンド、レベル2
;-------------------------------------------------
@COMNAME103
#FUNCTIONS
TSTR:コマンド名受渡 = 触手挿入
IF MODE_確認_完全一致("触手挿入", PLAYER, TARGET)
	TSTR:コマンド名受渡 = 触手挿入解除
ENDIF


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_103
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = V
同時モード許可 = 999


@COM_TOOLTIP_103
LOCALS = <br>■触手挿入<br>
LOCALS += "触手を膣に挿入するコマンド。<br>Ｖ挿入系モードとなり、モードを変更するか性行為を終了するまで持続的に快楽が発生する。<br>[処女]を失う。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｖ・接触・露出・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)・Ｖ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM103
#DIM 快楽基礎値

IF MODE_確認_完全一致("触手挿入", PLAYER, TARGET) > -1
	CALL MODE_DELETE_ターゲット側("触手挿入", TARGET)
ELSE
	CALL MODE_CHANGE("触手挿入", PLAYER, TARGET)
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 快楽基礎値 * 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 30)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 快楽基礎値 * 40)

;苦痛
LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF
;処女喪失
CALL LOST_VIRGIN(TARGET, PLAYER, 4)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:触手操作経験 ++
RETURN 1


@CAN_COM103(ARG)
#DIM 実行値
#DIM ＋表示フラグ

;あなた相手なら許可
SIF TARGET == MASTER
	RETURN 1

SKIPDISP ARG
PRINTFORML 触手挿入(%CALLNAME:TARGET%)

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0
;すべての命令に共通の要素を考慮

;モード実行中なら問答無用で許可
SIF MODE_確認_完全一致("触手挿入", PLAYER, TARGET) > -1
	RETURN 1

CALL COM_ORDER
実行値 = RESULT
	＋表示フラグ = S

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 3
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	＋表示フラグ = 1
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:Ｖ感覚 * 2
	PRINT Ｖ感覚
	PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
	＋表示フラグ = 1
ENDIF
;快楽刻印
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 3
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	＋表示フラグ = 1
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	PRINT  - 
	実行値 -= 5
	PRINT 潤滑不足
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情,5) * 3
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 2
	PRINT 羞恥心
	PRINTV '(,2,')
	＋表示フラグ = 1
ENDIF
;快感の否定
IF TALENT:快感応答 < 0 && !TALENT:恋慕
	PRINT  - 
	実行値 -= 5
	PRINT 快感の否定
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	実行値 -= 12
	PRINT 男嫌い
	PRINTV '(,12,')
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 恋慕
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF
;処女
LOCAL = MAX(10,40 - EXP:Ｖ経験)
IF TALENT:処女
	PRINT  - 
	実行値 -= LOCAL
	PRINT 処女
	PRINTV '(,LOCAL,')
	＋表示フラグ = 1
;V経験
ELSEIF EXP:Ｖ経験 < EXPLV:2
	PRINT  - 
	実行値 -= 15
	PRINT Ｖ経験
	PRINT 不足
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF
;触手経験
IF EXP:触手快楽経験 + EXP:触手絶頂経験 < EXPLV:2
	PRINT  - 
	実行値 -= 15
	PRINT 触手経験
	PRINT 不足
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 媚薬
	PRINTV '(,6,')
	＋表示フラグ = 1
ENDIF

;発情期
IF CFLAG:発情期フラグ < 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 発情中
	PRINTV '(,6,')
	＋表示フラグ = 1
ENDIF

;合計を表示(35以上で実行)
PRINT  = 
PRINTV 実行値

SIF 実行値 < 35
	PRINT  < 
SIF 実行値 == 35
	PRINT  = 
SIF 実行値 > 35
	PRINT  > 
PRINT 実行値
PRINTV 35

WAIT

;実行できない
SIF 実行値 < 35
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE103
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(103)
	RETURN RESULT
;すでにあるとだめ
SIF MODE_確認_ターゲット側("触手挿入", TARGET) > -1
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;Vがない場合駄目
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;前張り不可
SIF TEQUIP:下半身下着あり == 2
	RETURN 0
;処女かつ潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:3 && TALENT:処女
	RETURN 0
;処女でなくとも潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0
;膣を何かに使ってるならダメ
SIF TEQUIP:TARGET:膣 == 1
	RETURN 0
;操作術レベル不足
SIF あなた特殊能力:触手変化術 < 2
	RETURN 0
;将来的には許可する予定だけど（ユリウスとか）一旦あなた以外は封印
SIF PLAYER != MASTER
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM103
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は

IF FLAG:モード管理 == 2
	PRINT 幾つもの触手を
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT の膣へと挿入した
	RETURN 0
ENDIF

PRINTFORM 自らの触手を%CALLNAME:TARGET%の
IF TEQUIP:下腹部前鎧あり || TEQUIP:下腹部後鎧あり
	PRINT 鎧の隙間から
	IF TEQUIP:下タイツあり
		PRINT タイツの下に
	ENDIF
	PRINT 潜り込ませ、
ELSEIF TEQUIP:下半身服あり
	PRINT 服の隙間から
	IF TEQUIP:下タイツあり
		PRINT タイツの下に
	ENDIF
	PRINT 潜り込ませ、
ELSEIF TEQUIP:下半身下着あり
	IF TEQUIP:下タイツあり
		PRINT タイツの下から
	ENDIF
	PRINT 下着に潜り込ませ、
ENDIF
IF PALAM:欲情 >= PALAMLV:3 && PALAM:潤滑 < PALAMLV:3
	PRINT 興奮で濡れそぼった
ENDIF
PRINTFORML 膣へと挿入した

