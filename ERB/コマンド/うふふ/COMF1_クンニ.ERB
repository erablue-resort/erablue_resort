;-------------------------------------------------
;クンニ
;愛撫系コマンド　レベル1
;-------------------------------------------------
@COMNAME1
#FUNCTIONS
IF MODE_存在判定_完全一致("フェラチオ系", TARGET, PLAYER)
	TSTR:コマンド名受渡 = シックスナイン
ELSE
	TSTR:コマンド名受渡 = クンニ
ENDIF




;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_女
オートコマンド許可 = 1
連続オート許可 = 1


@COM_TOOLTIP_1
LOCALS = <br>■クンニ<br>
LOCALS += "相手のクリトリスを中心に舌で愛撫を行うコマンド。<br>フェラチオ系のモード継続時、「シックスナイン」に変化する。<br>実行者が[舌使い][猫舌]を持っている場合、効果が増減する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｃ・露出・接触・液体"
LOCALS += "<br>固有の取得経験：口淫経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM1
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
IF MODE_存在判定_完全一致("フェラチオ系", TARGET, PLAYER)
	CALL MODE_CHANGE("シックスナイン", TARGET, PLAYER)
	RFLAG:同時モード体位変化フラグ = 1
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

LOCAL = 150
;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 1.50
;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES LOCAL , 0.75
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, LOCAL)

CALL SOURCE_CALC_液体(TARGET, PLAYER, 60)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 80)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 3
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 3


;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
;口淫経験
EXP:PLAYER:口淫経験 ++

IF CFLAG:PLAYER:ファーストキスフラグ == 0
	CFLAG:PLAYER:ファーストキスフラグ = 10
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%の[クリトリス]にファーストキスを捧げた</font><img src='えっちハート'>","うふふ")
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE1
;クンニ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(1)
	RETURN RESULT
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;女性器がないとダメ
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;下半身着衣
;前鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0
IF MODE_存在判定_完全一致("フェラチオ系", TARGET, PLAYER)
ELSE
	;口を何かに使ってる・届かないなら無理
	SIF TEQUIP:PLAYER:口
		RETURN 0
	;クリを何かに使ってる・届かないなら無理
	SIF TEQUIP:TARGET:クリトリス
		RETURN 0
ENDIF
;挿入系時は個別にOFF
SIF MODE_存在判定_完全一致("Ｖ挿入系", PLAYER, TARGET) || MODE_存在判定_完全一致("Ａ挿入系", PLAYER, TARGET)
	RETURN 0
SIF MODE_存在判定_完全一致("Ｖ挿入系", TARGET, PLAYER) || MODE_存在判定_完全一致("Ａ挿入系", TARGET, PLAYER)
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF CFLAG:PLAYER:ファーストキスフラグ == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM1
PRINTFORM %CALLNAME:PLAYER%は

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML %CALLNAME:TARGET%と頭の位置が互い違いになるように体勢を変えて、
	PRINTFORM %CALLNAME:TARGET%の
ELSEIF MODE_存在判定_順不同("シックスナイン", TARGET, PLAYER)
	PRINTFORM 眼の前にある%CALLNAME:TARGET%の
ELSEIF !TEQUIP:下半身服あり && TEQUIP:下半身下着あり
	IF TALENT:恋慕 && PLAYER == MASTER
		PRINTFORM 自ら下着をずらして準備している
	ELSE
		PRINTFORM 下着をずらして露出させた
	ENDIF
	PRINTFORM %CALLNAME:TARGET%の
ELSEIF TEQUIP:ずらし可
	IF TALENT:恋慕 && PLAYER == MASTER
		PRINTFORM 自ら服をずらして準備している
	ELSE
		PRINTFORM 服をずらして露出させた
	ENDIF
ELSEIF TEQUIP:スカートめくり可
	IF TALENT:恋慕 && PLAYER == MASTER
		PRINTFORM 自ら服を捲り上げて準備している
	ELSE
		PRINTFORM 服を捲り上げて露出させた
	ENDIF
	PRINTFORM %CALLNAME:TARGET%の
ELSE
	SIF TALENT:恋慕 && PLAYER == MASTER
		PRINT 足を開いて準備している
	PRINTFORM %CALLNAME:TARGET%の
ENDIF
PRINTFORM 秘唇を丁寧に舐めほぐし
;ふたなり＋欲情のPLAMLV3以上
IF GETBIT(TALENT:性別, 1) && PALAM:欲情 >= PALAMLV:3
	PRINT 、そそり勃ったペニスに舌を這わせて舐め上げ
;ふたなり
ELSEIF GETBIT(TALENT:性別, 1)
	PRINT 、ペニスの付け根を舌で舐め上げ
;欲情のPALAMLV3以上
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINT 、尖りきったクリトリスを舌で吸い上げ
ENDIF
PRINTL た…

