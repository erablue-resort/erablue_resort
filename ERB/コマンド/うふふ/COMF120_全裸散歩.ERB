;-------------------------------------------------
;全裸散歩
;-------------------------------------------------
@COMNAME120
#FUNCTIONS
TSTR:コマンド名受渡 = 全裸散歩
SIF MODE_存在判定_ターゲット側("全裸散歩", TARGET)
	TSTR:コマンド名受渡 = 散歩をやめて戻る



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_120
#FUNCTION
IF MODE_存在判定_ターゲット側("全裸散歩", TARGET)
	TFLAG:コマンドタイプ受渡 = COMTYPE("全裸散歩")
ELSE
	TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
	;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
	;弱点コマンドカテゴリ = S
	同時モード許可 = 999
ENDIF


@COM_TOOLTIP_120
LOCALS = <br>■全裸散歩<br>
LOCALS += "キャラを全裸のまま、辺りを歩かせるコマンド。<br>モード中にユニークキャラと同じ場所に移動すると中断してもとの場所に戻る。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：露出"
LOCALS += "<br>固有の取得経験：露出快楽経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM120

IF MODE_存在判定_ターゲット側("全裸散歩", TARGET)
	;解除時
	CALL 全裸散歩終了処理()
	RETURN 1
ELSE
	TFLAG:全裸散歩場所記憶 = CFLAG:PLAYER:現在位置
	TFLAG:全裸散歩マップ種別記憶 = CFLAG:PLAYER:現在マップ種別
	CFLAG:TARGET:うふふ = 0
	CFLAG:PLAYER:うふふ = 0
	
	SIF !MODE_存在判定_プレイヤー側("全裸散歩", PLAYER)
		CALL 特定キャラモードリセット(PLAYER)

	;開始時
	CALL MODE_CHANGE("全裸散歩", PLAYER, TARGET)
	CALL CLOTHES_CHANGE(TARGET, "全裸")
	TCVAR:TARGET:自動着替え禁止 = 1
	;-------------------------------------------------
	;ソース追加処理
	;-------------------------------------------------
	IF TALENT:TARGET:毛皮 < 1
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 300)
		RCVAR:露出コマンドフラグ = 1
	ENDIF
	RFLAG:全裸散歩フラグ = 1
	EXP:TARGET:露出快楽経験 ++
ENDIF
RETURN 1


@全裸散歩終了処理()
CFLAG:PLAYER:現在位置 = TFLAG:全裸散歩場所記憶
CFLAG:PLAYER:現在マップ種別 = TFLAG:全裸散歩マップ種別記憶
FOR LOCAL, 0, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	IF MODE_存在判定_ターゲット側("全裸散歩", TARGET:LOCAL)
		CALL うふふ開始キャラ処理(TARGET:LOCAL, CSTR:(TARGET:LOCAL):うふふシチュ記録)
		CFLAG:(TARGET:LOCAL):現在位置 = TFLAG:全裸散歩場所記憶
		CFLAG:(TARGET:LOCAL):現在マップ種別 = TFLAG:全裸散歩マップ種別記憶
		CALL MODE_DELETE_ターゲット側("全裸散歩", TARGET:LOCAL)
		TCVAR:(TARGET:LOCAL):自動着替え禁止 = 0
	ENDIF
NEXT
CFLAG:PLAYER:うふふ = 1
TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)



@CAN_COM120(ARG)
#DIM CONST 必要実行値 = 50
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1
SIF MODE_存在判定_ターゲット側("全裸散歩", TARGET)
	RETURN 1

SKIPDISP ARG
PRINTFORML 全裸散歩(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 10
	PRINT 恥じらい(10)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF
;性知識不足
IF 知識素質:TARGET:性知識 > -1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 性知識不足(15)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;特殊言いなり
IF TCVAR:特殊言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINTFORM %特殊言いなり分岐(TARGET)%(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(50以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE120
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(120)
	RETURN RESULT
;将来的には許可する予定だけど一旦あなた以外は封印
SIF PLAYER != MASTER
	RETURN 0
;分身のみはダメ（バグるので
IF GETBIT(FLAG:モード管理, モードビット_同時)
	LOCAL:1 = 0
	FOR LOCAL, 0, 同時モード_選択数
		SIF 同時モード_選択キャラ:LOCAL < 0
			BREAK
		SIF TALENT:(同時モード_選択キャラ:LOCAL):分身 == 0
			LOCAL:1 = 1
	NEXT
	SIF LOCAL:1 == 0
		RETURN 0
ELSE
	SIF TALENT:TARGET:分身
		RETURN 0
ENDIF
;誰かを置いていくのもダメ
IF !MODE_存在判定_ターゲット側("全裸散歩", TARGET)
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		SIF GET_TARGETNUM() > 同時モード_選択数
			RETURN 0
	ELSE
		SIF GET_TARGETNUM() > 1
			RETURN 0
	ENDIF
ENDIF
;イベントうふふ中はダメ
SIF FLAG:イベントうふふ中フラグ
	RETURN 0
;泥酔時禁止
SIF TCVAR:TARGET:泥酔眠姦
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM120
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
IF CFLAG:PLAYER:うふふ
	PRINTFORML 散歩をやめ、元の場所へと戻った…
ELSE
	PRINTFORML 全裸の%CALLNAME:TARGET%を連れ、辺りを散歩することにした…
ENDIF


