
@COMNAME357
#FUNCTIONS

TSTR:コマンド名受渡 = ハーヴィンオナホ
SIF MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, TARGET) > -1
	TSTR:コマンド名受渡 = ハーヴィンオナホ解除
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_357
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_357
LOCALS = <br>■ハーヴィンオナホ<br>
LOCALS += "すでに合意を得ている、かつ体格がハーヴィン並の相手を<br>V、あるいはAに挿入したまま服の下に固定するコマンド。<br>同室者が全員陥落済みの時のみ可能だが、一緒に隠密時は同じ場所に非陥落キャラがいても可能。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM357

IF MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, TARGET) > -1
	;解除時
	CALL CLOTHES_CHANGE(TARGET, CSTR:TARGET:脱ぐ前の服)
	IF MODE_確認_完全一致("ハーヴィンオナホＶ", PLAYER, TARGET) > -1
		RFLAG:コマンド結果受渡し変数 = 1
	ELSE
		RFLAG:コマンド結果受渡し変数 = 2
	ENDIF
	TCVAR:TARGET:自動着替え禁止 = 0
	CALL MODE_DELETE_ターゲット側("ハーヴィンオナホ系", TARGET)
	RETURN 1
ENDIF

DRAWLINE
PRINTL ■挿入先を選んでください
PRINTL
LOCAL = 0
IF GETBIT(TALENT:TARGET:性別, 0) == 1 && TALENT:PLAYER:Ｖ感度 != -2
	IF TEQUIP:TARGET:膣 == 0
		PRINTBUTTONLC "[1]Ｖ挿入", 1
		LOCAL += 1
	ENDIF
ENDIF
IF TALENT:TARGET:Ａ感度 != -2
	IF TEQUIP:TARGET:アナル == 0
		PRINTBUTTONLC "[2]Ａ挿入", 2
		LOCAL += 1
	ENDIF
ENDIF
SIF LOCAL == 0
	PRINT 挿入先がありません。
PRINTL
PRINTL
PRINTBUTTONLC "[999]戻る", 999

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1 TO 2
		TRYCALLFORM CAN_オナホ(1, LOCAL)
		IF !RESULT
			TRYCALLFORM CAN_オナホ(0, LOCAL)
			RESTART
		ENDIF
	CASE 999
		RETURN -1
ENDSELECT

RFLAG:コマンド結果受渡し変数 = LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF LOCAL == 1
	CALL MODE_CHANGE("ハーヴィンオナホＶ", PLAYER, TARGET)
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 400)
	;処女喪失
	CALL LOST_VIRGIN(TARGET, PLAYER, 1)
	;苦痛
	LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
ELSE
	CALL MODE_CHANGE("ハーヴィンオナホＡ", PLAYER, TARGET)
	CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 400)
	;苦痛
	LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
ENDIF
CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 250)
RCVAR:TARGET:苦痛コマンドフラグ = 1

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 400)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

CALL CLOTHES_CHANGE(TARGET, "全裸")
TCVAR:TARGET:自動着替え禁止 = 1
TIME += 5
RETURN 1


@CAN_オナホ(ARG, 部位フラグ)
#DIM 実行値
#DIM ＋表示フラグ
#DIM 部位フラグ

SKIPDISP ARG
PRINTFORML ハーヴィンオナホ(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = S

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 3
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	＋表示フラグ = 1
ENDIF
IF 部位フラグ == 1
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += ABL:Ｖ感覚 * 2
		PRINT Ｖ感覚
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		＋表示フラグ = 1
	ENDIF
ELSE
	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		SIF ＋表示フラグ
			PRINT  + 
		実行値 += ABL:Ａ感覚 * 2
		PRINT Ａ感覚
		PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
		＋表示フラグ = 1
	ENDIF
ENDIF
;快楽刻印
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 3
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	＋表示フラグ = 1
ENDIF
;PALAM:潤滑（の不足）
IF RFLAG:指示モード対象あなたフラグ
	LOCAL = PALAM:MASTER:潤滑
ELSE
	LOCAL = PALAM:潤滑
ENDIF
IF LOCAL < PALAMLV:3
	PRINT  - 
	実行値 -= 5
	PRINT 潤滑不足
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情,5) * 3
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 2
	PRINT 羞恥心
	PRINTV '(,2,')
	＋表示フラグ = 1
ENDIF
;快感の否定
IF TALENT:快感応答 < 0 && !TALENT:恋慕
	PRINT  - 
	実行値 -= 5
	PRINT 快感の否定
	PRINTV '(,5,')
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	実行値 -= 12
	PRINT 男嫌い
	PRINTV '(,12,')
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 恋慕
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT セフレ
	PRINTV '(,10,')
	＋表示フラグ = 1
ENDIF
;未陥落
IF TALENT:セフレ == 0 && TALENT:恋慕 == 0
	SIF ＋表示フラグ
		PRINT  - 
	実行値 -= 10
	PRINT 未陥落
	PRINTV '(,-10,')
	＋表示フラグ = 1
ENDIF
IF 部位フラグ == 1
	;処女
	LOCAL = MAX(10,40 - EXP:Ｖ経験)
	IF TALENT:処女
		PRINT  - 
		実行値 -= LOCAL
		PRINT 処女
		PRINTV '(,LOCAL,')
		＋表示フラグ = 1
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		実行値 -= 15
		PRINT Ｖ経験
		PRINT 不足
		PRINTV '(,15,')
		＋表示フラグ = 1
	ENDIF
ELSE
	;A経験
	IF EXP:Ａ経験 < EXPLV:2
		PRINT  - 
		実行値 -= 15
		PRINT Ａ経験
		PRINT 不足
		PRINTV '(,15,')
		＋表示フラグ = 1
	ENDIF
ENDIF

;マゾ性感
IF ABL:マゾ性感 < 3
	PRINT  - 
	実行値 -= 15
	PRINT マゾ性感
	PRINT 不足
	PRINTV '(,15,')
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 4
	PRINT ふたなり
	PRINTV '(,4,')
	＋表示フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 媚薬
	PRINTV '(,6,')
	＋表示フラグ = 1
ENDIF

;発情期
IF CFLAG:発情期フラグ < 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 発情中
	PRINTV '(,6,')
	＋表示フラグ = 1
ENDIF

;合計を表示(45以上で実行)
PRINT  = 
PRINTV 実行値

SIF 実行値 < 45
	PRINT  < 
SIF 実行値 == 45
	PRINT  = 
SIF 実行値 > 45
	PRINT  > 
PRINT 実行値
PRINTV 45

WAIT

;実行できない
SIF 実行値 < 45
	RETURN 0
	
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE357
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0

IF MODE_確認_完全一致("ハーヴィンオナホＶ", PLAYER, TARGET) > -1 || MODE_確認_完全一致("ハーヴィンオナホＡ", PLAYER, TARGET) > -1
	;解除時は同室者影響
	IF !CFLAG:MASTER:隠密 || !CFLAG:TARGET:隠密
		;非隠密時は全員陥落済みでないとだめ
		FOR LOCAL,1,CHARANUM
			SIF TARGET:LOCAL < 1
				BREAK
			SIF 陥落チェック(TARGET:LOCAL) == 0
				RETURN 0
		NEXT
	ENDIF
	RETURN 1
ELSE
	;一括管理
	SIF GLOBAL_COMABLE(357)
		RETURN RESULT
	;あなたにちんこあること
	SIF GETBIT(TALENT:PLAYER:性別, 1) == 0 || TALENT:PLAYER:Ｃ感度 == -2
		RETURN 0
	;体格がハーヴィン並のみかつあなたがそれなりにデカいこと
	;男女は……まあいいか
	SIF TALENT:TARGET:体格 != 体格_ハーヴィン並
		RETURN 0
	SIF TALENT:PLAYER:体格 < 体格_普通
		RETURN 0

	SIF TEQUIP:PLAYER:ペニス
		RETURN 0

	;ほかは基本的に押し倒すと判定は同一
	CALL COM_ABLE350()
	RETURN RESULT
ENDIF
;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM357

IF MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, TARGET) > -1
	;実行時
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%を全裸に剥き、抱えるようにして
	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE 1
			PRINTL 秘裂に挿入した……
		CASE 2
			PRINTL 肛門に挿入した……
	ENDSELECT
	PRINTFORML そのまま%CALLNAME:TARGET%を密着させるように体に固定し、服の下に隠した。
ELSE
	;解除時
	PRINTFORML %CALLNAME:PLAYER%は服を脱ぎ、固定していた%CALLNAME:TARGET%を解放した。
ENDIF
