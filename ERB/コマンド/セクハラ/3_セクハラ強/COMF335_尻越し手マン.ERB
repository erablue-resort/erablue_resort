;===================================================
;尻越し手マン
;===================================================
@COMNAME335
#FUNCTIONS
TSTR:コマンド名受渡 = 尻越し手マン


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_335
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
弱点コマンドカテゴリ = V


;===================================================
;ツールチップ文章
;===================================================
@COM_TOOLTIP_335
TSTR:ツールチップ受渡 '= "<br>■尻越し手マン<br>"
TSTR:ツールチップ受渡 += "相手の背中側から手を回し、尻越しに膣に指を入れて弄りまわすコマンド。<br>"
TSTR:ツールチップ受渡 += "対象が下半身後ろ側に鎧を着ている場合・上下一体服を着ている場合は実行不可。<br>"
TSTR:ツールチップ受渡 += "連れ出し可能時、モード化して同行状態になる。<br>"
TSTR:ツールチップ受渡 += "COMタイプ：セクハラ<br>取得ソース：接触・露出・快Ｖ・苦痛<br>"
TSTR:ツールチップ受渡 += "固有の取得経験：なし<br>"


;===================================================
;コマンド実行処理
;===================================================
@COM335
#LOCALSIZE 1
#DIM 同行数
;尻越し手マン

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ 3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応 3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

;セクハラ強は基本的に喜び以外に入らないはず（合意条件であり、合意に入るにはそれなりに仲良くならなければならないため）

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 2
		;無反応
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 3
		;嫌悪
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
	CASE 4
		;拒否
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
	CASE 5
		;余裕
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 120)
	CASE 6
		;羞恥
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 300)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 350)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
	CASE 7
		;愉悦
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 130)
	CASE 8
		;汎用
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDSELECT
SIF 同性愛性癖判定(PLAYER, TARGET)
	EXP:PLAYER:同性愛経験 += 1
SIF 同性愛性癖判定(TARGET, PLAYER)
	EXP:TARGET:同性愛経験 += 1
;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 50)

;苦痛
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 40 * PAIN_CHECK_V(TARGET) / 1000)

;性的陥落済かつ同行が二人未満なら同行化
IF 陥落チェック_性的(TARGET) && (CFLAG:TARGET:デート == 0 || CFLAG:TARGET:デート == PLAYER + 100)
	同行数 = 0
	FOR LOCAL, 1, VARSIZE("TARGET")
		SIF TARGET:LOCAL < 1
			BREAK
		IF CFLAG:(TARGET:LOCAL):同行 
			同行数 ++
		ENDIF
	NEXT
	IF 同行数 < 2
		CALL MODE_CHANGE("尻越し手マン", PLAYER, TARGET)
		CFLAG:TARGET:同行 = 60 + (CFLAG:好感度レベル - 関係閾値:2) * 5
		RFLAG:コマンド結果受渡し変数３ = 1
	ENDIF
ENDIF

TIME += 10
RETURN 1


;===================================================
;実行可能判定
;===================================================
@COM_ABLE335
;尻越し手マン実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(335) && !RFLAG:罰ゲーム中
	RETURN RESULT
;睡眠中は駄目
SIF CFLAG:睡眠
	RETURN 0
;Vがない場合駄目
SIF TALENT:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:性別, 0) == 0
	RETURN 0
;処女は駄目
SIF TALENT:処女
	RETURN 0
;ある程度濡れてないとダメ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0

;下半身着衣
;後鎧、めくれない上下一体服、上下一体タイツ、前張りがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:上下一体服 && TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:上下一体タイツ && TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

;TARGETの好感度レベル
SIF CFLAG:好感度レベル < 1 + 関係閾値:2
	RETURN 0
RETURN 1


;===================================================
;実行時メッセージ
;===================================================
@MESSAGE_COM335

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の背中側から手を回
IF TEQUIP:下半身服あり
	PRINT して服の中に差し入れると
ELSE
	PRINT すと
ENDIF
PRINTFORM 、\@ TALENT:種族 == 4 ? 小振りな # \@尻越しに
;潤滑LV4以上
SIF PALAM:TARGET:潤滑 >= PALAMLV:4
	PRINT 濡れそぼった
PRINTFORML 秘裂に指を挿入れ、弄りまわした…

IF CUP:TARGET:快Ｖ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は思わず少し声を漏らしてしまった…
	PRINTL
ENDIF

IF RFLAG:コマンド結果受渡し変数３
	PRINTL 
	PRINTFORML %CALLNAME:PLAYER%はそのまま%CALLNAME:TARGET%を連れ歩くことにした
ENDIF

@MESSAGE_COM335_非口上時
;基本喜びだけのはずなので一旦分類せずに直接喜びパターンの文章を書く
SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		PRINTFORML %CALLNAME:TARGET%はよくわかっていないが、%CALLNAME:PLAYER%が楽しいなら良いと嬉しそうにしている
	CASE 2
		;無反応
		PRINTFORML %CALLNAME:TARGET%は平然としているが、少し顔を赤くして%CALLNAME:PLAYER%に体を擦り寄せてきた…
	CASE 3
		;嫌悪
		PRINTFORML %CALLNAME:TARGET%はしょうがない人だな、という顔で%CALLNAME:PLAYER%の好きにさせている…
	CASE 4
		;拒否
		PRINTFORML %CALLNAME:TARGET%は悪い気はしない様子で、%CALLNAME:PLAYER%の好きにさせている…
	CASE 5
		;余裕
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に、周囲にバレないようこっそりと触るように言ってきた…
	CASE 6
		;羞恥
		PRINTFORML %CALLNAME:TARGET%は赤くなった顔で%CALLNAME:PLAYER%を見つめ、そっと体を寄せてきた…
	CASE 7
		;愉悦
		PRINTFORML %CALLNAME:TARGET%は他の場所も触るかと、色っぽい表情で%CALLNAME:PLAYER%を挑発している…
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に触れられることを少し嬉しく思っているようだ…
ENDSELECT
