

@COMNAME324
#FUNCTIONS
TSTR:コマンド名受渡 = 乳首ひっかき

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_324
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
弱点コマンドカテゴリ = B
睡眠中許可 = 1

@COM_TOOLTIP_324
LOCALS = <br>■乳首ひっかき<br>
LOCALS += "服の上から相手の乳首を爪でカリカリと刺激するコマンド。<br>対象が上半身に鎧を着ている場合は実行不可。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：接触・露出・快Ｂ"
LOCALS += "<br>固有の取得経験：乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM324
;乳首ひっかき
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

RCVAR:乳首開発フラグ = 1
;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 150)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)


TIME += 10
SIF CFLAG:TARGET:睡眠
	RETURN 1

SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1


CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ 3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応 3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

;セクハラ強は基本的に喜び以外に入らないはず（合意条件であり、合意に入るにはそれなりに仲良くならなければならないため）

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 2
		;無反応
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 3
		;嫌悪
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
	CASE 4
		;拒否
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
	CASE 5
		;余裕
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 120)
	CASE 6
		;羞恥
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 300)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 350)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
	CASE 7
		;愉悦
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 130)
	CASE 8
		;汎用
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDSELECT
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE324
;乳首ひっかき実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(324) && !RFLAG:罰ゲーム中
	RETURN RESULT
SIF TEQUIP:上半身鎧あり
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM324

PRINTFORM %CALLNAME:PLAYER%は
SIF 体格差計算(PLAYER, TARGET) <= -3 && !CFLAG:睡眠
	PRINT しゃがんで
PRINTFORM %CALLNAME:TARGET%の
SELECTCASE TALENT:TARGET:バストサイズ
	CASE -2
		PRINT 殆ど膨らみがない、平らな胸に手を伸ばして
	CASE -1
		PRINT 小さく膨らみが見える胸に手を伸ばし、
	CASE 0
		PRINT 胸に手を伸ばし、
	CASE 1 TO 2
		PRINT 大きく柔らかな胸に手を伸ばし、
	CASE 3
		PRINT 両手に余るほど豊満な胸に手を伸ばし、
ENDSELECT
IF TEQUIP:上半身下着あり == -3 && !RAND:3
	PRINTL 安全ピンごと爪弾くように乳首をひっかいた
ELSEIF TEQUIP:上半身下着あり == -2 && !RAND:3
	PRINTL ピアスが揺れ動くように乳首をひっかいた
ELSE
	PRINTL 服の上から乳首をかりかりとひっかいた
ENDIF

IF CUP:TARGET:快Ｂ > 3000
	IF CFLAG:TARGET:睡眠
		PRINTFORML %CALLNAME:TARGET%は眠ったままでも、明らかに乳首で強く感じているようだ…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は嬌声を漏らし、乳首からの快楽に翻弄されている…
	ENDIF
	PRINTL
ELSEIF CUP:TARGET:快Ｂ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	IF CFLAG:TARGET:睡眠
		PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は眠ったまま小さく喘いだ…
	ELSE
		PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は思わず少し声を漏らしてしまった…
	ENDIF
	PRINTL
ENDIF

@MESSAGE_COM324_非口上時

IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:TARGET%はすやすやと眠っている……
	RETURN 0
ENDIF

IF CUP:TARGET:快Ｂ <= 3000
	SELECTCASE RFLAG:コマンド結果受渡し変数２
		CASE 1
			;無知
			PRINTFORML %CALLNAME:TARGET%はよくわかっていないが、%CALLNAME:PLAYER%が楽しいなら良いと嬉しそうにしている
		CASE 2
			;無反応
			PRINTFORML %CALLNAME:TARGET%は平然としているが、少し顔を赤くして%CALLNAME:PLAYER%に体を擦り寄せてきた…
		CASE 3
			;嫌悪
			PRINTFORML %CALLNAME:TARGET%はしょうがない人だな、という顔で%CALLNAME:PLAYER%の好きにさせている…
		CASE 4
			;拒否
			PRINTFORML %CALLNAME:TARGET%は悪い気はしない様子で、%CALLNAME:PLAYER%の好きにさせている…
		CASE 5
			;余裕
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に、周囲にバレないようこっそりと触るように言ってきた…
		CASE 6
			;羞恥
			PRINTFORML %CALLNAME:TARGET%は赤くなった顔で%CALLNAME:PLAYER%を見つめ、そっと体を寄せてきた…
		CASE 7
			;愉悦
			PRINTFORML %CALLNAME:TARGET%は他の場所も触るかと、色っぽい表情で%CALLNAME:PLAYER%を挑発している…
		CASE 8
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に触れられることを少し嬉しく思っているようだ…
	ENDSELECT
ENDIF
