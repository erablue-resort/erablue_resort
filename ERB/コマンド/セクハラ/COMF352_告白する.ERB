
@COMNAME352
#FUNCTIONS
TSTR:コマンド名受渡 = 告白する

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_352
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_352
LOCALS = <br>■告白する<br>
LOCALS += "相手と恋人状態になるためのコマンド。<br>相手を恋慕対象にした上で、恋慕レベルが一定以上必要。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;告白する
@COM352
IF 告白成功判定(TARGET) > 0
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	RFLAG:コマンド結果受渡し変数 = 告白成功判定(TARGET)
	RETURN 1
ENDIF

IF RFLAG:コマンド結果受渡し変数 == 1
	CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 1000)
	CALL SOURCE_CALC_征服(TARGET, PLAYER, 500)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 1000)
	TALENT:恋慕 = 1
	初体験日:TARGET:恋慕 = DAY
	IF CFLAG:PLAYER:現在マップ種別 == 100 ;お祭り会場
		TRYCALLFORMF GETPLACENAME_100_%開催予定祭り名%(CFLAG:MASTER:現在位置)
		IF TSTR:場所名受渡 != ""
			CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"%CALLNAME:TARGET%に%TSTR:場所名受渡%で告白した","日常")
			CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"%CALLNAME:MASTER%に%TSTR:場所名受渡%で告白された","日常")
		ENDIF
	ENDIF
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%を[恋慕]陥落させた</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[恋慕]を得た</font><img src='えっちハート'>","うふふ実績")
	SETBIT CFLAG:1 , 0
	IF TALENT:TARGET:恋人持ち > 0
		RFLAG:コマンド結果受渡し変数３ = 1
		TALENT:TARGET:浮気 = 1
	ELSEIF TALENT:TARGET:恋人持ち == -1
		RFLAG:コマンド結果受渡し変数３ = 2
		TALENT:TARGET:恋人持ち = 0
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE352
;告白する実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(352)
	RETURN RESULT
;二人きりでないとだめ
SIF GET_TARGETNUM() > 1
	RETURN 0
;飲み会時はだめ
SIF TFLAG:調教モード == 3
	RETURN 0
;恋慕対象設定じゃないとだめ
SIF !CFLAG:TARGET:ゲージ起動分類
	RETURN 0
;馴れ合い強度1
SIF TCVAR:MASTER:馴れ合い強度 < 1
	RETURN 0
;すでに恋慕を得ている場合はだめ
SIF TALENT:恋慕
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM352
IF RFLAG:コマンド結果受渡し変数 > 0
	PRINTFORML %CALLNAME:PLAYER%は意を決して%CALLNAME:TARGET%に恋情を伝えた…
	PRINTFORML %CALLNAME:TARGET%は喜び、%CALLNAME:PLAYER%の想いを受け入れてくれた…
	PRINTFORML %CALLNAME:TARGET%は［恋慕］を得ました。
	IF RFLAG:コマンド結果受渡し変数３ == 1
		PRINTFORM %CALLNAME:TARGET%は
		SETCOLOR カラーパレット("えっちな色")
		PRINT [浮気]
		RESETCOLOR
		PRINT を得た……
	ELSEIF RFLAG:コマンド結果受渡し変数３ == 2
		PRINTFORM %CALLNAME:TARGET%は
		SETCOLOR カラーパレット("えっちな色")
		PRINT [想い人あり]
		RESETCOLOR
		PRINT を失った……
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	PRINTFORML まだ%CALLNAME:TARGET%に告白しても受け入れてはもらえないだろう…
	PRINTL もう少し仲を深めてからにしよう。
ELSE
	PRINTFORML %CALLNAME:TARGET%には既に恋人がいる。
	PRINTL 関係を乱すのはやめておこう。
ENDIF


@告白成功判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ

SIF TALENT:対象キャラ:恋慕
	RETURNF 0
SIF !CFLAG:対象キャラ:ゲージ起動分類
	RETURNF 0
SIF CFLAG:対象キャラ:恋慕レベル <= 50
	RETURNF 0
SIF GETBIT(TALENT:対象キャラ:陥落不可, 0)
	RETURNF 0
SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And 関係性種別 = '恋慕'")
	RETURNF -1

TRYCALLFORMF 告白成功判定_キャラ個別条件_{NO:対象キャラ}(対象キャラ)
SIF RESULT > 0
	RETURNF 0

RETURNF 1

