
@COMNAME360
#FUNCTIONS

TSTR:コマンド名受渡 = 逆挿入歩き

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_360
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_360
LOCALS = <br>■逆挿入歩き<br>
LOCALS += "自身のV、あるいはAに挿入させたまま相手を行動させるコマンド。<br>同室者が全員陥落済みの時のみ可能だが、一緒に隠密時は同じ場所に非陥落キャラがいても可能。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：快Ｃ・接触"
LOCALS += "<br>固有の取得経験：ＶorＡ性交経験(PLAYER)・Ｃ性交経験_ＶorＡ挿入(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM360
;固定された後は自動で時間が進むのでモード化しない

DRAWLINE
PRINTL ■挿入先を選んでください
PRINTL
LOCAL = 0
IF GETBIT(TALENT:PLAYER:性別, 0) == 1 && TALENT:PLAYER:Ｖ感度 != -2
	IF TEQUIP:PLAYER:膣 == 0
		LOCALS = Ｖ挿入
		SIF TALENT:PLAYER:処女
			LOCALS += "（処女）"
		PRINTBUTTONLC @"[1] %LOCALS%", 1
		LOCAL += 1
	ENDIF
ENDIF
IF TALENT:PLAYER:Ａ感度 != -2
	IF TEQUIP:PLAYER:アナル == 0
		PRINTBUTTONLC "[2] Ａ挿入", 2
		LOCAL += 1
	ENDIF
ENDIF
SIF LOCAL == 0
	PRINT 挿入先がありません。
PRINTL
PRINTL
PRINTBUTTONLC "[999]戻る", 999

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1 TO 2
		TRYCALLFORM CAN_逆オナホ(1, LOCAL)
		IF !RESULT
			TRYCALLFORM CAN_逆オナホ(0, LOCAL)
			RESTART
		ENDIF
	CASE 999
		RETURN -1
ENDSELECT

RFLAG:コマンド結果受渡し変数 = LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF LOCAL == 1
	CALL MODE_CHANGE("挿入歩きＶ", TARGET, PLAYER)
	CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, 400)
	;処女喪失
	CALL LOST_VIRGIN(PLAYER, TARGET, 1)
	CALL LOST_MAN_VIRGIN(TARGET, PLAYER, 1)
	;苦痛
	LOCAL = 200 * PAIN_CHECK_V(PLAYER) / 1000
	CALL SOURCE_CALC_苦痛(PLAYER, TARGET, LOCAL)
ELSE
	CALL MODE_CHANGE("挿入歩きＡ", TARGET, PLAYER)
	CALL SOURCE_CALC_快Ａ(PLAYER, TARGET, 400)
	;A処女喪失
	CALL LOST_A_VIRGIN(PLAYER, TARGET, 1)
	CALL LOST_MAN_VIRGIN(TARGET, PLAYER, 2)
	;苦痛
	LOCAL = 200 * PAIN_CHECK_A(PLAYER) / 1000
	CALL SOURCE_CALC_苦痛(PLAYER, TARGET, LOCAL)
ENDIF
CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
CALL SOURCE_CALC_接触(PLAYER, TARGET, 200)
CALL SOURCE_CALC_露出(PLAYER, TARGET, 250)
RCVAR:PLAYER:露出コマンドフラグ = 1

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 400)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4


TIME += 15
TFLAG:オートコマンド連続発動 = 11
TFLAG:オートコマンド連続発動_逆オナホ = 2
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE360
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0

;一括管理
SIF GLOBAL_COMABLE(360)
	RETURN RESULT
;相手にちんこあること
SIF GETBIT(TALENT:TARGET:性別, 1) == 0 || TALENT:TARGET:Ｃ感度 == -2
	RETURN 0

;露出と欲情の同時
SIF フィールド展開:うふふ目撃スルー == 0
	RETURN 0

SIF TEQUIP:TARGET:ペニス
	RETURN 0

;ほかは基本的に押し倒すと判定は同一
CALL COM_ABLE350()
RETURN RESULT

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM360

PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%を全裸に剥き、後ろを向かせて
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		PRINTL 秘裂に挿入した
	CASE 2
		PRINTL 肛門に挿入した
ENDSELECT
PRINTFORML 更に挿入したまま、別の作業を始めた……

