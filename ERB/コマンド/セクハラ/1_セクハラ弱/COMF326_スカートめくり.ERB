
@COMNAME326
#FUNCTIONS
TSTR:コマンド名受渡 = スカートめくり

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_326
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
睡眠中許可 = 1

@COM_TOOLTIP_326
LOCALS = <br>■スカートめくり<br>
LOCALS += "相手のスカートをめくるコマンド。<br>スカート状ではない服だと不可能<br>その場にいる人数が多いほど不快・反感が増加する。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：接触・露出・不快・反感"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM326

TIME += 3
SIF CFLAG:TARGET:睡眠
	RETURN 1

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ  3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

;結果に関わりなく発生する快楽部分
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
;ノーパン時、露出が倍
IF TEQUIP:TARGET:下半身下着あり == 0
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
ENDIF


IF RFLAG:コマンド結果受渡し変数２ <= 2
	;無知、あるいは無反応の場合不快反感発生無し
	IF 陥落チェック(TARGET)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	ENDIF
ELSEIF GET_TARGETNUM() > 1
	;自身とあなた以外のキャラが居る場合
	CALL SOURCE_CALC_露出(TARGET, PLAYER, (GET_TARGETNUM() - 1) * 15)
	CALL SOURCE_CALC_不快(TARGET, PLAYER, (GET_TARGETNUM() - 1) * 10 + 50)
	CALL SOURCE_CALC_反感(TARGET, PLAYER, (GET_TARGETNUM() - 1) * 10 + 50)
ELSE
	;二人きりの場合
	IF 陥落チェック(TARGET)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	ELSE
		CALL SOURCE_CALC_不快(TARGET, PLAYER, 50)
		CALL SOURCE_CALC_反感(TARGET, PLAYER, 50)
	ENDIF
ENDIF	

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE326
;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(326)
	RETURN RESULT
;スカート着用必須
SIF TEQUIP:TARGET:下半身服あり == 0
	RETURN 0
SIF TEQUIP:TARGET:スカートめくり可 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM326

IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:PLAYER%は眠っている%CALLNAME:TARGET%の服の裾をゆっくりとまくり上げた。
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%にこっそりと近づき、服の裾をまくり上げた。
ENDIF

IF TEQUIP:TARGET:下半身下着あり == 0
	LOCALS = 何も履いていない下半身
ELSE
	LOCALS = %今日の下着文字列生成(TARGET)%
ENDIF
PRINTFORML 服の奥に%LOCALS%が見える……

@MESSAGE_COM326_非口上時

PRINTFORM %CALLNAME:TARGET%は
IF CFLAG:TARGET:睡眠
	PRINTFORML すやすやと眠っている……
	RETURN 0
ENDIF

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		PRINTFORM 何をしているのかわかっていない様子で、%CALLNAME:PLAYER%のことを見ている。
	CASE 2
		;無反応
		PRINTFORM 何も気にしないかのように無表情でされるがままになっている。
	CASE 3
		;嫌悪
		IF GET_TARGETNUM() > 1
			PRINTFORM 慌てて服を抑え、ドン引きしたような目で%CALLNAME:PLAYER%を見ている。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 慌てて服を抑え、ドン引きしたような目で%CALLNAME:PLAYER%を見ている。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM 服を抑え、呆れたような目で%CALLNAME:PLAYER%を見ている。
			ELSE
				;喜び
				PRINTFORM 馬鹿なことをしてるな、と呆れたような目で%CALLNAME:PLAYER%を見ている。
			ENDIF
		ENDIF
	CASE 4
		;拒否
		IF GET_TARGETNUM() > 1
			PRINTFORM 慌てて服を抑え、厳しい口調で%CALLNAME:PLAYER%を注意した。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 慌てて服を抑え、厳しい口調で%CALLNAME:PLAYER%を注意した。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM 服を抑え、やんわりと%CALLNAME:PLAYER%を注意した。
			ELSE
				;喜び
				PRINTFORM 他の人がいる時にはしないように、と%CALLNAME:PLAYER%を注意した。
			ENDIF
		ENDIF
	CASE 5
		;余裕
		IF GET_TARGETNUM() > 1
			PRINTFORM 静かに服を抑え、冷たい目で%CALLNAME:PLAYER%を睨みつけた。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 静かに服を抑え、冷たい目で%CALLNAME:PLAYER%を睨みつけた。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM 服を抑え、やんわりと%CALLNAME:PLAYER%を注意した。
			ELSE
				;喜び
				PRINTFORM 見たいなら遠慮することはない、と自分から服の裾をひらひらさせている。
			ENDIF
		ENDIF
	CASE 6
		;羞恥
		IF GET_TARGETNUM() > 1
			PRINTFORM 慌てて服を抑え、真っ赤な顔で%CALLNAME:PLAYER%から飛び退いた。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 慌てて服を抑え、真っ赤な顔で%CALLNAME:PLAYER%から飛び退いた。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM 慌てて服を抑え、真っ赤な顔で%CALLNAME:PLAYER%をぽかぽかと叩いてきた。
			ELSE
				;喜び
				PRINTFORM 服を抑え、真っ赤な顔で%CALLNAME:PLAYER%をじっと見つめている。
			ENDIF
		ENDIF
	CASE 7
		;愉悦
		IF GET_TARGETNUM() > 1
			PRINTFORM 静かに服を抑え、冷たい目で%CALLNAME:PLAYER%を睨みつけた。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 静かに服を抑え、冷たい目で%CALLNAME:PLAYER%を睨みつけた。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM そんなに中が気になるのかと%CALLNAME:PLAYER%をからかってきた。
			ELSE
				;喜び
				PRINTFORM めくるのは服だけでいいのかと%CALLNAME:PLAYER%をからかってきた。
			ENDIF
		ENDIF
	CASEELSE
		;汎用
		IF GET_TARGETNUM() > 1
			PRINTFORM 慌てて服を抑え、%CALLNAME:PLAYER%を睨みつけた。
		ELSE
			IF RFLAG:コマンド結果受渡し変数 == 1
				;拒絶
				PRINTFORM 慌てて服を抑え、%CALLNAME:PLAYER%を睨みつけた。
			ELSEIF RFLAG:コマンド結果受渡し変数 == 2
				;受け入れ
				PRINTFORM 服を抑え、困ったような顔で%CALLNAME:PLAYER%を見ている。
			ELSE
				;喜び
				PRINTFORM いたずらしては駄目だと、苦笑しながら%CALLNAME:PLAYER%を注意した。
			ENDIF
		ENDIF
ENDSELECT
