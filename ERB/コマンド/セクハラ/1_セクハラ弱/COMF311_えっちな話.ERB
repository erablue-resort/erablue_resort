
@COMNAME311
#FUNCTIONS
TSTR:コマンド名受渡 = えっちな話

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_311
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")

@COM_TOOLTIP_311
LOCALS = <br>■えっちな話<br>
LOCALS += "相手にえっちな話を振るコマンド。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：接触・露出・歓楽(一定条件時)・不快(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：会話経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM311
;えっちな話

CALL セクハラ反応タイプ判定(TARGET)
;拒絶受け入れ喜びに関しては他のセクハラコマンドとは別の処理で算出
CALL えっちな話嗜好判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:気分じゃない  2:ノる
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

SELECTCASE ABL:MASTER:話術技能
	CASE 0
		LOCAL = -50
	CASE 1
		LOCAL = -10
	CASE 2
		LOCAL = 10
	CASE 3
		LOCAL = 30
	CASE 4
		LOCAL = 50
	CASEELSE
		LOCAL = 80
ENDSELECT

IF RFLAG:コマンド結果受渡し変数 == 2 && TALENT:TARGET:性欲 > -2
	;ノった場合は性欲上昇
	BASE:TARGET:性欲 += ABL:MASTER:話術技能 * 30 + 10
ENDIF

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知、何の話かわからない
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
	CASE 2
		;無反応、聞いてはくれる
		LOCAL = MAX(LOCAL + 10, 0)
		CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
	CASE 3
		;嫌悪
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 50, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 20)
		ENDSELECT
	CASE 4
		;拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 50, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 20)
		ENDSELECT
	CASE 5
		;余裕
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 100, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 40)
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 50, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 40)
		ENDSELECT
	CASE 7
		;愉悦
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 30)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 150, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
	CASE 8
		;汎用
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 2
				;受け入れ
				LOCAL = MAX(LOCAL + 50, 0)
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 30)
		ENDSELECT
ENDSELECT



CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)


;会話累積値
LOCAL = 200 - (TALENT:TARGET:会話資質 * 50) - (TALENT:PLAYER:会話資質 * 50)
TCVAR:会話累積値 += LOCAL / (3 + ABL:MASTER:話術技能 + TALENT:TARGET:陽気／陰気)

TIME += 15
EXP:MASTER:会話経験 ++
RETURN 1

@えっちな話嗜好判定(ARG)
;えっちな話に対してノるかノらないかを決める
LOCAL = 0
LOCAL += CFLAG:ARG:好感度レベル
LOCAL += BASE:ARG:性欲 / 100
LOCAL += GETPALAMLV(PALAM:ARG:欲情, 5) * 10

LOCAL += TALENT:ARG:セフレ * 10
LOCAL += TALENT:ARG:会話資質 * 10
LOCAL += TALENT:ARG:精神的余裕 * 10
IF 知識素質:ARG:性知識 == 0 && TALENT:ARG:性的興味 > 0
	;耳年増時、補正が更に上昇
	LOCAL += 60
ELSE
	LOCAL += TALENT:ARG:性的興味 * 40
ENDIF
LOCAL += TALENT:ARG:性欲 * 20
LOCAL += TALENT:ARG:経験豊富 * 20
LOCAL += TALENT:ARG:快感応答 * 10
LOCAL += TALENT:ARG:倒錯的 * 20

LOCAL -= TALENT:ARG:プライド * 10
LOCAL -= TALENT:ARG:自制心 * 20
LOCAL -= TALENT:ARG:一線越えない * 20
LOCAL -= TALENT:ARG:貞操 * 10
LOCAL -= TALENT:ARG:羞恥心 * 40


IF LOCAL >= 100
	;ノリノリ
	RFLAG:コマンド結果受渡し変数 = 2
ELSE
	;乗ってこない
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE311
;会話実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(311)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;口を何かに使ってるなら無理
SIF TEQUIP:PLAYER:口 == 1
	RETURN 0
SIF TEQUIP:TARGET:口 == 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM311
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%
IF RFLAG:コマンド結果受渡し変数 == 2
	PRINTL とえっちな話をした
ELSE
	PRINTL にえっちな話を振ってみた
ENDIF


@MESSAGE_COM311_非口上時
SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知、何の話かわからない
		PRINTFORML しかし、%CALLNAME:TARGET%には何の話なのかイマイチよくわからなかったようだ…
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%は聞いているのかいないのか、特に反応せず無視している…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は無反応に見えて、たまに相槌を返してくれている
		ENDSELECT
	CASE 3
		;嫌悪
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を虫を見るような目で睨みつけている…
			CASE 2
				;受け入れ
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
				PRINTL
		ENDSELECT
	CASE 4
		;拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%はこういう話題はあまり好きではないようだ…
			CASE 2
				;受け入れ
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
				PRINTL
		ENDSELECT
	CASE 5
		;余裕
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%はさらりと話題を流し、別の話に切り替えた
			CASE 2
				;受け入れ
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
				PRINTL
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%は真っ赤になって押し黙ってしまった…
			CASE 2
				;受け入れ
				PRINTFORM まだ少し恥ずかしさが残っているようだが、
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
		ENDSELECT
	CASE 7
		;愉悦
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%はそういう気分ではなかったようだ…
			CASE 2
				;受け入れ
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
				PRINTL
		ENDSELECT
	CASE 8
		;汎用
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				PRINTFORML %CALLNAME:TARGET%はそういう気分ではなかったようだ…
			CASE 2
				;受け入れ
				PRINTDATA
					DATAFORM %CALLNAME:TARGET%から興味深い話を聞くことが出来た
					DATAFORM %CALLNAME:TARGET%と楽しく話することが出来た
					DATAFORM くだらない話で%CALLNAME:TARGET%と笑い合う事が出来た
					DATAFORM %CALLNAME:TARGET%の興味を引く話をすることが出来た
				ENDDATA
				PRINTL
		ENDSELECT
ENDSELECT
