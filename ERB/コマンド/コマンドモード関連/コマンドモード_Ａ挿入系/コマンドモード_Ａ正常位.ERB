@MODE_持続快楽_Ａ正常位(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号
CALL MODE_持続快楽_Ａ挿入系(実行キャラ番号, 対象キャラ番号, 100, 200)

@MODETYPE_Ａ正常位(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = 上から%CALLNAME:ターゲット番号%に覆いかぶさり、アナルにペニスを突き入れる
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 陥落チェック(ターゲット番号) == 0 && BASE:ターゲット番号:酩酊 > 300
		;未陥落の泥酔姦。ヤってる最中にも酩酊は下がり続けるので、泥酔の500からちょい下げて300までで
		;むしろ未陥落でもないと別に酔ってコトに及んだ情報残してもシコくなさそうなので未陥落のみ
		詳細文文字列受け渡し変数 = %TEXTR("酔った勢いで/酔いに浮かされて/泥酔して")%、%TEXTR("仰向けに組み敷かれて/正常位で")%%CALLNAME:プレイヤー番号%の%TEXTR("ペニス/チンポ")%に 
		詳細文文字列受け渡し変数 += @"%TEXTR("アナル/お尻/尻穴/ケツ穴")%を%TEXTR("性器として使われてしまっている/犯されている/ズボスボされている/楽しまれてしまっている/コキ捨て穴にされてしまっている/ほじくられている")%"
	ELSE
		詳細文文字列受け渡し変数 = %TEXTR("仰向けに押し倒されて/正常位で")%、%CALLNAME:プレイヤー番号%
		詳細文文字列受け渡し変数 += @"の%TEXTR("ペニス/チンポ")%を%TEXTR("アナル/お尻/尻穴")%に%TEXTR("に咥え込んだ/突き入れられた")%"
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;以下部位の専有判定
;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;挿入で使用するのはペニスと膣で1を返す
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_口
		IF プレイヤー番号 < 0 || ターゲット番号 < 0
			;キャラ不明の場合は一般論
			RETURN 部位状況_なし
		ELSEIF TALENT:ターゲット番号:種族 == 4 ^^ TALENT:プレイヤー番号:種族 == 4
			;どっちかだけがハーヴィンだと口は届かない
			RETURN 部位状況_届かない
		ELSE
			RETURN 部位状況_なし
		ENDIF
	CASE 部位_アナル
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT

@MESSAGE_MODE_Ａ正常位解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の肛門からゆっくりと陰茎を引き抜いた

