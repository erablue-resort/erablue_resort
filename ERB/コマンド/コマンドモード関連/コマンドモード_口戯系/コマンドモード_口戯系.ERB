@系統リスト_口戯系(モード名)
#FUNCTION
#DIMS モード名
IF GROUPMATCH(モード名, "クンニ") || 系統リスト_フェラチオ系(モード名)
	モード確認結果 = 1
	RETURNF 1
ENDIF


@系統リスト_シックスナイン可能(モード名)
#FUNCTION
#DIMS モード名
IF GROUPMATCH(モード名, "クンニ", "フェラチオ", "パイズリ", "パイズリフェラ")
	モード確認結果 = 1
	RETURNF 1
ENDIF


@MODE_シックスナイン中_片側(判定キャラ番号 = -1)
#FUNCTION
#DIM 判定キャラ番号
#DIM 配列カウンタ

IF 判定キャラ番号 == -1
	判定キャラ番号 = TARGET
ENDIF

;シックスナイン可能なモードがあり、実行キャラと対象キャラが逆のシックスナイン可能なモードもあるかどうかを見る
FOR 配列カウンタ, 0, DT_ROW_LENGTH("体位モードデータベース")
	SIF !MODE系統確認(DT_CELL_GETS("体位モードデータベース", 配列カウンタ, "モード名"), "シックスナイン可能")
		CONTINUE
	IF DT_CELL_GET("体位モードデータベース", 配列カウンタ, "実行キャラ") == 判定キャラ番号
		SIF MODE_存在判定_完全一致("シックスナイン可能", DT_CELL_GET("体位モードデータベース", 配列カウンタ, "対象キャラ"), 判定キャラ番号)
			RETURNF 1
	ELSEIF DT_CELL_GET("体位モードデータベース", 配列カウンタ, "対象キャラ") == 判定キャラ番号
		SIF MODE_存在判定_完全一致("シックスナイン可能", 判定キャラ番号, DT_CELL_GET("体位モードデータベース", 配列カウンタ, "対象キャラ"))
			RETURNF 1
	ENDIF
NEXT
RETURNF 0


@MODE_シックスナイン中_完全一致(判定キャラ番号１, 判定キャラ番号２)
#FUNCTION
#DIM 判定キャラ番号１
#DIM 判定キャラ番号２

IF 判定キャラ番号１ == -1
	判定キャラ番号１ = PLAYER
ENDIF
IF 判定キャラ番号２ == -1
	判定キャラ番号２ = TARGET
ENDIF

;シックスナイン可能なモードがあり、実行キャラと対象キャラが逆のシックスナイン可能なモードもあるかどうかを見る
IF MODE_存在判定_完全一致("シックスナイン可能", 判定キャラ番号１, 判定キャラ番号２)
	SIF MODE_存在判定_完全一致("シックスナイン可能", 判定キャラ番号２, 判定キャラ番号１)
		RETURNF 1
ENDIF
RETURNF 0



@MODE_持続快楽_クンニ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号
#DIM 快Ｃ数値

快Ｃ数値 = 150
;舌を使うモードでは、実行者の舌使いによって、快感のソース増加
SIF TALENT:実行キャラ番号:舌使い
	TIMES 快Ｃ数値, 1.50
;猫舌では減少
SIF TALENT:実行キャラ番号:猫舌
	TIMES 快Ｃ数値, 0.75

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 快Ｃ数値)
CALL SOURCE_CALC_液体(対象キャラ番号, 実行キャラ番号, 30)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 100)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_奉仕(対象キャラ番号, 実行キャラ番号, 130)
EXP:実行キャラ番号:口淫経験 ++
;奉仕快楽
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1


@MODETYPE_クンニ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%の秘唇%TEXTR("を口淫する/に舌を這わせる/を唇で愛撫する/を舌で愛撫する")%
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = 秘唇を%CALLNAME:プレイヤー番号%に%TEXTR("口淫されている/唇と舌で愛撫されている/口唇愛撫されている")%
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_クリトリス
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_口
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_届かない
	CASE 部位_胸
		;プレイヤーの胸には届かない
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_膣
		;プレイヤーの膣には届かない
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		;プレイヤーの尻には届かない
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT


@MODE_絶頂相手_クンニ(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果

IF 処理モード == "ターゲット"
	IF 絶頂部位 == 部位_Ｃ
		判定結果 = 絶頂相手_モード相手
		RETURNF 絶頂相手_モード相手
	ENDIF
ENDIF


@MESSAGE_MODE_クンニ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の秘唇から舌を離した…
