@MODE_持続快楽_足コキ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, MIN(ABL:実行キャラ番号:技巧 * 25, 100))
CALL SOURCE_CALC_征服(実行キャラ番号, 対象キャラ番号, 90)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 50)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 50)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 20)
;奉仕快楽
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1
;サドは嗜虐快楽
SIF ABL:実行キャラ番号:サド性感
	EXP:実行キャラ番号:嗜虐快楽経験 ++

RETURN 1


@MODETYPE_足コキ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	IF ABL:プレイヤー番号:サド性感 > 2
		詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を%TEXTR("両足で挟んで扱く/足指で弄りまわす/足で踏みつける")%
	ELSE
		詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を両足で挟んで扱く
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	SELECTCASE (ABL:プレイヤー番号:サド性感 > 2) ? RAND:3 # 0
	CASE 0
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の両足で%TEXTR("ペニス/チンポ/おちんぽ")%を扱かれている
	CASE 1
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の足指で%TEXTR("ペニス/チンポ/おちんぽ")%を弄りまわされている
	CASE 2
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の足に%TEXTR("ペニス/チンポ/おちんぽ")%を踏みつけられている
	ENDSELECT
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
IF 処理モード == "ターゲット" && 部位番号 == 部位_ペニス
	;ターゲットのペニスを占有
	RETURN 部位状況_占有
ELSE
	;他は全て届かない
	RETURN 部位状況_届かない
ENDIF


@MODE_絶頂相手_足コキ(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果

IF 処理モード == "ターゲット"
	IF 絶頂部位 == 部位_Ｃ
		判定結果 = 絶頂相手_モード相手
		RETURNF 絶頂相手_モード相手
	ENDIF
ENDIF


@MESSAGE_MODE_足コキ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF 対象キャラ番号 == MASTER
	PRINTFORML %CALLNAME:対象キャラ番号%は%CALLNAME:実行キャラ番号%を止め、陰茎から足を離させた
ELSE
	PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の陰茎から足を離した
ENDIF



@MODE_持続快楽_ボテ腹ズリ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 150)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 80)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 100)
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1


@MODETYPE_ボテ腹ズリ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %TEXTR("大きく膨らん/子供を孕ん")%だお腹で%CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を扱いている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %TEXTR("大きく膨らん/子供を孕ん")%だ%CALLNAME:プレイヤー番号%のお腹で%TEXTR("ペニス/チンポ/おちんぽ")%を扱いている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
	CASE 部位_クリトリス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_膣
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT


@MODE_絶頂相手_ボテ腹ズリ(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果

IF 処理モード == "ターゲット"
	IF 絶頂部位 == 部位_Ｃ
		判定結果 = 絶頂相手_モード相手
		RETURNF 絶頂相手_モード相手
	ENDIF
ENDIF


@MESSAGE_MODE_ボテ腹ズリ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF 対象キャラ番号 == MASTER
	PRINTFORML %CALLNAME:対象キャラ番号%は%CALLNAME:実行キャラ番号%に腹部での奉仕を止めさせた…
ELSE
	PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の陰茎から腹部を離した…
ENDIF



@MODE_持続快楽_全裸散歩(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF TALENT:TARGET:毛皮 < 1
	CALL SOURCE_CALC_露出(対象キャラ番号, 実行キャラ番号, 100)
	RCVAR:対象キャラ番号:露出コマンドフラグ = 1
ENDIF

RETURN 1


@MODETYPE_全裸散歩(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	;詳細文文字列受け渡し変数 = 
	;基本あなた専用なのでここに来ることはないはず
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 知識素質:ターゲット番号:性知識 < -1
		詳細文文字列受け渡し変数 = 身体を隠すことも無く堂々と歩く
	ELSE
		詳細文文字列受け渡し変数 = 不安そうに周囲を伺いながら歩く
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;部位専有なし



@MODE_持続快楽_首絞め(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF ABL:実行キャラ番号:技巧 < 3
	;技巧がないと苦しいだけ
	CALL SOURCE_CALC_苦痛(対象キャラ番号, 実行キャラ番号, 1000)
ELSEIF ABL:対象キャラ番号:マゾ性感 < 3 && ABL:実行キャラ番号:技巧 < 7
	CALL SOURCE_CALC_苦痛(対象キャラ番号, 実行キャラ番号, 50 * MAX(4, 14 - ABL:実行キャラ番号:技巧))
ELSE
	;上手い人だとあまり苦しくないらしいよ
	CALL SOURCE_CALC_苦痛(対象キャラ番号, 実行キャラ番号, 50 * MAX(4, 14 - ABL:実行キャラ番号:技巧))
	CALL SOURCE_CALC_快Ｓ(対象キャラ番号, 実行キャラ番号, 50 * MAX(2, ABL:実行キャラ番号:技巧 - 4))
ENDIF
;苦痛快楽
RCVAR:対象キャラ番号:苦痛コマンドフラグ = 1
RCVAR:実行キャラ番号:嗜虐コマンドフラグ = 1
;喘ぎを減らす
RCVAR:対象キャラ番号:汎用喘ぎ_文節低減フラグ = 1

RETURN 1


@MODETYPE_首絞め(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	;詳細文文字列受け渡し変数 = 
	;基本あなた専用なのでここに来ることはないはず
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の手で首を絞められ
	SELECTCASE 性癖素質:ターゲット番号:首絞め
		CASE 3
			;中毒
			詳細文文字列受け渡し変数 += @"%TEXTR("アヘ顔/トロ顔/蕩けた表情")%を晒している"
		CASE 2
			;愛好
			詳細文文字列受け渡し変数 += "その快楽の虜になっている"
		CASEELSE
			;それ以外
			IF ABL:ターゲット番号:マゾ性感 > 2
				詳細文文字列受け渡し変数 += "首絞めそのものに快楽を感じている"
			ELSE
				詳細文文字列受け渡し変数 += "苦悶の表情を浮かべている"
			ENDIF
	ENDSELECT
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
SELECTCASE 部位番号
	CASE 部位_指
		;実行者は両手を使わなければならない
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_強占有
ENDSELECT


@MODE_絶頂相手_首絞め(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果

IF 処理モード == "ターゲット"
	IF 絶頂部位 == 部位_Ｓ
		判定結果 = 絶頂相手_モード相手
		RETURNF 絶頂相手_モード相手
	ENDIF
ENDIF


@MESSAGE_MODE_首絞め解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の首から手を離した…
