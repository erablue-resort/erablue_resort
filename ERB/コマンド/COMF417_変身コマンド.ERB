;-------------------------------------------------
;戦闘訓練
;-------------------------------------------------

@COMNAME417
#FUNCTIONS
#DIM 変身ID

変身ID = FIND_変身ID(TARGET)
IF GET_変身_変身残りターン数(変身ID)
	TSTR:コマンド名受渡 '= GET_変身_変身解除コマンド名(変身ID)
ELSE
	TSTR:コマンド名受渡 '= GET_変身_変身コマンド名(変身ID)
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_417
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM417
;コマンド結果受渡し変数 = 0:変身解除 1:変身

IF GET_変身_変身残りターン数(FIND_変身ID(TARGET))
	RFLAG:コマンド結果受渡し変数 = 0
	CALL 変身解除実行処理(TARGET)
ELSE
	RFLAG:コマンド結果受渡し変数 = 1
	CALL 変身実行処理(TARGET)
ENDIF

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE417
IF GET_変身_変身残りターン数(FIND_変身ID(TARGET))
	CALL 変身解除ABLE(TARGET, "コマンド")
	SIF !RESULT
		RETURN 0
ELSE
	CALL 変身ABLE(TARGET, "コマンド")
	SIF !RESULT
		RETURN 0
ENDIF

SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
; SIF GLOBAL_COMABLE(417)
; 	RETURN RESULT

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM417
PRINTFORM %CALLNAME:TARGET%は

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML %GET_変身_変身コマンド名(FIND_変身ID(TARGET))%をした
ELSE
	PRINTFORML %GET_変身_変身解除コマンド名(FIND_変身ID(TARGET))%をした
ENDIF
