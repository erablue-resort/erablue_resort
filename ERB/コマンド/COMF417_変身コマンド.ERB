;-------------------------------------------------
;戦闘訓練
;-------------------------------------------------

@COMNAME417
#FUNCTIONS
DT_SELECT "変身データベース", @"キャラ番号 = {TARGET}"

IF DT_CELL_GET("変身データベース", RESULT:1, "変身残りターン数", 1)
	TSTR:コマンド名受渡 = %DT_CELL_GETS("変身データベース", RESULT:1, "変身解除コマンド名", 1)%
ELSE
	TSTR:コマンド名受渡 = %DT_CELL_GETS("変身データベース", RESULT:1, "変身コマンド名", 1)%
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_417
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM417
;コマンド結果受渡し変数 = 0:変身解除 1:変身
DT_SELECT "変身データベース", @"キャラ番号 = {TARGET}"
IF DT_CELL_GET("変身データベース", RESULT:1, "変身残りターン数", 1)
	RFLAG:コマンド結果受渡し変数 = 0
	CALL 変身解除実行処理(TARGET)
ELSE
	RFLAG:コマンド結果受渡し変数 = 1
	CALL 変身実行処理(TARGET)
ENDIF

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE417
DT_SELECT "変身データベース", @"キャラ番号 = {TARGET}"
IF DT_CELL_GET("変身データベース", RESULT:1, "変身残りターン数", 1)
	CALL 変身解除ABLE(TARGET, "コマンド")
	IF !RESULT
		RETURN 0
	ENDIF
ELSE
	CALL 変身ABLE(TARGET, "コマンド")
	IF !RESULT
		RETURN 0
	ENDIF
ENDIF

SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
; SIF GLOBAL_COMABLE(417)
; 	RETURN RESULT

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM417
PRINTFORM %CALLNAME:TARGET%は

DT_SELECT "変身データベース", @"キャラ番号 = {TARGET}"
IF RFLAG:コマンド結果受渡し変数
	LOCALS = %DT_CELL_GETS("変身データベース", RESULT:1, "変身コマンド名", 1)%
ELSE
	LOCALS = %DT_CELL_GETS("変身データベース", RESULT:1, "変身解除コマンド名", 1)%
ENDIF

PRINTFORML %LOCALS%をした
