

@COMNAME302
#FUNCTIONS
TSTR:コマンド名受渡 = スキンシップ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_302
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_302
LOCALS = <br>■スキンシップ<br>
LOCALS += "軽く体に触れてコミュニケーションを取るコマンド。一定確率で大成功、成功、失敗に分岐。<br>一度実行した後は１時間のインターバルが必要。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽・征服・接触・露出・不快(失敗時)"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM302
;スキンシップ
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF
;先制発生時は失敗しない
SIF TCVAR:20
	RFLAG:コマンド結果受渡し変数 = MAX(RFLAG:コマンド結果受渡し変数,0)

DOWNBASE:気力 += 30
DOWNBASE:MASTER:気力 += 30

;歓楽強度
LOCAL:0 = 200
;征服
LOCAL:1 = 100
IF RFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF


CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
IF RFLAG:コマンド結果受渡し変数 == -1
	CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
ELSE
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, LOCAL:0 / 5)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, LOCAL:0 / 3)
ENDIF
TIME += 15

;スキンシップしたフラグ
TCVAR:スキンシップ時刻 = 1440 * DAY + TIME
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE302
;スキンシップ実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(302)
	RETURN RESULT
;1時間間隔
SIF TIME_PROGRESS(TCVAR:スキンシップ時刻) < 60
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM302
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の背中を流した
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%とのスキンシップを図った
ENDIF
IF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORML %CALLNAME:TARGET%は何だか嬉しそうにしている…
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	PRINTFORML %CALLNAME:TARGET%もまんざらでもなさそうだ…
ELSEIF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORML …どうやら、少し馴れ馴れしくしすぎたようだ…
ENDIF

