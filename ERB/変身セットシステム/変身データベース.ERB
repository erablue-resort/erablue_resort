@CREATE変身データベース
DT_CREATE "変身データベース"
DT_COLUMN_ADD "変身データベース", "キャラ番号", 2
DT_COLUMN_ADD "変身データベース", "元NAME"
DT_COLUMN_ADD "変身データベース", "元CALLNAME"
DT_COLUMN_ADD "変身データベース", "変身NAME"
DT_COLUMN_ADD "変身データベース", "変身CALLNAME"
DT_COLUMN_ADD "変身データベース", "変身タイミング文字列"
DT_COLUMN_ADD "変身データベース", "解除タイミング文字列"
DT_COLUMN_ADD "変身データベース", "変身コマンド名"
DT_COLUMN_ADD "変身データベース", "変身解除コマンド名"
DT_COLUMN_ADD "変身データベース", "変身期間", 1
DT_COLUMN_ADD "変身データベース", "変身残りターン数", 1
DT_COLUMN_ADD "変身データベース", "変身コスト", 1

@RELEASE変身データベース
DT_RELEASE "変身データベース"

@INSERT変身データベース(キャラ番号)
;指定のキャラ番号の設定ファイルを読み込んでデータベースに登録する
;重複登録はしない
#DIM キャラ番号

IF DT_SELECT("変身データベース", @"キャラ番号 = {キャラ番号}") 
    ;登録済み
    RETURN 1
ENDIF

TRYCCALLFORM 変身設定_{NO:キャラ番号}
    {
        DT_ROW_ADD "変身データベース", 
            "キャラ番号", 変身配列INT:1, 
            "元NAME", 変身配列STR:0, 
            "元CALLNAME", 変身配列STR:1, 
            "変身NAME", 変身配列STR:2, 
            "変身CALLNAME", 変身配列STR:3, 
            "変身タイミング文字列", 変身配列STR:4, 
            "解除タイミング文字列", 変身配列STR:5, 
            "変身コマンド名", (変身配列STR:6 != "") ? 変身配列STR:6 # "変身", 
            "変身解除コマンド名", (変身配列STR:7 != "") ? 変身配列STR:7 # "変身解除", 
            "変身期間", 変身配列INT:2, 
            "変身残りターン数", 変身配列INT:3,
            "変身コスト", 変身配列INT:4
    }
    RETURN 1
CATCH
    ;変身設定がない
    RETURN 0
ENDCATCH

@UPDATE変身データベース(キャラ番号)
;列IDを維持する必要はないのでREMOVEしてINSERTする
#DIM キャラ番号

CALL REMOVE変身データベース(キャラ番号)
CALL INSERT変身データベース(キャラ番号)

@REMOVE変身データベース(キャラ番号)
#DIM キャラ番号
DT_SELECT "変身データベース", @"キャラ番号 = {キャラ番号}"
DT_ROW_REMOVE "変身データベース", RESULT:1

@GET変身データベース(キャラ番号)
;データベース情報を配列に読み込む
#DIM キャラ番号
DT_SELECT "変身データベース", @"キャラ番号 = {キャラ番号}"
変身配列INT:0 = RESULT:1
変身配列INT:1 = DT_CELL_GET("変身データベース", 変身配列INT, "キャラ番号", 1)
変身配列STR:0 '= DT_CELL_GETS("変身データベース", 変身配列INT, "元NAME", 1)
変身配列STR:1 '= DT_CELL_GETS("変身データベース", 変身配列INT, "元CALLNAME", 1)
変身配列STR:2 '= DT_CELL_GETS("変身データベース", 変身配列INT, "変身NAME", 1)
変身配列STR:3 '= DT_CELL_GETS("変身データベース", 変身配列INT, "変身CALLNAME", 1)
変身配列STR:4 '= DT_CELL_GETS("変身データベース", 変身配列INT, "変身タイミング文字列", 1)
変身配列STR:5 '= DT_CELL_GETS("変身データベース", 変身配列INT, "解除タイミング文字列", 1)
変身配列STR:6 '= DT_CELL_GETS("変身データベース", 変身配列INT, "変身コマンド名", 1)
変身配列STR:7 '= DT_CELL_GETS("変身データベース", 変身配列INT, "変身解除コマンド名", 1)
変身配列INT:2 = DT_CELL_GET("変身データベース", 変身配列INT, "変身期間", 1)
変身配列INT:3 = DT_CELL_GET("変身データベース", 変身配列INT, "変身残りターン数", 1)
変身配列INT:4 = DT_CELL_GET("変身データベース", 変身配列INT, "変身コスト", 1)

@SET変身データベース
;配列の値をデータベースに上書きする
;基本的にはGET変身データベースで引き出した列を戻す操作
{
    DT_ROW_SET "変身データベース", 変身配列INT:0, 
        "キャラ番号", 変身配列INT:1, 
        "元NAME", 変身配列STR:0, 
        "元CALLNAME", 変身配列STR:1, 
        "変身NAME", 変身配列STR:2, 
        "変身CALLNAME", 変身配列STR:3, 
        "変身タイミング文字列", 変身配列STR:4, 
        "解除タイミング文字列", 変身配列STR:5, 
        "変身コマンド名", (変身配列STR:6 != "") ? 変身配列STR:6 # "変身", 
        "変身解除コマンド名", (変身配列STR:7 != "") ? 変身配列STR:7 # "変身解除", 
        "変身期間", 変身配列INT:2, 
        "変身残りターン数", 変身配列INT:3,
        "変身コスト", 変身配列INT:4
}
