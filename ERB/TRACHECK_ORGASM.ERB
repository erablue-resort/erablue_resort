;────────────────────────────────────
;絶頂系の処理など
;────────────────────────────────────
;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
@ORGASM_ADD(ARG)
#DIM 絶頂残値
#DIM 強度一時保存
#DIM ソート用配列, 5
#DIM _C
#DIM _V
#DIM _A
#DIM _B
#DIM _S
#DIM _L

_C = 0
_V = 0
_A = 0
_B = 0
_S = 0

絶頂残値 = 絶頂残値算出処理(ARG)


;絶頂Ｃ
NOWEX:ARG:Ｃ絶頂 = 0
DO
	IF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ) / PALAMLV:4
		NOWEX:ARG:Ｃ絶頂 += 強度一時保存
		_C = NOWEX:ARG:Ｃ絶頂
		CDOWN:ARG:快Ｃ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
;絶頂Ｖ
NOWEX:ARG:Ｖ絶頂 = 0
DO
	IF CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ) / PALAMLV:4
		NOWEX:ARG:Ｖ絶頂 += 強度一時保存
		_V = NOWEX:ARG:Ｖ絶頂
		CDOWN:ARG:快Ｖ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ >= PALAMLV:4
;絶頂Ａ
NOWEX:ARG:Ａ絶頂 = 0
DO
	IF CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ) / PALAMLV:4
		NOWEX:ARG:Ａ絶頂 += 強度一時保存
		_A = NOWEX:ARG:Ａ絶頂
		CDOWN:ARG:快Ａ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ >= PALAMLV:4
;絶頂Ｂ
NOWEX:ARG:Ｂ絶頂 = 0
DO
	IF CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ) / PALAMLV:4
		NOWEX:ARG:Ｂ絶頂 += 強度一時保存
		_B = NOWEX:ARG:Ｂ絶頂
		CDOWN:ARG:快Ｂ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ >= PALAMLV:4
;絶頂Ｓ
NOWEX:ARG:Ｓ絶頂 = 0
DO
	IF CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ) / PALAMLV:4
		NOWEX:ARG:Ｓ絶頂 += 強度一時保存
		_S = NOWEX:ARG:Ｓ絶頂
		CDOWN:ARG:快Ｓ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ >= PALAMLV:4


; ;絶頂Ｃ
; IF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 5
; 	_C = 5
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 5 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 5
; 	;PRINTFORML 究極絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 4
; 	_C = 4
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 4 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 4
; 	;PRINTFORML 最強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 3
; 	_C = 3
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 3 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 3
; 	;PRINTFORML 超強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 2
; 	_C = 2
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 2 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 2
; 	;PRINTFORML 強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4
; 	_C = 1
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 1
; 	;PRINTFORML 絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ENDIF
; ;CDOWN:ARG:0で下げても絶頂以上なら
; ;その値-1になるように調整（10000で絶頂なら9999）
; SIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
; 	CDOWN:ARG:快Ｃ = CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - PALAMLV:4 + 1

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------
RCVAR:ARG:絶頂の強度 = _C + _V + _A + _B + _S
IF ARG == PLAYER
	TFLAG:調教者絶頂強度 = _C + _V + _A + _B + _S
ENDIF
IF ARG == TARGET
	;快楽刻印の処理
	IF RCVAR:ARG:絶頂の強度 > 3 && MARK:ARG:1 < 3
		RCVAR:ARG:快楽強度 = 3
	;Rが3以上で快楽刻印２に相当
	ELSEIF RCVAR:ARG:絶頂の強度 > 2 && MARK:ARG:1 < 2
		RCVAR:ARG:快楽強度 = 2
	;Rが2以上で快楽刻印１に相当
	ELSEIF RCVAR:ARG:絶頂の強度 > 1 && MARK:ARG:1 < 1
		RCVAR:ARG:快楽強度 = 1
	ELSEIF RCVAR:ARG:絶頂の強度 >= 1
		RCVAR:ARG:快楽強度 = 0
	ENDIF
ENDIF

;オナニーの場合はここを通らないので、通った時点で＝あなたによる絶頂とする
;例外はジータ-ルリアの感覚共有だけなのでそこだけ弾く
IF ARG != MASTER && PLAYER == MASTER && RCVAR:ARG:絶頂の強度 > 0
	IF !CFLAG:ARG:快楽計算フラグ || CFLAG:ARG:うふふ
		IF 初体験日:ARG:あなたの手で初絶頂 == 0
			初体験日:ARG:あなたの手で初絶頂 = DAY
			CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:ARG%を絶頂させた</font>","うふふ")
			CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:MASTER%の手で絶頂した</font>","うふふ")
		ENDIF
	ENDIF
ENDIF

;同時絶頂による補正
_L = 0
SELECTCASE MIN(_C, _V, _A, _B, _S)
	CASE IS >= 5
		_L = 5
		_C *= 60
		_V *= 60
		_A *= 60
		_B *= 60
		_S *= 60
	CASE 4
		_L = 5
		_C *= 48
		_V *= 48
		_A *= 48
		_B *= 48
		_S *= 48
	CASE 3
		_L = 5
		_C *= 36
		_V *= 36
		_A *= 36
		_B *= 36
		_S *= 36
	CASE 2
		_L = 5
		_C *= 24
		_V *= 24
		_A *= 24
		_B *= 24
		_S *= 24
	CASE 1
		_L = 5
		_C *= 12
		_V *= 12
		_A *= 12
		_B *= 12
		_S *= 12
	CASEELSE
		;絶頂状態部位がいくつあるかを検索
		SELECTCASE GROUPMATCH(0, _C, _V, _A, _B, _S)
			CASE 1
				_L = 4
				LOCAL = 8
			CASE 2
				_L = 3
				LOCAL = 4
			CASE 3
				_L = 2
				LOCAL = 2
			CASE 4
				_L = 1
				LOCAL = 1
			ENDSELECT
		_C *= LOCAL
		_V *= LOCAL
		_A *= LOCAL
		_B *= LOCAL
		_S *= LOCAL
ENDSELECT

IF _C
	SOURCE:ARG:露出 += 500 * _C
	SOURCE:ARG:屈従 += 200 * _C
	CUP:ARG:潤滑 += 1000 * _C
ENDIF
IF _V
	SOURCE:ARG:欲情 += 800 * _V
	SOURCE:ARG:恭順 += 500 * _V
	SOURCE:ARG:露出 += 700 * _V
	SOURCE:ARG:屈従 += 400 * _V
	CUP:ARG:潤滑 += 1500 * _V
ENDIF
IF _A
	SOURCE:ARG:欲情 += 500 * _A
	SOURCE:ARG:恭順 += 700 * _A
	SOURCE:ARG:露出 += 900 * _A
	SOURCE:ARG:屈従 += 500 * _A
	SOURCE:ARG:逸脱 += 200 * _A
	CUP:ARG:潤滑 += 1500 * _A
ENDIF
IF _B
	SOURCE:ARG:露出 += 500 * _B
	SOURCE:ARG:屈従 += 200 * _B
	CUP:ARG:潤滑 += 800 * _B
	SIF TALENT:ARG:性別  == 10
		SOURCE:ARG:逸脱 += 100 * _B
ENDIF
IF _S
	SOURCE:ARG:露出 += 500 * _S
	SOURCE:ARG:屈従 += 200 * _S
	SOURCE:ARG:逸脱 += 100 * _S
	CUP:ARG:潤滑 += 500 * _S
ENDIF

NOWEX:ARG:多重絶頂 = _L

;スコアを増やす
ソート用配列 = NOWEX:ARG:Ｃ絶頂, NOWEX:ARG:Ｖ絶頂, NOWEX:ARG:Ａ絶頂, NOWEX:ARG:Ｂ絶頂, NOWEX:ARG:Ｓ絶頂
ARRAYSORT ソート用配列, BACK
NOWEX:ARG:スコア += SUMARRAY(ソート用配列, 0, 4) * POWER(2, MIN(3, NOWEX:ARG:多重絶頂 - 1)) + ソート用配列:4

;絶頂回数を増やす
EX:ARG:Ｃ絶頂 += NOWEX:ARG:Ｃ絶頂
EX:ARG:Ｖ絶頂 += NOWEX:ARG:Ｖ絶頂
EX:ARG:Ａ絶頂 += NOWEX:ARG:Ａ絶頂
EX:ARG:Ｂ絶頂 += NOWEX:ARG:Ｂ絶頂
EX:ARG:Ｓ絶頂 += NOWEX:ARG:Ｓ絶頂
EX:ARG:多重絶頂 += _L

;絶頂経験を増やす
EXP:ARG:絶頂経験 += RCVAR:ARG:絶頂の強度
EXP:ARG:Ｃ絶頂経験 += NOWEX:ARG:Ｃ絶頂
EXP:ARG:Ｖ絶頂経験 += NOWEX:ARG:Ｖ絶頂
EXP:ARG:Ａ絶頂経験 += NOWEX:ARG:Ａ絶頂
EXP:ARG:Ｂ絶頂経験 += NOWEX:ARG:Ｂ絶頂
EXP:ARG:Ｓ絶頂経験 += NOWEX:ARG:Ｓ絶頂
SIF RCVAR:ARG:露出コマンドフラグ
	EXP:ARG:露出絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:苦痛コマンドフラグ && CUP:ARG:苦痛 >= MAX(1, 100 - (ABL:ARG:マゾ性感 * 20))
	EXP:ARG:苦痛絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:嗜虐コマンドフラグ
	EXP:ARG:嗜虐絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:奉仕コマンドフラグ
	EXP:ARG:奉仕絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:道具コマンドフラグ
	EXP:ARG:道具絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:触手コマンドフラグ
	EXP:ARG:触手絶頂経験 += RCVAR:ARG:絶頂の強度
SIF MODE_存在判定_ターゲット側("首絞め", ARG)
	EXP:ARG:首絞め絶頂経験 += RCVAR:ARG:絶頂の強度
IF MODE_存在判定_ターゲット側("Ｃ自慰系", ARG) && NOWEX:ARG:Ｃ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ｖ自慰系", ARG) && NOWEX:ARG:Ｖ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ａ自慰系", ARG) && NOWEX:ARG:Ａ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ｂ自慰系", ARG) && NOWEX:ARG:Ｂ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF

; ;絶頂による欲望ＬＶアップ
; IF ARG == TARGET && ARG
; 	;自制心があると１下がる
; 	SIF TALENT:ARG:自制心
; 		_L -= 1
; 	IF ABL:ARG:欲望 < _L
; 		ABL:ARG:欲望 = _L
; 		PRINTFORML そして欲望がLV{_L}になった
; 	ENDIF
; ENDIF

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------

;射精
IF TALENT:ARG:性別 & 2 && ARG == PLAYER
	CALL 射精処理_コマンド(ARG, TFLAG:調教者絶頂強度)
	SIF RESULT > 0
		CALL 勃起度下降_射精時(ARG)
ELSEIF TALENT:ARG:性別 & 2
	CALL 射精処理_コマンド(ARG, RCVAR:ARG:絶頂の強度, RCVAR:ARG:前立腺開発フラグ)
	SIF RESULT > 0
		CALL 勃起度下降_射精時(ARG)
	CALL 射精体力減少処理(ARG, PLAYER)
ENDIF

;ZPの取得
IF RCVAR:ARG:絶頂の強度 > 0
	CALL 絶頂体力減少処理(ARG, NOWEX:ARG:Ｃ絶頂, NOWEX:ARG:Ｖ絶頂, NOWEX:ARG:Ａ絶頂, NOWEX:ARG:Ｂ絶頂, NOWEX:ARG:Ｓ絶頂)
	CALL ZP_取得処理(ARG, RCVAR:ARG:絶頂の強度)
	CFLAG:ARG:体力成長値 += RCVAR:ARG:絶頂の強度 * 5
ENDIF

;連れ込み状態時、満足したか
;ついでに中毒欲求不満の判定も行う
CALL 中毒連れ込み判定_逆アナル(ARG)
IF RESULT
	IF NOWEX:ARG:Ｃ絶頂 && MODE_存在判定_完全一致("Ａ挿入系", ARG, MASTER)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "逆アナル"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF
CALL 中毒連れ込み判定_搾乳プレイ(ARG)
IF RESULT
	IF NOWEX:ARG:Ｂ絶頂 && MODE_存在判定_ターゲット側("搾乳機", ARG)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "搾乳プレイ"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF
CALL 中毒連れ込み判定_触手プレイ(ARG)
IF RESULT
	IF RCVAR:ARG:絶頂の強度 && MODE_存在判定_ターゲット側("触手系", ARG)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "触手プレイ"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF
CALL 中毒連れ込み判定_首絞め(ARG)
IF RESULT
	IF RCVAR:ARG:絶頂の強度 && MODE_存在判定_ターゲット側("首絞め", ARG)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "首絞め"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF

IF 連れ込みパターン == "一般" || 連れ込みパターン == "発情期"
	SIF RCVAR:ARG:絶頂の強度 + TCVAR:ARG:うふふ中Ｃ絶頂累積 + TCVAR:ARG:うふふ中Ｂ絶頂累積 + TCVAR:ARG:うふふ中Ｖ絶頂累積 + TCVAR:ARG:うふふ中Ａ絶頂累積 + TCVAR:ARG:うふふ中Ｓ絶頂累積 > 2
		RCVAR:ARG:連れ込み満足フラグ = 1
ENDIF

RETURN 1


@MESSAGE_PALAMCNG_絶頂強度表示(対象キャラ)
#DIM 対象キャラ
IF NOWEX:対象キャラ:多重絶頂 > 3
	DRAWLINE
	PRINTFORML 昇　天(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 > 2
	DRAWLINE
	PRINTFORML 超強絶頂(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 > 1
	DRAWLINE
	PRINTFORML 強絶頂(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 >= 1
	DRAWLINE
	PRINTFORML 絶頂(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:噴乳 > 1
	DRAWLINE
	PRINTFORML 大量噴乳(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:噴乳 > 0
	DRAWLINE
	PRINTFORML 噴乳(%CALLNAME:対象キャラ%)
ENDIF

SIF 対象キャラ == MASTER
	RETURN 0

;絶頂による欲望ＬＶアップ
LOCAL = NOWEX:対象キャラ:多重絶頂
;自制心があると１下がる
SIF TALENT:対象キャラ:自制心
	LOCAL -= 1

IF ABL:対象キャラ:欲望 < LOCAL
	ABL:対象キャラ:欲望 ++
	PRINTFORML そして%CALLNAME:対象キャラ%の欲望がLV{LOCAL}になった
ENDIF
;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
@TARGET_MILK_CHECK(ARG)
SIF !TALENT:ARG:母乳体質
	RETURN 0

LOCAL = CUP:ARG:快Ｃ / 5 + CUP:ARG:快Ｖ / 5 + CUP:ARG:快Ａ / 5 + CUP:ARG:快Ｂ * 3

;自制心
SIF TALENT:ARG:自制心
	LOCAL /= 2

;快感に素直
SIF TALENT:ARG:快感応答 > 0
	TIMES LOCAL , 1.20

;快感の否定
SIF TALENT:ARG:快感応答 < 0 && !TALENT:ARG:淫乱
	TIMES LOCAL , 0.80

;B敏感
SIF TALENT:ARG:Ｂ感度 > 0
	TIMES LOCAL , 1.20

;媚薬
SIF TEQUIP:ARG:媚薬
	LOCAL *= 2

;利尿剤
SIF TEQUIP:ARG:利尿剤
	LOCAL /= 2

;調教者が幼児退行
SIF TALENT:PLAYER:幼児／幼児退行
	TIMES LOCAL , 1.20

;調教者が幼稚
SIF TALENT:PLAYER:幼稚
	TIMES LOCAL , 1.20

;貧乳
SIF TALENT:ARG:バストサイズ < 0
	TIMES LOCAL , 0.50
LOCAL = TIME_PROGRESS(TFLAG:行動前時刻) * 2 + LOCAL / 2
BASE:ARG:母乳 += LOCAL
SIF !CFLAG:ARG:うふふ
	BASE:ARG:母乳 = MIN(BASE:ARG:母乳, MAXBASE:ARG:母乳 - 1)

IF  BASE:ARG:母乳 > MAXBASE:ARG:母乳 * 2
	LOCAL = 1
ELSEIF BASE:ARG:母乳 > MAXBASE:ARG:母乳
	LOCAL = 0
ELSE
	RETURN 0
ENDIF
LOCAL ++
IF EXP:ARG:噴乳経験 < EXPLV:1
	SOURCE:ARG:露出 += 5000 * LOCAL
	SOURCE:ARG:屈従 += 2500 * LOCAL
	SOURCE:ARG:不潔 += 1000 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:2
	SOURCE:ARG:露出 += 2500 * LOCAL
	SOURCE:ARG:屈従 += 1500 * LOCAL
	SOURCE:ARG:不潔 += 750 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:3
	SOURCE:ARG:露出 += 1500 * LOCAL
	SOURCE:ARG:屈従 += 750 * LOCAL
	SOURCE:ARG:不潔 += 500 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:4
	SOURCE:ARG:露出 += 750 * LOCAL
	SOURCE:ARG:屈従 += 400 * LOCAL
	SOURCE:ARG:不潔 += 350 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:5
	SOURCE:ARG:露出 += 500 * LOCAL
	SOURCE:ARG:屈従 += 250 * LOCAL
	SOURCE:ARG:不潔 += 200 * LOCAL
ELSE
	SOURCE:ARG:露出 += 250 * LOCAL
	SOURCE:ARG:屈従 += 150 * LOCAL
ENDIF
; PRINTFORML %CALLNAME:ARG%は乳首から\@ LOCAL == 2 ? 大量の # \@母乳を噴出した
EXP:ARG:噴乳経験 += LOCAL
;Ｂに母乳汚れ
SIF BASE:ARG:母乳 >= MAXBASE:ARG:母乳
	BASE:ARG:母乳 = MAXBASE:ARG:母乳 - 1
NOWEX:ARG:噴乳 += LOCAL
EX:ARG:噴乳 += LOCAL
NOWEX:ARG:スコア += LOCAL

; ;ビデオ
; IF TEQUIP:ARG:ビデオ撮影
; 	IF NOWEX:ARG:噴乳 == 1
; 		LOCALS = MILK/
; 	ELSEIF NOWEX:ARG:噴乳 == 2
; 		LOCALS = MILK/MILK/
; 	ENDIF
; 	TSTR:1 += LOCALS
; ENDIF


@射精体力減少処理(射精キャラ, 実行キャラ)
#DIM 射精キャラ
#DIM 実行キャラ
#DIM 確保精力

;通常は射精で体力は消費しない
;あなたが相手かつ[エナジードレイン]所持の場合のみ
SIF 実行キャラ != MASTER || !あなた特殊能力:エナジードレイン
	RETURN
SIF !あなた特殊能力_発揮条件_エナジードレイン()
	RETURN

;1回万全で射精できるだけの精力を体力を削って確保させる
確保精力 = (最大射精量(射精キャラ) * 4 + 2) / 3
SIF 確保精力 <= BASE:射精キャラ:精力
	RETURN

DOWNBASE:射精キャラ:体力 += (確保精力 - BASE:射精キャラ:精力) * 5
BASE:射精キャラ:精力 = 確保精力
EXP:射精キャラ:空打ち絶頂経験 ++


@絶頂体力減少処理(キャラ番号, Ｃ絶頂強度, Ｖ絶頂強度, Ａ絶頂強度, Ｂ絶頂強度, Ｓ絶頂強度)
#DIM キャラ番号
#DIM Ｃ絶頂強度
#DIM Ｖ絶頂強度
#DIM Ａ絶頂強度
#DIM Ｂ絶頂強度
#DIM Ｓ絶頂強度
#DIM 絶頂保存配列, 5
#DIM 部位番号
#DIM 体力減少量計算値
#DIM 追加体力減少量計算値
#DIM 最大体力減少量
#DIM 最大絶頂部位

絶頂保存配列:0 = Ｃ絶頂強度
絶頂保存配列:1 = Ｖ絶頂強度
絶頂保存配列:2 = Ａ絶頂強度
絶頂保存配列:3 = Ｂ絶頂強度
絶頂保存配列:4 = Ｓ絶頂強度

追加体力減少量計算値 = 0
FOR 部位番号, 0, 5
	IF 絶頂保存配列:部位番号 > 0
		SELECTCASE 部位番号
			CASE 0
				IF GETBIT(TALENT:キャラ番号:性別, 1)
					;男性　基礎150
					体力減少量計算値 = 150 - EXP:キャラ番号:Ｃ絶頂経験
				ELSE
					;女性　基礎80
					体力減少量計算値 = 80 - EXP:キャラ番号:Ｃ絶頂経験
				ENDIF
			CASE 1
				;基礎150
				体力減少量計算値 = 150 - EXP:キャラ番号:Ｖ絶頂経験
				IF RCVAR:キャラ番号:Ｇスポ開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:Ｇスポ開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:ポルチオ開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:ポルチオ開発 / 2)
				ENDIF
			CASE 2
				IF GETBIT(TALENT:キャラ番号:性別, 1)
					;男性　基礎150
					体力減少量計算値 = 150 - EXP:キャラ番号:Ａ絶頂経験
				ELSE
					;女性　基礎200
					体力減少量計算値 = 200 - EXP:キャラ番号:Ａ絶頂経験
				ENDIF
				IF RCVAR:キャラ番号:前立腺開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:前立腺開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:Ｓ字結腸開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:Ｓ字結腸開発 / 2)
				ENDIF
			CASE 3
				IF !GETBIT(TALENT:キャラ番号:性別, 0)
					;男性　基礎250
					体力減少量計算値 = 250 - EXP:キャラ番号:Ｂ絶頂経験
				ELSE
					;女性　基礎100
					体力減少量計算値 = 100 - EXP:キャラ番号:Ｂ絶頂経験
				ENDIF
				IF RCVAR:キャラ番号:乳首開発フラグ
					追加体力減少量計算値 += MAX(0, 100 - EXP:キャラ番号:乳首開発 / 2)
				ENDIF
			CASE 4
				;基礎250
				体力減少量計算値 = 250 - EXP:キャラ番号:Ｓ絶頂経験
				IF RCVAR:キャラ番号:角開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:角開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:尻尾開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:尻尾開発 / 2)
				ENDIF
		ENDSELECT
		;経験によって減らせるのは50まで
		体力減少量計算値 = MAX(50, 体力減少量計算値)
		体力減少量計算値 += 追加体力減少量計算値

		;素質による上下
		体力減少量計算値 -= TALENT:キャラ番号:性欲 * 5
		体力減少量計算値 -= TALENT:キャラ番号:快感応答 * 5

		;発情期
		SIF CFLAG:キャラ番号:発情期フラグ < 0
			体力減少量計算値 -= 20

		;最低値は20
		体力減少量計算値 = MAX(20, 体力減少量計算値)

		IF 絶頂保存配列:部位番号 > 1
			;強い絶頂の分は1/4して計算
			体力減少量計算値 = 体力減少量計算値 + (体力減少量計算値 * (絶頂保存配列:部位番号 - 1) / 4)
		ENDIF

		;最後に配列に計算値を保存
		絶頂保存配列:部位番号 = 体力減少量計算値
	ENDIF
	IF 部位番号 == 0 || 絶頂保存配列:部位番号 > 最大体力減少量
		最大体力減少量 = 絶頂保存配列:部位番号
		最大絶頂部位 = 部位番号
	ENDIF
NEXT

IF MATCH(絶頂保存配列, 0) < 4
	;他部位絶頂パターン
	;最も大きい絶頂をそのままにし、他の絶頂計算値を1/2にして合算する
	FOR 部位番号, 0, 5
		SIF 最大絶頂部位 != 部位番号
			絶頂保存配列:部位番号 = 絶頂保存配列:部位番号 / 2
	NEXT
ENDIF
体力減少量計算値 = SUMARRAY(絶頂保存配列)
体力減少量計算値 = 首絞め補正_消費体力(キャラ番号, 体力減少量計算値)

DOWNBASE:キャラ番号:体力 += 体力減少量計算値
TCVAR:キャラ番号:うふふ中消費体力累積 += 体力減少量計算値
CALL 満足ゲージ上昇処理(キャラ番号)


@絶頂相手算出(ARG)
#FUNCTION
#DIM 絶頂部位
#DIM 最大絶頂強度
#DIM モード絶頂相手
;あなた以外も色々動くようになったので、状況に合わせて口上用に絶頂相手を決定する

絶頂部位 = -1
最大絶頂強度 = 0
FOR LOCAL, 0, 5
	IF NOWEX:ARG:LOCAL > 最大絶頂強度
		絶頂部位 = LOCAL
		最大絶頂強度 = NOWEX:ARG:LOCAL
	ENDIF
NEXT

モード絶頂相手 = MODE_絶頂相手(ARG, 絶頂部位)
IF モード絶頂相手 < 0
	;モードから絶頂相手が決まらない場合
	IF PLAYER == ARG && TARGET == MASTER
		;コマンド実行者が自分、かつ対象がMASTERの場合
		;自慰・奉仕をMASTERが指示したケース
		RETURNF MASTER
	ELSE
		;それ以外はコマンド実行者(PLAYER)
		RETURNF PLAYER
	ENDIF
ELSE
	IF モード絶頂相手 == ARG && (PLAYER == MASTER || (PLAYER == ARG && TARGET == MASTER))
		;絶頂が自分自身によるもの、かつコマンド実行にMASTERが絡む場合
		;自慰・奉仕をMASTERが指示したケース
		RETURNF MASTER
	ELSE
		;それ以外はモードから求めた絶頂相手
		RETURNF モード絶頂相手
	ENDIF
ENDIF


@絶頂残値算出処理(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 絶頂残値

;基礎は1000
絶頂残値 = 1000

;イキやすさ素質で分岐
SELECTCASE TALENT:対象キャラ:イキやすさ
	CASE -1
		絶頂残値 -= 3000
	CASE 1
		絶頂残値 += 2000
	CASE 2
		絶頂残値 += 5000
ENDSELECT

;発情期は+2000
SIF CFLAG:対象キャラ:発情期フラグ < 0
	絶頂残値 += 2000

RETURNF 絶頂残値
