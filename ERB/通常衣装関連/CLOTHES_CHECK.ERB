@CLOTHES_CHECK(対象キャラ)
#DIM 対象キャラ
#DIM 着替え済みフラグ

着替え済みフラグ = 0

SIF !INRANGE(対象キャラ, 0, CHARANUM-1)
	RETURN -1

SIF TCVAR:対象キャラ:自動着替え禁止
	RETURN -1

SIF CFLAG:対象キャラ:キャラ同士うふふ
	RETURN -1

;風呂場から出てきたら浴衣になる
IF !BATHROOM(CFLAG:対象キャラ:現在マップ種別, CFLAG:対象キャラ:現在位置) && TCVAR:対象キャラ:入浴フラグ
	TRYCCALLFORM 風呂後着替え特殊処理_{NO:対象キャラ}(対象キャラ)
	CATCH
;		CALL CLOTHES_CHANGE(対象キャラ, "浴衣")
;		CSTR:対象キャラ:湯浴み着ポジション服名 = 浴衣
		CALL CLOTHES_CHANGE(対象キャラ, CSTR:対象キャラ:今日の湯上がり)
		CSTR:対象キャラ:浴衣ポジション服名 '= CSTR:対象キャラ:今日の湯上がり
	ENDCATCH
	着替え済みフラグ = 1
ENDIF

;水着エリアでは水着
IF SWIMROOM(CFLAG:対象キャラ:現在マップ種別, CFLAG:対象キャラ:現在位置) || TCVAR:対象キャラ:水着着替え状態
	IF フィールド展開:水泳時全裸化
		IF CSTR:対象キャラ:着せ替え服 != "全裸"
			CALL CLOTHES_CHANGE(対象キャラ, "全裸")
			CSTR:対象キャラ:水着ポジション服名 = 全裸
		ENDIF
		;水着全裸フィールド時はこれ以降の変動は許容しない
		RETURN 0
	ELSE
		IF CSTR:対象キャラ:着せ替え服 != "水着"
			TRYCCALLFORM 水着着替え特殊処理_{NO:対象キャラ}(対象キャラ)
			CATCH
				CALL CLOTHES_CHANGE(対象キャラ, "水着")
				CSTR:対象キャラ:水着ポジション服名 = 水着
			ENDCATCH
		ENDIF
	ENDIF
	着替え済みフラグ = 2
ENDIF

;風呂場では湯浴み着になる
IF BATHROOM(CFLAG:対象キャラ:現在マップ種別, CFLAG:対象キャラ:現在位置)
	IF CSTR:対象キャラ:着せ替え服 != "湯浴み着"
		TRYCCALLFORM 風呂場着替え特殊処理_{NO:対象キャラ}(対象キャラ)
		CATCH
			CALL CLOTHES_CHANGE(対象キャラ, "湯浴み着")
			CSTR:対象キャラ:湯浴み着ポジション服名 = 湯浴み着
		ENDCATCH
		TCVAR:対象キャラ:入浴フラグ = 1
	ENDIF
	着替え済みフラグ = 1
ENDIF

;自室では部屋着になる
IF CFLAG:対象キャラ:現在位置 == キャラクター部屋検索(対象キャラ) && CFLAG:対象キャラ:現在マップ種別 == 部屋検索_マップ種別
	IF CSTR:対象キャラ:着せ替え服 != "部屋着"
		TRYCCALLFORM 自室着替え特殊処理_{NO:対象キャラ}(対象キャラ)
		CATCH
			CALL CLOTHES_CHANGE(対象キャラ, "部屋着")
			CSTR:対象キャラ:部屋着ポジション服名 = 部屋着
		ENDCATCH
	ENDIF
	着替え済みフラグ = 1
ENDIF

IF 着替え済みフラグ != 2 && フィールド展開:常時全裸
	;水着時以外に全裸フィールド影響
	IF TALENT:対象キャラ:従業員 && 着替え済みフラグ == 0
		;従業員は制服着用パターンのみ制服優先
	ELSE
		CALL CLOTHES_CHANGE(対象キャラ, "全裸")
		着替え済みフラグ = 1
	ENDIF
ENDIF

;自室から出たら普段着に戻る
IF 着替え済みフラグ == 0
	CSTR:対象キャラ:着せ替え服 = 
	CALL CLOTHES_RESET_TRAIN(対象キャラ)
ENDIF

;		CALL CLOTHES_CHANGE(対象キャラ, CSTR:対象キャラ:今日の湯上がり)
;		CSTR:対象キャラ:浴衣ポジション服名 '= CSTR:対象キャラ:今日の湯上がり
