;-------------------------------------------------
;時間経過
;-------------------------------------------------
@TRACHECK_TIME
SELECTCASE SAVESTR:ゲームフェイズ管理
	CASE "通常モード"
		IF TFLAG:オートコマンドフラグ > 0
			;オートコマンド時は時間経過しない
		ELSEIF RSTR:SELECTCOM_S受け渡し != ""
			;うふふ系の派生は5分刻み
			SPLIT RSTR:SELECTCOM_S受け渡し, "_", LOCALS
			IF ISNUMERIC(LOCALS) && TOINT(LOCALS) < 200
				TIME += 5
			ENDIF
		ELSEIF SELECTCOM < 200
			IF PLAYER >= 0 && TALENT:PLAYER:絶倫 && RAND:3 == 0
			ELSE
				;めんどくさいので5分刻み
				TIME += 5
			ENDIF
		ENDIF
	CASE "マッサージモード"
		;めんどくさいので5分刻み
		TIME += 5
ENDSELECT
;-------------------------------------------------
;処女喪失時の処理
;ARG		処女喪失対象
;ARG:1	処女喪失させる人
;ARG:2	処女喪失手段(=1 チンコ	=2 バイブ	=3 指	=4 触手)
;-------------------------------------------------
@LOST_VIRGIN(ARG, ARG:1, ARG:2)

;処女以外の初挿入処理
IF ARG:2 == 1
	IF ARG:1 < 0 && STRFIND(経験相手:ARG:Ｖ挿入初体験, "_-1_") < 0
		経験相手:ARG:Ｖ挿入初体験 += "_-1_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて見知らぬ相手からＶ挿入された</font>","うふふ")
	ELSEIF STRFIND(経験相手:ARG:Ｖ挿入初体験, @"_{CFLAG:(ARG:1):人物番号}_") < 0
		SIF 初体験日:ARG:Ｖ挿入初体験 == 0 && ARG:1 == MASTER
			初体験日:ARG:Ｖ挿入初体験 = DAY
		経験相手:ARG:Ｖ挿入初体験 += @"_{CFLAG:(ARG:1):人物番号}_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:(ARG:1)%からＶ挿入された</font>","うふふ")
	ENDIF
ENDIF

;非処女はスルー
SIF !TALENT:ARG:処女
	RETURN 0
RCVAR:ARG:破瓜 = ARG:2
RCVAR:ARG:破瓜_相手番号 = ARG:1
VARSET LOCAL
;SOURCE追加
IF TALENT:ARG:処女 == 1
	PRINTFORM 処女喪失(%CALLNAME:ARG%)
	IF ARG:1 < 0
		CFLAG:LOCAL:処女相手番号 = ARG:1
	ELSE
		CFLAG:LOCAL:処女相手番号 = CFLAG:(ARG:1):人物番号
	ENDIF
	CFLAG:LOCAL:処女道具記録 = ARG:2
	;苦痛
	;チンコ
	IF ARG:2 == 1
		LOCAL = 2000
		IF ARG:1 < 0
			IF GETBIT(ABS(ARG:1), 1)
				PRINTW ：ペニス
			ELSE
				PRINTW ：ペニスバンド
				RCVAR:ARG:破瓜 = 5
			ENDIF
		ELSE
			IF GETBIT(TALENT:(ARG:1):性別, 1)
				PRINTW ：ペニス
			ELSE
				PRINTW ：ペニスバンド
				RCVAR:ARG:破瓜 = 5
			ENDIF
		ENDIF
	;バイブ
	ELSEIF ARG:2 == 2
		LOCAL = 1500
		PRINTW ：道具
	;指
	ELSEIF ARG:2 == 3
		LOCAL = 1000
		PRINTW ：指
	;触手
	ELSEIF ARG:2 == 4
		LOCAL = 2000
		PRINTW ：触手
	ENDIF
	LOCAL = LOCAL * PAIN_CHECK_V(ARG) / 1000
	CALL SOURCE_CALC_苦痛(ARG, ARG:1, LOCAL)
	;不潔
	;貞操観念
	IF TALENT:ARG:貞操 > 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
		;恋愛対象以外の相手
		ELSEIF ARG:1 != PLAYER
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 2000)
		;チンコじゃない
		ELSEIF ARG:2 != 1
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
		ENDIF
	ELSEIF TALENT:ARG:貞操 == 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
		;恋愛対象以外の相手
		ELSEIF ARG:1 != PLAYER
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
		;チンコじゃない
		ELSEIF ARG:2 != 1
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
		ENDIF
	ENDIF
	;陥落素質による処理
	;MASTERがTARGETの膣に挿入している
	IF ARG:1 == MASTER
		IF TALENT:ARG:貞操 > 0
			;非恋慕
			IF !TALENT:ARG:恋慕
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			ENDIF
		ELSEIF TALENT:ARG:貞操 == 0
			;非恋慕
			IF !TALENT:ARG:恋慕
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
			ENDIF
		ENDIF
		IF ARG:2 == 1
		;親愛かつ相手が主人
		IF TALENT:ARG:恋慕 > 1
			PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%が初めての相手である事にこの上ない幸せを感じているようだ……
			CALL SOURCE_CALC_歓楽(ARG, PLAYER, 5000)
		;恋慕かつ相手が主人
		ELSEIF TALENT:ARG:恋慕
			PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%に処女を捧げられた事に幸福を感じているようだ……
			CALL SOURCE_CALC_歓楽(ARG, PLAYER, 2000)
		ENDIF
	ENDIF
	ELSE
		IF TALENT:ARG:貞操 > 0
			;非恋慕
			IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			ENDIF
		ELSEIF TALENT:ARG:貞操 == 0
			;非恋慕
			IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
			ENDIF
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;A処女喪失時の処理
;ARG		処女喪失対象
;ARG:1	処女喪失させる人
;ARG:2	処女喪失手段(=1 チンコ	=4 触手)
;-------------------------------------------------
@LOST_A_VIRGIN(ARG, ARG:1, ARG:2)

;処女以外の初挿入処理
IF ARG:2 == 1
	IF ARG:1 < 0 && STRFIND(経験相手:ARG:Ａ挿入初体験, @"_-1_") < 0
		経験相手:ARG:Ａ挿入初体験 += "_-1_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて見知らぬ相手からＡ挿入された</font>","うふふ")
	ELSEIF STRFIND(経験相手:ARG:Ａ挿入初体験, @"_{CFLAG:(ARG:1):人物番号}_") < 0
		IF 初体験日:ARG:Ａ挿入初体験 == 0 && ARG:1 == MASTER
			初体験日:ARG:Ａ挿入初体験 = DAY
		ENDIF
		経験相手:ARG:Ａ挿入初体験 += @"_{CFLAG:(ARG:1):人物番号}_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:(ARG:1)%からＡ挿入された</font>","うふふ")
	ENDIF
ENDIF

;非処女はスルー
SIF !TALENT:ARG:Ａ処女
	RETURN 0
RCVAR:ARG:Ａ破瓜 = ARG:2
RCVAR:ARG:Ａ破瓜_相手番号 = ARG:1
VARSET LOCAL
;SOURCE追加
IF TALENT:ARG:Ａ処女 == 1
	PRINTFORM Ａ処女喪失(%CALLNAME:ARG%)
	IF ARG:1 < 0
		CFLAG:LOCAL:Ａ処女相手番号 = ARG:1
	ELSE
		CFLAG:LOCAL:Ａ処女相手番号 = CFLAG:(ARG:1):人物番号
	ENDIF
	CFLAG:LOCAL:Ａ処女道具記録 = ARG:2
	;苦痛
	;チンコ
	IF ARG:2 == 1
		LOCAL = 2000
		IF ARG:1 < 0
			IF GETBIT(ABS(ARG:1), 1)
				PRINTW ：ペニス
			ELSE
				PRINTW ：ペニスバンド
				RCVAR:ARG:Ａ破瓜 = 5
			ENDIF
		ELSE
			IF GETBIT(TALENT:(ARG:1):性別, 1)
				PRINTW ：ペニス
			ELSE
				PRINTW ：ペニスバンド
				RCVAR:ARG:Ａ破瓜 = 5
			ENDIF
		ENDIF
	;触手
	ELSEIF ARG:2 == 4
		LOCAL = 2000
		PRINTW ：触手
	ENDIF
	LOCAL = LOCAL * PAIN_CHECK_V(ARG) / 1000
	CALL SOURCE_CALC_苦痛(ARG, ARG:1, LOCAL)
	;不潔
	;Aなのでそもそもの基礎値が存在
	CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
	;貞操観念
	IF TALENT:ARG:貞操 > 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
		;恋愛対象以外の相手
		ELSEIF ARG:1 != PLAYER
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 2000)
		;チンコじゃない
		ELSEIF ARG:2 != 1
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
		ENDIF
	ELSEIF TALENT:ARG:貞操 == 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
		;恋愛対象以外の相手
		ELSEIF ARG:1 != PLAYER
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
		;チンコじゃない
		ELSEIF ARG:2 != 1
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
		ENDIF
	ENDIF
	;陥落素質による処理
	;MASTERがTARGETのＡに挿入している
	IF ARG:1 == MASTER
		IF TALENT:ARG:貞操 > 0
			;非恋慕
			IF !TALENT:ARG:恋慕
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			ENDIF
		ELSEIF TALENT:ARG:貞操 == 0
			;非恋慕
			IF !TALENT:ARG:恋慕
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
			ENDIF
		ENDIF
		IF ARG:2 == 1
		;親愛かつ相手が主人
		IF TALENT:ARG:恋慕 > 1
			PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%が後ろの初めての相手である事にこの上ない幸せを感じているようだ……
			CALL SOURCE_CALC_歓楽(ARG, PLAYER, 5000)
		;恋慕かつ相手が主人
		ELSEIF TALENT:ARG:恋慕
			PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%にＡ処女を捧げられた事に幸福を感じているようだ……
			CALL SOURCE_CALC_歓楽(ARG, PLAYER, 2000)
		ENDIF
	ENDIF
	ELSE
		IF TALENT:ARG:貞操 > 0
			;非恋慕
			IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 1000)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			ENDIF
		ELSEIF TALENT:ARG:貞操 == 0
			;非恋慕
			IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 500)
			;チンコじゃない
			ELSEIF ARG:2 != 1
				CALL SOURCE_CALC_不潔(ARG, ARG:1, 250)
			ENDIF
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;童貞喪失時の処理
;ARG		童貞喪失した人
;ARG:1	童貞喪失させる人
;ARG:2		童貞喪失対象（V：１　A：２）
;-------------------------------------------------
@LOST_MAN_VIRGIN(ARG, ARG:1, ARG:2)

;非童貞の初挿入処理
IF ARG:2 == 1
	IF ARG:1 < 0 && STRFIND(経験相手:ARG:Ｖ挿入初体験_男側, @"_-1_") < 0
		経験相手:ARG:Ｖ挿入初体験_男側 += "_-1_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて見知らぬ相手にＶ挿入した</font>","うふふ")
	ELSEIF STRFIND(経験相手:ARG:Ｖ挿入初体験_男側, @"_{CFLAG:(ARG:1):人物番号}_") < 0
		IF 初体験日:ARG:Ｖ挿入初体験_男側 == 0 && ARG:1 == MASTER
			初体験日:ARG:Ｖ挿入初体験_男側 = DAY
		ENDIF
		経験相手:ARG:Ｖ挿入初体験_男側 += @"_{CFLAG:(ARG:1):人物番号}_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:(ARG:1)%にＶ挿入した</font>","うふふ")
	ENDIF
ELSEIF ARG:2 == 2
	IF ARG:1 < 0 && STRFIND(経験相手:ARG:Ａ挿入初体験_男側, @"_-1_") < 0
		経験相手:ARG:Ａ挿入初体験_男側 += "_-1_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて見知らぬ相手にＡ挿入した</font>","うふふ")
	ELSEIF STRFIND(経験相手:ARG:Ａ挿入初体験_男側, @"_{CFLAG:(ARG:1):人物番号}_") < 0
		IF 初体験日:ARG:Ａ挿入初体験_男側 == 0 && ARG:1 == MASTER
			初体験日:ARG:Ａ挿入初体験_男側 = DAY
		ENDIF
		経験相手:ARG:Ａ挿入初体験_男側 += @"_{CFLAG:(ARG:1):人物番号}_"
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:(ARG:1)%にＡ挿入した</font>","うふふ")
	ENDIF
ENDIF

;非童貞はスルー
SIF GETBIT(TALENT:ARG:非童貞, ARG:2 - 1)
	RETURN 0
;ちんこなし（＝ペニバン）はスルー
SIF GETBIT(TALENT:ARG:性別, 1) == 0
	RETURN 0
RCVAR:ARG:童貞喪失 = ARG:2
RCVAR:ARG:童貞喪失_相手番号 = ARG:1
IF ARG:2 == 1
	IF ARG:1 < 0
		CFLAG:LOCAL:童貞相手番号 = ARG:1
	ELSE
		CFLAG:LOCAL:童貞相手番号 = CFLAG:(ARG:1):人物番号
	ENDIF
ELSE
	IF ARG:1 < 0
		CFLAG:LOCAL:Ａ童貞相手番号 = ARG:1
	ELSE
		CFLAG:LOCAL:Ａ童貞相手番号 = CFLAG:(ARG:1):人物番号
	ENDIF
ENDIF
VARSET LOCAL
;SOURCE追加
IF ARG:2 == 1
	PRINTFORML Ｖ童貞喪失(%CALLNAME:ARG%)
ELSEIF ARG:2 == 2
	PRINTFORML Ａ童貞喪失(%CALLNAME:ARG%)
ENDIF
;陥落素質による処理
;TARGETがMASTERの膣に挿入している
IF ARG:1 == MASTER
	;不潔
	;貞操観念
	IF TALENT:ARG:貞操 > 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 300)
		ENDIF
	ELSEIF TALENT:ARG:貞操 == 0
		;非恋慕
		IF !TALENT:ARG:恋慕
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 100)
		ENDIF
	ENDIF
	;親愛
	IF TALENT:ARG:恋慕 > 1
		PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%が初めての相手である事にこの上ない幸せを感じているようだ……
		CALL SOURCE_CALC_歓楽(ARG, PLAYER, 5000)
	;恋慕
	ELSEIF TALENT:ARG:恋慕
		PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%が初体験の相手である事に幸福を感じているようだ……
		CALL SOURCE_CALC_歓楽(ARG, PLAYER, 2000)
	ENDIF
ELSE
	;不潔
	;貞操観念
	IF TALENT:ARG:貞操 > 0
		;非恋慕
		IF ARG:1 < 0
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 300)
		ELSEIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 300)
		ENDIF
	ELSEIF TALENT:ARG:貞操 == 0
		;非恋慕
		IF ARG:1 < 0
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 100)
		ELSEIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And (対象キャラ１ = {CFLAG:(ARG:1):人物番号} Or 対象キャラ２ = {CFLAG:(ARG:1):人物番号}) And 関係性種別 = '恋慕'") == 0
			CALL SOURCE_CALC_不潔(ARG, ARG:1, 100)
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;EXP取得
;-------------------------------------------------
@SOURCE_EXP(ARG)
VARSET LOCAL
;同時モードの時でも肉体熟知は入る
IF CUP:ARG:快Ｃ
	EXP:ARG:0 ++
	SIF PLAYER == MASTER && ARG != MASTER
		EXP:ARG:肉体熟知 ++
ENDIF
IF CUP:ARG:快Ｖ && !TALENT:ARG:処女
	EXP:ARG:1 ++
	SIF PLAYER == MASTER && ARG != MASTER
		EXP:ARG:肉体熟知 ++
ENDIF
IF CUP:ARG:快Ａ
	EXP:ARG:2 ++
	SIF PLAYER == MASTER && ARG != MASTER
		EXP:ARG:肉体熟知 ++
ENDIF
IF CUP:ARG:快Ｂ
	EXP:ARG:3 ++
	SIF PLAYER == MASTER && ARG != MASTER
		EXP:ARG:肉体熟知 ++
ENDIF
IF CUP:ARG:快Ｓ
	EXP:ARG:4 ++
	SIF PLAYER == MASTER && ARG != MASTER
		EXP:ARG:肉体熟知 ++
ENDIF

SIF RCVAR:ARG:乳首開発フラグ
	EXP:ARG:乳首開発 = MIN(EXP:ARG:乳首開発 + RCVAR:ARG:乳首開発フラグ, 1000)
SIF RCVAR:ARG:Ｇスポ開発フラグ
	EXP:ARG:Ｇスポ開発 = MIN(EXP:ARG:Ｇスポ開発 + RCVAR:ARG:Ｇスポ開発フラグ, 1000)
SIF RCVAR:ARG:ポルチオ開発フラグ
	EXP:ARG:ポルチオ開発 = MIN(EXP:ARG:ポルチオ開発 + RCVAR:ARG:ポルチオ開発フラグ, 1000)
SIF RCVAR:ARG:前立腺開発フラグ
	EXP:ARG:前立腺開発 = MIN(EXP:ARG:前立腺開発 + RCVAR:ARG:前立腺開発フラグ, 1000)
SIF RCVAR:ARG:Ｓ字結腸開発フラグ
	EXP:ARG:Ｓ字結腸開発 = MIN(EXP:ARG:Ｓ字結腸開発 + RCVAR:ARG:Ｓ字結腸開発フラグ, 1000)
SIF RCVAR:ARG:角開発フラグ
	EXP:ARG:角開発 = MIN(EXP:ARG:角開発 + RCVAR:ARG:角開発フラグ, 1000)
SIF RCVAR:ARG:尻尾開発フラグ
	EXP:ARG:尻尾開発 = MIN(EXP:ARG:尻尾開発 + RCVAR:ARG:尻尾開発フラグ, 1000)

;-------------------------------------------------
;食いしばり発動のチェック
;-------------------------------------------------
@食いしばり発動処理(対象キャラ)
#DIM 対象キャラ

IF DOWNBASE:対象キャラ:体力 > 0 && BASE:対象キャラ:体力 > 1 && (BASE:対象キャラ:体力 - DOWNBASE:対象キャラ:体力) <= 1
	;指示モード解除
	IF PLAYER == 対象キャラ && FLAG:モード管理 == 1
		FLAG:モード管理 = 0
		TFLAG:あなたターゲット用 = 0
	ENDIF
	IF !MODE_存在判定_ターゲット側("首絞め", 対象キャラ)
		TCVAR:対象キャラ:食いしばりフラグ = (MAXBASE:対象キャラ:体力 - 1000) / 1000
		SIF キャラ一致チェック(対象キャラ, "[十天衆]サラーサ")
			TCVAR:対象キャラ:食いしばりフラグ += 3
		SIF 干渉強化変数:対象キャラ:干渉種類 == 10
			CALL 干渉強化関数_10("実補正処理", 対象キャラ)
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
; @MARK_GOT_CHECK(ARG)
; ;-------------------------------------------------
; ;苦痛刻印
; ;-------------------------------------------------
; IF CUP:ARG:苦痛 >= 2000 && CUP:ARG:苦痛 < 4000 && MARK:ARG:苦痛刻印 <= 0
; 	MARK:ARG:苦痛刻印 = 1
; 	TFLAG:22 = 1
; ELSEIF CUP:ARG:苦痛 >= 4000 && CUP:ARG:苦痛 < 8000 && MARK:ARG:苦痛刻印 <= 1
; 	MARK:ARG:苦痛刻印 = 2
; 	TFLAG:22 = 2
; 	;気丈・感情乏しいの場合は苦痛刻印による屈服度アップはなし
; 	IF TALENT:ARG:度胸 > 0 && !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 500, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ELSEIF CUP:ARG:苦痛 >= 8000 && MARK:ARG:苦痛刻印 <= 2
; 	MARK:ARG:苦痛刻印 = 3
; 	TFLAG:22 = 3
; 	;気丈・感情乏しいの場合は苦痛刻印による屈服度アップはなし
; 	IF TALENT:ARG:度胸 > 0 && !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 1000, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ENDIF


; ;-------------------------------------------------
; ;快楽刻印
; ;-------------------------------------------------
; IF TCVAR:ARG:快楽強度 == 1 && MARK:ARG:快楽刻印 <= 0
; 	MARK:ARG:快楽刻印 = 1
; 	TFLAG:23 = 1
; ELSEIF TCVAR:ARG:快楽強度 == 2 && MARK:ARG:快楽刻印 <= 1
; 	MARK:ARG:快楽刻印 = 2
; 	TFLAG:23 = 2
; ELSEIF TCVAR:ARG:快楽強度 == 3 && MARK:ARG:快楽刻印 <= 2
; 	MARK:ARG:快楽刻印 = 3
; 	TFLAG:23 = 3
; 	;自制心・感情乏しいの場合は快楽刻印による好感度・快楽堕ちアップはなし
; 	IF !TALENT:ARG:自制心 && !TALENT:ARG:感情乏しい
; 		CALL 関係性ゲージUP用関数(ARG, 500, "好感度")
; 		SIF CFLAG:ARG:ゲージ起動分類 == 2
; 			CALL 関係性ゲージUP用関数(ARG, 500, "快楽堕ち度")
; 		TFLAG:25 = 1
; 	ENDIF
; ENDIF

; ;-------------------------------------------------
; ;屈服刻印
; ;-------------------------------------------------
; A = CUP:ARG:快Ｃ + CUP:ARG:快Ｃ + CUP:ARG:快Ｃ + CUP:ARG:快Ｃ

; ;快楽によるLvUPは10番+取得した屈服刻印のレベルのTFLAGを与える
; IF A >= 1000 && A < 2400 && MARK:ARG:快楽刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 0
; 	MARK:ARG:屈服刻印 = 1
; 	TFLAG:24 = 11
; ELSEIF A >= 2400 && A < 6000 && MARK:ARG:快楽刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 1
; 	MARK:ARG:屈服刻印 = 2
; 	TFLAG:24 = 12
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 500, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ELSEIF A >= 6000 && MARK:ARG:快楽刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 2
; 	MARK:ARG:屈服刻印 = 3
; 	TFLAG:24 = 13
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 1000, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ENDIF


; A = CUP:ARG:苦痛

; ;苦痛と恐怖によるLvUPは20番+取得した屈服刻印のレベルのTFLAGを与える
; IF A >= 1000 && A < 2400 && MARK:ARG:屈服刻印 <= 0
; 	MARK:ARG:屈服刻印 = 1
; 	TFLAG:24 = 21
; ELSEIF A >= 2400 && A < 6000 && MARK:ARG:屈服刻印 <= 1
; 	MARK:ARG:屈服刻印 = 2
; 	TFLAG:24 = 22
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 500, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ELSEIF A >= 6000 && MARK:ARG:屈服刻印 <= 2
; 	MARK:ARG:屈服刻印 = 3
; 	TFLAG:24 = 23
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 1000, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ENDIF


; A = CUP:ARG:屈服

; ;恭順と屈服によるLvUPは30番+取得した屈服刻印のレベルのTFLAGを与える
; IF A >= 1000 && A < 2400 && MARK:ARG:屈服刻印 <= 0
; 	MARK:ARG:屈服刻印 = 1
; 	TFLAG:24 = 31
; ELSEIF A >= 2400 && A < 6000 && MARK:ARG:屈服刻印 <= 1
; 	MARK:ARG:屈服刻印 = 2
; 	TFLAG:24 = 32
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 500, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ELSEIF A >= 6000 && MARK:ARG:屈服刻印 <= 2
; 	MARK:ARG:屈服刻印 = 3
; 	TFLAG:24 = 33
; 	;感情乏しいの場合は屈服刻印による屈服度アップはなし
; 	IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 		CALL 関係性ゲージUP用関数(ARG, 1000, "屈服度")
; 		TFLAG:25 = 1
; 	ENDIF
; ENDIF

; ;-------------------------------------------------
; ;反発刻印
; ;-------------------------------------------------
; ;MASTERは除外
; SIF ARG == MASTER
; 	RETURN 0
; A = MAX(CUP:ARG:反感, CUP:ARG:不快)
; IF A >= 1000 && A < 2000 && TFLAG:150 == 0 && MARK:ARG:反発刻印 <= 0
; 	MARK:ARG:反発刻印 = 1
; 	SIF MARK:ARG:4 < 1
; 		MARK:ARG:4 = 1
; 	TFLAG:21 = 1
; 	CFLAG:ARG:不機嫌 = 1
; 	CFLAG:ARG:怒り = 1
; 	TCVAR:ARG:不機嫌 = 1
; ELSEIF A >= 2000 && A < 3000 && TFLAG:150 == 0 && MARK:ARG:反発刻印 <= 1
; 	MARK:ARG:反発刻印 = 2
; 	TFLAG:21 = 2
; 	IF MARK:ARG:4 < 2
; 		;感情乏しいの場合は反発刻印による屈服度ダウンはなし
; 		IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 			CALL 関係性ゲージUP用関数(ARG, -1000, "屈服度")
; 			TFLAG:25 = 1
; 		ELSEIF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 			CALL 関係性ゲージUP用関数(ARG, -1000, "屈服度")
; 			TFLAG:25 = 2
; 		ENDIF
; 	ENDIF
; 	SIF MARK:ARG:4 < 2
; 		MARK:ARG:4 = 2
; 	CFLAG:ARG:不機嫌 = 2
; 	CFLAG:ARG:怒り = 1
; 	TCVAR:ARG:不機嫌 = 1
; ELSEIF A >= 3000 && TFLAG:150 == 0 && MARK:ARG:反発刻印 <= 2
; 	MARK:ARG:反発刻印 = 3
; 	TFLAG:21 = 3
; 	IF MARK:ARG:4 < 3
; 		;感情乏しいの場合は反発刻印による屈服度ダウンはなし
; 		IF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 			CALL 関係性ゲージUP用関数(ARG, -1000, "屈服度")
; 			TFLAG:25 = 1
; 		ELSEIF !TALENT:ARG:感情乏しい && CFLAG:ARG:ゲージ起動分類 == 2
; 			CALL 関係性ゲージUP用関数(ARG, -1000, "屈服度")
; 			TFLAG:25 = 2
; 		ENDIF
; 	ENDIF
; 	SIF MARK:ARG:4 < 3
; 		MARK:ARG:4 = 3
; 	CFLAG:ARG:不機嫌 = 3
; 	CFLAG:ARG:怒り = 1
; 	TCVAR:ARG:不機嫌 = 1
; ENDIF
; TFLAG:150 = 0

;-------------------------------------------------
;屈服・快楽堕ちの上昇による[抵抗]の消滅をチェック
;-------------------------------------------------
@JUJUN_UP_CHECK(ARG)
IF (CFLAG:ARG:屈服レベル >= 40 || CFLAG:ARG:快楽堕ちレベル >= 40)  && TALENT:ARG:抵抗
	PRINTFORM %NAME:ARG%は[抵抗]を失った
	TALENT:ARG:抵抗 = 0
	PRINTL 否定の珠が半分になった
	JUEL:100 /= 2
ENDIF

;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
@EXP_GOT_CHECK(ARG)
;奉仕系である、奉仕快楽のチェック
LOCAL = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ
LOCAL:1 = 0
LOCAL:2 = MAX(CUP:ARG:快Ｃ, CUP:ARG:快Ｖ, CUP:ARG:快Ａ, CUP:ARG:快Ｂ, CUP:ARG:快Ｓ)

IF CUP:ARG:欲情 < 100
	LOCAL = 0
ELSEIF CUP:ARG:欲情 < 300
	LOCAL *= 2
ELSEIF CUP:ARG:欲情 < 700
	LOCAL *= 3
ELSEIF CUP:ARG:欲情 < 1500
	LOCAL *= 4
ELSE
	LOCAL *= 5
ENDIF

IF RCVAR:ARG:奉仕コマンドフラグ
	IF LOCAL >= 12000
		LOCAL:1 = 16
		TIMES CUP:ARG:反感 , 0.30
	ELSEIF LOCAL >= 8000
		LOCAL:1 = 12
		TIMES CUP:ARG:反感 , 0.40
	ELSEIF LOCAL >= 5000
		LOCAL:1 = 8
		TIMES CUP:ARG:反感 , 0.50
	ELSEIF LOCAL >= 3000
		LOCAL:1 = 4
		TIMES CUP:ARG:反感 , 0.60
	ELSEIF LOCAL >= 2000
		LOCAL:1 = 2
		TIMES CUP:ARG:反感 , 0.70
	ELSEIF LOCAL >= 1000
		LOCAL:1 = 1
		TIMES CUP:ARG:反感 , 0.80
	ELSE
		LOCAL:1 = 0
	ENDIF
	EXP:ARG:奉仕快楽経験 += LOCAL:1
ENDIF

IF RCVAR:ARG:露出コマンドフラグ
	IF LOCAL:2 >= 12000
		LOCAL:1 = 16
	ELSEIF LOCAL:2 >= 8000
		LOCAL:1 = 12
	ELSEIF LOCAL:2 >= 5000
		LOCAL:1 = 8
	ELSEIF LOCAL:2 >= 3000
		LOCAL:1 = 4
	ELSEIF LOCAL:2 >= 2000
		LOCAL:1 = 2
	ELSEIF LOCAL:2 >= 1000
		LOCAL:1 = 1
	ELSE
		LOCAL:1 = 0
	ENDIF
	EXP:ARG:露出快楽経験 += LOCAL:1
ENDIF

;触手快楽のチェック
IF RCVAR:ARG:触手コマンドフラグ
	IF LOCAL >= 12000
		LOCAL:1 = 16
	ELSEIF LOCAL >= 8000
		LOCAL:1 = 12
	ELSEIF LOCAL >= 5000
		LOCAL:1 = 8
	ELSEIF LOCAL >= 3000
		LOCAL:1 = 4
	ELSEIF LOCAL >= 2000
		LOCAL:1 = 2
	ELSEIF LOCAL >= 1000
		LOCAL:1 = 1
	ELSE
		LOCAL:1 = 0
	ENDIF
	EXP:ARG:触手快楽経験 += LOCAL:1
ENDIF

;苦痛快楽のチェック
LOCAL = 0
LOCAL:1 = CUP:ARG:快Ｃ + CUP:ARG:快Ｂ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｓ
IF LOCAL:1 >= 3000 && CUP:ARG:苦痛 >= MAX(1, 1500 - (ABL:ARG:マゾ性感 * 100))
	LOCAL = 16
ELSEIF LOCAL:1 >= 2500 && CUP:ARG:苦痛 >= MAX(1, 1000 - (ABL:ARG:マゾ性感 * 80))
	LOCAL = 12
ELSEIF LOCAL:1 >= 1500 && CUP:ARG:苦痛 >= MAX(1, 700 - (ABL:ARG:マゾ性感 * 60))
	LOCAL = 8
ELSEIF LOCAL:1 >= 1000 && CUP:ARG:苦痛 >= MAX(1, 500 - (ABL:ARG:マゾ性感 * 40))
	LOCAL = 4
ELSEIF LOCAL:1 >= 600 && CUP:ARG:苦痛 >= MAX(1, 300 - (ABL:ARG:マゾ性感 * 30))
	LOCAL = 2
ELSEIF LOCAL:1 >= 300 && CUP:ARG:苦痛 >= MAX(1, 100 - (ABL:ARG:マゾ性感 * 20))
	LOCAL = 1
ENDIF
IF LOCAL > 0
	EXP:ARG:苦痛快楽経験 += LOCAL
ENDIF

;嗜虐快楽経験
SIF RCVAR:ARG:嗜虐コマンドフラグ
	EXP:ARG:嗜虐快楽経験 += 1

;-------------------------------------------------
;好感度処理
;-------------------------------------------------
@FAVOR_CALC(ARG)
#DIM ループ用

;干渉/印象補正
IF GROUPMATCH(干渉強化変数:ARG:干渉種類, 1, 2, 3, 4)
	CALL 干渉補正実処理関数(ARG, "好感度増加時")
ELSE
	CALL 印象補正_好感度上昇(ARG)
ENDIF

;最大値を取る
NOWEX:ARG:好感度上昇格納 = MAX(NOWEX:ARG:友情度上昇格納, NOWEX:ARG:信頼度上昇格納, NOWEX:ARG:恋心度上昇格納, NOWEX:ARG:性愛度上昇格納, NOWEX:ARG:キャラ固有好感度要素上昇格納)

;SIF !TFLAG:調教モード
;	RETURN 0
SIF !ARG
	GOTO 他キャラループ
;覗くの時は発生しない
SIF SELECTCOM == 404
	RETURN 0
;主人(あなた)と対象の能力を計算

;ムードは恋愛対象外、あるいはその場に陥落してない他キャラがいると上がらない、MAX1500
IF ムード上昇状況チェック(ARG)
	;固定値上昇分
	BASE:ARG:ムード += CFLAG:ARG:好感度レベル + 恋慕_恋心比較(ARG)
	;コマンドなどでの上昇分
	BASE:ARG:ムード += NOWEX:ARG:ムード上昇格納
	BASE:ARG:ムード = LIMIT(BASE:ARG:ムード, 0, MAXBASE:ARG:ムード)
ELSE
	BASE:ARG:ムード = 0
ENDIF

;理性は発情期・性欲に応じて上限にキャップがつく、MAX1000
BASE:ARG:理性 -= NOWEX:ARG:理性減少格納
SIF TCVAR:ARG:弱点コマンドフラグ
	BASE:ARG:理性 -= 50
BASE:ARG:理性 -= (CUP:ARG:快Ｂ + CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｓ) / 100

BASE:ARG:理性 = LIMIT(BASE:ARG:理性, 0, 理性キャップ算出(ARG))
BASE:ARG:警戒 = LIMIT(BASE:ARG:警戒, 0, MAXBASE:ARG:警戒)

IF TFLAG:調教モード != 2
	SIF SOURCE:ARG:反感 > 0 && TFLAG:調教モード == 1
		BASE:ARG:怒り += SOURCE:ARG:反感 * 3 * (2000 - BASE:ARG:ムード) * (500 + BASE:ARG:理性) / ((2000 + BASE:ARG:ムード) * (500 + MAXBASE:ARG:理性))
	SIF BASE:ARG:怒り > MAXBASE:ARG:怒り
		CFLAG:ARG:怒り = 1
	
	BASE:ARG:怒り = MIN(BASE:ARG:怒り , MAXBASE:ARG:怒り )
	SIF BASE:ARG:怒り == MAXBASE:ARG:怒り
		RCVAR:ARG:不機嫌 = 1
	IF CFLAG:ARG:不機嫌
		BASE:ARG:怒り = MAXBASE:ARG:怒り
		CFLAG:ARG:怒り = 1
	ENDIF
ENDIF

;反感・不快と相殺する
;まず反感
IF CUP:ARG:反感 <= NOWEX:ARG:好感度上昇格納
	NOWEX:ARG:好感度上昇格納 -= CUP:ARG:反感
	CUP:ARG:反感 = 0
ELSE
	CUP:ARG:反感 -= NOWEX:ARG:好感度上昇格納
	NOWEX:ARG:好感度上昇格納 = 0
ENDIF
;不快
IF CUP:ARG:不快 <= NOWEX:ARG:好感度上昇格納
	NOWEX:ARG:好感度上昇格納 -= CUP:ARG:不快
	CUP:ARG:不快 = 0
ELSE
	CUP:ARG:不快 -= NOWEX:ARG:好感度上昇格納
	NOWEX:ARG:好感度上昇格納 = 0
ENDIF

;相殺分適用
NOWEX:ARG:友情度上昇格納 = MIN(NOWEX:ARG:友情度上昇格納, NOWEX:ARG:好感度上昇格納)
NOWEX:ARG:信頼度上昇格納 = MIN(NOWEX:ARG:信頼度上昇格納, NOWEX:ARG:好感度上昇格納)
NOWEX:ARG:恋心度上昇格納 = MIN(NOWEX:ARG:恋心度上昇格納, NOWEX:ARG:好感度上昇格納)
NOWEX:ARG:性愛度上昇格納 = MIN(NOWEX:ARG:性愛度上昇格納, NOWEX:ARG:好感度上昇格納)
NOWEX:ARG:キャラ固有好感度要素上昇格納 = MIN(NOWEX:ARG:キャラ固有好感度要素上昇格納, NOWEX:ARG:好感度上昇格納)

;恋愛対象外だと恋心を友情に、性愛を０に
IF 恋愛対象外チェック(ARG)
	NOWEX:ARG:友情度上昇格納 += NOWEX:ARG:恋心度上昇格納
	NOWEX:ARG:恋心度上昇格納 = 0
	NOWEX:ARG:性愛度上昇格納 = 0
ENDIF

;好感度表示
CALL 関係性ゲージUP用関数(ARG, 0, "好感度")

$他キャラループ
;TARGET以外の好感度上昇も見る
FOR ループ用, 0, CHARANUM
	SIF CFLAG:ループ用:滞在期間 < 0
		CONTINUE
	IF ループ用 != ARG
		;干渉/印象補正
		IF GROUPMATCH(干渉強化変数:ループ用:干渉種類, 1, 2, 3, 4)
			CALL 干渉補正実処理関数(ループ用, "好感度増加時")
		ELSE
			CALL 印象補正_好感度上昇(ループ用)
		ENDIF
		
		;最大値を取る
		NOWEX:ループ用:好感度上昇格納 = MAX(NOWEX:ループ用:友情度上昇格納, NOWEX:ループ用:信頼度上昇格納, NOWEX:ループ用:恋心度上昇格納, NOWEX:ループ用:性愛度上昇格納, NOWEX:ループ用:キャラ固有好感度要素上昇格納)

		;反感・不快と相殺する
		;まず反感
		IF CUP:ループ用:反感 <= NOWEX:ループ用:好感度上昇格納
			NOWEX:ループ用:好感度上昇格納 -= CUP:ループ用:反感
			CUP:ループ用:反感 = 0
		ELSE
			CUP:ループ用:反感 -= NOWEX:ループ用:好感度上昇格納
			NOWEX:ループ用:好感度上昇格納 = 0
		ENDIF
		;不快
		IF CUP:ループ用:不快 <= NOWEX:ループ用:好感度上昇格納
			NOWEX:ループ用:好感度上昇格納 -= CUP:ループ用:不快
			CUP:ループ用:不快 = 0
		ELSE
			CUP:ループ用:不快 -= NOWEX:ループ用:好感度上昇格納
			NOWEX:ループ用:好感度上昇格納 = 0
		ENDIF

		;相殺分適用
		NOWEX:ループ用:友情度上昇格納 = MIN(NOWEX:ループ用:友情度上昇格納, NOWEX:ループ用:好感度上昇格納)
		NOWEX:ループ用:信頼度上昇格納 = MIN(NOWEX:ループ用:信頼度上昇格納, NOWEX:ループ用:好感度上昇格納)
		NOWEX:ループ用:恋心度上昇格納 = MIN(NOWEX:ループ用:恋心度上昇格納, NOWEX:ループ用:好感度上昇格納)
		NOWEX:ループ用:性愛度上昇格納 = MIN(NOWEX:ループ用:性愛度上昇格納, NOWEX:ループ用:好感度上昇格納)
		NOWEX:ループ用:キャラ固有好感度要素上昇格納 = MIN(NOWEX:ループ用:キャラ固有好感度要素上昇格納, NOWEX:ループ用:好感度上昇格納)

		;恋愛対象外だと恋心を友情に、性愛を０に
		IF 恋愛対象外チェック(ループ用)
			NOWEX:ループ用:友情度上昇格納 += NOWEX:ループ用:恋心度上昇格納
			NOWEX:ループ用:恋心度上昇格納 = 0
			NOWEX:ループ用:性愛度上昇格納 = 0
		ENDIF
		CALL 関係性ゲージUP用関数(ループ用, 0, "好感度")
	ENDIF
NEXT

@ムード上昇状況チェック(キャラ番号)
#FUNCTION
#DIM キャラ番号

;恋愛対象外は対象外
SIF 恋愛対象外チェック(キャラ番号)
	RETURNF 0
;片方隠密時は対象外
SIF CFLAG:PLAYER:隠密 != CFLAG:キャラ番号:隠密
	RETURNF 0

;陥落していない第三者がいると対象外
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		BREAK
	SIF TARGET:LOCAL == キャラ番号
		CONTINUE
	;ただしこっちが隠密or相手が隠密ならカウントしない
	SIF CFLAG:PLAYER:隠密 != CFLAG:LOCAL:隠密
		CONTINUE
	SIF TALENT:(TARGET:LOCAL):恋慕 == 0 && TALENT:(TARGET:LOCAL):セフレ == 0
		RETURNF 0
NEXT

RETURNF 1

@理性キャップ算出(キャラ番号)
#FUNCTION
#DIM キャラ番号

LOCAL = MAXBASE:キャラ番号:理性 - BASE:キャラ番号:性欲 / 1000 * 100
SIF CFLAG:キャラ番号:発情期フラグ < 0
	LOCAL = LOCAL - 200
LOCAL = MAX(LOCAL, 1)

RETURNF LOCAL

@印象補正_好感度上昇(キャラ番号)
#DIM キャラ番号

SELECTCASE TALENT:MASTER:印象
	CASE 1
		IF NOWEX:キャラ番号:友情度上昇格納 > 0
			NOWEX:キャラ番号:友情度上昇格納 += NOWEX:キャラ番号:友情度上昇格納 / 2
		ENDIF
		IF NOWEX:キャラ番号:恋心度上昇格納 > 0
			NOWEX:キャラ番号:恋心度上昇格納 = MAX(1, NOWEX:キャラ番号:恋心度上昇格納 * 3 / 4)
		ENDIF
		IF NOWEX:キャラ番号:性愛度上昇格納 > 0
			NOWEX:キャラ番号:性愛度上昇格納 = MAX(1, NOWEX:キャラ番号:性愛度上昇格納 * 3 / 4)
		ENDIF
	CASE 2
		IF NOWEX:キャラ番号:信頼度上昇格納 > 0
			NOWEX:キャラ番号:信頼度上昇格納 += NOWEX:キャラ番号:信頼度上昇格納 / 2
		ENDIF
		IF NOWEX:キャラ番号:性愛度上昇格納 > 0
			NOWEX:キャラ番号:性愛度上昇格納 = MAX(1, NOWEX:キャラ番号:性愛度上昇格納 * 3 / 4)
		ENDIF
		IF NOWEX:キャラ番号:友情度上昇格納 > 0
			NOWEX:キャラ番号:友情度上昇格納 = MAX(1, NOWEX:キャラ番号:友情度上昇格納 * 3 / 4)
		ENDIF
	CASE 3
		IF NOWEX:キャラ番号:恋心度上昇格納 > 0
			NOWEX:キャラ番号:恋心度上昇格納 += NOWEX:キャラ番号:恋心度上昇格納 / 2
		ENDIF
		IF NOWEX:キャラ番号:友情度上昇格納 > 0
			NOWEX:キャラ番号:友情度上昇格納 = MAX(1, NOWEX:キャラ番号:友情度上昇格納 * 3 / 4)
		ENDIF
		IF NOWEX:キャラ番号:信頼度上昇格納 > 0
			NOWEX:キャラ番号:信頼度上昇格納 = MAX(1, NOWEX:キャラ番号:信頼度上昇格納 * 3 / 4)
		ENDIF
	CASE 4
		IF NOWEX:キャラ番号:性愛度上昇格納 > 0
			NOWEX:キャラ番号:性愛度上昇格納 += NOWEX:キャラ番号:性愛度上昇格納 / 2
		ENDIF
		IF NOWEX:キャラ番号:信頼度上昇格納 > 0
			NOWEX:キャラ番号:信頼度上昇格納 = MAX(1, NOWEX:キャラ番号:信頼度上昇格納 * 3 / 4)
		ENDIF
		IF NOWEX:キャラ番号:恋心度上昇格納 > 0
			NOWEX:キャラ番号:恋心度上昇格納 = MAX(1, NOWEX:キャラ番号:恋心度上昇格納 * 3 / 4)
		ENDIF
ENDSELECT

RETURN

;-------------------------------------------------
;恋慕度処理
;-------------------------------------------------
@Love_CALC(ARG)
#DIM ループ用

;覗くの時は発生しない
SIF SELECTCOM == 404
	RETURN 0

;反感・不快と相殺する
;まず反感
IF CUP:ARG:反感 <= NOWEX:ARG:恋慕度上昇格納
	NOWEX:ARG:恋慕度上昇格納 -= CUP:ARG:反感
	CUP:ARG:反感 = 0
ELSE
	CUP:ARG:反感 -= NOWEX:ARG:恋慕度上昇格納
	NOWEX:ARG:恋慕度上昇格納 = 0
ENDIF
;不快
IF CUP:ARG:不快 <= NOWEX:ARG:恋慕度上昇格納
	NOWEX:ARG:恋慕度上昇格納 -= CUP:ARG:不快
	CUP:ARG:不快 = 0
ELSE
	CUP:ARG:不快 -= NOWEX:ARG:恋慕度上昇格納
	NOWEX:ARG:恋慕度上昇格納 = 0
ENDIF


;恋愛対象外だと０に
IF 恋愛対象外チェック(ARG) || GETBIT(TALENT:ARG:陥落不可, 0)
	NOWEX:ARG:恋慕度上昇格納 = 0
ENDIF

;恋慕度
SIF CFLAG:ARG:ゲージ起動分類 == 1 && NOWEX:ARG:恋慕度上昇格納
	CALL 関係性ゲージUP用関数(ARG, NOWEX:ARG:恋慕度上昇格納, "恋慕度")

;TARGET以外の恋慕度上昇も見る
FOR ループ用, 0, CHARANUM
	SIF CFLAG:ループ用:滞在期間 < 0
		CONTINUE
	IF CFLAG:ループ用:ゲージ起動分類 == 1 && NOWEX:ループ用:恋慕度上昇格納 && ループ用 != ARG
		;反感・不快と相殺する
		;まず反感
		IF CUP:ループ用:反感 <= NOWEX:ループ用:恋慕度上昇格納
			NOWEX:ループ用:恋慕度上昇格納 -= CUP:ループ用:反感
			CUP:ループ用:反感 = 0
		ELSE
			CUP:ループ用:反感 -= NOWEX:ループ用:恋慕度上昇格納
			NOWEX:ループ用:恋慕度上昇格納 = 0
		ENDIF
		;不快
		IF CUP:ループ用:不快 <= NOWEX:ループ用:恋慕度上昇格納
			NOWEX:ループ用:恋慕度上昇格納 -= CUP:ループ用:不快
			CUP:ループ用:不快 = 0
		ELSE
			CUP:ループ用:不快 -= NOWEX:ループ用:恋慕度上昇格納
			NOWEX:ループ用:恋慕度上昇格納 = 0
		ENDIF


		;恋愛対象外だと０に
		IF 恋愛対象外チェック(ループ用) || GETBIT(TALENT:ループ用:陥落不可, 0)
			NOWEX:ループ用:恋慕度上昇格納 = 0
		ENDIF
		CALL 関係性ゲージUP用関数(ループ用, NOWEX:ループ用:恋慕度上昇格納, "恋慕度")
	ENDIF
NEXT

@恋愛対象外チェック(判定キャラ, 相手キャラ = -1)
#FUNCTION
#DIM 判定キャラ
#DIM 相手キャラ
#DIM 対象外フラグ
対象外フラグ = 0

;プレイヤーは無条件でOK
SIF 判定キャラ == MASTER
	RETURNF 0

SIF 相手キャラ == -1
	相手キャラ = MASTER

;性知識がマイナスなら恋愛という概念がないので対象外
IF 知識素質:判定キャラ:性知識 < 0
	;ただし、あなたの手で絶頂したことがあるなら対象になる
	IF 初体験日:判定キャラ:あなたの手で初絶頂 && 相手キャラ == MASTER
	ELSE
		対象外フラグ = 1
	ENDIF
ENDIF

;性愛傾向を見る
IF TALENT:判定キャラ:性愛傾向 == 3
	;両刀はスルー
ELSEIF 性癖素質:判定キャラ:同性愛 > 0
	;同性愛（異性愛）持ちもスルー
ELSEIF TALENT:判定キャラ:性別 == 3 || TALENT:相手キャラ:性別 == 3
	;どっちかがふたなりならスルー
ELSEIF TALENT:判定キャラ:性別 == 0 || TALENT:相手キャラ:性別 == 0
	;無性は恋愛しない
	対象外フラグ = 1
ELSE
	;それ以外のパターン
	SELECTCASE TALENT:判定キャラ:性愛傾向
		CASE 1
			SIF TALENT:相手キャラ:性別 != 1
				対象外フラグ = 1
		CASE 2
			SIF TALENT:相手キャラ:性別 != 2
				対象外フラグ = 1
		CASEELSE
			;同性時は基本対象外
			SIF TALENT:判定キャラ:性別 == TALENT:相手キャラ:性別
				対象外フラグ = 1
	ENDSELECT
ENDIF


RETURNF 対象外フラグ


@関係性ゲージUP用関数(ARG, ARG:1, ARGS, 干渉なしフラグ)
#DIM 干渉なしフラグ
;ARG：上昇対象キャラクター
;ARG:1：ゲージ上昇量
;ARGS：ゲージの種類

SELECTCASE ARGS
	CASE "好感度"
		LOCAL = 0
		IF NOWEX:ARG:友情度上昇格納
			IF CFLAG:ARG:友情度 < 経験値テーブル:99 && CFLAG:ARG:友情度 + NOWEX:ARG:友情度上昇格納 >= 経験値テーブル:99
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への友情度が最大になった","日常")
			ENDIF
			CFLAG:ARG:友情度 += NOWEX:ARG:友情度上昇格納
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORM \@(NOWEX:ARG:友情度上昇格納 >= 0) ? 好感度要素：友情＋{NOWEX:ARG:友情度上昇格納} # 好感度要素：友情－{-NOWEX:ARG:友情度上昇格納}\@
			IF 干渉強化変数:ARG:干渉種類 == 1 && 干渉なしフラグ == 0
				SETCOLOR カラーパレット("黄")
				PRINT 　干渉効果発動中！
				RESETCOLOR
			ENDIF
			PRINTL
			LOCAL = 1
		ENDIF
		IF NOWEX:ARG:信頼度上昇格納
			IF CFLAG:ARG:信頼度 < 経験値テーブル:99 && CFLAG:ARG:信頼度 + NOWEX:ARG:信頼度上昇格納 >= 経験値テーブル:99
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への信頼度が最大になった","日常")
			ENDIF
			CFLAG:ARG:信頼度 += NOWEX:ARG:信頼度上昇格納
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORM \@(NOWEX:ARG:信頼度上昇格納 >= 0) ? 好感度要素：信頼＋{NOWEX:ARG:信頼度上昇格納} # 好感度要素：信頼－{-NOWEX:ARG:信頼度上昇格納}\@
			IF 干渉強化変数:ARG:干渉種類 == 2 && 干渉なしフラグ == 0
				SETCOLOR カラーパレット("黄")
				PRINT 　干渉効果発動中！
				RESETCOLOR
			ENDIF
			PRINTL
			LOCAL = 1
		ENDIF
		IF NOWEX:ARG:恋心度上昇格納
			IF CFLAG:ARG:恋心度 < 経験値テーブル:99 && CFLAG:ARG:恋心度 + NOWEX:ARG:恋心度上昇格納 >= 経験値テーブル:99
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への恋心度が最大になった","日常")
			ENDIF
			CFLAG:ARG:恋心度 += NOWEX:ARG:恋心度上昇格納
			CFLAG:ARG:恋慕度 = MAX(CFLAG:ARG:恋慕度, CFLAG:ARG:恋心度 / 3)
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORM \@(NOWEX:ARG:恋心度上昇格納 >= 0) ? 好感度要素：恋心＋{NOWEX:ARG:恋心度上昇格納} # 好感度要素：恋心－{-NOWEX:ARG:恋心度上昇格納}\@
			IF 干渉強化変数:ARG:干渉種類 == 3 && 干渉なしフラグ == 0
				SETCOLOR カラーパレット("黄")
				PRINT 　干渉効果発動中！
				RESETCOLOR
			ENDIF
			PRINTL
			LOCAL = 2
		ENDIF
		IF NOWEX:ARG:性愛度上昇格納
			IF CFLAG:ARG:性愛度 < 経験値テーブル:99 && CFLAG:ARG:性愛度 + NOWEX:ARG:性愛度上昇格納 >= 経験値テーブル:99
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への性愛度が最大になった","日常")
			ENDIF
			CFLAG:ARG:性愛度 += NOWEX:ARG:性愛度上昇格納
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORM \@(NOWEX:ARG:性愛度上昇格納 >= 0) ? 好感度要素：性愛＋{NOWEX:ARG:性愛度上昇格納} # 好感度要素：性愛－{-NOWEX:ARG:性愛度上昇格納}\@
			IF 干渉強化変数:ARG:干渉種類 == 4 && 干渉なしフラグ == 0
				SETCOLOR カラーパレット("黄")
				PRINT 　干渉効果発動中！
				RESETCOLOR
			ENDIF
			PRINTL
			LOCAL = 1
		ENDIF
		IF NOWEX:ARG:キャラ固有好感度要素上昇格納
			IF CFLAG:ARG:キャラ固有好感度 < 経験値テーブル:99 && CFLAG:ARG:キャラ固有好感度 + NOWEX:ARG:キャラ固有好感度要素上昇格納 >= 経験値テーブル:99
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への%CSTR:ARG:キャラ固有好感度要素%が最大になった","日常")
			ENDIF
			CFLAG:ARG:キャラ固有好感度 += NOWEX:ARG:キャラ固有好感度要素上昇格納
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORML \@(NOWEX:ARG:キャラ固有好感度要素上昇格納 >= 0) ? 好感度要素：%CSTR:ARG:キャラ固有好感度要素%＋{NOWEX:ARG:キャラ固有好感度要素上昇格納} # 好感度要素：%CSTR:ARG:キャラ固有好感度要素%－{-NOWEX:ARG:キャラ固有好感度要素上昇格納}\@
			LOCAL = 1
		ENDIF
		SIF LOCAL
			CALL FAVOR_LvUPCheck(ARG)
		IF LOCAL == 2
			SKIPDISP 1
			CALL Love_LvUPCheck(ARG)
			SKIPDISP 0
		ENDIF

	CASE "恋慕度"
		;他キャラと恋慕してる時、恋慕度は上がらない
		SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And 関係性種別 = '恋慕'")
			RETURN 0
		IF ARG:1 > 0
			CFLAG:ARG:恋慕度 += ARG:1
			PRINTFORM %CALLNAME:ARG%：
			PRINTFORML \@(ARG:1 >= 0) ? 恋慕度＋{ARG:1} # 恋慕度－{-ARG:1}\@
			CALL Love_LvUPCheck(ARG)
		ENDIF
	
	; CASE "屈服度"
	; IF ARG:1 > 0
	; 	CFLAG:ARG:屈服度 += ARG:1
	; 	PRINTFORM %CALLNAME:ARG%：
	; 	PRINTFORML \@(ARG:1 >= 0) ? 屈服度＋{ARG:1} # 屈服度－{-ARG:1}\@
	; 	;CALL Love_LvUPCheck(ARG)
	; ENDIF
	
	; CASE "快楽堕ち度"
	; IF ARG:1 > 0
	; 	CFLAG:ARG:快楽堕ち度 += ARG:1
	; 	PRINTFORM %CALLNAME:ARG%：
	; 	PRINTFORML \@(ARG:1 >= 0) ? 快楽堕ち度＋{ARG:1} # 快楽堕ち度－{-ARG:1}\@
	; 	;CALL Love_LvUPCheck(ARG)
	; ENDIF
ENDSELECT



@FAVOR_LvUPCheck(ARG)
#DIM ループ用
#DIM 要素最大値
#DIM 計算後レベル
#DIM 友情度記録
#DIM 信頼度記録
#DIM 恋心度記録
#DIM 性愛度記録
#DIM キャラ固有好感度記録
#DIMS 関係名, 6, 5

IF 関係名:0:0 == ""
	FOR LOCAL, 0, 6
		SPLIT 関係呼称:LOCAL, "/", LOCALS
		FOR ループ用, 0, 4
			関係名:LOCAL:ループ用 '= LOCALS:ループ用
		NEXT
	NEXT
ENDIF

LOCAL = 100501
SIF 干渉強化変数:ARG:干渉種類 == 1
	LOCAL += 2
SIF TALENT:MASTER:印象 == 1
	LOCAL += 1
友情度記録 = CFLAG:ARG:友情度
SIF 友情度記録 >= 100500
	友情度記録 = LOCAL

LOCAL = 100501
SIF 干渉強化変数:ARG:干渉種類 == 2
	LOCAL += 2
SIF TALENT:MASTER:印象 == 2
	LOCAL += 1
信頼度記録 = CFLAG:ARG:信頼度
SIF 信頼度記録 >= 100500
	信頼度記録 = LOCAL

LOCAL = 100501
SIF 干渉強化変数:ARG:干渉種類 == 3
	LOCAL += 2
SIF TALENT:MASTER:印象 == 3
	LOCAL += 1
恋心度記録 = CFLAG:ARG:恋心度
SIF 恋心度記録 >= 100500
	恋心度記録 = LOCAL

LOCAL = 100501
SIF 干渉強化変数:ARG:干渉種類 == 4
	LOCAL += 2
SIF TALENT:MASTER:印象 == 4
	LOCAL += 1
性愛度記録 = CFLAG:ARG:性愛度
SIF 性愛度記録 >= 100500
	性愛度記録 = LOCAL

LOCAL = 100501
キャラ固有好感度記録 = MIN(LOCAL, CFLAG:ARG:キャラ固有好感度)

SIF !INRANGE(ARG, 1, CHARANUM-1)
	RETURN

;テーブルと比較してレベル算出
要素最大値 = MAX(友情度記録, 信頼度記録, 恋心度記録, 性愛度記録, キャラ固有好感度記録)
FOR ループ用, 0, 100
	IF 経験値テーブル:ループ用 >= 要素最大値
		BREAK
	ENDIF
NEXT
計算後レベル = ループ用
要素最大値 = MAXFIND(5, 友情度記録, 信頼度記録, 恋心度記録, 性愛度記録, キャラ固有好感度記録)
TRYCCALLFORM キャラ固有好感度関係名_{NO:ARG}()
	FOR ループ用, 0, 6
		CALLFORM キャラ固有好感度関係名_{NO:ARG}(関係閾値:ループ用)
		関係名:ループ用:4 '= RESULTS
	NEXT
CATCH
	SIF 要素最大値 == 5
		要素最大値 = MAXFIND(4, 友情度記録, 信頼度記録, 恋心度記録, 性愛度記録)
ENDCATCH

;現在レベルより高ければレベルアップ処理
IF CFLAG:ARG:好感度レベル < 計算後レベル
	IF あなた特殊能力:フレンドリーセンス
		PRINTFORML 好感度LvUP!! {CFLAG:ARG:好感度レベル} > {計算後レベル}
	ENDIF
	IF CFLAG:ARG:好感度レベル <= 関係閾値:0 && 計算後レベル > 関係閾値:0
		LOCALS '= 関係名:1:要素最大値
		PRINTFORML 初対面 > %LOCALS%
		CSTR:ARG:現在の好感度表示 '= LOCALS
	ENDIF
	FOR ループ用, 1, 5
		IF CFLAG:ARG:好感度レベル <= 関係閾値:ループ用 && 計算後レベル > 関係閾値:ループ用
			LOCALS '= 関係名:(ループ用+1):要素最大値
			SIF CSTR:ARG:現在の好感度表示 != LOCALS
				PRINTFORML %CSTR:ARG:現在の好感度表示% > %LOCALS%
			CSTR:ARG:現在の好感度表示 '= LOCALS
		ENDIF
	NEXT
	IF CFLAG:ARG:好感度レベル < 100 && 計算後レベル >= 100
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"%CALLNAME:MASTER%への好感度レベルが最大になった","日常")
	ENDIF
	CFLAG:ARG:好感度レベル = 計算後レベル
ELSE
	LOCAL = RANGEFIND(計算後レベル, 関係閾値:0, 関係閾値:1, 関係閾値:2, 関係閾値:3, 関係閾値:4, 関係閾値:5) + 1
	LOCALS '= 関係名:LOCAL:要素最大値
	IF CSTR:ARG:現在の好感度表示 != LOCALS
		PRINTFORML %CALLNAME:ARG%の%CALLNAME:MASTER%を見る目が変わった……
		PRINTFORML %CSTR:ARG:現在の好感度表示% > %LOCALS% 
		CSTR:ARG:現在の好感度表示 '= LOCALS
	ENDIF
ENDIF
PRINTW 

@Love_LvUPCheck(ARG)
#DIM ループ用
#DIM 計算後レベル

;テーブルと比較してレベル算出
FOR ループ用, 0, 100
	IF 経験値テーブル:ループ用 >= CFLAG:ARG:恋慕度
		BREAK
	ENDIF
NEXT
計算後レベル = ループ用

;現在レベルより高ければレベルアップ処理
IF CFLAG:ARG:恋慕レベル < 計算後レベル
	PRINTFORML 恋慕度LvUP!! {CFLAG:ARG:恋慕レベル} > {計算後レベル}
	SIF CFLAG:ARG:恋慕レベル <= 0 && 計算後レベル > 0
		PRINTL 脈ナシ > ちょっと気になる
	SIF CFLAG:ARG:恋慕レベル <= 10 && 計算後レベル > 10
		PRINTL ちょっと気になる > なんかいいかも
	SIF CFLAG:ARG:恋慕レベル <= 20 && 計算後レベル > 20
		PRINTL なんかいいかも > 好意を自覚
	SIF CFLAG:ARG:恋慕レベル <= 50 && 計算後レベル > 50
		PRINTL 好意を自覚 > ラブラブ
	SIF CFLAG:ARG:恋慕レベル <= 80 && 計算後レベル > 80
		PRINTL ラブラブ > 最愛の人
	IF CFLAG:ARG:恋慕レベル < 100 && 計算後レベル >= 100
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:MASTER%への恋慕レベルが最大になった</font>","日常")
	ENDIF
	CFLAG:ARG:恋慕レベル = 計算後レベル
ENDIF
PRINTW 

;V調教の苦痛、反発補正(フィストなど激しいものは別処理)(実際には1000倍されてるので使用時は1/1000に)
@PAIN_CHECK_V(対象キャラ)
#FUNCTION
#DIM 対象キャラ
VARSET LOCAL
;EXP:Ｖ経験をみる
IF EXP:対象キャラ:Ｖ経験 < EXPLV:1
	LOCAL = 20
ELSEIF EXP:対象キャラ:Ｖ経験 < EXPLV:2
	LOCAL = 12
ELSEIF EXP:対象キャラ:Ｖ経験 < EXPLV:3
	LOCAL = 10
ELSEIF EXP:対象キャラ:Ｖ経験 < EXPLV:4
	LOCAL = 8
ELSEIF EXP:対象キャラ:Ｖ経験 < EXPLV:5
	LOCAL = 6
ELSEIF EXP:対象キャラ:Ｖ経験 >= EXPLV:5
	LOCAL = 4
ELSE
	LOCAL = 2
ENDIF

;EXP:ポルチオ開発を見る（ポルチオ時のみ）
IF RCVAR:対象キャラ:ポルチオ開発フラグ
	SELECTCASE EXP:対象キャラ:ポルチオ開発
		CASE IS < EXPLV:2
			LOCAL += 10
		CASE IS < EXPLV:3
			LOCAL += 8
		CASE IS < EXPLV:4
			LOCAL += 5
		CASE IS < EXPLV:5
			LOCAL += 2
		CASEELSE
			LOCAL += 0
	ENDSELECT
ENDIF

;PALAM:Ｖ感覚をみる
IF EX:対象キャラ:Ｖ絶頂 > 0
	LOCAL:1 = 0
ELSEIF PALAM:対象キャラ:快Ｖ < PALAMLV:1
	LOCAL:1 = 15
ELSEIF PALAM:対象キャラ:快Ｖ < EXPLV:2
	LOCAL:1 = 12
ELSEIF PALAM:対象キャラ:快Ｖ < EXPLV:3
	LOCAL:1 = 8
ELSEIF PALAM:対象キャラ:快Ｖ < EXPLV:4
	LOCAL:1 = 5
ELSEIF PALAM:対象キャラ:快Ｖ < EXPLV:5
	LOCAL:1 = 2
ELSEIF PALAM:対象キャラ:快Ｖ >= EXPLV:5
	LOCAL:1 = 1
ELSE
	LOCAL:1 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:対象キャラ:潤滑 < PALAMLV:1
	LOCAL:2 = 15
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:2
	LOCAL:2 = 12
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:3
	LOCAL:2 = 10
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:4
	LOCAL:2 = 8
ELSEIF PALAM:対象キャラ:潤滑 >= PALAMLV:4
	LOCAL:2 = 5
ELSE
	LOCAL:2 = 2
ENDIF

RETURNF LOCAL * LOCAL:1 * LOCAL:2

;アナル調教の苦痛補正(実際には1000倍されてるので使用時は1/1000に)
@PAIN_CHECK_A(対象キャラ)
#FUNCTION
#DIM 対象キャラ
VARSET LOCAL
;EXP:Ａ経験をみる
IF EXP:対象キャラ:Ａ経験 < EXPLV:1
	LOCAL = 20
ELSEIF EXP:対象キャラ:Ａ経験 < EXPLV:2
	LOCAL = 12
ELSEIF EXP:対象キャラ:Ａ経験 < EXPLV:3
	LOCAL = 10
ELSEIF EXP:対象キャラ:Ａ経験 < EXPLV:4
	LOCAL = 8
ELSEIF EXP:対象キャラ:Ａ経験 < EXPLV:5
	LOCAL = 6
ELSEIF EXP:対象キャラ:Ａ経験 >= EXPLV:5
	LOCAL = 4
ELSE
	LOCAL = 2
ENDIF

;EXP:Ｓ字結腸開発を見る（Ｓ字結腸時のみ）
IF RCVAR:対象キャラ:Ｓ字結腸開発フラグ
	SELECTCASE EXP:対象キャラ:Ｓ字結腸開発
		CASE IS < EXPLV:2
			LOCAL += 10
		CASE IS < EXPLV:3
			LOCAL += 8
		CASE IS < EXPLV:4
			LOCAL += 5
		CASE IS < EXPLV:5
			LOCAL += 2
		CASEELSE
			LOCAL += 0
	ENDSELECT
ENDIF

;PALAM:Ａ感覚をみる
IF PALAM:対象キャラ:快Ａ < PALAMLV:1
	LOCAL:1 = 20
ELSEIF PALAM:対象キャラ:快Ａ < EXPLV:2
	LOCAL:1 = 15
ELSEIF PALAM:対象キャラ:快Ａ < EXPLV:3
	LOCAL:1 = 10
ELSEIF PALAM:対象キャラ:快Ａ < EXPLV:4
	LOCAL:1 = 6
ELSEIF PALAM:対象キャラ:快Ａ < EXPLV:5
	LOCAL:1 = 2
ELSEIF PALAM:対象キャラ:快Ａ >= EXPLV:5
	LOCAL:1 = 1
ELSE
	LOCAL:1 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:対象キャラ:潤滑 < PALAMLV:1
	LOCAL:2 = 15
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:2
	LOCAL:2 = 12
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:3
	LOCAL:2 = 10
ELSEIF PALAM:対象キャラ:潤滑 < PALAMLV:4
	LOCAL:2 = 8
ELSEIF PALAM:対象キャラ:潤滑 >= PALAMLV:4
	LOCAL:2 = 5
ELSE
	LOCAL:2 = 2
ENDIF

RETURNF LOCAL * LOCAL:1 * LOCAL:2


;-------------------------------------------------
;パラメータUP処理
;-------------------------------------------------
@技巧上昇判定(ARG)
;上昇必要量
;0>1=10  1>2=20  2>3=50  3>4=100  4>5=170
;技巧経験値は奉仕ソースを取得した時、その強度に応じて自動的に上昇する

SIF ABL:ARG:技巧 > 4
	RETURN 0

IF 技巧経験値:ARG:0 > (ABL:ARG:技巧 * ABL:ARG:技巧 * 10 + 10)
	技巧経験値:ARG:0 = 0
	ABL:ARG:技巧 += 1
	PRINTFORML %CALLNAME:ARG%の技巧Lvが{ABL:ARG:技巧}に上昇した
	RETURN 1
ENDIF

RETURN 0

@性知識上昇判定(ARG)

SIF 知識素質:ARG:性知識 > ABL:ARG:技巧
	RETURN 0

;最大値は３
SIF 知識素質:ARG:性知識 > 2
	RETURN 0

;性知識マイナスの場合、基礎値に0を使用する
LOCAL = MAX(知識素質:ARG:性知識, 0)

IF 性知識経験値:ARG:0 > ((LOCAL + 1) * (LOCAL + 1) * (LOCAL + 1) * 10 + 10)
	性知識経験値:ARG:0 = 0
	知識素質:ARG:性知識 += 1
	PRINTFORML %CALLNAME:ARG%の性知識Lvが%性知識段階表示(知識素質:ARG:性知識 - 1, ARG)%から%性知識段階表示(知識素質:ARG:性知識, ARG)%に上昇した
	RETURN 1
ENDIF

RETURN 0


;-------------------------------------------------
;サキュバスボディによる反射処理
;-------------------------------------------------
@サキュバスボディ適用(対象キャラ)
#DIM 対象キャラ
#DIM CONST 係数 = 0, 1, 2, 10
#DIM CONST 反射キャップ = 0, 2000, 5000, 10000

;自分自身に反射しない
SIF 対象キャラ == MASTER
	RETURN

;そもそもあなたに快楽を与えていないなら処理無し
SIF TCVAR:対象キャラ:あなた快楽上昇 <= 0
	RETURN

;あなたがサキュバスボディを所持・発揮しているか
SIF あなた特殊能力:サキュバスボディ <= 0
	RETURN
SIF !あなた特殊能力_発揮条件_サキュバスボディ()
	RETURN

;PALAM:快Ｃ(0)～快Ｓ(4)のうち上昇しているものを数える
LOCAL:1 = 0
FOR LOCAL, 0, 5
	SIF CUP:対象キャラ:LOCAL > 0
		LOCAL:1 ++
NEXT

;上昇しているものがある場合、その箇所に快楽を振り分けて反射
IF LOCAL:1 > 0
	;PALAM:快Ｃ(0)～快Ｂ(3)へそれぞれ反射
	FOR LOCAL, 0, 4
		SIF CUP:対象キャラ:LOCAL > 0
			CUP:対象キャラ:LOCAL += MIN(TCVAR:対象キャラ:あなた快楽上昇 * 係数:(あなた特殊能力:サキュバスボディ) / LOCAL:1, 反射キャップ:(あなた特殊能力:サキュバスボディ))
	NEXT
	;PALAM:快Ｓへ反射 他の1/3
	SIF CUP:対象キャラ:快Ｓ > 0
		CUP:対象キャラ:快Ｓ += MIN(TCVAR:対象キャラ:あなた快楽上昇 * 係数:(あなた特殊能力:サキュバスボディ) / LOCAL:1 / 3, 反射キャップ:(あなた特殊能力:サキュバスボディ))
ELSE
	;上昇しているものが無い場合、PALAM:快Ｓへ反射 1/30
	CUP:対象キャラ:快Ｓ += MIN(TCVAR:対象キャラ:あなた快楽上昇 * 係数:(あなた特殊能力:サキュバスボディ) / 30, 反射キャップ:(あなた特殊能力:サキュバスボディ))
ENDIF


;-------------------------------------------------
;首絞めによる補正適用
;-------------------------------------------------
@首絞め補正_快楽(対象キャラ, 補正前数値, 快Ｓフラグ = 0)
#FUNCTION
#DIM 対象キャラ
#DIM 補正前数値
#DIM 快Ｓフラグ
#DIM 補正後数値
#DIM 実行キャラ
#DIM 最大ボーナス補正

IF 補正前数値 <= 0
	RETURNF 補正前数値
ELSEIF MODE_存在判定_ターゲット側("首絞め", 対象キャラ)
	実行キャラ = DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
	;まず基礎補正2～5倍（快Ｓのみ、対象キャラの首絞め性癖により変動）
	IF 快Ｓフラグ
		補正後数値 = 補正前数値 * (2 + MIN(性癖素質:対象キャラ:首絞め, 3))
	ELSE
		補正後数値 = 補正前数値 * 2
	ENDIF

	;次に実行キャラの技巧により最大ボーナス補正
	IF ABL:実行キャラ:技巧 < 3
		;下手だと無し
		RETURNF 補正後数値
	ELSEIF ABL:実行キャラ:技巧 < 7
		最大ボーナス補正 = 2 + (ABL:実行キャラ:技巧 - 3) / 2
	ELSE
		最大ボーナス補正 = 4 + MIN(3, ABL:実行キャラ:技巧 - 7) * 2
	ENDIF

	;最後に対象キャラの体力割合が小さいほどボーナス補正
	RETURNF 補正後数値 * 最大ボーナス補正 * MAXBASE:対象キャラ:体力 / ((最大ボーナス補正 - 1) * BASE:対象キャラ:体力 + MAXBASE:対象キャラ:体力)
ELSEIF 性癖素質:対象キャラ:首絞め == 3
	;首絞め中毒は首絞めてないと半減
	RETURNF MAX(1, 補正前数値 / 2)
ELSEIF 性癖素質:対象キャラ:首絞め == 2
	;首絞め愛好は首絞めてないと2割減
	RETURNF MAX(1, 補正前数値 * 4 / 5)
ELSE
	RETURNF 補正前数値
ENDIF


@首絞め補正_消費体力(対象キャラ, 補正前数値)
#FUNCTION
#DIM 対象キャラ
#DIM 補正前数値
#DIM 実行キャラ

IF 補正前数値 <= 0
	RETURNF 補正前数値
ELSEIF MODE_存在判定_ターゲット側("首絞め", 対象キャラ)
	実行キャラ = DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
	;5倍～1.5倍（実行キャラの技巧により変動）
	RETURNF 補正前数値 * (10 - LIMIT(ABL:実行キャラ:技巧 - 3, 0, 7)) / 2
ELSE
	RETURNF 補正前数値
ENDIF
