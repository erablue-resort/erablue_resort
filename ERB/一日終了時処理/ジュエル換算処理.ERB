
@JUEL_CHECK(ARG)
#DIM ループ用
#DIM 獲得量
#DIM CONST PALAM_潤滑 = GETNUM(PALAM, "潤滑")
#DIM CONST PALAM_否定 = GETNUM(PALAM, "否定")

FOR ループ用, 0, VARSIZE("PALAM")
	SIF STRLENSU(PALAMNAME:ループ用) == 0
		CONTINUE
	;PALAM:潤滑は珠にならない
	SIF ループ用 == PALAM_潤滑
		CONTINUE
	;PALAM:否定は存在しない。反感や不快が否定の珠になる
	SIF ループ用 == PALAM_否定
		CONTINUE

	SELECTCASE PALAM:ARG:ループ用
		CASE IS < PALAMLV:1
			獲得量 = 0
		CASE IS < PALAMLV:1 * 3
			獲得量 = 1
		CASE IS < PALAMLV:2
			獲得量 = 2
		CASE IS < PALAMLV:2 * 3
			獲得量 = 10
		CASE IS < PALAMLV:3
			獲得量 = 20
		CASE IS < PALAMLV:3 * 2
			獲得量 = 100
		CASE IS < PALAMLV:4
			獲得量 = 200
		CASE IS < PALAMLV:5
			獲得量 = 1000
		CASE IS < PALAMLV:6
			獲得量 = 2000
		CASE IS < PALAMLV:7
			獲得量 = 3000
		CASE IS < PALAMLV:8
			獲得量 = 5000
		CASE IS < PALAMLV:9
			獲得量 = 8000
		CASE IS < PALAMLV:10
			獲得量 = 12000
		CASE IS < PALAMLV:11
			獲得量 = 25000
		CASE IS < PALAMLV:12
			獲得量 = 40000
		CASE IS < PALAMLV:13
			獲得量 = 60000
		CASE IS < PALAMLV:14
			獲得量 = 100000
		CASEELSE
			獲得量 = 150000
	ENDSELECT
	
	;快感系
	IF ループ用 >= 0 && ループ用 < 5
		獲得量 = 獲得量 + EX:ARG:ループ用 * 1000
	ENDIF
	
	SIF ループ用 == 0 && !獲得量 && TALENT:ARG:性別 == 2 &&  OPTION変数:男性感度上昇低下
		獲得量 = 獲得量 / 250 + 1
		
	SIF ループ用 == 4 && !獲得量 && TALENT:ARG:性別 == 2 &&  OPTION変数:男性感度上昇低下
		獲得量 = 獲得量 / 50 + 1
		
	SIF (ループ用 == 3 || ループ用 == 10 || ループ用 == 12 || ループ用 == 16) && TALENT:ARG:性別 == 2 &&  OPTION変数:男性感度上昇低下
		獲得量 = 獲得量 / 1000
	
	IF IS_DENIAL_PALAM(ループ用)
		;否定の珠
		JUEL:ARG:否定 += 獲得量 * 2
	ELSE
		JUEL:ARG:ループ用 += 獲得量
	ENDIF
NEXT


@IS_DENIAL_PALAM(ARG)
#FUNCTION
;否定そのものは含めない（PALAM:否定は存在しないから）
;抑鬱もCSVに定義されていないので無視する
#DIM CONST 否定系PALAM = GETNUM(PALAM, "反感"), GETNUM(PALAM, "不快")
RETURNF MATCH(否定系PALAM, ARG) > 0


@DENIAL_CHECK(ARG)
SIF JUEL:ARG:否定 == 0
	RETURN 0
	
LOCAL = 1

LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:奉仕 && !JUEL:ARG:恥情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:征服 && !JUEL:ARG:苦痛 && !JUEL:ARG:習得)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:4
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 13
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

