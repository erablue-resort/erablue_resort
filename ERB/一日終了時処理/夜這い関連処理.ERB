
@一日終了時夜這い判定(対象キャラ)
#DIM 対象キャラ
#DIM 夜這い相手
#DIM 夜這候補
#DIM シャッフル番号
#DIM 追加最大人数
#DIM 対象配列, 500
#DIM 候補配列, 500
#DIM 候補数
#DIM 関係数
#DIM 夜這い条件性欲
#DIMS 関係性種別
;0を返すとオナニー判定へ進む
;1を返すとオナニー判定はスキップする


;酔い潰れや体力０の時は夜這いしない
IF CFLAG:対象キャラ:酔いすぎ
	RETURN 0
ENDIF
IF CFLAG:対象キャラ:体力限界
	RETURN 0
ENDIF
;対象になってる時も夜這いしない
IF TCVAR:対象キャラ:夜這い済み
	RETURN 0
ENDIF
;通常夜這い条件
夜這い条件性欲 = 1000
SIF TALENT:対象キャラ:処女 && GETBIT(TALENT:対象キャラ:性別, 0)
	夜這い条件性欲 += 300
SIF TALENT:対象キャラ:非童貞 == 0 && GETBIT(TALENT:対象キャラ:性別, 1)
	夜這い条件性欲 -= 300
SIF TALENT:対象キャラ:自制心
	夜這い条件性欲 += TALENT:対象キャラ:自制心 * 100
SIF TALENT:対象キャラ:無関心 > 0
	夜這い条件性欲 += 100
SIF TALENT:対象キャラ:感情乏しい > 0
	夜這い条件性欲 += 150
SIF TALENT:対象キャラ:性的興味
	夜這い条件性欲 -= TALENT:対象キャラ:性的興味 * 100
SIF TALENT:対象キャラ:羞恥心
	夜這い条件性欲 += TALENT:対象キャラ:羞恥心 * 100
SIF TALENT:対象キャラ:貞操 > 0 && EXP:対象キャラ:夜這い経験 < 10
	夜這い条件性欲 += 300
SIF TALENT:対象キャラ:貞操 < 0
	夜這い条件性欲 -= 200
SIF TALENT:対象キャラ:豪快
	夜這い条件性欲 -= TALENT:対象キャラ:豪快 * 100
SIF BASE:対象キャラ:性欲 < 夜這い条件性欲
	RETURN 0

;相手の選定
VARSET 候補配列
VARSET 対象配列
候補数 = 0
関係数 = DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')", , 対象配列)
IF 関係数 > 0
	FOR LOCAL, 0, 関係数
		IF DT_CELL_GETS("関係性データベース", 対象配列:LOCAL, "関係性種別", 1) == "恋慕"
			;恋慕はセフレより確率が高いので２個入れる
			LOCAL:1 = 関係性_相手人物番号取得(CFLAG:対象キャラ:人物番号, 対象配列:LOCAL)
			SIF LOCAL:1 <= 0
				CONTINUE
			夜這い相手 = FIND_CHARA_FROM_PERSON_ID(LOCAL:1)
			SIF 夜這い相手 < 0
				CONTINUE
			;既に夜這いしてるか対象になってる相手はキャンセル
			SIF TCVAR:夜這い相手:夜這い済み
				CONTINUE
			;島にいない相手は当然ダメ
			SIF CFLAG:夜這い相手:滞在期間 < 0
				CONTINUE
			;性知識がどっちもない場合は夜這いしない
			SIF MAX(知識素質:対象キャラ:性知識, 知識素質:夜這い相手:性知識) < 0
				CONTINUE
			候補配列:候補数 = LOCAL:1
			候補数 ++
			候補配列:候補数 = LOCAL:1
			候補数 ++
		ELSE
			;セフレ
			LOCAL:1 = 関係性_相手人物番号取得(CFLAG:対象キャラ:人物番号, 対象配列:LOCAL)
			SIF LOCAL:1 <= 0
				CONTINUE
			夜這い相手 = FIND_CHARA_FROM_PERSON_ID(LOCAL:1)
			SIF 夜這い相手 < 0
				CONTINUE
			;既に夜這いしてるか対象になってる相手はキャンセル
			SIF TCVAR:夜這い相手:夜這い済み
				CONTINUE
			;島にいない相手は当然ダメ
			SIF CFLAG:夜這い相手:滞在期間 < 0
				CONTINUE
			;性知識がどっちもない場合は夜這いしない
			SIF MAX(知識素質:対象キャラ:性知識, 知識素質:夜這い相手:性知識) < 0
				CONTINUE
			候補配列:候補数 = LOCAL:1
			候補数 ++
		ENDIF
	NEXT
ENDIF
IF TALENT:対象キャラ:恋慕 && (TCVAR:MASTER:夜這い済み == 0 || (TCVAR:MASTER:夜這い済み == 1 && あなた特殊能力:ハーレム))
	候補配列:候補数 = CFLAG:MASTER:人物番号
	候補数 ++
	候補配列:候補数 = CFLAG:MASTER:人物番号
	候補数 ++
ENDIF
IF TALENT:対象キャラ:セフレ && TCVAR:MASTER:夜這い済み == 0
	候補配列:候補数 = CFLAG:MASTER:人物番号
	候補数 ++
ENDIF
IF 候補数 < 1
	RETURN 0
ENDIF

;ランダムに決定
夜這い相手 = FIND_CHARA_FROM_PERSON_ID(候補配列:(RAND:候補数))
IF 夜這い相手 == MASTER
	IF TALENT:対象キャラ:恋慕
		関係性種別 = 恋慕
	ELSE
		関係性種別 = セフレ
	ENDIF
ELSE
	IF MATCH(候補配列, CFLAG:夜這い相手:人物番号) > 1
		関係性種別 = 恋慕
	ELSE
		関係性種別 = セフレ
	ENDIF
ENDIF


CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:対象キャラ%は性欲が溜まり、むらむらとした気分になっている<br>"

LOCAL = キャラクター部屋検索(対象キャラ)
IF 夜這い相手 == MASTER && CFLAG:MASTER:現在マップ種別 == 部屋検索_マップ種別 && CFLAG:MASTER:現在位置 == LOCAL
	;同じ部屋で寝てる場合、失敗パターンなし
ELSE
	IF EXP:対象キャラ:夜這い経験 < 10 && TALENT:対象キャラ:経験豊富 == 0 && RAND:2
		;夜這い経験がないとまごまごする
		CSTR:対象キャラ:夜這い報告_記録文字列 += @"しかし、夜這いをする勇気が出ないようだ……<br>"
		CSTR:対象キャラ:夜這い報告_記録文字列 += @"しばらく%CALLNAME:夜這い相手%の部屋の前でまごまごした後、自分の部屋へと帰っていった。<br>"
		CSTR:対象キャラ:夜這い口上渡し_記録文字列 = まごまご
		RETURN (夜這い相手 + 1) * -1
	ENDIF
	
	LOCAL = キャラクター部屋検索(対象キャラ)
	IF 夜這い相手 == MASTER && (CFLAG:MASTER:現在マップ種別 != 0 || CFLAG:MASTER:現在位置 != CFLAG:MASTER:自室位置) && (CFLAG:MASTER:現在マップ種別 != 部屋検索_マップ種別 || CFLAG:MASTER:現在位置 != LOCAL)
		;あなた居ないパターン
		CSTR:対象キャラ:夜這い報告_記録文字列 += @"夜のお誘いをしようと%CALLNAME:夜這い相手%の部屋を訪れたが、留守のようだ……。<br>"
		CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:対象キャラ%は残念そうに自室に帰っていった。<br>"
		CSTR:対象キャラ:夜這い口上渡し_記録文字列 = 不在
		RETURN (夜這い相手 + 1) * -1
	ENDIF
ENDIF

VARSET 対象配列, -1

CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:対象キャラ%は"
IF 関係性種別 == "恋慕"
	CSTR:対象キャラ:夜這い報告_記録文字列 += "恋人の"
ELSE
	;夜這い成功時、相手がセフレのとき、他のセフレを呼ぶことがある
	;最大人数をセット
	IF 知識素質:対象キャラ:性知識 >= 0
		;疎いと呼ばない、Lv1以上の時に呼ぶ可能性がある
		追加最大人数 = RAND:(知識素質:対象キャラ:性知識 + 1)
	ELSE
		;性知識がない場合、逆に人数が多くなる
		追加最大人数 = RAND:10
	ENDIF
	IF 追加最大人数 > 0
		VARSET 対象配列
		VARSET 候補配列
		候補数 = 0
		関係数 = DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And 関係性種別 = 'セフレ'", , 対象配列)
		IF 関係数 > 0
			FOR LOCAL, 0, 関係数
				;セフレ
				LOCAL:1 = 関係性_相手人物番号取得(CFLAG:対象キャラ:人物番号, 対象配列:LOCAL)
				SIF LOCAL:1 <= 0
					CONTINUE
				夜這候補 = FIND_CHARA_FROM_PERSON_ID(LOCAL:1)
				SIF 夜這候補 < 0
					CONTINUE
				SIF 夜這候補 == 夜這い相手
					CONTINUE
				;既に夜這いしてるか対象になってる相手はキャンセル
				SIF TCVAR:夜這候補:夜這い済み
					CONTINUE
				;島にいない相手は当然ダメ
				SIF CFLAG:夜這候補:滞在期間 < 0
					CONTINUE
				候補配列:候補数 = LOCAL:1
				候補数 ++
			NEXT
			IF TALENT:対象キャラ:セフレ && TCVAR:MASTER:夜這い済み == 0
				候補配列:候補数 = CFLAG:MASTER:人物番号
				候補数 ++
			ENDIF
			VARSET 対象配列, -1
			IF 候補数 <= 追加最大人数
				FOR LOCAL, 0, 候補数
					対象配列:LOCAL = FIND_CHARA_FROM_PERSON_ID(候補配列:LOCAL)
					SIF LOCAL > 0
						CSTR:対象キャラ:夜這い報告_記録文字列 += "、"
					CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:(対象配列:LOCAL)%"
				NEXT
				CSTR:対象キャラ:夜這い報告_記録文字列 += "を呼び出して"
				追加最大人数 = 候補数
			ELSE
				FOR LOCAL, 0, 追加最大人数
					シャッフル番号 = RAND(LOCAL, 候補数)
					SWAP 候補配列:シャッフル番号, 候補配列:LOCAL
				NEXT
				FOR LOCAL, 0, 追加最大人数
					対象配列:LOCAL = FIND_CHARA_FROM_PERSON_ID(候補配列:LOCAL)
					SIF LOCAL > 0
						CSTR:対象キャラ:夜這い報告_記録文字列 += "、"
					CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:(対象配列:LOCAL)%"
				NEXT
				CSTR:対象キャラ:夜這い報告_記録文字列 += "を呼び出して"
			ENDIF
		ENDIF
	ENDIF
	CSTR:対象キャラ:夜這い報告_記録文字列 += "セフレ相手の"
ENDIF

LOCAL = キャラクター部屋検索(対象キャラ)
IF 夜這い相手 == MASTER && CFLAG:MASTER:現在マップ種別 == 部屋検索_マップ種別 && CFLAG:MASTER:現在位置 == LOCAL
	CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:夜這い相手%と、自分の部屋で熱い夜を過ごした……<br>"
ELSE
	CSTR:対象キャラ:夜這い報告_記録文字列 += @"%CALLNAME:夜這い相手%の部屋を訪れ、熱い夜を過ごした……<br>"
ENDIF
CSTR:対象キャラ:夜這い報告_記録文字列 += "<br><br>ここでWAITを入れて表示を分離"
;受け攻め設定を取り出す
DT_SELECT "関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And (対象キャラ１ = {CFLAG:夜這い相手:人物番号} Or 対象キャラ２ = {CFLAG:夜這い相手:人物番号})"
RFLAG:夜這い処理中フラグ = 1
CALL 夜這い実処理(対象キャラ, 夜這い相手, RESULT:1, 関係性種別)
;3P以上の時
IF 対象配列:0 > -1
	FOR LOCAL, 0, 追加最大人数
		;受け攻め設定を取り出す
		DT_SELECT "関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And (対象キャラ１ = {CFLAG:(対象配列:LOCAL):人物番号} Or 対象キャラ２ = {CFLAG:(対象配列:LOCAL):人物番号})"
		CALL 夜這い実処理(対象キャラ, (対象配列:LOCAL), RESULT:1, 関係性種別)
	NEXT
ENDIF

RFLAG:夜這い処理中フラグ = 0
;夜這い相手の番号＋１００を返す
RETURN 夜這い相手 + 100 


@夜這い実処理(実行キャラ, 夜這い相手, 配列id, 関係性種別)
#DIM 実行キャラ
#DIM 実行キャラ_快楽基礎値
#DIM 夜這い相手
#DIM 夜這い相手_快楽基礎値
#DIM 対象キャラ
#DIM 対象キャラ_快楽基礎値
#DIM 相手キャラ
#DIM 相手キャラ_快楽基礎値
#DIM あなた_攻めパワー
#DIM あなた_受けパワー
#DIM ZP量保存, 3
#DIM 絶頂保存
#DIM 精液量保存
#DIM 配列id
#DIM 逆フラグ
#DIM リンクキャラ
#DIMS EXP計算フラグ
#DIMS 関係性種別
#DIM 場所保存

逆フラグ = 0
VARSET ZP量保存
;両者とも各部位に経験と絶頂を付与する
;そのために基礎値を算出

実行キャラ_快楽基礎値 = 1 + ABL:夜這い相手:技巧 + ABL:夜這い相手:指テク + ABL:夜這い相手:舌技
夜這い相手_快楽基礎値 = 1 + ABL:実行キャラ:技巧 + ABL:実行キャラ:指テク + ABL:実行キャラ:舌技

;初設定
場所保存 = キャラクター部屋検索(夜這い相手)
CALL 初体験日登録処理(実行キャラ, 夜這い相手, "初うふふ", DAY * 1440 + TIME, GETPLACENAME(部屋検索_マップ種別, 場所保存), "夜這い", "", 1)
CALL 初体験日登録処理(実行キャラ, 夜這い相手, "うふふ", DAY * 1440 + TIME, GETPLACENAME(部屋検索_マップ種別, 場所保存), "夜這い", "", 0)
IF RESULT
	;相手とのうふふが初の場合にここに入る
	CALL 履歴データベース登録(CFLAG:実行キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:夜這い相手%とえっちをした</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(夜這い相手, 実行キャラ, "初うふふ", DAY * 1440 + TIME, GETPLACENAME(部屋検索_マップ種別, 場所保存), "夜這い", "", 1)
CALL 初体験日登録処理(夜這い相手, 実行キャラ, "うふふ", DAY * 1440 + TIME, GETPLACENAME(部屋検索_マップ種別, 場所保存), "夜這い", "", 0)
IF RESULT
	;相手とのうふふが初の場合にここに入る
	CALL 履歴データベース登録(CFLAG:夜這い相手:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:実行キャラ%とえっちをした</font><img src='えっちハート'>","うふふ")
ENDIF


;性別ごとに部位に経験を付与

;まずは組み合わせ不問で発生する愛撫関連
IF 関係性種別 == "恋慕"
	;恋慕だとキス経験増加
	LOCAL = RAND:(MAX(夜這い相手_快楽基礎値 + ABL:実行キャラ:舌技, 実行キャラ_快楽基礎値 + ABL:夜這い相手:舌技)) + 5
	SIF TALENT:実行キャラ:舌使い
		LOCAL += 5
	SIF TALENT:夜這い相手:舌使い
		LOCAL += 5
	SIF TALENT:実行キャラ:キス嗜好
		LOCAL += TALENT:実行キャラ:キス嗜好 * 15
	SIF TALENT:夜這い相手:キス嗜好
		LOCAL += TALENT:実行キャラ:キス嗜好 * 15

	EXP:実行キャラ:キス経験 += LOCAL
	EXP:夜這い相手:キス経験 += LOCAL
	IF CFLAG:実行キャラ:ファーストキスフラグ == 0
		CFLAG:実行キャラ:ファーストキスフラグ = 1
		CFLAG:実行キャラ:ファーストキス相手番号 = CFLAG:夜這い相手:人物番号
		RCVAR:実行キャラ:ファーストキス中フラグ = 1
		CALL 履歴データベース登録(CFLAG:実行キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:夜這い相手%の[唇]にファーストキスを捧げた</font><img src='えっちハート'>","うふふ")
	ENDIF
	IF CFLAG:夜這い相手:ファーストキスフラグ == 0
		CFLAG:夜這い相手:ファーストキスフラグ = 1
		CFLAG:夜這い相手:ファーストキス相手番号 = CFLAG:実行キャラ:人物番号
		RCVAR:夜這い相手:ファーストキス中フラグ = 1
		CALL 履歴データベース登録(CFLAG:夜這い相手:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:実行キャラ%の[唇]にファーストキスを捧げた</font><img src='えっちハート'>","うふふ")
	ENDIF
ENDIF
FOR LOCAL, 0, 2
	;処理を共通させるために入れ替えながら汎用処理
	IF LOCAL == 0
		対象キャラ = 実行キャラ
		対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
		相手キャラ = 夜這い相手
	ELSE
		対象キャラ = 夜這い相手
		対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
		相手キャラ = 実行キャラ
	ENDIF

	;愛撫B関連
	IF TALENT:対象キャラ:Ｂ感度 != -2
		;女の時と男の時とで分岐
		IF GETBIT(TALENT:対象キャラ:性別, 0)
			EXP:対象キャラ:Ｂ経験 += 対象キャラ_快楽基礎値 * 2
			絶頂保存 = (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｂ感覚 * 2) / 3
			EXP:対象キャラ:Ｂ絶頂経験 += 絶頂保存
			EXP:対象キャラ:乳首開発 += 対象キャラ_快楽基礎値
		ELSE
			EXP:対象キャラ:Ｂ経験 += 対象キャラ_快楽基礎値 / 3
			絶頂保存 = (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｂ感覚 * 2) / 10
			EXP:対象キャラ:Ｂ絶頂経験 += 絶頂保存
			EXP:対象キャラ:乳首開発 += 対象キャラ_快楽基礎値 / 3
		ENDIF
		IF TALENT:対象キャラ:母乳体質
			EXP:対象キャラ:噴乳経験 += 絶頂保存
		ENDIF
		BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
		EXP:対象キャラ:絶頂経験 += 絶頂保存
		JUEL:対象キャラ:快Ｂ += 絶頂保存 * 10
		ZP量保存:LOCAL += 絶頂保存
	ENDIF

	;愛撫C関連
	IF TALENT:対象キャラ:Ｃ感度 != -2
		;ちんこがあるかないかで分岐
		IF GETBIT(TALENT:対象キャラ:性別, 1)
			EXP:対象キャラ:Ｃ経験 += 対象キャラ_快楽基礎値 * 2
			絶頂保存 = (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｃ感覚 * 2) / 2
			EXP:対象キャラ:Ｃ絶頂経験 += 絶頂保存
			CALL コマンド外精力消費処理(対象キャラ, 絶頂保存)
			EXP:対象キャラ:射精経験 += RESULT
		ELSE
			EXP:対象キャラ:Ｃ経験 += 対象キャラ_快楽基礎値 * 2
			絶頂保存 = (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｃ感覚 * 2) / 2
			EXP:対象キャラ:Ｃ絶頂経験 += 絶頂保存
		ENDIF
		BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
		EXP:対象キャラ:絶頂経験 += 絶頂保存
		JUEL:対象キャラ:快Ｃ += 絶頂保存 * 10
		ZP量保存:LOCAL += 絶頂保存
	ENDIF

	;愛撫V関連
	IF TALENT:対象キャラ:Ｖ感度 != -2 && GETBIT(TALENT:対象キャラ:性別, 0)
		EXP:対象キャラ:Ｖ経験 += MAX(1, 対象キャラ_快楽基礎値 * 2 - TALENT:対象キャラ:処女 * 5)
		絶頂保存 = MAX(0, (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｖ感覚 * 2) / 2 - TALENT:対象キャラ:処女 * 3)
		EXP:対象キャラ:Ｖ絶頂経験 += 絶頂保存
		EXP:対象キャラ:絶頂経験 += 絶頂保存
		JUEL:対象キャラ:快Ｖ += 絶頂保存 * 10
		ZP量保存:LOCAL += 絶頂保存
		BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
	ENDIF

	;愛撫A関連
	;両方にA経験がないなら発動しない
	IF TALENT:対象キャラ:Ａ感度 != -2 && EXP:対象キャラ:Ａ経験 > 0 && EXP:相手キャラ:Ａ経験 > 0
		SELECTCASE EXP:対象キャラ:Ａ経験
			CASE IS < 10
				IF TALENT:相手キャラ:性別 != 2 && TALENT:対象キャラ:性別 == 2
					EXP:対象キャラ:Ａ経験 += RAND:2
					絶頂保存 = 0
				ELSE
					EXP:対象キャラ:Ａ経験 += 1
					絶頂保存 = 0
				ENDIF
			CASE IS < 50
				IF TALENT:相手キャラ:性別 != 2 && TALENT:対象キャラ:性別 == 2
					EXP:対象キャラ:Ａ経験 += RAND:2
					絶頂保存 = 0
				ELSE
					EXP:対象キャラ:Ａ経験 += MAX(1, 対象キャラ_快楽基礎値 / 4)
					絶頂保存 = 1
				ENDIF
			CASE IS < 100
				IF TALENT:相手キャラ:性別 != 2 && TALENT:対象キャラ:性別 == 2
					絶頂保存 = RAND:3
					EXP:対象キャラ:Ａ経験 += 絶頂保存 * 2
				ELSE
					EXP:対象キャラ:Ａ経験 += 対象キャラ_快楽基礎値 / 2
					絶頂保存 = (対象キャラ_快楽基礎値 / 4 + ABL:対象キャラ:Ａ感覚) / 2
				ENDIF
			CASEELSE
				EXP:対象キャラ:Ａ経験 += 対象キャラ_快楽基礎値
				絶頂保存 = (対象キャラ_快楽基礎値 / 2 + ABL:対象キャラ:Ａ感覚 * 2) / 2
		ENDSELECT
		EXP:対象キャラ:Ａ絶頂経験 += 絶頂保存
		EXP:対象キャラ:絶頂経験 += 絶頂保存
		JUEL:対象キャラ:快Ａ += 絶頂保存 * 10
		ZP量保存:LOCAL += 絶頂保存
		BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
	ENDIF
NEXT

;次に組み合わせが必要な性交関連
;ちんこなし組み合わせの場合、性知識が必要
IF GETBIT(TALENT:実行キャラ:性別, 1) == 0 && GETBIT(TALENT:夜這い相手:性別, 1) == 0
	SIF MAX(知識素質:実行キャラ:性知識, 知識素質:夜這い相手:性知識) < 1
		RETURN 0
ENDIF

;同性愛　レズなら戻り値1ゲイなら戻り値2
SIF 同性愛性癖判定(実行キャラ,夜這い相手)
	EXP:実行キャラ:同性愛経験 += 50
SIF 同性愛性癖判定(夜這い相手,実行キャラ)
	EXP:夜這い相手:同性愛経験 += 50
IF (同性愛性癖判定(実行キャラ,夜這い相手) || 同性愛性癖判定(夜這い相手,実行キャラ)) && MAX(性癖素質:実行キャラ:同性愛, 性癖素質:夜這い相手:同性愛) < 2
	;同性愛素質が愛好にならない限りはセックス処理には入らない
	GOTO 結果フェイズ
ENDIF


;基礎値に追加
実行キャラ_快楽基礎値 += ABL:夜這い相手:腰使い
夜這い相手_快楽基礎値 += ABL:実行キャラ:腰使い
;受け攻めを確定
IF GETBIT(TALENT:実行キャラ:性別, 1) != GETBIT(TALENT:夜這い相手:性別, 1)
	;ちんこありANDなしの組み合わせの場合、ありが攻め・なしが受け固定
	IF GETBIT(TALENT:実行キャラ:性別, 1) == 0
		;対象＝受け
		対象キャラ = 実行キャラ
		対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
		;相手＝攻め
		相手キャラ = 夜這い相手
		相手キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
		逆フラグ = 1
	ELSE
		対象キャラ = 夜這い相手
		対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
		相手キャラ = 実行キャラ
		相手キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
	ENDIF
ELSEIF 夜這い相手 == MASTER
	;あなたへの夜這いの場合、受け攻めは両者のABLにより決定される
	;基本リバ、相手に合わせる傾向多し
	あなた_攻めパワー = POWER(ABL:実行キャラ:マゾ性感, 3) + ABL:夜這い相手:サド性感 + 1
	あなた_受けパワー = POWER(ABL:実行キャラ:サド性感, 3) + ABL:夜這い相手:マゾ性感
	;IF !あなた特殊能力:バリネコ && (あなた特殊能力:バリタチ || RAND:(あなた_攻めパワー + あなた_受けパワー) < あなた_攻めパワー)
	IF RAND:(あなた_攻めパワー + あなた_受けパワー) < あなた_攻めパワー
		;対象＝受け
		対象キャラ = 実行キャラ
		対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
		;相手＝攻め
		相手キャラ = 夜這い相手
		相手キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
		逆フラグ = 1
	ELSE
		;対象＝受け
		対象キャラ = 夜這い相手
		対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
		;相手＝攻め
		相手キャラ = 実行キャラ
		相手キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
	ENDIF
ELSE
	;男女以外の場合、受け攻め設定を見る
	SELECTCASE DT_CELL_GETS("関係性データベース", 配列id, "受け攻め設定", 1)
		CASE ""
			IF RAND:2
				;対象＝受け
				対象キャラ = 実行キャラ
				対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
				;相手＝攻め
				相手キャラ = 夜這い相手
				相手キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				逆フラグ = 1
			ELSE
				;対象＝受け
				対象キャラ = 夜這い相手
				対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				;相手＝攻め
				相手キャラ = 実行キャラ
				相手キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
			ENDIF
		CASE "順"
			IF CFLAG:実行キャラ:人物番号 == DT_CELL_GET("関係性データベース", 配列id, "対象キャラ１", 1)
				;対象＝受け
				対象キャラ = 夜這い相手
				対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				;相手＝攻め
				相手キャラ = 実行キャラ
				相手キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
			ELSE
				;対象＝受け
				対象キャラ = 実行キャラ
				対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
				;相手＝攻め
				相手キャラ = 夜這い相手
				相手キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				逆フラグ = 1
			ENDIF
		CASE "逆"
			IF CFLAG:実行キャラ:人物番号 == DT_CELL_GET("関係性データベース", 配列id, "対象キャラ１", 1)
				;対象＝受け
				対象キャラ = 実行キャラ
				対象キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
				;相手＝攻め
				相手キャラ = 夜這い相手
				相手キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				逆フラグ = 1
			ELSE
				;対象＝受け
				対象キャラ = 夜這い相手
				対象キャラ_快楽基礎値 = 夜這い相手_快楽基礎値
				;相手＝攻め
				相手キャラ = 実行キャラ
				相手キャラ_快楽基礎値 = 実行キャラ_快楽基礎値
			ENDIF
	ENDSELECT
ENDIF




;V快楽算出処理
IF OPTION変数:夜這い処女・童貞 && HETEROSEX(実行キャラ, 夜這い相手) == 0
	SIF TALENT:対象キャラ:処女
		GOTO A挿入フェイズ
	SIF GETBIT(TALENT:相手キャラ:非童貞, 0) == 0
		GOTO A挿入フェイズ
ENDIF
IF OPTION変数:同性愛処女・童貞
	IF HETEROSEX(実行キャラ, 夜這い相手) == 1
		;V同性愛は女女しか発生しないので男男については記述しない
		SIF TALENT:対象キャラ:処女
			GOTO A挿入フェイズ
	ENDIF
ENDIF
;妊娠中はV挿入禁止
IF TALENT:対象キャラ:妊娠
	GOTO A挿入フェイズ
ENDIF

IF GETBIT(TALENT:対象キャラ:性別, 0) && TALENT:対象キャラ:Ｖ感度 != -2
	EXP:対象キャラ:Ｖ経験 += MAX(1, 対象キャラ_快楽基礎値 * 2 - TALENT:対象キャラ:処女 * 5)
	EXP:対象キャラ:Ｖ性交経験 += MAX(1, 対象キャラ_快楽基礎値 * 2 - TALENT:対象キャラ:処女 * 5)
	絶頂保存 = MAX(0, (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ｖ感覚 * 2) / 2 - TALENT:対象キャラ:処女 * 3)
	EXP:対象キャラ:Ｖ絶頂経験 += 絶頂保存
	EXP:対象キャラ:絶頂経験 += 絶頂保存
	JUEL:対象キャラ:快Ｖ += 絶頂保存 * 10
	IF 逆フラグ
		ZP量保存:0 += 絶頂保存
	ELSE
		ZP量保存:1 += 絶頂保存
	ENDIF
	BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
	;処女喪失
	CALL LOST_VIRGIN(対象キャラ, 相手キャラ, 1)
	CALL LOST_MAN_VIRGIN(相手キャラ, 対象キャラ)
	;ちんこがあるかないかで分岐
	IF GETBIT(TALENT:相手キャラ:性別, 1)
		EXP:相手キャラ:Ｃ経験 += 相手キャラ_快楽基礎値 * 2
		EXP:相手キャラ:Ｃ性交経験_Ｖ挿入 += 相手キャラ_快楽基礎値 * 2
		絶頂保存 = (相手キャラ_快楽基礎値 + ABL:相手キャラ:Ｃ感覚 * 2) / 2
		EXP:相手キャラ:Ｃ絶頂経験 += 絶頂保存
		EXP:相手キャラ:絶頂経験 += 絶頂保存
		BASE:相手キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 10))
		IF 逆フラグ
			ZP量保存:1 += 絶頂保存
		ELSE
			ZP量保存:0 += 絶頂保存
		ENDIF

		CALL コマンド外精力消費処理(相手キャラ, 絶頂保存)
		絶頂保存 = RESULT
		精液量保存 = RESULT:1
		IF 絶頂保存 > 0
			EXP:相手キャラ:射精経験 += 絶頂保存
			EXP:対象キャラ:精液経験 += 絶頂保存

			;履歴登録
			RFLAG:履歴用数値保存 = 絶頂保存
			CALL 履歴登録チェック(対象キャラ, "膣内射精回数")
			履歴用実績:対象キャラ:膣内射精回数 += 絶頂保存

			IF GETBIT(CFLAG:対象キャラ:妊娠個別設定フラグ, 0)
				SIF GETBIT(CFLAG:対象キャラ:妊娠個別設定フラグ, 3) == 0
					CALL TAKE_SPERM(対象キャラ, 相手キャラ, 精液量保存)
			ELSE
				SIF OPTION変数:夜這い時妊娠 == 0
					CALL TAKE_SPERM(対象キャラ, 相手キャラ, 精液量保存)
			ENDIF

			EXP:対象キャラ:膣内射精絶頂経験 += 絶頂保存 / (4 - MAX(0, 性癖素質:対象キャラ:膣内射精))
			IF 性癖素質:対象キャラ:膣内射精 > 0
				EXP:対象キャラ:Ｖ絶頂経験 += 絶頂保存 * 性癖素質:対象キャラ:膣内射精
				EXP:対象キャラ:絶頂経験 += 絶頂保存 * 性癖素質:対象キャラ:膣内射精
				IF 逆フラグ
					ZP量保存:0 += 絶頂保存 * 性癖素質:対象キャラ:膣内射精
				ELSE
					ZP量保存:1 += 絶頂保存 * 性癖素質:対象キャラ:膣内射精
				ENDIF
				BASE:対象キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 性癖素質:対象キャラ:膣内射精 * 10))
			ENDIF
		ENDIF
	ELSEIF TALENT:相手キャラ:Ｃ感度 != -2
		;ないならペニバン
		EXP:相手キャラ:Ｖ経験 += 相手キャラ_快楽基礎値 * 2
		絶頂保存 = (相手キャラ_快楽基礎値 + ABL:相手キャラ:Ｖ感覚 * 2) / 2
		EXP:相手キャラ:Ｖ絶頂経験 += 絶頂保存
		EXP:相手キャラ:絶頂経験 += 絶頂保存
		BASE:相手キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 10))
		IF 逆フラグ
			ZP量保存:1 += 絶頂保存
		ELSE
			ZP量保存:0 += 絶頂保存
		ENDIF
	ENDIF

	SIF 同性愛性癖判定(実行キャラ,夜這い相手)
		EXP:実行キャラ:同性愛経験 += 20
	SIF 同性愛性癖判定(夜這い相手,実行キャラ)
		EXP:夜這い相手:同性愛経験 += 20
ENDIF

$A挿入フェイズ
;A快楽算出処理
IF OPTION変数:夜這い処女・童貞 && HETEROSEX(実行キャラ, 夜這い相手) == 0
	SIF TALENT:対象キャラ:Ａ処女
		GOTO 結果フェイズ
	SIF GETBIT(TALENT:相手キャラ:非童貞, 1) == 0
		GOTO 結果フェイズ
ENDIF
IF OPTION変数:同性愛処女・童貞
	IF HETEROSEX(実行キャラ, 夜這い相手) == 2
		SIF TALENT:対象キャラ:Ａ処女
			GOTO 結果フェイズ
		SIF GETBIT(TALENT:相手キャラ:非童貞, 1) == 0
			GOTO 結果フェイズ
	ELSEIF HETEROSEX(実行キャラ, 夜這い相手) == 1
		SIF TALENT:対象キャラ:Ａ処女
			GOTO 結果フェイズ
	ENDIF
ENDIF

;A経験が両方低いないなら発動しない
IF TALENT:対象キャラ:Ａ感度 != -2 && EXP:相手キャラ:Ｃ性交経験_Ａ挿入 + EXP:対象キャラ:Ａ経験 > 80
	EXP:対象キャラ:Ａ経験 += 対象キャラ_快楽基礎値 * 2
	EXP:対象キャラ:Ａ性交経験 += 対象キャラ_快楽基礎値 * 2
	絶頂保存 = (対象キャラ_快楽基礎値 + ABL:対象キャラ:Ａ感覚 * 2) / 2
	EXP:対象キャラ:Ａ絶頂経験 += 絶頂保存
	EXP:対象キャラ:絶頂経験 += 絶頂保存
	JUEL:対象キャラ:快Ａ += 絶頂保存 * 10
	BASE:対象キャラ:性欲 = MAX(0, BASE:対象キャラ:性欲 - (絶頂保存 * 10))
	IF 逆フラグ
		ZP量保存:0 += 絶頂保存
	ELSE
		ZP量保存:1 += 絶頂保存
	ENDIF
	;Ａ処女喪失
	CALL LOST_A_VIRGIN(対象キャラ, 相手キャラ, 1)
	CALL LOST_MAN_A_VIRGIN(相手キャラ, 対象キャラ)
	;ちんこがあるかないかで分岐
	IF GETBIT(TALENT:相手キャラ:性別, 1)
		EXP:相手キャラ:Ｃ経験 += 相手キャラ_快楽基礎値 * 2
		EXP:相手キャラ:Ｃ性交経験_Ａ挿入 += 相手キャラ_快楽基礎値 * 2
		絶頂保存 = (相手キャラ_快楽基礎値 + ABL:相手キャラ:Ｃ感覚 * 2) / 2
		EXP:相手キャラ:Ｃ絶頂経験 += 絶頂保存
		EXP:相手キャラ:絶頂経験 += 絶頂保存
		BASE:相手キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 10))
		IF 逆フラグ
			ZP量保存:1 += 絶頂保存
		ELSE
			ZP量保存:0 += 絶頂保存
		ENDIF

		CALL コマンド外精力消費処理(相手キャラ, 絶頂保存)
		絶頂保存 = RESULT
		精液量保存 = RESULT:1
		IF 絶頂保存 > 0
			EXP:相手キャラ:射精経験 += 絶頂保存
			EXP:対象キャラ:精液経験 += 絶頂保存

			;履歴登録
			RFLAG:履歴用数値保存 = 絶頂保存
			CALL 履歴登録チェック(対象キャラ, "腸内射精回数")
			履歴用実績:対象キャラ:腸内射精回数 += 絶頂保存
			
			;腸内射精された精液を計上
			CALL TAKE_SPERM_A(対象キャラ, 相手キャラ, 精液量保存)

			EXP:対象キャラ:腸内射精絶頂経験 += 絶頂保存 / (4 - MAX(0, 性癖素質:対象キャラ:腸内射精))
			IF 性癖素質:対象キャラ:腸内射精 > 0
				EXP:対象キャラ:Ａ絶頂経験 += 絶頂保存 * 性癖素質:対象キャラ:腸内射精
				EXP:対象キャラ:絶頂経験 += 絶頂保存 * 性癖素質:対象キャラ:腸内射精
				BASE:対象キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 性癖素質:対象キャラ:腸内射精 * 10))
				IF 逆フラグ
					ZP量保存:0 += 絶頂保存 * 性癖素質:対象キャラ:腸内射精
				ELSE
					ZP量保存:1 += 絶頂保存 * 性癖素質:対象キャラ:腸内射精
				ENDIF
			ENDIF
		ENDIF
	ELSEIF TALENT:相手キャラ:Ｃ感度 != -2
		;ないならペニバン
		EXP:相手キャラ:Ｖ経験 += 相手キャラ_快楽基礎値 * 2
		絶頂保存 = (相手キャラ_快楽基礎値 + ABL:相手キャラ:Ｖ感覚 * 2) / 2
		EXP:相手キャラ:Ｖ絶頂経験 += 絶頂保存
		EXP:相手キャラ:絶頂経験 += 絶頂保存
		BASE:相手キャラ:性欲 = MAX(0, BASE:相手キャラ:性欲 - (絶頂保存 * 10))
		IF 逆フラグ
			ZP量保存:1 += 絶頂保存
		ELSE
			ZP量保存:0 += 絶頂保存
		ENDIF
	ENDIF

	SIF 同性愛性癖判定(実行キャラ,夜這い相手)
		EXP:実行キャラ:同性愛経験 += 20
	SIF 同性愛性癖判定(夜這い相手,実行キャラ)
		EXP:夜這い相手:同性愛経験 += 20
ENDIF

$結果フェイズ
;-------------------------------------------------
;経験表示
;-------------------------------------------------
EXP計算フラグ = 
FOR LOCAL, 0, 2
	IF LOCAL == 0
		対象キャラ = 実行キャラ
	ELSE
		対象キャラ = 夜這い相手
	ENDIF
	FOR LOCAL:1, 0, 200
		IF EXP_UP(LOCAL:1, 対象キャラ)
			LOCAL:2 ++
			SIF LOCAL:2 == 1
				CSTR:実行キャラ:夜這い報告_記録文字列 += "<br>"
			LOCALS = %GET_EXPNAME(LOCAL:1, 対象キャラ)%
			CSTR:実行キャラ:夜這い報告_記録文字列 += @"%LOCALS,15,LEFT%"
			LOCALS = +{EXP_UP(LOCAL:1, 対象キャラ)}
			CSTR:実行キャラ:夜這い報告_記録文字列 += @"%LOCALS,4,LEFT%(%CALLNAME:対象キャラ%)<br>"

			;命のリンクを個別処理
			IF TALENT:対象キャラ:命のリンク && LOCAL:1 <= 12
				IF !キャラ一致チェック(対象キャラ, "[碧く麗しき十天の輝星]ジータ")
					リンクキャラ = キャラ検索("[碧く麗しき十天の輝星]ジータ")
					IF TALENT:リンクキャラ:命のリンク && CFLAG:リンクキャラ:滞在期間 > 0 && リンクキャラ != 実行キャラ && リンクキャラ != 夜這い相手
						EXP:リンクキャラ:(LOCAL:1) += EXP_UP(LOCAL:1, 対象キャラ)
						SIF STRFIND(EXP計算フラグ, @"{リンクキャラ}_") != 0 && STRFIND(EXP計算フラグ, @"_{リンクキャラ}_") == -1
							EXP計算フラグ += @"{リンクキャラ}_"
					ENDIF
				ENDIF
				IF !キャラ一致チェック(対象キャラ, "[蒼の少女]ルリア")
					リンクキャラ = キャラ検索("[蒼の少女]ルリア")
					IF TALENT:リンクキャラ:命のリンク && CFLAG:リンクキャラ:滞在期間 > 0 && リンクキャラ != 実行キャラ && リンクキャラ != 夜這い相手
						EXP:リンクキャラ:(LOCAL:1) += EXP_UP(LOCAL:1, 対象キャラ)
						SIF STRFIND(EXP計算フラグ, @"{リンクキャラ}_") != 0 && STRFIND(EXP計算フラグ, @"_{リンクキャラ}_") == -1
							EXP計算フラグ += @"{リンクキャラ}_"
					ENDIF
				ENDIF
				IF !キャラ一致チェック(対象キャラ, "[夢抱く者]グラン")
					リンクキャラ = キャラ検索("[夢抱く者]グラン")
					IF TALENT:リンクキャラ:命のリンク && CFLAG:リンクキャラ:滞在期間 > 0 && リンクキャラ != 実行キャラ && リンクキャラ != 夜這い相手
						EXP:リンクキャラ:(LOCAL:1) += EXP_UP(LOCAL:1, 対象キャラ)
						SIF STRFIND(EXP計算フラグ, @"{リンクキャラ}_") != 0 && STRFIND(EXP計算フラグ, @"_{リンクキャラ}_") == -1
							EXP計算フラグ += @"{リンクキャラ}_"
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	NEXT
	LOCAL:2 = 0
NEXT

CALL EXP変動記録処理(実行キャラ)
CALL EXP変動記録処理(夜這い相手)

LOCALS:1 = 
IF EXP計算フラグ != ""
	VARSET RESULTS
	SPLIT EXP計算フラグ, "_", RESULTS
	FOR LOCAL, 0, RESULT
		SIF ISNUMERIC(RESULTS:LOCAL) == 0
			CONTINUE
		リンクキャラ = TOINT(RESULTS:LOCAL)
		FOR LOCAL:1, 0, 20
			IF EXP_UP(LOCAL:1, リンクキャラ)
				LOCAL:2 ++
				SIF LOCAL:2 == 1
					CSTR:実行キャラ:夜這い報告_記録文字列 += "<br>"
				LOCALS = %GET_EXPNAME(LOCAL:1, リンクキャラ)%
				CSTR:実行キャラ:夜這い報告_記録文字列 += @"%LOCALS,15,LEFT%"
				LOCALS = +{EXP_UP(LOCAL:1, リンクキャラ)}
				CSTR:実行キャラ:夜這い報告_記録文字列 += @"%LOCALS,4,LEFT%(%CALLNAME:リンクキャラ%)<br>"
				IF LOCAL:1 == 10
					;絶頂経験をZPに投げる
					LOCALS:1 += @" + {EXP_UP(LOCAL:1, リンクキャラ)}(%CALLNAME:リンクキャラ%)"
					ZP量保存:2 += EXP_UP(LOCAL:1, リンクキャラ)
				ENDIF
			ENDIF
		NEXT
		CALL EXP変動記録処理(リンクキャラ)
	NEXT
ENDIF

;-------------------------------------------------
;ZPの取得量表示
;-------------------------------------------------
IF MAX(ZP量保存:0, ZP量保存:1) > 0
	CSTR:実行キャラ:夜這い報告_記録文字列 += "<br>"
	CSTR:実行キャラ:夜這い報告_記録文字列 += @"ZP:{FLAG:ZP所持量}"
	IF ZP量保存:0
		IF 実行キャラ == MASTER
			ZP量保存:0 = 0
		ELSE
			CSTR:実行キャラ:夜這い報告_記録文字列 += @" + {ZP量保存:0}(%CALLNAME:実行キャラ%)"
		ENDIF
	ENDIF
	IF ZP量保存:1
		IF 夜這い相手 == MASTER
			ZP量保存:1 = 0
		ELSE
			CSTR:実行キャラ:夜這い報告_記録文字列 += @" + {ZP量保存:1}(%CALLNAME:夜這い相手%)"
		ENDIF
	ENDIF
	IF ZP量保存:2
		CSTR:実行キャラ:夜這い報告_記録文字列 += @"%LOCALS:1%"
	ENDIF
	FLAG:ZP所持量 += ZP量保存:0 + ZP量保存:1 + ZP量保存:2
	CSTR:実行キャラ:夜這い報告_記録文字列 += @" = {FLAG:ZP所持量}<br>"
ENDIF

;性癖成長
;自動取得性癖_記録文字列に誰が何を取得したかを記録し、後でメッセージ再生に使う
CALL 性癖取得チェック(実行キャラ, 1)
CSTR:実行キャラ:自動取得性癖_記録文字列 += @"%RSTR:コマンド結果受渡し文字列%"
CALL 性癖取得チェック(夜這い相手, 1)
CSTR:実行キャラ:自動取得性癖_記録文字列 += @"%RSTR:コマンド結果受渡し文字列%"

;自動成長性癖_記録文字列に誰が何を何レベルから成長したかを記録し、後でメッセージ再生に使う
CALL 性癖成長チェック(実行キャラ, 1)
CSTR:実行キャラ:自動成長性癖_記録文字列 += @"%RSTR:コマンド結果受渡し文字列%"
CALL 性癖成長チェック(夜這い相手, 1)
CSTR:実行キャラ:自動成長性癖_記録文字列 += @"%RSTR:コマンド結果受渡し文字列%"

;夜這いにあなたが絡んでいる場合は、相手側の獲得ZPをもとに精力回復
SIF 実行キャラ == MASTER
	CALL 精力ゲージ_コマンド外回復処理(ZP量保存:1)
SIF 夜這い相手 == MASTER
	CALL 精力ゲージ_コマンド外回復処理(ZP量保存:0)

;フラグ建て
TCVAR:実行キャラ:夜這い済み = 1
TCVAR:夜這い相手:夜這い済み = 1
EXP:実行キャラ:夜這い経験 += 1

;口上へ渡す用の文字列を突っ込む
RSTR:コマンド結果受渡し文字列 = {夜這い相手}+++{ZP量保存:0}+++{ZP量保存:1}

