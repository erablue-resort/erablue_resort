@おさわりファイルアーカイブ作成(フォルダ, ファイルアーカイブ, ファイル数, 衣装種類, 差分種類)
#DIMS フォルダ
#DIMS REF ファイルアーカイブ,0,0,0
#DIM REF ファイル数,0,0
#DIMS REF 衣装種類,0
#DIMS REF 差分種類,0
#DIM 衣装種類数
#DIM 差分種類数

衣装種類数 = VARSIZE("衣装種類")
差分種類数 = VARSIZE("差分種類")

VARSET ファイルアーカイブ
VARSET ファイル数
FOR LOCAL, 0, 衣装種類数
	FOR LOCAL:1, 0, 差分種類数
		ファイル数:LOCAL:(LOCAL:1) = ENUMFILES(フォルダ, 衣装種類:LOCAL + "_差分_" + 差分種類:(LOCAL:1) + "*.png")
		FOR LOCAL:2, 0, ファイル数:LOCAL:(LOCAL:1)
			ファイルアーカイブ:LOCAL:(LOCAL:1):(LOCAL:2) '= SUBSTRING(RESULTS:(LOCAL:2), STRLENS(フォルダ), -1)
		NEXT
	NEXT
	VARSET RESULTS
	RESULT = ENUMFILES(フォルダ, 衣装種類:LOCAL + ".png")
	ファイルアーカイブ:LOCAL:0:(ファイル数:LOCAL:0) '= SUBSTRING(RESULTS:0, STRLENS(フォルダ), -1)
	ファイル数:LOCAL:0 += RESULT
NEXT

RETURN


@おさわりファイル差分選出(ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
;同属性のファイルを抽選する
;欲情などの状態を監視して差分選出をする
#DIM REF ファイル数,0,0
#DIMS REF 衣装種類,0
#DIM REF 適正衣装種類ID
#DIM REF 適正差分種類ID
#DIM REF 対象衣装種類ID
#DIM REF 対象差分種類ID
#DIM REF 対象ファイルID
#DIMS REF メイン画像更新タイプ

SELECTCASE メイン画像更新タイプ
CASE "描画"
	;更新がないときはチェックしない
	RETURN
ENDSELECT

;対象属性の画像が複数枚あるときは、現在と違う画像を選出する
;対象差分がないときは適正衣装、デフォルトの順に差分をチェックして代わりに表示する
SELECTCASE ファイル数:適正衣装種類ID:適正差分種類ID
	CASE 0
		IF ファイル数:適正衣装種類ID:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
			対象衣装種類ID = 適正衣装種類ID
			対象差分種類ID = 1
		ELSEIF ファイル数:適正衣装種類ID:0
			対象衣装種類ID = 適正衣装種類ID
			対象差分種類ID = 0
		ELSEIF ファイル数:0:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
			対象衣装種類ID = 0
			対象差分種類ID = 1
		ELSEIF ファイル数:0:0
			対象衣装種類ID = 0
			対象差分種類ID = 0
		ELSE
		ENDIF
		IF ファイル数:対象衣装種類ID:対象差分種類ID > 1
			FOR LOCAL, 0, 10
				LOCAL:1 = RAND:(ファイル数:対象衣装種類ID:対象差分種類ID)
				SIF 対象ファイルID == LOCAL:1
					CONTINUE
				対象ファイルID = LOCAL:1
				BREAK
			NEXT
		ELSE
			対象ファイルID = 0
		ENDIF
	CASE 1
		対象衣装種類ID = 適正衣装種類ID
		対象差分種類ID = 適正差分種類ID
		対象ファイルID = 0
	CASEELSE
		対象衣装種類ID = 適正衣装種類ID
		対象差分種類ID = 適正差分種類ID
		FOR LOCAL, 0, 10
			LOCAL:1 = RAND:(ファイル数:対象衣装種類ID:対象差分種類ID)
			SIF 対象ファイルID == LOCAL:1
				CONTINUE
			対象ファイルID = LOCAL:1
			BREAK
		NEXT
ENDSELECT


@おさわりファイル適正選出(ファイル数, 衣装種類, 現在衣装種類ID, 適正衣装種類ID, 差分種類, 適正差分種類ID, マッサージ画像優先フラグ, 画像切り替えカウンタ, メイン画像更新タイプ)
#DIM REF ファイル数,0,0
#DIMS REF 衣装種類,0
#DIMS REF 差分種類,0
#DIM 現在衣装種類ID
#DIM REF 適正衣装種類ID
#DIM REF 適正差分種類ID
#DIM 前回欲情
#DIM マッサージ画像優先フラグ
#DIM REF 画像切り替えカウンタ
#DIMS REF メイン画像更新タイプ


;マッサージモードの時
;マッサージの種類を判定して衣装種類を設定する
;フラグを持っていればマッサージモードかつフラグONになる
IF マッサージ画像優先フラグ
	;指定マッサージ画像を適正衣装にセットする
	IF CUP:TARGET:快Ｂ ;胸マッサージのとき
		適正衣装種類ID = FINDELEMENT(衣装種類, "Ｂマッサージ")
		画像切り替えカウンタ = 3
		メイン画像更新タイプ '= "更新"
	ELSEIF CUP:TARGET:快Ａ ;尻マッサージのとき
		適正衣装種類ID = FINDELEMENT(衣装種類, "Ａマッサージ")
		画像切り替えカウンタ = 3
		メイン画像更新タイプ '= "更新"
	ELSEIF CUP:TARGET:快Ｖ ;Ｖマッサージのとき
		適正衣装種類ID = FINDELEMENT(衣装種類, "Ｖマッサージ")
		画像切り替えカウンタ = 3
		メイン画像更新タイプ '= "更新"
	ELSEIF CUP:TARGET:快Ｃ ;Ｃマッサージのとき
		適正衣装種類ID = FINDELEMENT(衣装種類, "Ｃマッサージ")
		画像切り替えカウンタ = 3
		メイン画像更新タイプ '= "更新"
	ENDIF

	;自然画像切り替え時に標準マッサージ画像をセットする
	IF 画像切り替えカウンタ == 5
		IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
			適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始うつ伏せ")
		ELSE
			適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始")
		ENDIF
	ENDIF

	;ここからは適正衣装が存在するかしないかで振り分けるブロック------------------------
	;部位指定マッサージかチェック
	IF STRFIND(衣装種類:適正衣装種類ID, "マッサージ") > 1
		;先頭文字が"マ"でないマッサージ画像
		;該当のマッサージ画像があるかチェック
		IF ファイル数:適正衣装種類ID:0
			;適正衣装種類ID = 適正衣装種類ID のままパスする
			GOTO 差分
		ENDIF
	ENDIF
	;表裏でチェックして、あればその画像でなければ着せ替え服
	適正衣装種類ID = 現在衣装種類ID
	IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
		IF ファイル数:FINDELEMENT(衣装種類, "マッサージ開始うつ伏せ"):0
			適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始うつ伏せ")
		ELSE
			IF ファイル数:FINDELEMENT(衣装種類, "マッサージ開始"):0
				適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始")
			ELSE
			ENDIF
		ENDIF
	ELSE
		IF ファイル数:FINDELEMENT(衣装種類, "マッサージ開始"):0
			適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始")
		ELSE
			IF ファイル数:FINDELEMENT(衣装種類, "マッサージ開始うつ伏せ"):0
				適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始うつ伏せ")
			ELSE
			ENDIF
		ENDIF
	ENDIF
ELSE
	;マッサージ優先でないときは着せ替え服
	適正衣装種類ID = 現在衣装種類ID
ENDIF

$差分
; ;今回初めて欲情になったとき、欲情画像を優先表示候補にする
; IF PALAM:TARGET:欲情 >= PALAMLV:4 && 前回欲情 < PALAMLV:4
;     適正差分種類ID = 1
;     メイン画像更新タイプ '= "更新"
; ENDIF
; 前回欲情 = PALAM:TARGET:欲情

;絶頂差分から復帰するために表示候補を変える
IF PALAM:TARGET:欲情 >= PALAMLV:4
	適正差分種類ID = 1
ELSE
	適正差分種類ID = 0
ENDIF

@おさわり画像基準セット(サイズ基準ファイルパス, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
#DIM レイヤー番号 = 50
#DIMS サイズ基準ファイルパス
#DIM おさわりあり限定解除フラグ
#DIM 画像最大化フラグ
#DIMS セットタイプ
#DIM REF 画像位置,0
#DIM REF 画像サイズ,0
#DIM REF システムコマンド位置基準,0
#DIM REF システムコマンドサイズ,0
#DIM REF アクセスコマンド位置基準,0
#DIM REF アクセスコマンドサイズ,0
#DIM REF アイテム表示位置基準

GDISPOSE レイヤー番号 + 19 
;おさわりフォルダごと指定されているので、いずれのファイルでもこの形で呼び出す
GCREATEFROMFILE レイヤー番号 + 19 , @"%CSTR:TARGET:画像フォルダ%/%サイズ基準ファイルパス%"
;#DIM アクセスコマンド位置基準 = 1020, 40 ;x= 画像位置(620) + WIDTH(360) + 余白(40)

IF おさわりあり限定解除フラグ
	IF 画像最大化フラグ
		;最大化する
		セットタイプ '= "最大化"
	ELSE
		;汎用かつ最大化しないのでシステム推奨サイズに出来る
		セットタイプ '= "システム推奨"
	ENDIF
ELSE
	;当たり判定付きなので当たり判定に準拠したサイズ
	セットタイプ '= "判定基準"
ENDIF

SELECTCASE セットタイプ
CASE "最大化" ;ウィンドウサイズ縦いっぱいまで広げる
	画像サイズ = GWIDTH(レイヤー番号 + 19 ) * CLIENTHEIGHT() / GHEIGHT(レイヤー番号 + 19 ), CLIENTHEIGHT()
	画像位置 = 420, 0
	システムコマンドサイズ = 400, 40
	システムコマンド位置基準 = 0, 0
	アクセスコマンドサイズ = 400, 40
	; アクセスコマンド位置基準 = CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40, 40
	アクセスコマンド位置基準 = MIN(画像位置:0 + 画像サイズ:0 + 40, CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40), 40
	アイテム表示位置基準 = アクセスコマンド位置基準:0, 620
CASE "システム推奨"
	画像サイズ = GWIDTH(レイヤー番号 + 19 ) * 720 / GHEIGHT(レイヤー番号 + 19 ), 720
	画像位置 = 420, 0
	システムコマンドサイズ = 400, 40
	システムコマンド位置基準 = 0, 0
	アクセスコマンドサイズ = 400, 40
	; アクセスコマンド位置基準 = CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40, 40
	アクセスコマンド位置基準 = MIN(画像位置:0 + 画像サイズ:0 + 40, CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40), 40
	アイテム表示位置基準 = アクセスコマンド位置基準:0, 620
CASE "判定基準"
	画像サイズ = GWIDTH(レイヤー番号 + 19 ), GHEIGHT(レイヤー番号 + 19 )
	画像位置 = 420, 0
	システムコマンドサイズ = 400, 40
	システムコマンド位置基準 = 0, 0
	アクセスコマンドサイズ = 400, 40
	; アクセスコマンド位置基準 = CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40, 40
	アクセスコマンド位置基準 = MIN(画像位置:0 + 画像サイズ:0 + 40, CLIENTWIDTH() - アクセスコマンドサイズ:0 - 40), 40
	アイテム表示位置基準 = アクセスコマンド位置基準:0, 620
ENDSELECT

