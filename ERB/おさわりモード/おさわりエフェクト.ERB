@クリックグラフィックセットアップ
#DIM カラーマトリクス, 5, 5
#DIM CONST 色種類数 = 7
;80に素材呼出
GDISPOSE 80
GCREATEFROMFILE 80, @"カットイン素材/クリックアイコン.png"
GDISPOSE 81
GCREATE 81, 720 * 色種類数, 720
FOR LOCAL, 0, 色種類数
    SELECTCASE LOCAL
        CASE 0
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 256,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 256,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 1 ;RED
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0,  64,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,  64,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 2 ;GREEN
            カラーマトリクス:0:0 =   0,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,   0,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 3 ;BLUE
            カラーマトリクス:0:0 =   0,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 256,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 4 ;YELLOW
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,  64,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 5 ;ORANGE
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 192,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,   0,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
        CASE 6 ;PINK
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0,   0,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 192,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
    ENDSELECT
    GDRAWG 81, 80, 720 * LOCAL, 0, 720, 720, 0, 0, 720, 720, カラーマトリクス:0:0
NEXT

;82にハート書き込み
GDISPOSE 82
GCREATE 82, 720, 720

カラーマトリクス:0:0 = 256,   0,   0,   0,   0
カラーマトリクス:1:0 =   0,  64,   0,   0,   0
カラーマトリクス:2:0 =   0,   0, 192,   0,   0
カラーマトリクス:3:0 =   0,   0,   0, 256,   0
カラーマトリクス:4:0 =   0,   0,   0,   0, 256

GDRAWG 82, 80, 0, 0, 720, 720, 0, 0, 720, 720, カラーマトリクス:0:0

SETANIMETIMER 50

@ハートエフェクト(ARG, ARG:1, ARG:2, ARG:3, ハート個数, 表示レイヤー層)
#DIM ハート個数
#DIM 表示レイヤー層
#DIM DYNAMIC ループカウンタ = 0

;セットアップがなかったら
IF !GCREATED(82)
    CALL クリックグラフィックセットアップ
ENDIF

DO
    SPRITEDISPOSE @"クリックアイコン_ハート{LOCAL}"
    SPRITEANIMECREATE @"クリックアイコン_ハート{LOCAL}", 180, 180
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 0, 360, 180, 180,  0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 180, 360, 180, 180,  0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 360, 360, 180, 180,    0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 540, 360, 180, 180, 0,   0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 0, 540, 180, 180,  0,   0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 180, 540, 180, 180, 0,  0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 360, 540, 180, 180, 0,  0, 10000000

    CBGREMOVERANGE 表示レイヤー層 - LOCAL, 表示レイヤー層 - LOCAL

    CBGSETSPRITE @"クリックアイコン_ハート{LOCAL}", RAND:(ARG:2) + ARG - 90, - RAND(ARG:3 * 8 / 10) - 90, 表示レイヤー層 - LOCAL
    AWAIT 10

    LOCAL ++
    SIF LOCAL > 5
        LOCAL = -5

    ループカウンタ ++
LOOP ループカウンタ < ハート個数


@クリックアイコン(ARG, ARG:1, ARG:2 = 0, 表示レイヤー層)
#DIM 表示レイヤー層
#DIM カラーマトリクス, 5, 5

SPRITEDISPOSE @"クリックアイコン{LOCAL:1}"
SPRITEANIMECREATE @"クリックアイコン{LOCAL:1}", 180, 180

SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 0, 0, 180, 180,  0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 180, 0, 180, 180,  0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 360, 0, 180, 180,    0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 540, 0, 180, 180, 0,   0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 0, 180, 180, 180,  0,   0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 180, 180, 180, 180, 0,  0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, ARG:2 * 720 + 360, 180, 180, 180, 0,  0, 10000000

CBGREMOVERANGE 表示レイヤー層 - LOCAL:1, 表示レイヤー層 - LOCAL:1

CBGSETSPRITE @"クリックアイコン{LOCAL:1}", ARG - 90, ARG:1 + 90, 表示レイヤー層 - LOCAL:1

LOCAL:1 ++
SIF LOCAL:1 > 5
    LOCAL:1 = 0

