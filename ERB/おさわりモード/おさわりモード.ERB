@おさわりモード(キャラ番号)
#DIM TARGET保存
#DIM キャラ番号
#DIM レイヤー番号 = 50
#DIM カラーマトリクス, 5, 5
#DIMS おさわりフォルダ = "おさわりフォルダ/"
#DIMS 対象フォルダ
#DIMS 対象ファイルアーカイブ,100,100,100 ;カテゴリ分けしてファイル名を預かる
#DIM 対象ファイル数,100,100
#DIMS 汎用ファイルアーカイブ,100,100,100 ;おさわりファイルなしファイルを預かる
#DIM 汎用ファイル数,100,100
{
    #DIMS 衣装種類 =
    "短冊_デフォルト", "短冊_水着", "短冊_湯浴み着", "短冊_全裸", "短冊_浴衣",
    "短冊_部屋着", "短冊_おしゃれ着", "短冊_バニー服", "短冊_メイド服", "短冊_マイクロビキニ",
    "短冊_逆バニー", "短冊_チャイナドレス", "短冊_ミニチャイナ", "短冊_セクシーランジェリー", "短冊_ミニスカサンタ", 
    "短冊_トリック・オア・トリートメント", "短冊_酒を呑み干す童鬼の装束", "マッサージ開始", "マッサージ開始うつ伏せ", "Ｂマッサージ",
    "Ａマッサージ", "Ｖマッサージ", "Ｃマッサージ"
}
{
    #DIMS 差分種類 = 
    "表情", "欲情", "絶頂",
}
#DIM 現在衣装種類ID ;モード外で現在着用している衣装
#DIM 適正衣装種類ID ;次回セットしたい種類　ex.コスプレ、脱衣状態など
#DIM 適正差分種類ID
#DIM 対象衣装種類ID ;実際にセットする種類　
#DIM 対象差分種類ID
#DIM 対象ファイルID
#DIM 選択コマンドID ;おさわり内でのID
#DIMS 選択コマンド文字列
#DIM クリック座標,2 ;0=x,1=y
#DIM クリック色指定
#DIM 弱点コマンド指定
#DIM おさわり容認カウンタ
#DIM おさわり実効カウンタ,100
#DIM おさわり警告カウンタ
#DIM おさわり警告フラグ
#DIM おさわり実効フラグ
#DIM アクティブコマンドリスト;モードなどでコマンド対象群を切り替える　使用中のコマンドリストを保存する
#DIM 画像位置,2
#DIM 画像サイズ,2

#DIM 表示レイヤー層配列,3,100;モード,レイヤー
;表示レイヤー層配列:ログモード:画像レイヤー　の形で一括変更させる
#DIM CONST ログ表示モード = 1
#DIM CONST 全背景モード = 2

;レイヤーに名前をふっておく
#DIM CONST メイン画像レイヤー層 = 0
#DIM CONST システムコマンドレイヤー層 = 1
#DIM CONST アクセスコマンドレイヤー層 = 2
#DIM CONST クリックアイコンレイヤー層 = 3
#DIM CONST ハートエフェクトレイヤー層 = 4
#DIM CONST ステート表示レイヤー層 = 5
{
    #DIMS システムコマンド種類 = 
    "", "おさわりモード終了", "画像ロック", "おさわりあり限定解除", "ログ表示切替", "体位を変える", 
    "マッサージ画像優先", "画像最大化"
}
{
    #DIM システムコマンドチェックボックスあり =
    0, 0, 1, 1, 1, 0, 1, 1,
}
{
    #DIM システムコマンドチェック状態 = 
    0, 0, 0, 0, 0, 0, 0, 0,
    ;参照を容易にするため機能の変数とは別に保存する
}

#DIM システムコマンド展開フラグ ;0=閉じている　1=開く予約　2=閉じる予約

#DIM システムコマンド位置基準,2
#DIM システムコマンドサイズ,2
#DIM システムコマンド間隔 = 10

#DIMS システムコマンド選択
#DIM システムコマンド格納, 10

#DIM アクセスコマンド展開フラグ ;展開状態を管理する　0=表 1=表から裏 2=裏 3=裏から表

;#DIM アクセスコマンド位置基準 = 1020, 40 ;x= 画像位置(620) + WIDTH(360) + 余白(40)
#DIM アクセスコマンド位置基準,2
#DIM アクセスコマンドサイズ,2
#DIM アクセスコマンド間隔 = 30
#DIM アクセスコマンド最大表示数 = 6

#DIM アクセスコマンドIDリスト,20
;0=おさわりありの画像のみを表示する　1=なくてもOK 2=なくてもOK解除不可(ありに切り替えるコマンドを表示しない)
{
    #DIM コマンドIDカラーコード変換 = 0x0000C0, 0x00C000, 0x00C0C0, 0xC00000, 0xC000C0, 
    0xC0C000, 0xC0C0C0, 0x0000A0, 0x00A000, 0x00A0A0, 
    0xA00000, 0xA000A0, 0xA0A000, 0xA0A0A0, 0x000080, 
    0x008000, 0x008080, 0x800000, 0x800080, 0x808000,
    0x808080, 0x000040, 0x004000, 0x004040, 0x400000, 
    0x400040, 0x404000, 0x404040,
}
#DIM キーボード選択コマンドID ;キーボード押下時の入力を変換するための預かり

;切り替えは次の評価で利用する
;ロックは切り替えを評価しない
#DIM おさわりあり限定解除フラグ
#DIM 画像最大化フラグ
#DIM 画像切り替えカウンタ
#DIM 画像切り替えフラグ
#DIM 画像ロックフラグ
#DIMS メッセージ予約 ;システムメッセージ窓用
#DIMS プリントメッセージ保存 ;HTML_PRINT向け保存

#DIMS システムコマンド更新タイプ
#DIMS アクセスコマンド更新タイプ
#DIMS メイン画像更新タイプ
#DIMS アイテム画像更新タイプ
#DIM 画像更新フラグ

#DIM 表示レイヤー層モード = 0
;テスト用カウンタ
#DIM 開始TIME保存
#DIM 経過TIME保存
#DIM DYNAMIC クリックカウンタ
#DIM DYNAMIC コマンドカウンタ
#DIM DYNAMIC タイマカウンタ

#DIM コマンド枠幅 = 4

#DIM アイテム表示位置基準,2

;0=オレンジ　警戒度減少強化
;1=青　理性抑制
;2=ピンク　警戒度上昇抑制
#DIM 有効アロマフラグ,3
#DIM 所持アロマ数配列,3
#DIM アロマ種類数 = 3
#DIM マッサージ画像優先フラグ

; #DIM CONST メイン画像レイヤー層 = 0
; #DIM CONST システムコマンドレイヤー層 = 1
; #DIM CONST アクセスコマンドレイヤー層 = 2
; #DIM CONST クリックアイコンレイヤー層 = 3
; #DIM CONST ハートエフェクトレイヤー層 = 4
; #DIM CONST ステート表示レイヤー層 = 5
;デフォルトのposz
表示レイヤー層配列:0:0 = -10, -40, -20, -60, -50, -30
;ログモード時のposz
表示レイヤー層配列:ログ表示モード:0 = 10, -40, -20, -60, -50, -30
;全背景モード
表示レイヤー層配列:全背景モード:0 = 80, 30, 50, 10, 20, 40
;システムウィンドウ展開時に画面が暗転するのでシステムはアクセスより手前

;対象キャラをTARGETに入れるのでTARGETを保存する
TARGET保存 = TARGET
TARGET = キャラ番号

アクティブコマンドリスト = 0

VARSET システムコマンドチェック状態
システムコマンド更新タイプ '= "初期化"
アクセスコマンド更新タイプ '= "初期化"
メイン画像更新タイプ = "初期化"
アイテム画像更新タイプ '= "初期化"
システムコマンド展開フラグ = 0
表示レイヤー層モード = 0
画像最大化フラグ = 0

VARSET メッセージ予約
VARSET プリントメッセージ保存

経過TIME保存 = 0
開始TIME保存 = TIME
TIME:2 = TIME

マッサージ画像優先フラグ = 0
;-------------------------マッサージモード切替
IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
    アクティブコマンドリスト = 10
    所持アロマ数配列 = 1, 1, 1
    VARSET 有効アロマフラグ
    マッサージ画像優先フラグ = 1
    システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "マッサージ画像優先")) = 1
ENDIF

SIF CSTR:キャラ番号:画像フォルダ == ""
    CSTR:キャラ番号:画像フォルダ = {NO:キャラ番号}%CALLNAME:キャラ番号%

対象フォルダ '= "resources\\" + CSTR:キャラ番号:画像フォルダ

;着せ替え服に入っている衣装を基本としてセット
;デフォルトの時は今日の衣装で再チェック
SELECTCASE CSTR:TARGET:着せ替え服
    CASE "水着"
        現在衣装種類ID = 1
    CASE "湯浴み着"
        現在衣装種類ID = 2
    CASE "全裸"
        現在衣装種類ID = 3
    CASE "浴衣"
        現在衣装種類ID = 4
    CASE "部屋着"
        現在衣装種類ID = 5
    CASE "おしゃれ着"
        現在衣装種類ID = 6
    CASE "バニー服"
        現在衣装種類ID = 7
    CASE "メイド服"
        現在衣装種類ID = 8
    CASE "マイクロビキニ"
        現在衣装種類ID = 9
    CASE "逆バニー"
        現在衣装種類ID = 10
    CASE "チャイナドレス"
        現在衣装種類ID = 11
    CASE "ミニチャイナ"
        現在衣装種類ID = 12
    CASE "セクシーランジェリー"
        現在衣装種類ID = 13
    CASE "ミニスカサンタ"
        現在衣装種類ID = 14
    CASE "トリック・オア・トリートメント"
        現在衣装種類ID = 15
    CASE "酒を呑み干す童鬼の装束"
        現在衣装種類ID = 16
    CASEELSE ;デフォルト
        SELECTCASE CSTR:TARGET:今日の衣装
            CASE "水着"
                現在衣装種類ID = 1
            CASE "湯浴み着"
                現在衣装種類ID = 2
            CASE "全裸"
                現在衣装種類ID = 3
            CASE "浴衣"
                現在衣装種類ID = 4
            CASE "部屋着"
                現在衣装種類ID = 5
            CASE "おしゃれ着"
                現在衣装種類ID = 6
            CASE "バニー服"
                現在衣装種類ID = 7
            CASE "メイド服"
                現在衣装種類ID = 8
            CASE "マイクロビキニ"
                現在衣装種類ID = 9
            CASE "逆バニー"
                現在衣装種類ID = 10
            CASE "チャイナドレス"
                現在衣装種類ID = 11
            CASE "ミニチャイナ"
                現在衣装種類ID = 12
            CASE "セクシーランジェリー"
                現在衣装種類ID = 13
            CASE "ミニスカサンタ"
                現在衣装種類ID = 14
            CASE "トリック・オア・トリートメント"
                現在衣装種類ID = 15
            CASE "酒を呑み干す童鬼の装束"
                現在衣装種類ID = 16
            CASEELSE
                現在衣装種類ID = 0
        ENDSELECT
ENDSELECT

;ランダムキャラ、汎用立ち絵はおさわりあり限定解除でロック状態にしてアーカイブ作成をまたぐ
IF OPTION変数:汎用立ち絵制御 && CSTR:キャラ番号:汎用立ち絵登録文字列 != "" && ENUMFILES("resources/999" + CALLNAME:TARGET, "*.png") <= 0
	CSTR:キャラ番号:画像フォルダ = 汎用グラフィック
    VARSET 汎用ファイルアーカイブ
    VARSET 汎用ファイル数
    汎用ファイルアーカイブ:0:0:0 '= "短冊_" + CSTR:キャラ番号:汎用立ち絵登録文字列 + ".png"
    汎用ファイル数:0:0 = 1
    おさわりあり限定解除フラグ = 2
    システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")) = 1
    FOR LOCAL, 0, 50
        PRINTL
    NEXT
    PRINTL コマンドを押しておさわりしよう！
    GOTO ランダムキャラ用スキップ
ENDIF

;個別画像持ちキャラは該当の画像があるかチェックする 湯浴み着は全裸も通す
IF CSTR:TARGET:着せ替え服 == "湯浴み着"
    IF EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ||  EXISTFILE(対象フォルダ + "/短冊_デフォルト.png") || EXISTFILE(対象フォルダ + "/短冊_全裸.png")
    ELSE ;どれもない
        PRINTL 着用中の衣装画像がないため実行できません
        WAIT
        RETURN
    ENDIF
    SIF !EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ;全裸画像を使っている
        現在衣装種類ID = 3
ELSE
    IF EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ||  EXISTFILE(対象フォルダ + "/短冊_デフォルト.png")
    ELSE ;どれもない
        PRINTL 着用中の衣装画像がないため実行できません
        WAIT
        RETURN
    ENDIF
ENDIF

;実行確定したのでログを流す
FOR LOCAL, 0, 50
    PRINTL
NEXT

;おさわりファイルがないときはおさわりあり限定解除状態で固定する
IF ENUMFILES(対象フォルダ + "/" + おさわりフォルダ + "/", @"*.png") > 0
    おさわりあり限定解除フラグ = 0
    システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")) = 0
ELSE
    おさわりあり限定解除フラグ = 2
    システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")) = 1
ENDIF

;アーカイブ作成
IF !おさわりあり限定解除フラグ
    CALL おさわりファイルアーカイブ作成(対象フォルダ + "/" + おさわりフォルダ, 対象ファイルアーカイブ, 対象ファイル数, 衣装種類, 差分種類)
ENDIF
CALL おさわりファイルアーカイブ作成(対象フォルダ + "/", 汎用ファイルアーカイブ, 汎用ファイル数, 衣装種類, 差分種類)

$ランダムキャラ用スキップ
CALL クリックグラフィックセットアップ

;初期画像の設定
適正衣装種類ID = 現在衣装種類ID
SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
    適正衣装種類ID = FINDELEMENT(衣装種類, "マッサージ開始")
IF PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0
    適正差分種類ID = 1
    メッセージ予約 = %CALLNAME:TARGET%は熱のこもった目でこちらを見つめている……
ELSEIF PALAM:TARGET:欲情 >= PALAMLV:2
    適正差分種類ID = 0
    メッセージ予約 = %CALLNAME:TARGET%はどことなく期待しているようにも見える……
ELSE
    適正差分種類ID = 0
    メッセージ予約 = %CALLNAME:TARGET%はおさわりできるような雰囲気ではない……
ENDIF

;最初のCOM実行までのクリック数をランダムにする
FOR LOCAL, 0, 100
    おさわり実効カウンタ:LOCAL = RAND:100
NEXT
VARSET おさわり警告カウンタ
VARSET おさわり容認カウンタ

VARSET 画像ロックフラグ
画像切り替えカウンタ = 5

$メインループ

SIF 画像ロックフラグ
    メイン画像更新タイプ '= "描画" 

;システムウィンドウ
;スタンスごとに表示するコマンドを指定する
IF システムコマンド更新タイプ == "更新" || システムコマンド更新タイプ == "初期化"

    ;表示するシステムコマンドをスタックしていく
    VARSET システムコマンド格納

    IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "マッサージ画像優先") ;マッサージ画像優先
        ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "体位を変える") ;体位を変える
    ENDIF
    SIF おさわりあり限定解除フラグ < 2
        ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")
    ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "画像最大化")
    ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "ログ表示切替")
    ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "画像ロック") ;画像ロック
    ;おさわり終了を最後に入れる
    ARRAYSHIFT システムコマンド格納, 1, FINDELEMENT(システムコマンド種類, "おさわりモード終了")

ENDIF

IF おさわりあり限定解除フラグ
    CALL おさわりファイル適正選出(汎用ファイル数, 衣装種類, 現在衣装種類ID, 適正衣装種類ID, 差分種類, 適正差分種類ID, マッサージ画像優先フラグ, 画像切り替えカウンタ, メイン画像更新タイプ)
    CALL おさわりファイル差分選出(汎用ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
    CALL おさわり画像基準セット(汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
    CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
ELSE ;おさわりあり限定
    CALL おさわりファイル適正選出(対象ファイル数, 衣装種類, 現在衣装種類ID, 適正衣装種類ID, 差分種類, 適正差分種類ID, マッサージ画像優先フラグ, 画像切り替えカウンタ, メイン画像更新タイプ)
    CALL おさわりファイル差分選出(対象ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
    CALL おさわり画像基準セット(おさわりフォルダ + 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
    CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
ENDIF

CALL おさわりシステムコマンド(システムコマンド更新タイプ, システムコマンド展開フラグ, システムコマンド種類, システムコマンドチェックボックスあり, システムコマンドチェック状態, システムコマンド格納, システムコマンド位置基準, システムコマンドサイズ, システムコマンド間隔, 表示レイヤー層配列:表示レイヤー層モード:システムコマンドレイヤー層)
CALL おさわりアクセスコマンド(アクセスコマンド更新タイプ, アクセスコマンド展開フラグ, アクティブコマンドリスト, アクセスコマンドIDリスト, コマンドIDカラーコード変換, アクセスコマンド位置基準, アクセスコマンドサイズ, アクセスコマンド間隔, アクセスコマンド最大表示数, 表示レイヤー層配列:表示レイヤー層モード:アクセスコマンドレイヤー層)
SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
    CALL おさわりアイテム画像(アイテム画像更新タイプ, 有効アロマフラグ, 所持アロマ数配列, アロマ種類数, アイテム表示位置基準, 画像位置, 画像サイズ, 表示レイヤー層配列:表示レイヤー層モード:ステート表示レイヤー層)
; CBGREMOVERANGE -5, -5
; GDISPOSE レイヤー番号 + 11
; GCREATE レイヤー番号 + 11, 1600, CLIENTHEIGHT()
; GSETFONT レイヤー番号 + 11, GETFONT(), 18
; SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
;     アクセスコマンド更新タイプ '= "更新"

; ;特記事項がないときTIPSを表示する
; IF メッセージ予約 == ""
;     SELECTCASE RAND:5
;         CASE 0
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:各機能切り替えはクリックでも変更できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 1
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｌキーで画像を表示中のものに固定できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 2
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｓキーでおさわり強度を変更できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 3
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:欲情度が低いときはおさわり厳禁", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 4
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｚキーでおさわりモードが終了できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 5
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:右クリックで別のセクハラができる場所がある", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 5
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:有効グラフィック全体化時はテンキーでおさわりできる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;         CASE 6
;             GDRAWTEXT レイヤー番号 + 11, "TIPS:NUMLOCKを解除したテンキーは右クリックの効果がある", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
;     ENDSELECT
; ELSE
;     GDRAWTEXT レイヤー番号 + 11, メッセージ予約, システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
; ENDIF
; VARSET メッセージ予約
; CBGSETG レイヤー番号 + 11, 0, 0, -5


;当たり判定画像を統合してBMAPとしてセットする
GDISPOSE レイヤー番号 + 29 
GCREATE レイヤー番号 + 29 , CLIENTWIDTH(), CLIENTHEIGHT()
IF システムコマンド展開フラグ
    ;システムコマンドだけ当たり判定を持つ
    GDRAWG レイヤー番号 + 29 , レイヤー番号 + 23 , 0, 0, CLIENTWIDTH(), CLIENTHEIGHT(), 0, 0, CLIENTWIDTH(), CLIENTHEIGHT()
ELSE
    ;画像
    GDRAWG レイヤー番号 + 29 , レイヤー番号 + 20 , 画像位置:0, 画像位置:1, 画像サイズ:0, 画像サイズ:1, 0, 0, GWIDTH(レイヤー番号 + 20 ), GHEIGHT(レイヤー番号 + 20 )
    ;システムコマンド
    GDRAWG レイヤー番号 + 29 , レイヤー番号 + 23 , 0, 0, CLIENTWIDTH(), CLIENTHEIGHT(), 0, 0, CLIENTWIDTH(), CLIENTHEIGHT()
    ;アクセスコマンド
    GDRAWG レイヤー番号 + 29 , レイヤー番号 + 24 , 0, 0, CLIENTWIDTH(), CLIENTHEIGHT(), 0, 0, CLIENTWIDTH(), CLIENTHEIGHT()
    ;マッサージモードUI
    SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        GDRAWG レイヤー番号 + 29 , レイヤー番号 + 25 , 0, 0, CLIENTWIDTH(), CLIENTHEIGHT(), 0, 0, CLIENTWIDTH(), CLIENTHEIGHT()
ENDIF
CBGSETBMAPG レイヤー番号 + 29
VARSET RESULT
INPUTMOUSEKEY ;--------------------メインループ
クリック座標:0 = RESULT:2
クリック座標:1 = RESULT:3
クリックカウンタ ++

; PRINTFORML DEBUG MOUSEKEY {RESULT}, {RESULT:1} 
; PRINTFORML DEBUG PRINTPOINT X {RESULT:2}, Y {RESULT:3},
; PRINTFORML DEBUG PRINTCOLOR {RESULT:4}

;入力キーの識別と割り当て
選択コマンドID = 100
システムコマンド選択 = 
IF RESULT == 1
    IF RESULT:1 == 0x100000
        SELECTCASE RESULT:4
        CASE 0x010000
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:0)
        CASE 0x010001
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:1)
        CASE 0x010002
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:2)
        CASE 0x010003
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:3)
        CASE 0x010004
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:4)
        CASE 0x010005
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:5)
        CASE 0x010006
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:6)
        CASE 0x01000A
            システムコマンド選択 '= "システムメニュー展開"
        CASE 0x020000
            システムコマンド選択 '= "アロマボトル"
        CASE 0x020001
            システムコマンド選択 '= "アロマボトル"
        CASE 0x020002
            システムコマンド選択 '= "アロマボトル"
        CASE 0x020003
            システムコマンド選択 '= "アロマボトル"
        CASE 0x020004
            システムコマンド選択 '= "アロマボトル"
        CASE 0x0000C0 ;背中
            選択コマンドID = 0
        CASE 0x00C000 ;耳
            選択コマンドID = 1
        CASE 0x00C0C0 ;角
            選択コマンドID = 2
        CASE 0xC00000 ;尻尾
            選択コマンドID = 3
        CASE 0xC000C0 ;お尻
            選択コマンドID = 4
        CASE 0xC0C000 ;お腹
            選択コマンドID = 5
        CASE 0xC0C0C0 ;頭
            選択コマンドID = 6
        CASE 0x0000A0 ;髪
            選択コマンドID = 7
        CASE 0x00A000 ;脇
            選択コマンドID = 8
        CASE 0x00A0A0 ;配置換えのため欠番
            選択コマンドID = 9
        CASE 0xA00000 ;へそ
            選択コマンドID = 10
        CASE 0xA000A0 ;口
            選択コマンドID = 11
        CASE 0xA0A000 ;太もも
            選択コマンドID = 12
        CASE 0xA0A0A0 ;下腹部
            選択コマンドID = 13
        CASE 0x000080 ;乳首
            選択コマンドID = 14
        CASE 0x008000 ;C
            選択コマンドID = 15
        CASE 0x008080 ;スカート
            選択コマンドID = 16
        CASE 0x800000 ;V
            選択コマンドID = 17
        CASE 0x800080 ;B
            選択コマンドID = 18
        CASE 0x808000 ;A
            選択コマンドID = 19
        CASE 0x808080 ;
            選択コマンドID = 20
        CASE 0x000040 ;
            選択コマンドID = 21
        CASE 0x004000 ;
            選択コマンドID = 22
        CASE 0x004040 ;
            選択コマンドID = 23
        CASE 0x400000 ;
            選択コマンドID = 24
        CASE 0x400040 ;
            選択コマンドID = 25
        CASE 0x404000 ;
            選択コマンドID = 26
        CASE 0x404040 ;
            選択コマンドID = 27
        CASEELSE
            システムコマンド選択 = 
        ENDSELECT
        ;おさわりの範囲
        IF 選択コマンドID < 100
            GOTO おさわり実行処理
        ENDIF
        CALL クリックアイコン(クリック座標:0, クリック座標:1, 0, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)
    ELSE
        システムコマンド選択 '= "アクセスコマンド展開"
    ENDIF
ELSEIF RESULT == 3 ;キーボード入力
    SELECTCASE RESULT:1
        CASE 49 ;1
            キーボード選択コマンドID = 0
        CASE 50 ;2
            キーボード選択コマンドID = 1
        CASE 51 ;3
            キーボード選択コマンドID = 2
        CASE 52 ;4
            キーボード選択コマンドID = 3
        CASE 53 ;5
            キーボード選択コマンドID = 4
        CASE 54 ;6
            キーボード選択コマンドID = 5
        CASE 55 ;7
            キーボード選択コマンドID = 6
        CASE 56 ;8
            キーボード選択コマンドID = 7
        CASE 57 ;9
            キーボード選択コマンドID = 8
        CASE 48 ;0 システムコマンド展開
            キーボード選択コマンドID = 9
        ;---------------専用キー
        CASE 16 ;SHIFT アクセスコマンド展開
            キーボード選択コマンドID = 10
        CASE 33 ;PAGE UP ログ
            キーボード選択コマンドID = 11
        ;---------------ショートカット
        CASE 66 ;B "おさわりあり限定解除"
            キーボード選択コマンドID = FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")
        CASE 76 ;L 画像ロック
            キーボード選択コマンドID = FINDELEMENT(システムコマンド種類, "画像ロック")
        CASE 82 ;R 体位を変える
            キーボード選択コマンドID = FINDELEMENT(システムコマンド種類, "体位を変える")
        ; CASE 83 ;S おさわり強度
        ;     キーボード選択コマンドID = FINDELEMENT(システムコマンド種類, 3)
        CASE 90 ;Z おさわりモード終了
            キーボード選択コマンドID = FINDELEMENT(システムコマンド種類, "おさわりモード終了")
        ;----------------
        CASE 96 ;NUM0
            キーボード選択コマンドID = 100
        CASE 97 ;NUM1
            キーボード選択コマンドID = 101
        CASE 98 ;NUM2
            キーボード選択コマンドID = 102
        CASE 99 ;NUM3
            キーボード選択コマンドID = 103
        CASE 100 ;NUM4
            キーボード選択コマンドID = 104
        CASE 101 ;NUM5
            キーボード選択コマンドID = 105
        CASE 102 ;NUM6
            キーボード選択コマンドID = 106
        CASE 103 ;NUM7
            キーボード選択コマンドID = 107
        CASE 104 ;NUM8
            キーボード選択コマンドID = 108
        CASE 105 ;NUM9
            キーボード選択コマンドID = 109
        ;-------------
        CASEELSE
            キーボード選択コマンドID = -1
    ENDSELECT
    ;クリックアイコンの表示のため入力コマンドの位置に対応した座標をセットする
    SELECTCASE キーボード選択コマンドID
        CASE IS >= 100 ;アクセスコマンドは選択コマンドIDを入れて、クリック座標を渡す
            選択コマンドID = アクセスコマンドIDリスト:(キーボード選択コマンドID - 100)
            クリック座標:0 = アクセスコマンド位置基準:0 + 100
            クリック座標:1 = - CLIENTHEIGHT() + アクセスコマンド位置基準:1 + (キーボード選択コマンドID - 100) * (アクセスコマンドサイズ:1 + アクセスコマンド間隔) + 20
            IF 選択コマンドID < 100
                GOTO おさわり実行処理
            ENDIF
        CASE 11 ;PAGEUP
            システムコマンド選択 '= "ログ一時表示モード"
        CASE 10 ;アクセスコマンド展開
            システムコマンド選択 '= "アクセスコマンド展開"
        CASE 9 ;システムメニュー
            システムコマンド選択 '= "システムメニュー展開"
            ;開閉時でアイコンの位置が違う
            IF システムコマンド展開フラグ
                クリック座標:0 = システムコマンド位置基準:0 + 500, システムコマンド位置基準:1 + 10 - CLIENTHEIGHT()
            ELSE
                クリック座標:0 = システムコマンド位置基準:0 + 100, システムコマンド位置基準:1 + 10 - CLIENTHEIGHT()
            ENDIF
            CALL クリックアイコン(クリック座標:0, クリック座標:1, 0, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)
        CASE IS >= 0 ;システムコマンド
            ;IDは0からなので一つずらす
            キーボード選択コマンドID -= 1
            システムコマンド選択 '= システムコマンド種類:(システムコマンド格納:キーボード選択コマンドID)
            ;開閉時でアイコンの位置が違う
            IF システムコマンド展開フラグ
                クリック座標:0 = システムコマンド位置基準:0 + 100, - CLIENTHEIGHT() + システムコマンド位置基準:1 + キーボード選択コマンドID * (システムコマンドサイズ:1 + システムコマンド間隔) + 20
            ELSE
                クリック座標:0 = システムコマンド位置基準:0, - CLIENTHEIGHT() + システムコマンド位置基準:1 + キーボード選択コマンドID * (システムコマンドサイズ:1 + システムコマンド間隔) + 20
            ENDIF
            CALL クリックアイコン(クリック座標:0, クリック座標:1, 0, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)
        CASEELSE ;-1=該当コマンドなし
    ENDSELECT
ELSE ;マウスホイール
    システムコマンド選択 '= "ログ一時表示モード"
ENDIF

;システム操作はここ　操作フィールドが多いのでおさわりシステムコマンド関数には含めないでおく
SELECTCASE システムコマンド選択
    CASE "画像最大化"
        IF 画像最大化フラグ
            画像最大化フラグ = 0
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "画像最大化")) = 0
        ELSE
            画像最大化フラグ = 1
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "画像最大化")) = 1
        ENDIF
        メイン画像更新タイプ '= "更新"
        アイテム画像更新タイプ '= "初期化"
    CASE "マッサージ画像優先"
        IF マッサージ画像優先フラグ
            マッサージ画像優先フラグ = 0
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "マッサージ画像優先")) = 0
        ELSE
            マッサージ画像優先フラグ = 1
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "マッサージ画像優先")) = 1
            画像切り替えカウンタ = 0
        ENDIF
        メイン画像更新タイプ '= "更新"
        アイテム画像更新タイプ '= "初期化"
    CASE "体位を変える"
       	IF PREGNANCY_SIZE(TARGET) >= 2
            PRINTFORML %CALLNAME:TARGET%は妊娠してお腹が大きくなっている……。
            PRINTL うつ伏せに寝ることは出来なさそうだ。
        ELSE
            INVERTBIT TCVAR:TARGET:マッサージ_うつ伏せフラグ, 0
            PRINTFORM %CALLNAME:TARGET%に
            IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
                PRINTL うつ伏せに寝てもらった。
            ELSE
                PRINTL 仰向けに寝てもらった。
            ENDIF
        ENDIF
        画像切り替えカウンタ = 0
        メイン画像更新タイプ '= "更新"
        アクセスコマンド更新タイプ '= "更新"
    CASE "アロマボトル"
        ;一の位を拾って対応した処理を起こす
        ;0xなので16の剰余
        LOCAL = RESULT:4 % 16
        有効アロマフラグ:LOCAL = 10
        所持アロマ数配列:LOCAL --
        CALL おさわりアイテムメッセージ("アロマオイル", LOCAL, プリントメッセージ保存)
    CASE "アクセスコマンド展開"
        ;システムメニューが開いているときは右クリック、SHIFT=キャンセル
        IF システムコマンド展開フラグ
            システムコマンド展開フラグ ++
        ELSE
            アクセスコマンド展開フラグ ++
        ENDIF
    CASE "システムメニュー展開"
        システムコマンド展開フラグ ++
    CASE "おさわりモード終了"
        GOTO 終了処理
    CASE "画像ロック"
        IF 画像ロックフラグ
            画像ロックフラグ = 0
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "画像ロック")) = 0
            メッセージ予約 = 画像の固定を解除します
        ELSE
            画像ロックフラグ = 1
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "画像ロック")) = 1
            メッセージ予約 = 表示中の画像で固定します
        ENDIF
    CASE "おさわりあり限定解除"
        IF おさわりあり限定解除フラグ == 1
            おさわりあり限定解除フラグ = 0
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")) = 0
            メッセージ予約 = ボタン操作を無効にし、おさわり判定のあるグラフィックに限定して表示します
        ELSEIF おさわりあり限定解除フラグ == 0
            おさわりあり限定解除フラグ = 1
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "おさわりあり限定解除")) = 1
            メッセージ予約 = ボタン操作を有効にし、おさわり判定のないグラフィックを表示候補に追加します
        ELSE
        ENDIF
            メイン画像更新タイプ '= "更新"
            アクセスコマンド更新タイプ '= "更新"
    CASE "ログ表示切替"
        ;モードを変える前に現在レイヤーの画像を消す
        CBGREMOVERANGE 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層 - 4, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層 + 4
        IF 表示レイヤー層モード == ログ表示モード
            表示レイヤー層モード = 0
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "ログ表示切替")) = 0
        ELSE
            表示レイヤー層モード = ログ表示モード
            システムコマンドチェック状態:(FINDELEMENT(システムコマンド種類, "ログ表示切替")) = 1
        ENDIF
    CASE "ログ一時表示モード"
        CBGCLEAR
        ;ログモードで呼び出す
        IF おさわりあり限定解除フラグ
            CALL おさわりメイン画像(メイン画像更新タイプ, ログ表示モード, おさわりフォルダ, 汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:ログ表示モード:メイン画像レイヤー層)
        ELSE ;おさわりあり限定
            CALL おさわりメイン画像(メイン画像更新タイプ, ログ表示モード, おさわりフォルダ, 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:ログ表示モード:メイン画像レイヤー層)
        ENDIF
        WAIT
ENDSELECT

GOTO メインループ


$おさわり実行処理

IF アクセスコマンド展開フラグ
    選択コマンド文字列 '= おさわりコマンドリスト(アクティブコマンドリスト + 1, 選択コマンドID)
ELSE
    選択コマンド文字列 '= おさわりコマンドリスト(アクティブコマンドリスト, 選択コマンドID)
ENDIF

;対応コマンドなしは空振り
IF 選択コマンド文字列 == ""
    CALL クリックアイコン(クリック座標:0, クリック座標:1, 0, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)
    GOTO メインループ
ENDIF


;実行できないときは空振り
;おさわりとボタンの区別がないため、アクセスコマンドの配置とは別にここでも判定を行う
;想定されるケース　おさわり画像から直接コマンドを選択した場合

CALL おさわり可否判定(選択コマンド文字列)
IF !RESULT
    CALL クリックアイコン(クリック座標:0, クリック座標:1, 0, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)
    GOTO メインループ
ENDIF

;おさわりに実体があるかどうか判定する
;実体があればCOMが実行される、なければ地の文を呼び出す
;技巧が高ければ高いほど短いサイクルでコマンドが実行される
おさわり実効フラグ = 0
おさわり実効カウンタ:選択コマンドID += RAND(0, 10) + SQRT(ABL:PLAYER:技巧 * 1000)
IF おさわり実効カウンタ:選択コマンドID > 100
    おさわり実効カウンタ:選択コマンドID = おさわり実効カウンタ:選択コマンドID % 100
    おさわり実効フラグ = 1
    ;その気でないときは一発警告
    IF PALAM:TARGET:欲情 < PALAMLV:2 && CFLAG:TARGET:発情期フラグ >= 0
        おさわり容認カウンタ = 100
    ENDIF
ENDIF

;おさわり実体があるときに行う処理
;COMの実行とSOURCECHECKから抽出したものを実行する
IF おさわり実効フラグ
    コマンドカウンタ ++

    ;弱点チェックしてフラグを持っていく
    ;メッセージとハートの量に使う
    弱点コマンド指定 = 0
    IF RSTR:SELECTCOM_S受け渡し == ""
        LOCALS:1 '= TOSTR(SELECTCOM)
    ELSE
        LOCALS:1 '= RSTR:SELECTCOM_S受け渡し
    ENDIF
    FOR LOCAL, 0, 500
        IF 弱点コマンド枠:キャラ番号:LOCAL == LOCALS:1
            弱点コマンド指定 = 1
            BREAK
        ENDIF
        SIF 弱点コマンド枠:キャラ番号:LOCAL == ""
            BREAK
    NEXT

    ;5回以内に特殊な切り替えがなかったとき、画像およびコマンドをリフレッシュする
    画像切り替えカウンタ --
    IF !画像切り替えカウンタ
        画像切り替えカウンタ = 5
        メイン画像更新タイプ '= "更新"
        アクセスコマンド更新タイプ '= "更新"
    ENDIF
    ;マッサージモードの時は毎ターンコマンド更新して可否判定をさせる
    SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        アクセスコマンド更新タイプ '= "更新"

    PRINTL

   ;----------------コマンド実行処理
   ;DRAWLINE,WAIT,SOURCE表示などをよけてテキストの抽出とCOM実行をする
   ;CALL_COM
    IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        CALL おさわりMASSAGE_COM(有効アロマフラグ)
    ELSEIF RSTR:SELECTCOM_S受け渡し != ""
        CALL おさわりCOM_S
    ELSE
        CALL おさわりCOM
    ENDIF

    SKIPDISP 1
    CALL SOURCE_補正処理(TARGET)
    CALL SOURCE_ABLUP(TARGET)
    SKIPDISP 0

    ;ランダム、弱点で倍、とりあえずマッサージも倍
    IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        CALL ハートエフェクト(画像位置:0, 画像位置:1, 画像サイズ:0, 画像サイズ:1, RAND(1,4) * 2, 表示レイヤー層配列:表示レイヤー層モード:ハートエフェクトレイヤー層)
    ELSE
        CALL ハートエフェクト(画像位置:0, 画像位置:1, 画像サイズ:0, 画像サイズ:1, RAND(1,4) * (弱点コマンド指定 + 1), 表示レイヤー層配列:表示レイヤー層モード:ハートエフェクトレイヤー層)
    ENDIF

    ;セクハラで汎用喘ぎを出すために、快Ｓに下駄を履かせて判定に渡す
    ;1点でもソースがあれば声が出る
    IF CUP:キャラ番号:快Ｓ == MAX(CUP:キャラ番号:快Ｃ, CUP:キャラ番号:快Ｂ, CUP:キャラ番号:快Ｖ, CUP:キャラ番号:快Ａ, CUP:キャラ番号:快Ｓ)    
        CUP:キャラ番号:快Ｓ += 999
    ENDIF
ELSE
    ;効果がなくてもおさわり一回は一回
    TIME += 1
ENDIF
    ;非COM実行時にも行う処理のためIFから出る----------------------------------------------

    CALL TRACHECK_TIME
    ;毎ターン補正して時間経過する
    経過TIME保存 += (TIME - TIME:2) / 3
    TIME = TIME:2 + (TIME - TIME:2) / 3

    TIME = TIME > 1440 ? TIME - 1440 # TIME
    TIME:2 = TIME

    ;マッサージモードの時は警戒度にすり替える
    おさわり警告フラグ = 0
    おさわり容認カウンタ += RAND(10, 30)
    SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
        おさわり容認カウンタ = BASE:TARGET:警戒
    IF おさわり容認カウンタ > 1000
        おさわり容認カウンタ = おさわり容認カウンタ % 1000
        おさわり警告フラグ = 1
    ENDIF

    ;警告までの長さでクリックアイコンの色を変える
    IF おさわり警告フラグ
        クリック色指定 = 1
    ELSE
        クリック色指定 = 0
        SELECTCASE おさわり容認カウンタ
            CASE IS < 250
                クリック色指定 = 3
            CASE IS < 500
                クリック色指定 = 2
            CASE IS < 750
                クリック色指定 = 4
            CASEELSE
                クリック色指定 = 5
        ENDSELECT
    ENDIF

    CALL クリックアイコン(クリック座標:0, クリック座標:1, クリック色指定, 表示レイヤー層配列:表示レイヤー層モード:クリックアイコンレイヤー層)

    ;------------対面プレイのテキストの抽出
    ;地の文は呼び出す
    IF FLAG:汎用文章表示切り替えオプション > 0
        IF TCVAR:TARGET:弱点コマンドフラグ
            SETCOLOR カラーパレット("えっちな色")
            PRINTL <<<弱点コマンド！！>>>
            RESETCOLOR
        ENDIF
        IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
            IF RSTR:SELECTCOM_S受け渡し != ""
                TRYCCALLFORM MASSAGE_MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%
                CATCH
                ENDCATCH
            ELSE
                TRYCCALLFORM MASSAGE_MESSAGE_COM{SELECTCOM}
                CATCH
                ENDCATCH
            ENDIF
        ELSE
            IF RSTR:SELECTCOM_S受け渡し != ""
                TRYCCALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%
                CATCH
                ENDCATCH
            ELSE
                TRYCCALLFORM MESSAGE_COM{SELECTCOM}
                CATCH
                ENDCATCH
            ENDIF
        ENDIF
    ENDIF
    IF K < 1 ;&& LOCAL:1 == 1
        ;通常口上も複数人プレイ口上も無かった場合、非口上メッセージを出す
        ;口上なしで汎用喘ぎを使うならここで表示
        IF CFLAG:TARGET:汎用喘ぎ禁止フラグ == 1
            ;禁止時はなにもしない
        ELSEIF TFLAG:オートコマンドフラグ == 0
            IF TALENT:TARGET:性別 == 2
                SIF GETBIT(OPTION変数:汎用喘ぎ使用_男, 0) && GETBIT(OPTION変数:汎用喘ぎ使用_男, 1)
                    CALL 汎用喘ぎ処理(TARGET)
            ELSE
                SIF GETBIT(OPTION変数:汎用喘ぎ使用, 0) && GETBIT(OPTION変数:汎用喘ぎ使用, 1)
                    CALL 汎用喘ぎ処理(TARGET)
            ENDIF
        ENDIF
        IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
            IF RSTR:SELECTCOM_S受け渡し != ""
                TRYCCALLFORM MASSAGE_MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
                CATCH
                ENDCATCH
            ELSE
                TRYCCALLFORM MASSAGE_MESSAGE_COM{SELECTCOM}_非口上時
                CATCH
                ENDCATCH
            ENDIF
        ELSE
            IF RSTR:SELECTCOM_S受け渡し != ""
                TRYCCALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
                CATCH
                ENDCATCH
            ELSE
                TRYCCALLFORM MESSAGE_COM{SELECTCOM}_非口上時
                CATCH
                ENDCATCH
            ENDIF
        ENDIF
    ENDIF

    ;地の文を読み終えたのでコマンド実行時のみの処理に変える----------------------------------------------
IF おさわり実効フラグ
    ;汎用喘ぎのためにあげた分を戻す
    IF CUP:キャラ番号:快Ｓ == MAX(CUP:キャラ番号:快Ｃ, CUP:キャラ番号:快Ｂ, CUP:キャラ番号:快Ｖ, CUP:キャラ番号:快Ａ, CUP:キャラ番号:快Ｓ)    
        CUP:キャラ番号:快Ｓ -= 999
    ENDIF

    IF おさわりあり限定解除フラグ
        CALL おさわりファイル適正選出(汎用ファイル数, 衣装種類, 現在衣装種類ID, 適正衣装種類ID, 差分種類, 適正差分種類ID, マッサージ画像優先フラグ, 画像切り替えカウンタ, メイン画像更新タイプ)
    ELSE ;おさわりあり限定
        CALL おさわりファイル適正選出(対象ファイル数, 衣装種類, 現在衣装種類ID, 適正衣装種類ID, 差分種類, 適正差分種類ID, マッサージ画像優先フラグ, 画像切り替えカウンタ, メイン画像更新タイプ)
    ENDIF

    ;絶頂時のグラフィック切り替え予約と画面効果
    ;抽出テキスト
    IF NOWEX:TARGET:多重絶頂 || NOWEX:TARGET:射精
        適正差分種類ID = 2 ;絶頂差分を表示候補にする
        ;絶頂画像は最大3回のコマンド実行で戻る
        画像切り替えカウンタ = 3
        アクセスコマンド更新タイプ '= "更新"

        IF NOWEX:TARGET:多重絶頂
            ;セクハラS絶頂があるので地の文で教える
            SIF NOWEX:TARGET:4
                PRINTFORML %CALLNAME:TARGET%は小刻みに肌をひくつかせて絶頂した 
            CALL KOJO_ORGASM_絶頂(キャラ番号)
            PRINTL
        ENDIF
		IF NOWEX:キャラ番号:射精
			SIF NOWEX:キャラ番号:射精 > 1
				PRINT 大量
			PRINTFORML 射精(%CALLNAME:キャラ番号%)
			RFLAG:フィニッシュフラグ = NOWEX:キャラ番号:射精
			CALL EVENT_SHOOT_CHARA(キャラ番号)
			CALL KOJO_ORGASM_射精(キャラ番号)
			PRINTL
		ENDIF

        ;絶頂演出込みでの更新をかける
        メイン画像更新タイプ '= "絶頂"
        IF おさわりあり限定解除フラグ
            CALL おさわりファイル差分選出(汎用ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
            CALL おさわり画像基準セット(汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
            CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
        ELSE ;おさわりあり限定
            CALL おさわりファイル差分選出(対象ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
            CALL おさわり画像基準セット(おさわりフォルダ + 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
            CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
        ENDIF
    ENDIF

    ;ステータス変動　表示はSKIP
    SKIPDISP 1
    CALL 技巧上昇判定(TARGET)
        SIF RESULT
            TRYCALLFORM KOJO_SKILLUP_技巧_{NO:TARGET}(TARGET)
    CALL 性知識上昇判定(TARGET)
        SIF RESULT
            TRYCALLFORM KOJO_SKILLUP_性知識_{NO:TARGET}(TARGET)
    CALL JUJUN_UP_CHECK(TARGET)
    CALL Love_CALC(TARGET)
    CALL FAVOR_CALC(TARGET)
    CALL 性癖取得チェック(TARGET)
    CALL 性癖成長チェック(TARGET)

    ;--------------------SOURCE_CHECKから抽出　中身はそのまま
        SIF CFLAG:TARGET:滞在期間 < 0
            CONTINUE
        SIF RCVAR:TARGET:表示カット
            SKIPDISP 1
        LOCAL:3 = 0
        CALL 干渉強化関数_12("適用条件", TARGET)
        LOCAL:4 = RESULT
        FOR LOCAL,0,100
            ;コマンド入力前のCUPを追加
            CUP:TARGET:LOCAL += TCVAR:TARGET:(600 + LOCAL)
            SIF PALAMNAME:LOCAL == ""
                CONTINUE
            SIF CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL == 0
                CONTINUE
            IF !LOCAL:3
                PRINTL
                PRINTFORML %CALLNAME:TARGET%
            ENDIF
            LOCAL:3 ++
            LOCAL:2 = CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL + TCVAR:TARGET:(600 + LOCAL)
            PRINTFORM %PALAMNAME:LOCAL,8,LEFT% {PALAM:TARGET:LOCAL,10,LEFT} + {CUP:TARGET:LOCAL,10,LEFT} =   {PALAM:TARGET:LOCAL + CUP:TARGET:LOCAL,10,LEFT}
            IF 干渉強化変数:TARGET:干渉種類 - 5 == LOCAL && LOCAL < 5
                SETCOLOR カラーパレット("黄")
                PRINT 　干渉効果発動中！
                RESETCOLOR
            ELSEIF LOCAL:4 && LOCAL < 5
                SETCOLOR カラーパレット("黄")
                PRINT 　干渉効果発動中！
                RESETCOLOR
            ENDIF
            PRINTL
            PALAM:TARGET:LOCAL += CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL
            PALAM:TARGET:LOCAL = MAX(0, PALAM:TARGET:LOCAL)
            CUP:TARGET:LOCAL = 0
            CDOWN:TARGET:LOCAL = 0
        NEXT
        ;干渉ランク上昇判定
        IF 干渉強化変数:TARGET:干渉ランク < 5
            IF 干渉経験値テーブル:(干渉強化変数:TARGET:干渉ランク) <= 干渉強化変数:TARGET:干渉経験値
                干渉強化変数:TARGET:干渉ランク += 1
                PRINTFORMW %CALLNAME:TARGET%の干渉ランクが上昇した！
            ENDIF
        ENDIF
        ;警戒度表示より
        IF BASE:(TARGET):警戒 != TCVAR:(TARGET):前ターン警戒度
            TCVAR:(TARGET):前ターン警戒度 = BASE:(TARGET):警戒
        ENDIF

        SKIPDISP 0
    ;-----------------------------------------------------

        ;知識系素質の取得処理
        CALL 知識系素質の取得(TARGET)

    ;-----------------------------------------------------
    CALL TURN_RESET
    CALL 特定キャラモードリセット(TARGET)

    IF BASE:TARGET:体力 == 0
        プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%の体力が尽きた……"
        GOTO 終了処理
    ENDIF

ENDIF

IF おさわり警告フラグ
    VARSET おさわり容認カウンタ
    IF PALAM:TARGET:欲情 < PALAMLV:2
        プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%は咎めるような視線をこちらに向けている<br>あまり乗り気ではないようだ……<br>"
    ELSE
        プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%は咎めるような視線をこちらに向けている<br>もう少し丁寧にやった方がよさそうだ……<br>"
    ENDIF
    FOR LOCAL, 0, 10
        AWAIT 50
    NEXT
    おさわり警告カウンタ ++
    IF おさわり警告カウンタ > 2
       プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%に制止されてしまった<br>どうやらここまでのようだ……<br>"
       HTML_PRINT プリントメッセージ保存
       プリントメッセージ保存 = 
       GOTO 終了処理
    ENDIF
ENDIF

HTML_PRINT プリントメッセージ保存
プリントメッセージ保存 = 

GOTO メインループ

$終了処理

;PRINTFORM  CLICK {クリックカウンタ} COM {コマンドカウンタ} {(TIME - 開始TIME保存) / 60}時間{(TIME - 開始TIME保存) % 60}分経過
IF 経過TIME保存 / 60 > 0 ;1h以上経過
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%の身体を{経過TIME保存 / 60}時間{経過TIME保存 % 60}分間たっぷり楽しんだ……"
ELSEIF 経過TIME保存 % 60 == 0 ;何もしなかった
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%に伸ばした手を引っ込めた……"
ELSE
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%の身体を{経過TIME保存 % 60}分間楽しんだ……"
ENDIF

SIF システムコマンド展開フラグ
    システムコマンド展開フラグ ++
CALL おさわりシステムコマンド(システムコマンド更新タイプ, システムコマンド展開フラグ, システムコマンド種類, システムコマンドチェックボックスあり, システムコマンドチェック状態, システムコマンド格納, システムコマンド位置基準, システムコマンドサイズ, システムコマンド間隔, 表示レイヤー層配列:表示レイヤー層モード:システムコマンドレイヤー層)
CBGCLEAR
;全イメージを背景に押し込んでからフェードアウト演出
表示レイヤー層モード = 全背景モード
IF おさわりあり限定解除フラグ
    CALL おさわりファイル差分選出(汎用ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
    CALL おさわり画像基準セット(汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
    CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
ELSE ;おさわりあり限定
    CALL おさわりファイル差分選出(対象ファイル数, 衣装種類, 適正衣装種類ID, 適正差分種類ID, 対象衣装種類ID, 対象差分種類ID, 対象ファイルID, メイン画像更新タイプ)
    CALL おさわり画像基準セット(おさわりフォルダ + 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, おさわりあり限定解除フラグ, 画像最大化フラグ, 画像位置, 画像サイズ, システムコマンド位置基準, システムコマンドサイズ, アクセスコマンド位置基準, アクセスコマンドサイズ, アイテム表示位置基準)
    CALL おさわりメイン画像(メイン画像更新タイプ, 表示レイヤー層モード, おさわりフォルダ, 対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID, 画像サイズ, 画像位置, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層)
ENDIF

CALL おさわりシステムコマンド(システムコマンド更新タイプ, システムコマンド展開フラグ, システムコマンド種類, システムコマンドチェックボックスあり, システムコマンドチェック状態, システムコマンド格納, システムコマンド位置基準, システムコマンドサイズ, システムコマンド間隔, 表示レイヤー層配列:表示レイヤー層モード:システムコマンドレイヤー層)
CALL おさわりアクセスコマンド(アクセスコマンド更新タイプ, アクセスコマンド展開フラグ, アクティブコマンドリスト, アクセスコマンドIDリスト, コマンドIDカラーコード変換, アクセスコマンド位置基準, アクセスコマンドサイズ, アクセスコマンド間隔, アクセスコマンド最大表示数, 表示レイヤー層配列:表示レイヤー層モード:アクセスコマンドレイヤー層)
SIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
    CALL おさわりアイテム画像(アイテム画像更新タイプ, 有効アロマフラグ, 所持アロマ数配列, アロマ種類数, アイテム表示位置基準, 画像位置, 画像サイズ, 表示レイヤー層配列:表示レイヤー層モード:ステート表示レイヤー層)

FOR LOCAL, 0, 5
    CBGREMOVERANGE 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層, 表示レイヤー層配列:表示レイヤー層モード:メイン画像レイヤー層
    GDISPOSE レイヤー番号 + 1 
    GCREATE レイヤー番号 + 1 , CLIENTWIDTH(), CLIENTHEIGHT()
    GSETBRUSH レイヤー番号 + 1 , 0x40101010 + LOCAL * 0x20000000
    GFILLRECTANGLE レイヤー番号 + 1 , 0, 0, CLIENTWIDTH(), CLIENTHEIGHT()
    CBGSETG レイヤー番号 + 1 , 0, 0, 1
    AWAIT 50
NEXT

; FOR LOCAL, 0, 50
;     PRINTL
; NEXT

CBGCLEAR
HTML_PRINT プリントメッセージ保存
WAIT
PRINTL
CALLFORM EVENTCOMEND0_%SAVESTR:ゲームフェイズ管理%
CALL TURN_RESET
CALL 特定キャラモードリセット(TARGET)
CBGCLEAR
SETANIMETIMER -1
GDISPOSE レイヤー番号 + 0 ;リソース
GDISPOSE レイヤー番号 + 1 ;今回ループ画像
GDISPOSE レイヤー番号 + 2 ;前回ループ画像
GDISPOSE レイヤー番号 + 3 ;表示用透過画像
GDISPOSE レイヤー番号 + 4 ;
GDISPOSE レイヤー番号 + 5 ;
GDISPOSE レイヤー番号 + 6 ;アロマリソース
GDISPOSE レイヤー番号 + 7 ;アロマパレット
GDISPOSE レイヤー番号 + 8 ;アロマエフェクトリソース
GDISPOSE レイヤー番号 + 9 ;アロマエフェクトパレット
GDISPOSE レイヤー番号 + 10 ;
GDISPOSE レイヤー番号 + 11 ;
GDISPOSE レイヤー番号 + 12 ;
GDISPOSE レイヤー番号 + 13 ;
GDISPOSE レイヤー番号 + 14 ;
GDISPOSE レイヤー番号 + 15 ;
GDISPOSE レイヤー番号 + 16 ;アクセスコマンドセパレータ
GDISPOSE レイヤー番号 + 19 ;画像サイズ決定用リソースファイル読み込み
GDISPOSE レイヤー番号 + 20 ;おさわり画像
GDISPOSE レイヤー番号 + 21 ;おさわり画像可視化
GDISPOSE レイヤー番号 + 23 ;システムコマンド
GDISPOSE レイヤー番号 + 24 ;アクセスコマンド
GDISPOSE レイヤー番号 + 25 ;マッサージUI
GDISPOSE レイヤー番号 + 29 ;統合ボタンマップ
GDISPOSE 80
GDISPOSE 81
GDISPOSE 82
GDISPOSE 83
SPRITEDISPOSEALL 0

TARGET = TARGET保存
RETURN

@おさわりコマンドリスト(アクティブコマンドリスト, 選択ID)
#FUNCTIONS
#DIM アクティブコマンドリスト
#DIM 選択ID
{
    ;おさわりコマンドテンプレ
	#DIMS CONST おさわりコマンドリスト = 
	"背中", "耳", "角", "尻尾", "お尻", "お腹", "頭", "髪", "脇", "", 
    "へそ", "口", "太もも", "下腹部", "乳首", "C", "スカート", "V", "B", "A", 
    "", "", "", "", "", "", "", "", "", "", 
}

{
    #DIMS CONST おさわりコマンドリストL0 = 
	"306_1", "306_15", "306_3", "306_4", "306_5", "306_6", "306_7", "306_8", "306_9", "",
    "306_11", "311", "306_10", "306_14", "321", "", "326", "", "321", "306_5", 
    "", "", "", "", "", "", "", "", "", "", 
}
{
    #DIMS CONST おさわりコマンドリストR0 = 
	"", "306_2", "306_12", "306_13", "320", "", "", "", "", "",
    "", "20", "", "", "324", "327", "", "327", "323", "327", 
    "", "", "", "", "", "", "", "", "", "", 
}

{
    ;マッサージおさわりコマンドテンプレ
	#DIMS CONST おさわりコマンドリスト = 
	"足裏", "ふくらはぎ", "肩こり", "背中", "くびれ", "腰", "頭皮", "尻尾", "角", "ケモミミ", 
    "大胸筋", "ふともも", "大殿筋", "デコルテ", "尾骨", "鼠径部", "乳首", "ポルチオ", "恥丘", "C", 
    "V", "A", "口", "", "", "", "", "", "", "", "", 
}
{
    #DIMS CONST おさわりコマンドリストL2 = 
	"01", "02", "03", "04", "05", "06", "07", "08", "09", "10",
    "11", "12", "13", "14", "15", "", "", "", "", "",
    "", "28", "30", "", "", "", "", "", "", "", 
}
{
    #DIMS CONST おさわりコマンドリストR2 = 
	"", "", "", "", "", "", "", "", "", "",
    "21", "", "", "", "", "22", "23", "24", "25", "26", 
    "27", "29", "31", "", "", "", "", "", "", "", 
}

SELECTCASE アクティブコマンドリスト
    CASE 0
            RETURNF おさわりコマンドリストL0:選択ID
    CASE 1
            RETURNF おさわりコマンドリストR0:選択ID
    CASE 10
            RETURNF おさわりコマンドリストL2:選択ID
    CASE 11
            RETURNF おさわりコマンドリストR2:選択ID
ENDSELECT

