@CUSTOM_CHARAMAKE(ARG)
#DIM スタイル記録
;NOは適当に設定
NO:ARG = 0

;種族(1=ヒューマン 2=エルーン 3=ドラフ 4=ハーヴィン 5=星晶獣 6=その他)
TALENT:ARG:種族 = 1
;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:性別 = 1
SIF ARG == 0
	TALENT:ARG:性別 = 2
;年齢層
TALENT:ARG:年齢層 = 年齢層_大人
;体力
MAXBASE:ARG:体力 = 2000
BASE:ARG:体力 = MAXBASE:ARG:体力

DRAWLINE
PRINTL スタイルを選択してください。
DRAWLINE
PRINTBUTTON "[0] 通常スタイル", 0
PRINTL
PRINTL 　　…あなたの設定を自由に決定するスタイル。
PRINTL
PRINTBUTTON "[1] 通常スタイル_スタートダッシュモード", 1
PRINTL
PRINTL 　　…あなたの設定を自由に決定するスタイル。初期特典付き
PRINTL
PRINTBUTTON "[10] グランくんスタイル", 10
PRINTL
PRINTL 　　…あなた＝グランとして設定するスタイル。開発中につき想定外の挙動をする可能性があります。
PRINTL
PRINTBUTTON "[11] ジータちゃんスタイル", 11
PRINTL
PRINTL 　　…あなた＝ジータとして設定するスタイル。開発中につき想定外の挙動をする可能性があります。
PRINTL
PRINTBUTTON "[20] 任意キャラスタイル", 20
PRINTL
PRINTL 　　…あなた＝その他のキャラとして設定するスタイル。開発中につき想定外の挙動をする可能性があります。
PRINTL
BINPUT
スタイル記録 = RESULT
SELECTCASE スタイル記録
	CASE 0
		PRINTL ■通常スタイル
		PRINTL 　あなたの設定を自由に決定するスタイル。
		PRINTL 　基本的にこのバリアントはこのスタイルを想定して作成されている。
	CASE 1
		PRINTL ■通常スタイル
		PRINTL 　あなたの設定を自由に決定するスタイル。
		PRINTL 　基本的にこのバリアントはこのスタイルを想定して作成されている。
		PRINTL
		PRINTL ■スタートダッシュモード
		PRINTL 　最序盤の立ち上がりをサポートする初期特典。
		PRINTL 　ランダムキャラ、あるいは任意のキャラ（EXキャラ含む）を４人選び、初期好感度を増加・従業員化させる。
		PRINTL 　（一部選択できないキャラあり）
		PRINTL 　最初の一ヶ月の間、従業員の給料無料。
		PRINTL 　これを選択してもイベントやシナリオに影響が出ることはない。
	CASE 10
		PRINTL ■グランくんスタイル
		PRINTL 　グランくんを「あなた」として設定するスタイル。
		PRINTL 　あなたのステータスがグランと同じになり、リゾートにグランが登場しなくなる。
		PRINTL 　初期従業員として「ルリア」「ビィ」が登録される。素質「命のリンク」の所持者があなたとルリアとして設定される。
		PRINTL 　一部キャラの初期好感度が上昇し、常連素質を付与する。
		PRINTL 　あなたが戦闘に参加できないのは通常と同一。
		PRINTL
		PRINTL ※開発中なので想定外の不具合が発生することがあります。
		PRINTL ※イベント文章などは通常スタイルを想定しているので、話の流れがおかしくなる場合があります。
	CASE 11
		PRINTL ■ジータちゃんスタイル
		PRINTL 　ジータちゃんを「あなた」として設定するスタイル。
		PRINTL 　あなたのステータスがジータと同じになり、リゾートにジータが登場しなくなる。
		PRINTL 　初期従業員として「ルリア」「ビィ」が登録される。素質「命のリンク」の所持者があなたとルリアとして設定される。
		PRINTL 　一部キャラの初期好感度が上昇し、常連素質を付与する。
		PRINTL 　あなたが戦闘に参加できないのは通常と同一。
		PRINTL
		PRINTL ※開発中なので想定外の不具合が発生することがあります。
		PRINTL ※イベント文章などは通常スタイルを想定しているので、話の流れがおかしくなる場合があります。
	CASE 20
		PRINTL ■任意キャラスタイル
		PRINTL 　任意のキャラを「あなた」として設定するスタイル。
		PRINTL 　あなたのステータスが該当キャラと同じになり、リゾートに該当キャラが登場しなくなる。
		PRINTL 　あなたが戦闘に参加できないのは通常と同一。
		PRINTL 　その他のボーナスなどは存在しない。
		PRINTL
		PRINTL ※開発中なので想定外の不具合が発生することがあります。
		PRINTL ※イベント文章などは通常スタイルを想定しているので、話の流れがおかしくなる場合があります。
ENDSELECT
PRINTL
PRINTL このスタイルで開始してもよろしいですか？
PRINTBUTTONLC "[0] はい", 0
PRINTBUTTONLC "[1] いいえ", 1
PRINTL
BINPUT
IF RESULT == 0
	SELECTCASE スタイル記録
		CASE 0
			SAVESTR:あなたスタイル = 
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 1
			PRINTW 初期従業員は初回設定が全て終わった後に選択することができます。
			SAVESTR:あなたスタイル = スタートダッシュモード
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 10
			SAVESTR:あなたスタイル = グランくんスタイル
		CASE 11
			SAVESTR:あなたスタイル = ジータちゃんスタイル
		CASE 20
			SAVESTR:あなたスタイル = 任意キャラスタイル
			PRINTFORMW 任意キャラは初回設定が全て終わった後に選択します。
	ENDSELECT
ELSE
	RESTART
ENDIF


@仮置きCUSTOM_TERMINAL(ARG)
LOCAL = 3
SIF BASE:ARG:身長
	CLEARBIT LOCAL, 0
SIF BASE:ARG:バスト
	CLEARBIT LOCAL, 1
SIF LOCAL
	CALL SET_BODYSIZE(ARG, LOCAL)

$個別素質変更_LOOP
PRINTL
DRAWLINE
PRINTL 各種ステータスを決定してください。
DRAWLINE
PRINTL ■種族
FOR LOCAL, 1, 7
	IF TALENT:ARG:種族 == LOCAL
		PRINTFORM [%GET_TALENTNAME(200, LOCAL)%]
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", 100 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■性別
SELECTCASE TALENT:ARG:性別
	CASE 2
		PRINT [男性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 1
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		PRINT [女性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 3
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		PRINT [ふたなり]
ENDSELECT
PRINTL
DRAWLINE
PRINTL ■容姿
FOR LOCAL, -1, 3
	IF TALENT:ARG:容姿 == LOCAL
		PRINTFORM [%GET_TALENTNAME(160, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(160, LOCAL)%]", 300 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTL ■印象
FOR LOCAL, 0, 5
	IF TALENT:ARG:印象 == LOCAL
		IF LOCAL == 0
			PRINTFORM [淡白]
		ELSE
			PRINTFORM [%GET_TALENTNAME(95, LOCAL)%]
		ENDIF
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[淡白]", 330
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(95, LOCAL)%]", 330 + LOCAL
		ENDIF
		RESETCOLOR
	ENDIF
	SIF LOCAL != 4
		PRINT  / 
NEXT
IF TALENT:ARG:印象 == 0
	HTML_PRINT "<br>■淡白<br>性格や態度がさっぱりしていること。こだわりやしつこさがないこと。また、そのさま。<br>好感度要素への補正がない。<br> "
ELSE
	HTML_PRINT GET_TALENTNAME(95, TALENT:ARG:印象, 1)
ENDIF
PRINTL
PRINTL ■年齢
FOR LOCAL, 年齢層_幼い, 年齢層_人外 + 1
	IF TALENT:ARG:年齢層 == LOCAL
		PRINTFORM [%GET_TALENTNAME(164, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(164, LOCAL)%]", 340 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTFORML ■体格 \@ BASE:ARG:身長 ? （現状%DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cm）# \@
FOR LOCAL, -3, 3
	IF TALENT:ARG:体格 == LOCAL
		PRINTFORM [%GET_TALENTNAME(161, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(161, LOCAL)%]", 400 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINT  / 
PRINTBUTTON @"[数値指定]", 410
PRINTL
PRINTL ■体型
FOR LOCAL, -3, 4
	IF TALENT:ARG:体型 == LOCAL
		PRINTFORM [%GET_TALENTNAME(163, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(163, LOCAL)%]", 450 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 3
		PRINT  / 
NEXT
PRINTL
IF TALENT:ARG:性別 & 2
	PRINTL ■男性器サイズ
	FOR LOCAL, -2, 3
		IF TALENT:ARG:男性器サイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(162, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", 500 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 2
			PRINT  / 
	NEXT
	PRINTL
ENDIF
IF TALENT:ARG:性別 & 1
	PRINTFORML ■バストサイズ \@ BASE:ARG:バスト ? （現状%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%cm %GET_CUPSIZE(ARG), LOCAL, LEFT%）# \@
	FOR LOCAL, -2, 4
		IF TALENT:ARG:バストサイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(205, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", 600 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 3
			PRINT  / 
	NEXT
	PRINT  / 
	PRINTBUTTON @"[数値指定]", 610
	PRINTL
ENDIF

PRINTL
PRINTBUTTON "[999] これで決定する", 999
PRINTL

BINPUT

SELECTCASE RESULT
	CASE 101 TO 106
		TALENT:ARG:種族 = RESULT - 100
		CALL RESET_種族(ARG)
		GOTO 個別素質変更_LOOP
	CASE 201 TO 203
		TALENT:ARG:性別 = RESULT - 200
		CALL RESET_性別(ARG)
		GOTO 個別素質変更_LOOP
	CASE 299 TO 302
		TALENT:ARG:容姿 = RESULT - 300
		GOTO 個別素質変更_LOOP
	CASE 330 TO 334
		TALENT:ARG:印象 = RESULT - 330
		GOTO 個別素質変更_LOOP
	CASE 350 TO 357
		TALENT:ARG:年齢層 = RESULT - 340
		GOTO 個別素質変更_LOOP
	CASE 397 TO 402
		TALENT:ARG:体格 = RESULT - 400
		CALL SET_BODYSIZE(ARG, 0)
		GOTO 個別素質変更_LOOP
	CASE 410
		DRAWLINE
		PRINTL 身長をmm単位で入力してください
		INPUT
		BASE:ARG:身長 = RESULT
		PRINTFORML 身長を%DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cmに設定しました
		TALENT:ARG:体格 = 0
		CALL INIT_BODYSIZE_HEIGHT(ARG)
		PRINTFORMW 体格が%GET_TALENTNAME(161, TALENT:ARG:体格)%に設定されました
		GOTO 個別素質変更_LOOP
	CASE 447 TO 453
		TALENT:ARG:体型 = RESULT - 450
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 498 TO 502
		TALENT:ARG:男性器サイズ = RESULT - 500
		GOTO 個別素質変更_LOOP
	CASE 598 TO 603
		TALENT:ARG:バストサイズ = RESULT - 600
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 610
		DRAWLINE
		PRINTL バストサイズをmm単位で入力してください
		INPUT
		BASE:ARG:バスト = RESULT
		PRINTFORML バストサイズを%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%cmに設定しました
		TALENT:ARG:バストサイズ = 0
		CALL INIT_BODYSIZE_BUST(ARG)
		PRINTFORMW バストサイズ素質が%GET_TALENTNAME(205, TALENT:ARG:バストサイズ)%に設定されました
		GOTO 個別素質変更_LOOP
	CASE 999
		RETURN 0
ENDSELECT


;性別に関連する経験等の初期化
@RESET_性別(ARG)
IF !(TALENT:ARG:性別 & 1)
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF

IF !(TALENT:ARG:性別 & 2)
	EXP:ARG:射精経験 = 0
ENDIF

IF TALENT:ARG:性別 == 1
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSEIF TALENT:ARG:性別 == 2
	EQUIP:ARG:上半身下着あり = 0
	EQUIP:ARG:スカートめくり可 = 0
	EQUIP:ARG:下半身下着抜き取り可 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSE
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ENDIF

CALL SET_BODYSIZE(ARG, 0)


;種族に関連する素質等の初期化
@RESET_種族(ARG)
SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		IF TALENT:ARG:性別 == 2
			TALENT:ARG:体格 = 2
		ELSE
			TALENT:ARG:体格 = -1
		ENDIF
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
		TALENT:ARG:体格 = -2
	CASEELSE
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
ENDSELECT
CALL SET_BODYSIZE(ARG, 0)


;名前の設定
@NAME_CUSTOM(ARG)
#DIM LCOUNT
#DIMS AFTER_NAME
#DIMS AFTER_CALLNAME

LCOUNT = LINECOUNT

$ネーム再入力
PRINTL キャラのNAMEを入力してください
IF STRLENS(NAME:ARG)
	;PRINTFORM 現在のNAME：%NAME表示(ARG)%      
	;CSTR:ARG:スタイル管理文字列を変えるわけではないので%NAME:ARG%表示のほうがいいはず
	PRINTFORM 現在のNAME：%NAME:ARG%      
	PRINTBUTTON "[変更しない]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF ARG == 0 && RESULTS == ""
	AFTER_NAME = %NAME表示(ARG)%
ELSEIF RESULTS != ""
	AFTER_NAME = %RESULTS%
ELSE
	AFTER_NAME = %NAME:ARG%
ENDIF
IF !STRLENS(AFTER_NAME)
	PRINTW NAMEを空欄にはできません
	CLEARLINE 2
	GOTO ネーム再入力
ENDIF

$コールネーム再入力
PRINTL キャラのCALLNAMEを入力してください
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在のCALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[変更しない]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF RESULTS == ""
	AFTER_CALLNAME = %CALLNAME:ARG%
ELSE
	AFTER_CALLNAME = %RESULTS%
ENDIF
IF !STRLENS(AFTER_CALLNAME)
	PRINTW CALLNAMEを空欄にはできません
	CLEARLINE 2
	GOTO コールネーム再入力
ENDIF

CALL CHOICE(@"%NAME表示(ARG)%のNAMEを「%NAME:ARG%」 → 「%AFTER_NAME%」、CALLNAMEを「%CALLNAME:ARG%」 → 「%AFTER_CALLNAME%」に変更しますか？", "はい", "いいえ")
IF RESULT
	CLEARLINE LINECOUNT - LCOUNT
	GOTO ネーム再入力
ENDIF

SWAP NAME:ARG, AFTER_NAME
SWAP CALLNAME:ARG, AFTER_CALLNAME

IF ARG == MASTER && CALLNAME:MASTER != AFTER_CALLNAME
	FOR LOCAL, 1, CHARANUM
		IF CSVCSTR(NO:LOCAL, GETNUM(CSTR, "二人称")) == "" || CSTR:LOCAL:二人称 == ""
;			DEBUGPRINTFORM %CALLNAME:LOCAL%の二人称「%CSTR:LOCAL:二人称%」→
			IF (STRCOUNT(CSTR:LOCAL:二人称, AFTER_NAME) || STRCOUNT(CSTR:LOCAL:二人称, AFTER_CALLNAME)) && EXISTFUNCTION(@"口上側LOADENDパラメータ変更_{NO:LOCAL}")
				CSTR:LOCAL:二人称 '= ""
;				DEBUGPRINT (@)
				CALLFORM 口上側LOADENDパラメータ変更_{NO:LOCAL}
			ELSEIF STRCOUNT(CSTR:LOCAL:二人称, AFTER_CALLNAME) && (!STRCOUNT(CSTR:LOCAL:二人称, AFTER_NAME) || AFTER_CALLNAME == AFTER_NAME)
				CSTR:LOCAL:二人称 '= REPLACE(CSTR:LOCAL:二人称, AFTER_CALLNAME, CALLNAME:MASTER)
			ELSEIF STRCOUNT(CSTR:LOCAL:二人称, AFTER_NAME)
				CSTR:LOCAL:二人称 '= REPLACE(CSTR:LOCAL:二人称, AFTER_NAME, NAME:MASTER)
			ENDIF
			SIF CSTR:LOCAL:二人称 == ""
				CSTR:LOCAL:二人称 = %CALLNAME:MASTER%
;			DEBUGPRINTFORML 「%CSTR:LOCAL:二人称%」
		ENDIF
	NEXT
ENDIF


;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:体力, MAXBASE:ARG:体力, 16
PRINTFORM ({BASE:ARG:体力, 4}/{MAXBASE:ARG:体力, 4})
IF BASE:ARG:体力 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:体力 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL


;素質の設定
@TALENT_CUSTOM(ARG, ARG:1)
VARSET LOCAL
FOR LOCAL, ARG:1 * 40, 40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155) || (LOCAL >= 211 && LOCAL <= 215)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL, 1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS =  [{LOCAL, 3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS, 35, LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 前のページ                 ", 998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 前のページ", 35, LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 5
	PRINTBUTTON "[999] - 次のページ", 999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 次のページ", 35, LEFT%
	RESETCOLOR
ENDIF
PRINTL


@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL, 0, 50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL, 12%:{ABL:ARG:LOCAL, 5, LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL


@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL, 0, 200
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL, 2}] - %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5, LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL


@任意キャラスタイル_キャラ選定()
#DIM 候補配列, キャラクタ数上限
#DIM 候補数
#DIM キャラ番号
#DIM ページ数

;キャラを選んだ後、キャラのNOを返す
VARSET 候補配列
候補数 = 0
FOR キャラ番号, 1, CHARANUM
	;ランダムキャラはダメ
	SIF NO:キャラ番号 == 999
		CONTINUE
	;ジータとグランはダメ
	SIF NO:キャラ番号 == 27
		CONTINUE
	SIF NO:キャラ番号 == 146
		CONTINUE
	候補配列:候補数 = キャラ番号
	候補数 ++
NEXT
CALL キャラ配列フィルタ処理(候補配列)
候補数 = RESULT


$表示位置

;キャラ一覧を表示
DRAWLINE
PRINTL 誰をオーナーとして開始しますか？
PRINTL ※ジータ・グランを除く全キャラを対象として選択することが出来ます。
PRINTL ※対象キャラによってはイベント・シナリオの展開に矛盾が出る場合があります。
DRAWLINE

LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	SIF 候補配列:LOCAL < 1
		BREAK
	IF ページ数 == LOCAL:1 / 項目表示数
		LOCALS += @"<button value='{LOCAL}'>[{LOCAL,3}] %GET_NAME(候補配列:LOCAL, 1)%</button>　%ソート情報表示(候補配列:LOCAL, 1)%　"
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
DRAWLINE
;IF ページ数 > 0
;	PRINTBUTTONLC "[9000] 前のページへ", 9000
;ELSE
;	PRINTC   
;ENDIF
;IF ページ数 < (LOCAL:1 + 1) / 30
;	PRINTBUTTONLC "[9001] 次のページへ", 9001
;ELSE
;	PRINTC   
;ENDIF
;PRINTL
;ページめくり表示
CALL ページ送り共通表示(ページ数, 項目表示数, 最大表示数, 9000, 9003)
DRAWLINE
CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
;DRAWLINE
;PRINTBUTTON "[9999] 戻る", 9999
PRINTL 

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(LOCAL, 9020, 9030, 9031, 9040)
		ページ数 = 0
		RESTART
	CASE 9000 TO 9007
		CALL ページ送り共通実行(LOCAL, ページ数, 項目表示数, 最大表示数, 9000, 9003)
		GOTO 表示位置
	CASE 9999
		WHILE 1
			CALL 任意キャラスタイル_セット確認(候補配列:LOCAL)
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスト表示へ戻る
					GOTO 表示位置
				CASE 1
					;- あなたで始める
					ページ数 = 0
					RETURN 0
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補数
						LOCAL = 0
			ENDSELECT
		WEND
;	CASE 9000
;		ページ数 --
;		GOTO 表示位置
;	CASE 9001
;		ページ数 ++
;		GOTO 表示位置
	CASEELSE
		WHILE 1
			CALL 任意キャラスタイル_セット確認(候補配列:LOCAL)
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスト表示へ戻る
					GOTO 表示位置
				CASE 1
					;- キャラ選択
					COPYCHARA 候補配列:LOCAL, MASTER
					NO:MASTER = 0
					CFLAG:(候補配列:LOCAL):招待不可フラグ = -1
					FLAG:あなたキャラ化記録 = NO:(候補配列:LOCAL)
					RETURN NO:(候補配列:LOCAL)
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補数
						LOCAL = 0
			ENDSELECT
		WEND
ENDSELECT

RESTART

@任意キャラスタイル_セット確認(対象キャラ)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 返り値
#DIM 描画開始行
#DIM 対象画像数
#DIM リロールボタン有効
#DIM 実行結果
#DIM セットフラグ
#DIM リスタートフラグ
#DIM TFLAG保存
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	描画開始行 = LINECOUNT
ENDIF

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
PRINTFORML %NAME表示(対象キャラ)%をオーナーとして始めますか？
; ボタン表示
PRINTBUTTONLC "[0]はい", 0
PRINTBUTTONLC "[1]いいえ", 1
PRINTBUTTONLC "[10]詳細能力を表示", 10
PRINTL
PRINTBUTTONLC "[2]前のキャラ", 2
PRINTBUTTONLC "[3]次のキャラ", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]画像のランダム表示をリロール", 4
PRINTL

; 入力ループ
WHILE 1
	BINPUT
	SELECTCASE RESULT
		CASE 0
			返り値 = 1
			BREAK
		CASE 1
			返り値 = 0
			BREAK
		CASE 4
			RESTART
		CASE 2
			;- [2]前のキャラ
			返り値 = 10
			BREAK
		CASE 3
			;- [3]次のキャラ
			返り値 = 11
			BREAK
		CASE 10
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					返り値 = 10
					BREAK
				CASE 101
					;次のキャラへ
					返り値 = 11
					BREAK
				CASE 1
					;はい
					返り値 = 1
					BREAK
				CASE 0
					;いいえ
					返り値 = 0
					BREAK
			ENDSELECT
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)

RETURN 返り値
