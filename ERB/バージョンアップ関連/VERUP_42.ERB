;Ver0.042と旧バージョンとの互換のための処理
;Ver0.045にしたとき消す、互換を43に

;追加TALENT、追加CSTRのチェック
@VERUP_42_CHARA(ARG)

IF 下半身フォロー == 0 || (下半身フォロー == 1 && ARG == 0)
	SIF CSVBASE(NO:ARG, 22)
		CFLAG:ARG:ウエスト初期値 = CSVBASE(NO:ARG, 22)
		BASE:ARG:ウエスト = CFLAG:ARG:ウエスト初期値
	SIF CSVBASE(NO:ARG, 23)
		CFLAG:ARG:ヒップ初期値 = CSVBASE(NO:ARG, 23)
		BASE:ARG:ヒップ = CFLAG:ARG:ヒップ初期値

	IF TALENT:ARG:下半身 == 0 && CSVTALENT(NO:ARG, 210) != 0
		TALENT:ARG:下半身 = CSVTALENT(NO:ARG, 210)
		IF TALENT:ARG:体型 != CSVTALENT(NO:ARG, 163)
			TALENT:ARG:体型 = CSVTALENT(NO:ARG, 163)
			CALL SET_BODYSIZE(ARG, 1p1)
		ENDIF
	ENDIF

	IF !BASE:ARG:ウエスト || !BASE:ARG:ヒップ
		IF CFLAG:ARG:ウエスト初期値 && !BASE:ARG:ウエスト
			BASE:ARG:ウエスト = CFLAG:ARG:ウエスト初期値
			MAXBASE:ARG:ウエスト = BASE:ARG:ウエスト
		ENDIF

		IF CFLAG:ARG:ヒップ初期値 && !BASE:ARG:ヒップ
			BASE:ARG:ヒップ = CFLAG:ARG:ヒップ初期値
			MAXBASE:ARG:ヒップ = BASE:ARG:ヒップ
		ENDIF
	ENDIF
	LOCAL = SETBIT_PI(ARG)
	SIF LOCAL > 0
		CALL SET_BODYSIZE(ARG, SETBIT_PI(ARG))
ENDIF

IF Ａ処女フォロー == 0 || (Ａ処女フォロー == 1 && ARG == 0)
	SIF 初体験日:ARG:Ａ挿入初体験 <= 0
		TALENT:ARG:Ａ処女 = CSVTALENT(NO:ARG, GETNUM(TALENT, "Ａ処女"))
ENDIF

IF 年齢フォロー == 0
	IF NO:ARG == 0 || TALENT:ARG:ランダムキャラ
		;あなたorランダムキャラの場合、変換する
		SELECTCASE TALENT:ARG:年齢層
			CASE -2
				;旧幼い
				TALENT:ARG:年齢層 = 年齢層_幼い
				TALENT:ARG:外見年齢 = 0
			CASE -1
				;旧若い
				TALENT:ARG:年齢層 = 年齢層_若い
				TALENT:ARG:外見年齢 = 0
			CASE 0
				;旧成人
				TALENT:ARG:年齢層 = 年齢層_成人
				TALENT:ARG:外見年齢 = 0
			CASE 1
				;旧大人
				TALENT:ARG:年齢層 = 年齢層_大人
				TALENT:ARG:外見年齢 = 0
			CASE 2
				;旧中年
				TALENT:ARG:年齢層 = 年齢層_壮年
				TALENT:ARG:外見年齢 = 0
			CASE 3
				;旧外見年齢
				TALENT:ARG:年齢層 = 年齢層_老年
				TALENT:ARG:外見年齢 = 年齢層_大人
			CASE 4
				;旧ロリババア
				TALENT:ARG:年齢層 = 年齢層_老年
				TALENT:ARG:外見年齢 = 年齢層_幼い
			CASEELSE
				;それ以外は年齢不詳とする
				TALENT:ARG:年齢層 = 年齢層_不詳
				TALENT:ARG:外見年齢 = 0
		ENDSELECT
	ELSE
		;ユニークキャラの場合、CSVから再定義する
		BASE:ARG:年齢 = CSVBASE(NO:ARG, 26)
		IF BASE:ARG:年齢 > 0
			TALENT:ARG:年齢層 = 年齢層算出処理(BASE:ARG:年齢)
		ELSE
			TALENT:ARG:年齢層 = CSVTALENT(NO:ARG, 164)
		ENDIF
		TALENT:ARG:外見年齢 = CSVTALENT(NO:ARG, 165)
	ENDIF
ELSEIF 年齢フォロー == 1 && ARG == 0
	SELECTCASE TALENT:ARG:年齢層
		CASE -2
			;旧幼い
			TALENT:ARG:年齢層 = 年齢層_幼い
			TALENT:ARG:外見年齢 = 0
		CASE -1
			;旧若い
			TALENT:ARG:年齢層 = 年齢層_若い
			TALENT:ARG:外見年齢 = 0
		CASE 0
			;旧成人
			TALENT:ARG:年齢層 = 年齢層_成人
			TALENT:ARG:外見年齢 = 0
		CASE 1
			;旧大人
			TALENT:ARG:年齢層 = 年齢層_大人
			TALENT:ARG:外見年齢 = 0
		CASE 2
			;旧中年
			TALENT:ARG:年齢層 = 年齢層_壮年
			TALENT:ARG:外見年齢 = 0
		CASE 3
			;旧長命者
			TALENT:ARG:年齢層 = 年齢層_老年
			TALENT:ARG:外見年齢 = 年齢層_大人
		CASE 4
			;旧ロリババア
			TALENT:ARG:年齢層 = 年齢層_老年
			TALENT:ARG:外見年齢 = 年齢層_幼い
		CASE 年齢層_幼い TO 年齢層_老年
			;新バージョン対応なら変換不要
		CASEELSE
			;それ以外は年齢不詳とする
			TALENT:ARG:年齢層 = 年齢層_不詳
			TALENT:ARG:外見年齢 = 0
	ENDSELECT
ENDIF

;表示奥義設定
SIF 表示奥義:ARG:0 != ""
	RETURN
SIF 陥落奥義:ARG:0 != ""
	RETURN
SIF !EXISTCSV(NO:ARG) || NO:ARG == 999
	RETURN

VARSET LOCALS
SUBSTRING CSVCSTR(NO:ARG, GETNUM(CSTR, "表示奥義設定")), 0, -1
IF RESULTS != ""
	SPLIT RESULTS, "_", LOCALS
	表示奥義:ARG:0 '= LOCALS:0
	表示奥義:ARG:1 '= LOCALS:1
	表示奥義:ARG:2 '= LOCALS:2
ENDIF
VARSET LOCALS
SUBSTRING CSVCSTR(NO:ARG, GETNUM(CSTR, "陥落奥義設定")), 0, -1
IF RESULTS != ""
	SPLIT RESULTS, "_", LOCALS
	陥落奥義:ARG:0 '= LOCALS:0
	陥落奥義:ARG:1 '= LOCALS:1
	陥落奥義:ARG:2 '= LOCALS:2
ENDIF

;その他広域変数の書き換えなど
@VERUP_42()

IF あなた画像フォロー == 0
	CSTR:MASTER:画像フォルダ = 0あなた
ENDIF

CALLNAME:(キャラ検索("[撃槍・ガングニール]立花響")) = 立花響
CSTR:(キャラ検索("[撃槍・ガングニール]立花響")):画像フォルダ = 1051立花響

IF 空艇港店舗屋号フォロー == 0
	FOR LOCAL, 0, 10
		IF 空艇港店舗状況:LOCAL:0 != 0 && 空艇港店舗屋号記録:LOCAL == ""
			空艇港店舗屋号記録:LOCAL = %空艇港_店舗屋号ランダム決定()%
		ENDIF
	NEXT
	DRAWLINE
	PRINTL 屋号を実装したため、現在存在する空艇港の店舗にランダムで屋号を付与しました。
	PRINTL 屋号を変更したい場合は、変更したい店舗に赴いて「店舗を改装する」から変更してください。
	DRAWLINE
	WAIT
ENDIF

簡易コマンドフィルタ:9 = 0

; IF 乗機属性フォロー == 0
; 	DT_COLUMN_ADD "所持乗機データベース", "乗機属性", 1
; 	DT_COLUMN_ADD "所持乗機データベース", "乗機属性_強化後", 1
; ENDIF

;対応処理用フラグを対応済状態にセット
@VERUP_42_FINISH()
下半身フォロー = 2
Ａ処女フォロー = 2
年齢フォロー = 2
あなた画像フォロー = 1
空艇港店舗屋号フォロー = 1
; 乗機属性フォロー = 1
