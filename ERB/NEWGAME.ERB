;-------------------------------------------------
;タイトル画面
;-------------------------------------------------
@SYSTEM_TITLE
DRAWLINE
ALIGNMENT CENTER
PRINT_IMG "タイトルロゴ", 5330, 1520
PRINTL
PRINTL 
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
ALIGNMENT LEFT
DRAWLINE
[IF_DEBUG]
PRINTBUTTON "Ver" + TOSTR(GAMEBASE_VERSION, "0,.000"), 5656
[ELSE]
PRINTS "Ver" + TOSTR(GAMEBASE_VERSION, "0,.000")
[ENDIF]
PRINTL
PRINTFORML %GAMEBASE_INFO%
DRAWLINE
PRINTBUTTON "[0] 最初から始める", 0
PRINTL
PRINTBUTTON "[1] ロードして始める", 1
PRINTL
PRINTL
PRINTL
PRINTL ■データエディタ機能
PRINT 　├
PRINTBUTTON "[2] CSVエディタを起動する", 2
PRINTL
PRINT 　├
PRINTBUTTON "[3] 服ERBエディタを起動する", 3
PRINTL
PRINT 　├
PRINTBUTTON "[4] 弱点コマンド設定ERBエディタを起動する", 4
PRINTL
PRINT 　└
PRINTBUTTON "[5] 一言コメント/解説エディタを起動する", 5
[IF_DEBUG]
PRINTL
PRINTL
PRINTBUTTON "[9] 【デバッグ用】画像ファイル名のチェック", 9
[ENDIF]

BINPUT
SELECTCASE RESULT
	CASE 0
		BEGIN FIRST
	CASE 1
		LOADGAME
	CASE 2
		CSVエディタフラグ = 1
		CALL 従業員素質指定雇用
		CSVエディタフラグ = 0
	CASE 3
		CALL 服ERB生成処理
	CASE 4
		CALL 弱点コマンド設定ERB生成処理
	CASE 5
		CALL 一言コメント解説エディタ
[IF_DEBUG]
	CASE 9
		CALL デバッグ用_画像ファイル名チェック()
	CASE 5656
		CALL バージョンアップ準備処理
[ENDIF]
ENDSELECT

RESTART


;-------------------------------------------------
;ニューゲーム
;-------------------------------------------------
@EVENTFIRST
#DIM 引き継いだキャラ人数
#DIM 引き継いだランダムキャラ, 10
#DIM 設定中キャラ番号
#DIM 弱点配列
#DIMS 大分割文字列, 10
#DIMS 小分割文字列, 1000
VARSET LOCAL
VARSET LOCALS
VARSET 引き継いだランダムキャラ, -1
VARSET 大分割文字列
VARSET 小分割文字列
TARGET = 0

CALL DEFAULT_OPTION

;モード選択（後々使うかもしれないから消さずにコメントアウト）
;PRINTL ★★モードを選択してください★★
;PRINTL [0]START
;
;$INPUT_LOOP
;INPUT
;IF RESULT == 0
;	FLAG:5 = 0
;ELSE
;	GOTO INPUT_LOOP
;ENDIF

;キャラの作成前に必要なデータベース
IF 引き継ぎ中フラグ
	LOADTEXT "dat/人物DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/人物DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "人物データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/履歴DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/履歴DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "履歴データベース", LOCALS:1, LOCALS:0
ELSE
	CALL 人物データベースセット
	CALL 履歴データベース作成
ENDIF

初期設定中 = 1
LOCAL:1 = 0
LOCAL:2 = 0
IF あなた引き継ぎ
	;あなた登録
	LOADCHARA "あなた"
	引き継いだキャラ人数 = LOCAL:1
	FOR LOCAL, 0, 100
		あなた特殊能力:LOCAL = あなた特殊能力引き継ぎ:LOCAL
	NEXT
ELSE
	;あなた登録
	ADDCHARA 0
	CALL ADD_PERSON_FROM_CHARA(CHARANUM - 1)
	CALL CUSTOM_CHARAMAKE(MASTER)
	SIF TALENT:MASTER:2 & 2
		MAXBASE:MASTER:5 = 1500
ENDIF

IF 引き継ぎ中フラグ
	FOR LOCAL, 0, 10
		IF 引き継ぎキャラ番号:LOCAL > 0
			;引き継ぎキャラは先に登録
			LOADCHARA TOSTR(LOCAL)
			LOCAL:1 += 1
			IF NO:(LOCAL:1) == 999
				引き継いだランダムキャラ:(LOCAL:2) = ランダムキャラ記録位置判別(LOCAL)
				LOCAL:2 += 1
			ENDIF
			VARSET 現在仕事:(LOCAL:1):0
		ENDIF
	NEXT
ENDIF

FOR LOCAL, 1, キャラクタ数上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	;ランダムキャラ排除
	SIF LOCAL == 999
		CONTINUE
	;登録済みのキャラ(=引き継ぎキャラ)はスキップ
	GETCHARA LOCAL
	SIF RESULT > -1
		CONTINUE
	ADDCHARA LOCAL
	設定中キャラ番号 = CHARANUM - 1
	CALL ADD_PERSON_FROM_CHARA(設定中キャラ番号)
	IF 引き継ぎ中フラグ && LOCAL != 0
		FOR LOCAL:1, 0, 500
			IF キャラ変数引き継ぎ:(LOCAL:1):キャラナンバー == NO:設定中キャラ番号
				EXP:設定中キャラ番号:肉体熟知 = キャラ変数引き継ぎ:(LOCAL:1):肉体熟知
				FOR 弱点配列, 0, 50
					弱点看破:設定中キャラ番号:弱点配列 = キャラ変数引き継ぎ:(LOCAL:1):(弱点配列 + 2)
					弱点コマンド枠:設定中キャラ番号:弱点配列 = %キャラ文字列引き継ぎ:(LOCAL:1):弱点配列%
				NEXT
				BREAK
			ENDIF
		NEXT
		CALL 弱点妥当性チェック(設定中キャラ番号)
	ENDIF
NEXT

;引き継ぎランダムキャラの追加
IF 引き継ぎ中フラグ
	FOR LOCAL, 0, 従業員初期値最大記録数
		SIF MATCH(引き継いだランダムキャラ, LOCAL) > 0
			CONTINUE
		IF 従業員初期値記録:LOCAL != ""
			VARSET 大分割文字列
			VARSET 小分割文字列
			ADDCHARA 999
			設定中キャラ番号 = CHARANUM - 1
			SPLIT 従業員初期値記録:LOCAL, "^^^", 大分割文字列
			;弱点
			FOR LOCAL:1, 0, 500
				IF キャラ変数引き継ぎ:(LOCAL:1):キャラナンバー == LOCAL * -1
					EXP:設定中キャラ番号:肉体熟知 = キャラ変数引き継ぎ:(LOCAL:1):肉体熟知
					FOR 弱点配列, 0, 50
						弱点看破:設定中キャラ番号:弱点配列 = キャラ変数引き継ぎ:(LOCAL:1):(弱点配列 + 2)
						弱点コマンド枠:設定中キャラ番号:弱点配列 = %キャラ文字列引き継ぎ:(LOCAL:1):弱点配列%
					NEXT
					BREAK
				ENDIF
			NEXT
			;名前
			VARSET 小分割文字列
			SPLIT 大分割文字列:1, "_", 小分割文字列
			NAME:設定中キャラ番号 = [一般人]%小分割文字列:1%
			CALLNAME:設定中キャラ番号 = %小分割文字列:1%
			;素質
			VARSET 小分割文字列
			SPLIT 大分割文字列:2, "_", 小分割文字列
			FOR LOCAL:1, 0, 900, 3
				SIF 小分割文字列:(LOCAL:1) == ""
					BREAK
				TALENT:設定中キャラ番号:(TOINT(小分割文字列:(LOCAL:1 + 1))) = TOINT(小分割文字列:(LOCAL:1 + 2))
			NEXT
			;フレーバー素質
			VARSET 小分割文字列
			SPLIT 大分割文字列:3, "_", 小分割文字列
			FOR LOCAL:1, 0, 30, 3
				SIF 小分割文字列:(LOCAL:1) == ""
					BREAK
				フレーバー素質:設定中キャラ番号:(TOINT(小分割文字列:(LOCAL:1 + 1))) = TOINT(小分割文字列:(LOCAL:1 + 2))
			NEXT
			;ABL
			VARSET 小分割文字列
			SPLIT 大分割文字列:4, "_", 小分割文字列
			FOR LOCAL:1, 0, 150, 3
				SIF 小分割文字列:(LOCAL:1) == ""
					BREAK
				ABL:設定中キャラ番号:(TOINT(小分割文字列:(LOCAL:1 + 1))) = TOINT(小分割文字列:(LOCAL:1 + 2))
			NEXT
			;ABL
			VARSET 小分割文字列
			SPLIT 大分割文字列:5, "_", 小分割文字列
			FOR LOCAL:1, 0, 300, 3
				SIF 小分割文字列:(LOCAL:1) == ""
					BREAK
				EXP:設定中キャラ番号:(TOINT(小分割文字列:(LOCAL:1 + 1))) = TOINT(小分割文字列:(LOCAL:1 + 2))
			NEXT
			;戦闘系
			VARSET 小分割文字列
			SPLIT 大分割文字列:6, "_", 小分割文字列
			基礎BATTLE_STATE:設定中キャラ番号:属性 = TOINT(小分割文字列:1)
			基礎BATTLE_STATE:設定中キャラ番号:得意武器 = TOINT(小分割文字列:3)
			CALL 初期ステータス設定_テンプレート適用(タイプ数値文字列変換(TOINT(小分割文字列:5)), 設定中キャラ番号)
			知識素質:設定中キャラ番号:性知識 = TOINT(小分割文字列:7)
			CALL ADD_PERSON_FROM_CHARA(設定中キャラ番号)
			CFLAG:設定中キャラ番号:招待不可フラグ = 0
		ENDIF
	NEXT
ENDIF

CALL コマンド存在チェック


スイートルーム部屋数 = 1
開発済み料理名フラグ文字列 += "_"

CALL ユニークキャラ親子関係セット
CALL イベントデータベースセット
CALL 戦闘用データベースセット
IF 引き継ぎ中フラグ
	CALL 人物データベースGC

	LOADTEXT "dat/素材DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/素材DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持素材データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/証DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/証DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持ジョブ証データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/指輪DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/指輪DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持指輪データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/耳飾りDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/耳飾りDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持耳飾りデータベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/アイテムDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/アイテムDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持アイテムデータベース", LOCALS:1, LOCALS:0

	所持制服文字列 = %制服引き継ぎ%
	所持エロ衣装文字列 = %エロ衣装引き継ぎ%
	MONEY = 所持ルピ引き継ぎ
	VARSET オート射精先オプション, -1
	ARRAYCOPY "オート射精設定引き継ぎ", "オート射精先オプション"
	ARRAYCOPY "オプション引き継ぎ", "OPTION変数"
	ARRAYCOPY "素質切り替え引き継ぎ", "素質切り替えOPTION記憶"
	ARRAYCOPY "画像サイズ切り替え引き継ぎ", "画像サイズ切り替えOPTION記憶"
	ARRAYCOPY "一日終了時オナニー引き継ぎ", "一日終了時オナニーOPTION"
ELSE

	CALL アイテムデータベースセット
	CALL 指輪データベースセット
	CALL 耳飾りデータベースセット
	CALL ジョブ証データベースセット
	CALL 素材アイテムデータベースセット

	CALL 制服登録処理("リゾート従業員制服")
	CALL 制服登録処理("バニー服")
	CALL 制服登録処理("メイド服")
	CALL 制服登録処理("チャイナドレス")
	CALL 制服登録処理("ミニチャイナ")

	CALL エロ衣装登録処理("逆バニー")
	CALL エロ衣装登録処理("マイクロビキニ")
	CALL エロ衣装登録処理("セクシーランジェリー")

	LOCAL:1 = ENUMFUNCENDSWITH("CLOTHES_初期所持関数_")
	IF LOCAL:1 > 0
		FOR LOCAL, 0, LOCAL:1
			CALLFORM %RESULTS:LOCAL%
		NEXT
	ENDIF
	
	MONEY = 5000
	OPTION変数:身体項目表示切り替え = 1p0 | 1p1 | 1p2 | 1p3 | 1p5
	VARSET オート射精先オプション, -1
ENDIF

CALL 食堂メニューデータベースセット
CALL 写真データベースセット
CALL 乗機データベースセット
CALL 所持兵器データベースセット
CALL 従業員業務用マップセット
CALL 自動探索用マップセット
CALL 体位モードデータベースセット
CALL 縁結びデータベースセット
CALL 関係性データベースセット
CALL 初体験データベースセット
CALL デートスコアデータベースセット
CALL 弱点の欠片データベースセット
ZPキャラ無料回数 = 3

CALL 素材アイテム登録処理

従業員部屋数 = 2
施設改造度:0:0 = 1
CALL RESET_GRAPH_0

PRINTW パラメータの初期化処理を行います
PRINTL 
SAVESTR:10 = /
LOCAL:1 = CHARANUM / 10
REDRAW 0
FOR LOCAL, 0, CHARANUM
	IF LOCAL > 0 && LOCAL <= 引き継いだキャラ人数
		CALL キャラクター初期化(LOCAL, 1)
	ELSEIF NO:LOCAL == 999
		CALL キャラクター初期化(LOCAL, 1)
	ELSE
		CALL キャラクター初期化(LOCAL)
	ENDIF
	IF LOCAL % LOCAL:1 == 0
		CLEARLINE 1
		PRINTFORML 進行度……{LOCAL / LOCAL:1 * 10}\%
		AWAIT
		;TWAIT 100, 1
	ENDIF
NEXT
CLEARLINE 1
PRINTL 進行度……100%

REDRAW 1



PRINTW パラメータの初期化処理が終了しました

;呼び出し設定
PRINTL
PRINTW 初期設定オプションを呼び出します。
PRINTL
IF SAVESTR:あなたスタイル != "グランくんスタイル"
	CALL OPTION_命のリンク初期切り替え
ENDIF
CALL OPTION_キャラ制限初期切り替え
PRINTL

;呼び出し設定
PRINTL
PRINTW キャラクターの招待制御オプションを呼び出します。
PRINTL
CALL OPTION_招待切り替え
PRINTL
PRINTW このオプションはゲーム開始後もOPTIONから呼び出すことが出来ます。

;素質設定
PRINTL
PRINTW 素質の切り替えオプションを呼び出します。
PRINTL
CALL OPTION_素質切り替え
PRINTL
PRINTW このオプションはゲーム開始後もOPTIONから呼び出すことが出来ます。

;フレーバー設定
PRINTL
PRINTW フレーバー素質の設定を呼び出します。
PRINTL
;先に口上側の設定を読み込んでおく
FOR COUNT, 1, CHARANUM
	TRYCALLFORM フレーバー素質初期設定変更_{COUNT}
NEXT
CALL フレーバー素質初期設定
PRINTL

IF SAVESTR:あなたスタイル == "グランくんスタイル"
	CALL グランくんスタイルセット(0)
	CALL シナリオ_OP_グランくんスタイル(0)
ELSEIF SAVESTR:あなたスタイル == "ジータちゃんスタイル"
	CALL グランくんスタイルセット(1)
	CALL シナリオ_OP_グランくんスタイル(1)
ELSEIF SAVESTR:あなたスタイル == "スタートダッシュモード"
	従業員部屋数 = 4
	PRINTW 初期追加する従業員を設定します。
	CALL 初期従業員選択(4)
	
	PRINTL メインシナリオ進行度を特定のところまでスキップできます
	PRINTL 
	PRINTL ※注意※
	PRINTL 後から変更することはできません
	PRINTBUTTON "[0] スキップしない", 0
	PRINTL 
	PRINTBUTTON "[1] 人食い鳥の島 クリアまでスキップ（ＥＸキャラ解放可能）", 1
	PRINTL 
	PRINTBUTTON "[2] 温泉解放までスキップ（マッサージ解放可能）", 2
	$INPUT_LOOP_Q
	BINPUT
	SELECTCASE RESULT
	CASE 0
	CASE 1 TO 2
		CALL QUICK_START(RESULT)
	CASEELSE
		GOTO INPUT_LOOP_Q
	ENDSELECT
	
	CALL シナリオ_OP
ELSE
	CALL シナリオ_OP
ENDIF

;起床時刻
CFLAG:MASTER:自室位置 = 20
CFLAG:MASTER:現在位置 = CFLAG:MASTER:自室位置
CFLAG:MASTER:現在マップ種別 = 0
CFLAG:MASTER:滞在期間 = 999
CFLAG:MASTER:起床予定時刻 = 420
CFLAG:MASTER:睡眠 = 1
初体験日:MASTER:初対面 = 999
依頼同時最大数 = 1
DAY = 1
OPTION変数:汎用喘ぎ使用 = 3
OPTION変数:汎用喘ぎ使用_男 = 0
CALL 最大レベル算出保存
VARSET 同時モード_選択キャラ, -1

ボタン化用カラーマトリクス:0:0 = 768,256,256,  0,  0
ボタン化用カラーマトリクス:1:0 = 256,768,256,  0,  0
ボタン化用カラーマトリクス:2:0 = 256,256,256,  0,  0
ボタン化用カラーマトリクス:3:0 =   0,  0,  0,256,  0
ボタン化用カラーマトリクス:4:0 =   0,  0,  0,  0,256

VARSET 通常キャラ招待
VARSET ＤＭキャラ招待

;初期値設定
滞在キャラ数 = 0
滞在キャラ上限 = 20
モブ観光客数 = 0
;あなたは初期状態で雑務に従事している
現在仕事:0:0 = 1

;編成画面初期表示
メンバーリスト表示欄:1 = Ｌｖ
メンバーリスト表示欄:2 = 属性
メンバーリスト表示欄:3 = 固有

初期設定中 = 0
IF 引き継ぎ中フラグ
	;使用する変数を掃除
	VARSET 周回回数累計_退避
	VARSET キャラ変数引き継ぎ
	VARSET キャラ文字列引き継ぎ
	制服引き継ぎ = 
	所持ルピ引き継ぎ = 0
	VARSET 引き継ぎキャラ番号
	VARSET あなた特殊能力引き継ぎ
	VARSET 従業員初期値記録_退避
	引き継ぎ中フラグ = 0
	あなた引き継ぎ = 0

	最大レベル保存_周回 = 最大レベル引き継ぎ
	;最大レベルに応じて経験値アイテム配布
	CALL 素材入手処理("星晶の欠片", 最大レベル保存_周回, 1)
ENDIF

;バージョンアップ時の処理を不要としてマーク
FOR LOCAL:1, GAMEBASE_ALLOWVERSION, GAMEBASE_VERSION + 1
	CALLFORM VERUP_{LOCAL:1}_FINISH()
NEXT

;変数リセット
CALL TURN_RESET
CALL 最大レベル算出保存

;ログボ処理
LOCAL = GETSECOND() / 3600
;日単位
LOCAL:1 = (LOCAL + 19) / 24

IF (REAL_TIME < LOCAL:1)
	REAL_TIME = LOCAL:1
	CALL ログインボーナス処理
ENDIF

BEGIN SHOP

@EVENTLOAD
CALL VERSION_UP




@キャラクター初期化(ARG, 引き継ぎキャラフラグ = 0)
#DIM 引き継ぎキャラフラグ
#DIM 得意武器記録
#DIMS CSTR切り分け文字列格納, 2
VARSET LOCALS
VARSET CSTR切り分け文字列格納


;全員睡眠状態
CFLAG:ARG:睡眠 = 1
CFLAG:ARG:滞在期間 = -1
CFLAG:ARG:現在位置 = 999
CFLAG:ARG:現在マップ種別 = 0
BASE:ARG:酩酊 = 0
MAXBASE:ARG:酩酊 = 1000
BASE:ARG:母乳 = 0
MAXBASE:ARG:母乳 = 10000
BASE:ARG:性欲 = 0
MAXBASE:ARG:性欲 = 1200
BASE:ARG:満足 = 0
MAXBASE:ARG:満足 = 1200
BASE:ARG:警戒 = 0
MAXBASE:ARG:警戒 = 1001
CALL CLOTHES_RESET(ARG)
CSTR:ARG:二人称 = %CALLNAME:PLAYER%
CALL 精力ゲージ_初期化(ARG)

;勃起、ムード、理性、怒り
BASE:ARG:勃起 = 0
BASE:ARG:ムード = 0
BASE:ARG:理性 = MAXBASE:ARG:理性
BASE:ARG:怒り = 0
BASE:ARG:勃起 = 0
CFLAG:ARG:勃起度 = 0
CFLAG:ARG:同行 = 0
CFLAG:ARG:デート = 0
CFLAG:ARG:酔いすぎ = 0
;発情期
; SIF 発情期判定(ARG)
; 	CFLAG:ARG:発情期フラグ = RAND:9 + 1

IF 引き継ぎキャラフラグ == 0
	VARSET LOCALS
	;招待時同行キャラ
	REPLACE CSTR:ARG:同行キャラ, " ", ""
	SPLIT RESULTS, "%", LOCALS
	LOCAL:2 = RESULT - 1
	FOR LOCAL, 0, LOCAL:2
		SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
		;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
		LOCAL:10 = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
		IF LOCAL:10 >= 0
			同行候補キャラ番号:ARG:LOCAL = LOCAL:10
			同行候補キャラ確率:ARG:LOCAL = TOINT(CSTR切り分け文字列格納:1)
		ENDIF
	NEXT

	;性別保存
	CFLAG:ARG:元性別保存 = TALENT:ARG:性別

	VARSET LOCALS
	;知識素質
	SUBSTRING CSTR:ARG:知識素質設定, 1, -1
	SPLIT RESULTS, " ", LOCALS
	LOCAL:2 = RESULT - 1
	FOR LOCAL, 0, LOCAL:2
		SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
		;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
		知識素質:ARG:(CSTR切り分け文字列格納:0) = TOINT(CSTR切り分け文字列格納:1)
	NEXT

	VARSET LOCALS
	;性癖素質
	SUBSTRING CSTR:ARG:性癖素質設定, 1, -1
	SPLIT RESULTS, " ", LOCALS
	LOCAL:2 = RESULT - 1
	FOR LOCAL, 0, LOCAL:2
		SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
		;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
		性癖素質:ARG:(CSTR切り分け文字列格納:0) = TOINT(CSTR切り分け文字列格納:1)
	NEXT

	;素質切り替え
	IF TALENT:ARG:陥没乳首 && 素質切り替えOPTION記憶:陥没乳首_なし
		TALENT:ARG:陥没乳首 = 0
	ENDIF
	IF TALENT:ARG:母乳体質 && 素質切り替えOPTION記憶:母乳体質_なし
		TALENT:ARG:母乳体質 = 0
	ENDIF
	IF TALENT:ARG:性別 == 3 && 素質切り替えOPTION記憶:ふたなり_女性
		TALENT:ARG:性別 = 1
	ENDIF

	IF EXP:ARG:キス経験
		CALL 初体験日登録処理(ARG, -1, "ファーストキス", -1, "不明", "唇", "", 1)
	ENDIF

	SIF ARG == MASTER
		RETURN 0

	VARSET LOCALS
	;戦闘ステータス
	SUBSTRING CSTR:ARG:戦闘基礎ステータス設定, 1, -1
	SPLIT RESULTS, " ", LOCALS
		;属性
		SPLIT LOCALS:0, "_", CSTR切り分け文字列格納
		基礎BATTLE_STATE:ARG:属性 = 属性文字列数値変換(CSTR切り分け文字列格納:1)
		;得意武器
		SPLIT LOCALS:1, "_", CSTR切り分け文字列格納
		VARSET RESULTS
		SPLIT CSTR切り分け文字列格納:1, "・", RESULTS
		得意武器記録 = 0
		FOR LOCAL, 0, RESULT
			SETBIT 得意武器記録, 得意武器文字列数値変換(RESULTS:LOCAL)
		NEXT
		基礎BATTLE_STATE:ARG:得意武器 = 得意武器記録
		;初期ステータス
		SPLIT LOCALS:2, "_", CSTR切り分け文字列格納
		CALL 初期ステータス設定_テンプレート適用(CSTR切り分け文字列格納:1, ARG)
		TRYCALLFORM 戦闘初期ステータス設定_{NO:ARG}(ARG)
	;表示奥義設定
	VARSET LOCALS
	IF CSTR:ARG:表示奥義設定 != ""
		SPLIT CSTR:ARG:表示奥義設定, "_", LOCALS
		表示奥義:ARG:0 '= LOCALS:0
		表示奥義:ARG:1 '= LOCALS:1
		表示奥義:ARG:2 '= LOCALS:2
	ENDIF
	VARSET LOCALS
	IF CSTR:ARG:陥落奥義設定 != ""
		SPLIT CSTR:ARG:陥落奥義設定, "_", LOCALS
		陥落奥義:ARG:0 '= LOCALS:0
		陥落奥義:ARG:1 '= LOCALS:1
		陥落奥義:ARG:2 '= LOCALS:2
	ENDIF

	CALL 弱点コマンド初期設定(ARG)
	TRYCALLFORM 口上側初期パラメータ変更_{NO:ARG}
	;この時点で身長・スリーサイズが入っている場合、それを初期値として登録
	SIF BASE:ARG:身長
		CFLAG:ARG:身長初期値 = BASE:ARG:身長
	SIF BASE:ARG:バスト
		CFLAG:ARG:バスト初期値 = BASE:ARG:バスト
	SIF BASE:ARG:ウエスト
		CFLAG:ARG:ウエスト初期値 = BASE:ARG:ウエスト
	SIF BASE:ARG:ヒップ
		CFLAG:ARG:ヒップ初期値 = BASE:ARG:ヒップ

	;正の年齢が入っている場合、それをもとに年齢層を計算
	SIF BASE:ARG:年齢 > 0
		TALENT:ARG:年齢層 = 年齢層算出処理(BASE:ARG:年齢)

	SIF 従業員追加中フラグ
		従業員追加中フラグ = 0

	;身長・体格決定
	IF !BASE:ARG:身長 || !TALENT:ARG:体格
		CALL INIT_BODYSIZE_HEIGHT(ARG)
	ENDIF
	;ウエスト・体型決定
	IF !BASE:ARG:ウエスト || !TALENT:ARG:体型
		CALL INIT_BODYSIZE_WAIST(ARG)
	ENDIF
	;バスト決定
	IF !BASE:ARG:バスト || !TALENT:ARG:バストサイズ
		CALL INIT_BODYSIZE_BUST(ARG)
	ENDIF
	;ヒップ決定
	IF !BASE:ARG:ヒップ || !TALENT:ARG:下半身
		CALL INIT_BODYSIZE_HIP(ARG)
	ENDIF


	; IF !BASE:ARG:身長 || (!BASE:ARG:バスト && TALENT:ARG:性別 != 2) || !BASE:ARG:ウエスト || !BASE:ARG:ヒップ
	; 	CALL INIT_BODYSIZE(ARG)
	; ELSE
	; 	IF BASE:ARG:身長
	; 		;身長があるなら体格の設定
	; 		CALL INIT_BODYTALENT(ARG)
	; 	ENDIF
	; ENDIF

	;画像フォルダで有効なものを探す
	CALL 画像フォルダ有効チェック(ARG)

ELSE
	SIF !BASE:ARG:身長
		CALL INIT_BODYSIZE(ARG)

	;同行キャラをCSVを見て入れ直す
	VARSET LOCAL
	VARSET LOCALS
	;まず引き継ぎキャラ同士の同行記録を見る
	IF CSTR:ARG:引き継ぎ用同行キャラ確率保存 != ""
		SPLIT CSTR:ARG:引き継ぎ用同行キャラ確率保存, "_", LOCALS
		FOR LOCAL, 0, 20
			SIF LOCALS:LOCAL == ""
				BREAK
			同行候補キャラ番号:ARG:(LOCAL:1) = FINDCHARA(NAME, LOCALS:LOCAL)
			同行候補キャラ確率:ARG:(LOCAL:1) = TOINT(LOCALS:(LOCAL + 1))
			LOCAL:1 += 1
			LOCAL += 1
		NEXT
	ENDIF
	;招待時同行キャラをCSVから見直す
	VARSET LOCALS
	CSTR:ARG:同行キャラ = %CSVCSTR(NO:ARG, GETNUM(CSTR, "同行キャラ"))%
	REPLACE CSTR:ARG:同行キャラ, " ", ""
	SPLIT RESULTS, "%", LOCALS
	FOR LOCAL, 0, 100
		SIF LOCALS:LOCAL == ""
			BREAK
		SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
		SIF FINDELEMENT(同行候補キャラ番号:ARG:0, FINDCHARA(NAME, CSTR切り分け文字列格納:0)) > -1
			CONTINUE
		;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
		同行候補キャラ番号:ARG:(LOCAL:1) = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
		同行候補キャラ確率:ARG:(LOCAL:1) = TOINT(CSTR切り分け文字列格納:1)
		LOCAL:1 += 1
	NEXT
ENDIF

CALL 体力変動素質適用処理(ARG)
BASE:ARG:体力 = MAXBASE:ARG:体力

;引き継ぎバグ対応のため関係性はリセットしてあるので、全員セットし直す
CALL INIT_RELATION(ARG)

;性癖の設定
IF TALENT:ARG:性愛傾向 == 3
	性癖素質:ARG:同性愛 = 2
ENDIF

@弱点コマンド初期設定(ARG)
#DIMS 弱点候補_セクハラ, 200
#DIMS 弱点候補_うふふ, 200
#DIMS 敏感弱点候補_セクハラ, 200
#DIMS 敏感弱点候補_うふふ, 200
#DIM 敏感セクハラ候補数
#DIM 敏感うふふ候補数
#DIM セクハラ候補数
#DIM うふふ候補数
#DIM チェック完了

;弱点が全て埋まっている場合（=周回中）はここで処理終了
SIF MATCH(弱点コマンド枠:ARG:0, "", 0, 5) == 0 && MATCH(弱点コマンド枠:ARG:0, "-1", 0, 5) == 0
	RETURN

;弱点初期化
VARSET 弱点コマンド枠:ARG:0

;まず口上側で弱点の設定があるか見に行く
TRYCCALLFORM 弱点コマンド変更_{NO:ARG}
	;弱点が全て埋まった場合はここで処理終了
	SIF MATCH(弱点コマンド枠:ARG:0, "", 0, 5) == 0 && MATCH(弱点コマンド枠:ARG:0, "-1", 0, 5) == 0
		RETURN
CATCH
ENDCATCH

セクハラ候補数 = 0
うふふ候補数 = 0
敏感セクハラ候補数 = 0
敏感うふふ候補数 = 0


;敏感素質に対応した弱点候補をリスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合+敏感素質に対応してる場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}") && 敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}")
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
				CASE 1
					敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}
					敏感セクハラ候補数 += 1
				CASE 2
					敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}
					敏感うふふ候補数 += 1
			ENDSELECT
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるOR敏感素質に対応してないなら飛ばす
			SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}") || !敏感素質対応判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CONTINUE
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CASE 1
					敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{LOCAL:1}
					敏感セクハラ候補数 += 1
				CASE 2
					敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{LOCAL:1}
					敏感うふふ候補数 += 1
			ENDSELECT
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるOR敏感素質に対応してないなら飛ばす
		SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}") || !敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CASE 1
				敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{NO:ARG}
				敏感セクハラ候補数 += 1
			CASE 2
				敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{NO:ARG}
				敏感うふふ候補数 += 1
		ENDSELECT
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:5, 0, 1
				FOR LOCAL:2, 0, 20
					SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
						CONTINUE
					LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
					;口上側と被ってるなら飛ばす
					SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}") || !敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
						CONTINUE
					SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
						CASE 1
							敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{LOCAL:3}
							敏感セクハラ候補数 += 1
						CASE 2
							敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{LOCAL:3}
							敏感うふふ候補数 += 1
					ENDSELECT
				NEXT
			NEXT
		NEXT
	ENDIF
NEXT


;敏感対応の候補からセクハラとうふふに一つずつ設定
;ただし、以下の条件を守る
;・弱点コマンド枠は最低一つ空ける
;・うふふとセクハラのどちらか一つしか入らないならうふふ優先

;うふふ設定
LOCAL = 0
WHILE 1
	SIF LOCAL > 3 || !敏感うふふ候補数
		BREAK
	IF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		LOCAL ++
		CONTINUE
	ENDIF
	LOCAL:1 = RAND:敏感うふふ候補数
	弱点コマンド枠:ARG:LOCAL = %敏感弱点候補_うふふ:(LOCAL:1)%
	BREAK
WEND

;セクハラ設定
LOCAL = 0
WHILE 1
	SIF LOCAL > 3 || !敏感セクハラ候補数
		BREAK
	IF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		LOCAL ++
		CONTINUE
	ENDIF
	LOCAL:1 = RAND:敏感セクハラ候補数
	弱点コマンド枠:ARG:LOCAL = %敏感弱点候補_セクハラ:(LOCAL:1)%
	BREAK
WEND

;弱点候補リスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}")
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
				CASE 1
					弱点候補_セクハラ:セクハラ候補数 = {LOCAL}
					セクハラ候補数 += 1
				CASE 2
					弱点候補_うふふ:うふふ候補数 = {LOCAL}
					うふふ候補数 += 1
			ENDSELECT
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるなら飛ばす
			SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}")
				CONTINUE
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CASE 1
					弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{LOCAL:1}
					セクハラ候補数 += 1
				CASE 2
					弱点候補_うふふ:うふふ候補数 = {LOCAL}_{LOCAL:1}
					うふふ候補数 += 1
			ENDSELECT
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるなら飛ばす
		SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}")
			CONTINUE
		SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CASE 1
				弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{NO:ARG}
				セクハラ候補数 += 1
			CASE 2
				弱点候補_うふふ:うふふ候補数 = {LOCAL}_{NO:ARG}
				うふふ候補数 += 1
		ENDSELECT
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
					CONTINUE
				LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
				;口上側と被ってるなら飛ばす
				SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					CONTINUE
				SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					CASE 1
						弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
						セクハラ候補数 += 1
					CASE 2
						弱点候補_うふふ:うふふ候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
						うふふ候補数 += 1
				ENDSELECT
			NEXT
		NEXT
	ENDIF
NEXT

;空いている枠にセクハラ系から２つ、うふふ系から２つ、完全ランダムで１つ設定する

FOR LOCAL, 0, 5
	SIF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		CONTINUE
	SELECTCASE LOCAL
		CASE 0 TO 1
			LOCAL:1 = RAND:セクハラ候補数
			弱点コマンド枠:ARG:LOCAL = %弱点候補_セクハラ:(LOCAL:1)%
			FOR LOCAL:2, LOCAL:1, セクハラ候補数
				弱点候補_セクハラ:(LOCAL:2) = %弱点候補_セクハラ:(LOCAL:2 + 1)%
			NEXT
			セクハラ候補数 -= 1
		CASE 2 TO 3
			LOCAL:1 = RAND:うふふ候補数
			弱点コマンド枠:ARG:LOCAL = %弱点候補_うふふ:(LOCAL:1)%
			FOR LOCAL:2, LOCAL:1, うふふ候補数
				弱点候補_うふふ:(LOCAL:2) = %弱点候補_うふふ:(LOCAL:2 + 1)%
			NEXT
			うふふ候補数 -= 1
		CASEELSE
			LOCAL:1 = RAND:(セクハラ候補数 + うふふ候補数)
			IF LOCAL:1 < セクハラ候補数
				弱点コマンド枠:ARG:LOCAL = %弱点候補_セクハラ:(LOCAL:1)%
			ELSE
				弱点コマンド枠:ARG:LOCAL = %弱点候補_うふふ:(LOCAL:1 - セクハラ候補数)%
			ENDIF
	ENDSELECT
NEXT

@弱点コマンド候補判定(ARG, ARGS)
;返り値
;0=候補外
;1=弱点候補_セクハラ
;2=弱点候補_うふふ
#FUNCTION
弱点コマンドカテゴリ = 
TFLAG:コマンドタイプ受渡 = 999
CALLFORMF %ARGS%
SIF 弱点コマンドカテゴリ == ""
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "角") >= 0 && TALENT:ARG:角あり == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "尻尾") >= 0 && TALENT:ARG:尻尾あり == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "ちんこ") >= 0 && GETBIT(TALENT:ARG:性別, 1) == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "女") >= 0 && GETBIT(TALENT:ARG:性別, 0) == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "V") >= 0 && GETBIT(TALENT:ARG:性別, 0) == 0
	RETURNF 0
IF GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), ARG) == "異性愛"
	;性癖名が異性愛＝[女性愛]持ち女性or[男性愛]持ち男性
	SIF STRFIND(弱点コマンドカテゴリ, "異性") >= 0 && 性癖素質:ARG:同性愛 <= 0
		RETURNF 0
ELSE
	SIF STRFIND(弱点コマンドカテゴリ, "同性") >= 0 && 性癖素質:ARG:同性愛 <= 0
		RETURNF 0
ENDIF
SELECTCASE TFLAG:コマンドタイプ受渡
	CASE 1 TO 3
		RETURNF 1
	CASE 100, 101
		;複数コマンド
		SIF STRFIND(TSTR:複合コマンドタイプ受け渡し, "セクハラ") >= 0
			RETURNF 1
		SIF STRFIND(TSTR:複合コマンドタイプ受け渡し, "うふふ") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し, "Ｖ系") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し,"Ａ系") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し, "道具系") >= 0
			RETURNF 2
	CASEELSE
		;うふふ扱い
		RETURNF 2
ENDSELECT

@弱点コマンドランダム選出(ARG)
#DIMS 弱点候補, 1000
#DIM 弱点候補数

VARSET 弱点候補
弱点候補数 = 0
;弱点候補リスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
			弱点候補:弱点候補数 = {LOCAL}
			弱点候補数 += 1
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるなら飛ばす
			IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}") && 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				弱点候補:弱点候補数 = {LOCAL}_{LOCAL:1}
				弱点候補数 += 1
			ENDIF
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるなら飛ばす
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			弱点候補:弱点候補数 = {LOCAL}_{NO:ARG}
			弱点候補数 += 1
		ENDIF
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
					CONTINUE
				LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
				;口上側と被ってるなら飛ばす
				IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					弱点候補:弱点候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
					弱点候補数 += 1
				ENDIF
			NEXT
		NEXT
	ENDIF
NEXT


SIF 弱点候補数 > 0
	RESULTS = %弱点候補:(RAND:弱点候補数)%


@敏感素質対応判定(ARG, ARGS)
#FUNCTION
;敏感素質を検出
弱点コマンドカテゴリ = 
CALLFORMF %ARGS%
;弱点コマンドカテゴリに対応する敏感素質があれば1を返す
SIF 弱点コマンドカテゴリ == ""
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "C") >= 0 && TALENT:ARG:Ｃ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "V") >= 0 && TALENT:ARG:Ｖ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "A") >= 0 && TALENT:ARG:Ａ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "B") >= 0 && TALENT:ARG:Ｂ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "S") >= 0 && TALENT:ARG:Ｓ感度 > 0
	RETURNF 1
;ここまで来た時点で該当するものがないのでオワリ
RETURNF 0

@コマンド存在チェック
#DIM コマンド総数
#DIM ループ用

コマンド総数 = ENUMFUNCBEGINSWITH("COMTYPE_")

;通常コマンド
FOR LOCAL, 0, 1000
	IF MATCH(RESULTS, @"COMTYPE_{LOCAL}")
		SETBIT 通常コマンド存在フラグ:LOCAL, 0
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF MATCH(RESULTS, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				SETBIT 通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1
		NEXT
	ENDIF
NEXT
;場所固有コマンド
FOR LOCAL, 0, 5
	FOR LOCAL:3, 0, 1
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF MATCH(RESULTS, @"COMTYPE_{LOCAL + 380}_{LOCAL:3}_{LOCAL:1 * 100 + LOCAL:2 + 1}")
					SETBIT 場所固有コマンド存在フラグ:LOCAL:(LOCAL:3):(LOCAL:1), LOCAL:2
			NEXT
		NEXT
	NEXT
NEXT

@年齢層算出処理(年齢)
#FUNCTION
#DIM 年齢
#DIM ループ用

FOR ループ用, 年齢層_幼い, 年齢層_人外 + 1
	SIF 年齢 < 年齢ボーダー:ループ用
		RETURNF ループ用
NEXT
RETURNF 年齢層_不詳

@体力変動素質適用処理(キャラ番号)
#DIM キャラ番号
#DIM 体力計算値
#DIM 性欲計算値

体力計算値 = CSVBASE(NO:キャラ番号, 0)

;素質によって体力を加算減算する
SIF GETBIT(TALENT:キャラ番号:性別, 0) && TALENT:キャラ番号:処女 == 0
	体力計算値 += 200
SIF GETBIT(TALENT:キャラ番号:性別, 1) && TALENT:キャラ番号:非童貞 > 0
	体力計算値 += 200
体力計算値 += TALENT:キャラ番号:度胸 * 50
SIF TALENT:キャラ番号:精神的余裕 > 0
	体力計算値 += 100
体力計算値 += TALENT:キャラ番号:性的興味 * 100
体力計算値 += TALENT:キャラ番号:性欲 * 100
体力計算値 += TALENT:キャラ番号:経験豊富 * 200
体力計算値 += TALENT:キャラ番号:自慰しやすい * 50
体力計算値 += TALENT:キャラ番号:快感応答 * 50
体力計算値 += TALENT:キャラ番号:絶倫 * 400
体力計算値 += TALENT:キャラ番号:子持ち * 400
IF TALENT:キャラ番号:恋人持ち > 0
	体力計算値 += 300
ELSEIF TALENT:キャラ番号:セフレあり
	体力計算値 += 700
ENDIF
SIF 知識素質:キャラ番号:性知識 > 0
	体力計算値 += 知識素質:キャラ番号:性知識 * 200

SIF CFLAG:キャラ番号:元気になる薬フラグ > 0
	体力計算値 += 1000

SELECTCASE あなた特殊能力:体力成長の加護
	CASE 1
		体力計算値 += EXP:キャラ番号:ダウン経験 * 10
	CASE 2
		体力計算値 += EXP:キャラ番号:ダウン経験 * 15
	CASE 3
		体力計算値 += EXP:キャラ番号:ダウン経験 * 20
	CASE 4
		体力計算値 += EXP:キャラ番号:ダウン経験 * 30
ENDSELECT

;初期EXPによる変動は行わない（数値の設定バランスが記述者によってまちまちなため）

;体力成長分適用
体力計算値 += CFLAG:キャラ番号:体力成長値

MAXBASE:キャラ番号:体力 = MIN(体力計算値, 9999)
SIF 妊娠時疲労(キャラ番号)
	MAXBASE:キャラ番号:体力 -= 妊娠時疲労(キャラ番号,1)
;性欲は体力と同値
;発情期は性欲ゲージにのみ掛かる
性欲計算値 = 体力計算値
IF CFLAG:キャラ番号:発情期フラグ < 0
	性欲計算値 = MIN(体力計算値 * 3 / 2, 9999)
ENDIF
MAXBASE:キャラ番号:性欲 = MIN(性欲計算値, 9999)

RESULT = 体力計算値
TRYCALLFORM 固有体力補正_{NO:キャラ番号}(キャラ番号, 体力計算値)
MAXBASE:キャラ番号:体力 = MIN(RESULT, 9999)

RESULT = 性欲計算値
TRYCALLFORM 固有性欲補正_{NO:キャラ番号}(キャラ番号, 性欲計算値)
MAXBASE:キャラ番号:性欲 = MIN(RESULT, 9999)

BASE:キャラ番号:体力 = MIN(MAXBASE:キャラ番号:体力, BASE:キャラ番号:体力)
BASE:キャラ番号:性欲 = MIN(MAXBASE:キャラ番号:性欲, BASE:キャラ番号:性欲)
MAXBASE:キャラ番号:満足 = MAXBASE:キャラ番号:性欲
BASE:キャラ番号:満足 = 0


@イベントデータベースセット

DT_CREATE "各イベント用変数配列"
DT_COLUMN_ADD "各イベント用変数配列", "イベント名"
DT_COLUMN_ADD "各イベント用変数配列", "イベントカテゴリ"
DT_COLUMN_ADD "各イベント用変数配列", "イベント発生フラグ", 3
DT_COLUMN_ADD "各イベント用変数配列", "イベント完了フラグ", 3
DT_COLUMN_ADD "各イベント用変数配列", "フリー変数１", 3
DT_COLUMN_ADD "各イベント用変数配列", "フリー変数２", 3
DT_COLUMN_ADD "各イベント用変数配列", "フリー変数３", 3
DT_COLUMN_ADD "各イベント用変数配列", "フリー文字列１"
DT_COLUMN_ADD "各イベント用変数配列", "フリー文字列２"
DT_COLUMN_ADD "各イベント用変数配列", "フリー文字列３"
DT_COLUMN_ADD "各イベント用変数配列", "イベント再発生CT", 3
DT_COLUMN_ADD "各イベント用変数配列", "イベント再発生確率", 3
DT_COLUMN_ADD "各イベント用変数配列", "イベント消滅CT", 3
DT_COLUMN_ADD "各イベント用変数配列", "イベント消滅確率", 3
DT_COLUMN_ADD "各イベント用変数配列", "汎用イベ用_キャラNO保存", 3
CALL イベント追加チェック


@イベント追加チェック
#DIM イベント数
#DIMS イベント名保存
VARSET LOCAL
VARSET LOCALS

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	LOCALS:LOCAL = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_メインシナリオ_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 28)%
	IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "メインシナリオ"
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_サブイベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 26)%
	IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "サブイベント"
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_キャライベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 28)%
	IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "キャライベント"
	ENDIF
NEXT

;汎用イベントを突っ込む
FOR LOCAL, 0, CHARANUM
	IF GETBIT(TALENT:LOCAL:陥落不可, 0) && !CFLAG:LOCAL:恋慕不可_汎用解除禁止
		イベント名保存 = %ESCAPE(@"【汎用】%CALLNAME:LOCAL%[恋慕不可]解除")%
		IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
			IF NO:LOCAL == 999
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%CALLNAME:LOCAL%[恋慕不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", LOCAL + 90000
			ELSE
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%CALLNAME:LOCAL%[恋慕不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", NO:LOCAL
			ENDIF
		ENDIF
	ENDIF
	IF GETBIT(TALENT:LOCAL:陥落不可, 1) && !CFLAG:LOCAL:セフレ不可_汎用解除禁止
		イベント名保存 = %ESCAPE(@"【汎用】%CALLNAME:LOCAL%[セフレ不可]解除")%
		IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
			IF NO:LOCAL == 999
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%CALLNAME:LOCAL%[セフレ不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", LOCAL + 90000
			ELSE
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%CALLNAME:LOCAL%[セフレ不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", NO:LOCAL
			ENDIF
		ENDIF
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_突発イベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 26)%
	IF FINDELEMENT(LOCALS, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "突発イベント"
	ENDIF
NEXT

@イベント削除チェック
#DIM イベント数
#DIM 対象キャラ
#DIMS イベント名保存
VARSET LOCAL

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	イベント名保存 = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	イベント数 = ENUMFUNCBEGINSWITH(@"イベント関数_メインシナリオ_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_サブイベント_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_キャライベント_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_突発イベント_%イベント名保存%")
	;汎用イベを見る
	IF STRCOUNT(イベント名保存, "【汎用】")
		対象キャラ = DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存")
		IF 対象キャラ > 90000
			対象キャラ = 対象キャラ - 90000
		ELSE
			対象キャラ = GETCHARA(対象キャラ)
		ENDIF
		IF STRCOUNT(イベント名保存, "恋慕不可]解除") && !CFLAG:対象キャラ:恋慕不可_汎用解除禁止
			イベント数 = 1
		ELSEIF STRCOUNT(イベント名保存, "セフレ不可]解除") && !CFLAG:対象キャラ:セフレ不可_汎用解除禁止
			イベント数 = 1
		ENDIF
	ENDIF
	IF イベント数 < 1
		DT_ROW_REMOVE "各イベント用変数配列", DT_CELL_GET("各イベント用変数配列", LOCAL, "id")
	ENDIF
NEXT



@アイテムデータベースセット
DT_CREATE "所持アイテムデータベース"
DT_ROW_ADD "所持アイテムデータベース"
DT_ROW_ADD "所持アイテムデータベース"

@指輪データベースセット
DT_CREATE "所持指輪データベース"
DT_COLUMN_ADD "所持指輪データベース", "指輪名"
DT_COLUMN_ADD "所持指輪データベース", "基礎ステータス補正"
DT_COLUMN_ADD "所持指輪データベース", "特殊ステータス補正"
DT_COLUMN_ADD "所持指輪データベース", "オプションステータス補正"
DT_COLUMN_ADD "所持指輪データベース", "オプション特殊ステータス補正"
DT_COLUMN_ADD "所持指輪データベース", "装備キャラ番号", 3
;DT_COLUMN_ADD "所持指輪データベース", "装備者キャラ番号", 1
;0番にはダミーデータ
LOCAL = DT_ROW_ADD("所持指輪データベース")

@耳飾りデータベースセット
DT_CREATE "所持耳飾りデータベース"
DT_COLUMN_ADD "所持耳飾りデータベース", "耳飾り名"
DT_COLUMN_ADD "所持耳飾りデータベース", "変更属性"
DT_COLUMN_ADD "所持耳飾りデータベース", "奥義補正"
DT_COLUMN_ADD "所持耳飾りデータベース", "奥義名補正"
DT_COLUMN_ADD "所持耳飾りデータベース", "装備キャラ番号", 3
;DT_COLUMN_ADD "所持耳飾りデータベース", "装備者キャラ番号", 1
;0番にはダミーデータ
LOCAL = DT_ROW_ADD("所持耳飾りデータベース")

@ジョブ証データベースセット
DT_CREATE "所持ジョブ証データベース"
DT_COLUMN_ADD "所持ジョブ証データベース", "ジョブ証名"
DT_COLUMN_ADD "所持ジョブ証データベース", "改造履歴"
DT_COLUMN_ADD "所持ジョブ証データベース", "ジョブ証種別"
;0番にはダミーデータ
LOCAL = DT_ROW_ADD("所持ジョブ証データベース")

@乗機データベースセット
DT_CREATE "所持乗機データベース"
DT_COLUMN_ADD "所持乗機データベース", "乗機名"
DT_COLUMN_ADD "所持乗機データベース", "乗機攻撃力", 3
DT_COLUMN_ADD "所持乗機データベース", "乗機攻撃力_強化後", 3
DT_COLUMN_ADD "所持乗機データベース", "乗機命中率", 3
DT_COLUMN_ADD "所持乗機データベース", "乗機命中率_強化後", 3
DT_COLUMN_ADD "所持乗機データベース", "乗機属性", 1
DT_COLUMN_ADD "所持乗機データベース", "乗機属性_強化後", 1
DT_COLUMN_ADD "所持乗機データベース", "武装0"
DT_COLUMN_ADD "所持乗機データベース", "武装1"
DT_COLUMN_ADD "所持乗機データベース", "武装2"
DT_COLUMN_ADD "所持乗機データベース", "武装3"
DT_COLUMN_ADD "所持乗機データベース", "武装4"
DT_COLUMN_ADD "所持乗機データベース", "武装5"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装0"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装1"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装2"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装3"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装4"
DT_COLUMN_ADD "所持乗機データベース", "強化後武装5"
;あなたが使うだけだからこれいらないと思うんだけど将来的に使うパターンに備えて列だけ置いとく
DT_COLUMN_ADD "所持乗機データベース", "装備キャラ番号", 3
;このフラグが立ってる時は持ってない。設計可能
DT_COLUMN_ADD "所持乗機データベース", "設計状態フラグ", 1
;何人サブパイロットが乗れるか？
DT_COLUMN_ADD "所持乗機データベース", "サブ搭乗数", 1
DT_COLUMN_ADD "所持乗機データベース", "サブ搭乗数_強化後", 1

@所持兵器データベースセット
DT_CREATE "所持兵器データベース"
DT_COLUMN_ADD "所持兵器データベース", "兵器名"
DT_COLUMN_ADD "所持兵器データベース", "攻撃力補正", 3
DT_COLUMN_ADD "所持兵器データベース", "攻撃力補正_強化後", 3
DT_COLUMN_ADD "所持兵器データベース", "命中率補正", 3
DT_COLUMN_ADD "所持兵器データベース", "命中率補正_強化後", 3
DT_COLUMN_ADD "所持兵器データベース", "攻撃力変動値", 3
DT_COLUMN_ADD "所持兵器データベース", "攻撃力変動値_強化後", 3
DT_COLUMN_ADD "所持兵器データベース", "命中率変動値", 3
DT_COLUMN_ADD "所持兵器データベース", "命中率変動値_強化後", 3
DT_COLUMN_ADD "所持兵器データベース", "残弾種別", 1
DT_COLUMN_ADD "所持兵器データベース", "残弾数", 1
DT_COLUMN_ADD "所持兵器データベース", "残弾数_強化後", 1
DT_COLUMN_ADD "所持兵器データベース", "兵器カテゴリ"
DT_COLUMN_ADD "所持兵器データベース", "兵器距離適性"
DT_COLUMN_ADD "所持兵器データベース", "装備乗機id", 4
;このフラグが立ってる時は持ってない。設計可能
DT_COLUMN_ADD "所持兵器データベース", "設計状態フラグ", 1

@戦闘用データベースセット
; FOR LOCAL, 0, 20
; 	SIF INRANGE(LOCAL, 4, 9)
; 		CONTINUE
; 	SIF DT_EXIST(@"バフデータベース_{LOCAL}")
; 		DT_RELEASE @"バフデータベース_{LOCAL}"
; 	DT_CREATE @"バフデータベース_{LOCAL}"
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "バフ枠"
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "バフ対象能力"
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "バフ効果量_割合", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "バフ効果量_固定値", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "バフ名"
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "持続ターン", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "持続回数", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "持続行動回数", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "持続被ダメ回数", 3
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "消去不可フラグ", 1
; 	DT_COLUMN_ADD @"バフデータベース_{LOCAL}", "特殊表示オプション", 1
; 	SIF DT_EXIST(@"デバフデータベース_{LOCAL}")
; 		DT_RELEASE @"デバフデータベース_{LOCAL}"
; 	DT_CREATE @"デバフデータベース_{LOCAL}"
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "デバフ枠"
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "デバフ対象能力"
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "デバフ効果量_割合", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "デバフ効果量_固定値", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "デバフ名"
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "持続ターン", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "持続回数", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "持続行動回数", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "持続被ダメ回数", 3
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "消去不可フラグ", 1
; 	DT_COLUMN_ADD @"デバフデータベース_{LOCAL}", "特殊表示オプション", 1
; NEXT

SIF DT_EXIST("戦闘効果データベース")
	DT_RELEASE "戦闘効果データベース"
DT_CREATE "戦闘効果データベース"
DT_COLUMN_ADD "戦闘効果データベース", "効果名"
DT_COLUMN_ADD "戦闘効果データベース", "バフ・デバフフラグ"
DT_COLUMN_ADD "戦闘効果データベース", "適用範囲"
DT_COLUMN_ADD "戦闘効果データベース", "効果枠"
DT_COLUMN_ADD "戦闘効果データベース", "対象能力"
DT_COLUMN_ADD "戦闘効果データベース", "効果量_割合", 3
DT_COLUMN_ADD "戦闘効果データベース", "効果量_固定値", 3
DT_COLUMN_ADD "戦闘効果データベース", "持続ターン", 3
DT_COLUMN_ADD "戦闘効果データベース", "持続回数", 3
DT_COLUMN_ADD "戦闘効果データベース", "持続行動回数", 3
DT_COLUMN_ADD "戦闘効果データベース", "持続被ダメ回数", 3
DT_COLUMN_ADD "戦闘効果データベース", "消去不可フラグ", 1
DT_COLUMN_ADD "戦闘効果データベース", "特殊表示オプション", 1
DT_COLUMN_ADD "戦闘効果データベース", "付随効果枠"
DT_COLUMN_ADD "戦闘効果データベース", "付随効果枠_対象隊列"
DT_COLUMN_ADD "戦闘効果データベース", "付随効果量_割合", 3
DT_COLUMN_ADD "戦闘効果データベース", "付随効果量_固定値", 3

@素材アイテムデータベースセット
DT_CREATE "所持素材データベース"
DT_COLUMN_ADD "所持素材データベース", "素材アイテム名"
DT_COLUMN_ADD "所持素材データベース", "所持素材数", 3
DT_COLUMN_ADD "所持素材データベース", "累計入手素材数", 3
DT_COLUMN_ADD "所持素材データベース", "素材カテゴリ"
DT_COLUMN_ADD "所持素材データベース", "素材ランク", 1
DT_COLUMN_ADD "所持素材データベース", "素材属性", 1
DT_COLUMN_ADD "所持素材データベース", "素材値相場", 3
DT_COLUMN_ADD "所持素材データベース", "プレゼントカテゴリ"
DT_COLUMN_ADD "所持素材データベース", "食材分類"

@食堂メニューデータベースセット
DT_CREATE "料理メニューデータベース"
DT_COLUMN_ADD "料理メニューデータベース", "メニュー名"
DT_COLUMN_ADD "料理メニューデータベース", "料理ランク", 1
DT_COLUMN_ADD "料理メニューデータベース", "料理属性", 1
DT_COLUMN_ADD "料理メニューデータベース", "ルピ収入補正", 3
DT_COLUMN_ADD "料理メニューデータベース", "ZP収入補正", 3
DT_COLUMN_ADD "料理メニューデータベース", "メイン食材ID", 4
DT_COLUMN_ADD "料理メニューデータベース", "追加食材１ID", 4
DT_COLUMN_ADD "料理メニューデータベース", "追加食材２ID", 4
DT_COLUMN_ADD "料理メニューデータベース", "追加食材３ID", 4
DT_COLUMN_ADD "料理メニューデータベース", "調理器具ID", 1
DT_COLUMN_ADD "料理メニューデータベース", "特殊料理フラグ", 1

@写真データベースセット
DT_CREATE "写真データベース"
DT_COLUMN_ADD "写真データベース", "写真タイトル"
DT_COLUMN_ADD "写真データベース", "撮影日時"
DT_COLUMN_ADD "写真データベース", "撮影マップ種別", 3
DT_COLUMN_ADD "写真データベース", "撮影場所", 3
DT_COLUMN_ADD "写真データベース", "撮影場所名前"
DT_COLUMN_ADD "写真データベース", "撮影対象NAME"
DT_COLUMN_ADD "写真データベース", "撮影対象NO", 3
DT_COLUMN_ADD "写真データベース", "撮影シチュカテゴリ", 1
DT_COLUMN_ADD "写真データベース", "撮影対象画像リソース"
DT_COLUMN_ADD "写真データベース", "撮影時追加文字列１"
DT_COLUMN_ADD "写真データベース", "撮影時追加文字列２"
DT_COLUMN_ADD "写真データベース", "撮影時追加文字列３"
DT_COLUMN_ADD "写真データベース", "写真添付口上"
DT_COLUMN_ADD "写真データベース", "写真添付メモ"

@従業員業務用マップセット
SIF MAP_EXIST("常駐業務名")
	MAP_RELEASE "常駐業務名"
MAP_CREATE "常駐業務名"
MAP_SET "常駐業務名", "ロビー", "ロビー受付"
MAP_SET "常駐業務名", "バーエリア", "バーテンダー"
MAP_SET "常駐業務名", "図書室", "司書"
MAP_SET "常駐業務名", "プールサイド", "プール監視員"
MAP_SET "常駐業務名", "プール", "プール監視員"
MAP_SET "常駐業務名", "道具屋", "道具屋"
MAP_SET "常駐業務名", "土産物屋", "土産物屋"
MAP_SET "常駐業務名", "食事処", "食事処"
MAP_SET "常駐業務名", "装飾品店", "装飾品店"

@編成画面情報用マップセット
SIF MAP_EXIST("編成画面情報")
	MAP_RELEASE "編成画面情報"
MAP_CREATE "編成画面情報"
MAP_SET "編成画面情報", "ロビー", "ロビー受付"
MAP_SET "編成画面情報", "バーエリア", "バーテンダー"
MAP_SET "編成画面情報", "図書室", "司書"
MAP_SET "編成画面情報", "プールサイド", "プール監視員"
MAP_SET "編成画面情報", "プール", "プール監視員"
MAP_SET "編成画面情報", "道具屋", "道具屋"
MAP_SET "編成画面情報", "土産物屋", "土産物屋"
MAP_SET "編成画面情報", "食事処", "食事処"
MAP_SET "編成画面情報", "装飾品店", "装飾品店"

@自動探索用マップセット
MAP_CREATE "自動探索対象ダンジョン"

@体位モードデータベースセット
SIF DT_EXIST("体位モードデータベース")
	DT_RELEASE "体位モードデータベース"
DT_CREATE "体位モードデータベース"
DT_COLUMN_ADD "体位モードデータベース", "モード名"
DT_COLUMN_ADD "体位モードデータベース", "実行キャラ", 3
DT_COLUMN_ADD "体位モードデータベース", "対象キャラ", 3
DT_COLUMN_ADD "体位モードデータベース", "同時実行id", 4, 1
DT_COLUMN_ADD "体位モードデータベース", "弱点フラグ", 1
DT_COLUMN_ADD "体位モードデータベース", "フリー文字列"
DT_COLUMN_ADD "体位モードデータベース", "フリー変数", 3

@縁結びデータベースセット
DT_CREATE "縁結びデータベース"
DT_COLUMN_ADD "縁結びデータベース", "対象キャラ１", 4
DT_COLUMN_ADD "縁結びデータベース", "対象キャラ２", 4
DT_COLUMN_ADD "縁結びデータベース", "進展度", 3
DT_COLUMN_ADD "縁結びデータベース", "縁結び種別"
DT_COLUMN_ADD "縁結びデータベース", "受け攻め設定"
DT_COLUMN_ADD "縁結びデータベース", "完遂フラグ", 1

@関係性データベースセット
DT_CREATE "関係性データベース"
DT_COLUMN_ADD "関係性データベース", "対象キャラ１", 4
DT_COLUMN_ADD "関係性データベース", "対象キャラ２", 4
DT_COLUMN_ADD "関係性データベース", "関係性種別"
DT_COLUMN_ADD "関係性データベース", "受け攻め設定"


@初体験データベースセット
DT_CREATE "初体験データベース"
DT_COLUMN_ADD "初体験データベース", "体験キャラ人物番号", 4
DT_COLUMN_ADD "初体験データベース", "相手キャラ人物番号", 4
DT_COLUMN_ADD "初体験データベース", "体験名"
DT_COLUMN_ADD "初体験データベース", "体験日時", 4
DT_COLUMN_ADD "初体験データベース", "体験場所"
DT_COLUMN_ADD "初体験データベース", "体験方法"
DT_COLUMN_ADD "初体験データベース", "その他"

@デートスコアデータベースセット
DT_CREATE "デートスコアデータベース"
DT_COLUMN_ADD "デートスコアデータベース", "デート相手人物番号", 4
DT_COLUMN_ADD "デートスコアデータベース", "デートスコア", 3
DT_COLUMN_ADD "デートスコアデータベース", "デート日時", 4
DT_COLUMN_ADD "デートスコアデータベース", "その他"

@弱点の欠片データベースセット
DT_CREATE "弱点の欠片データベース"
DT_COLUMN_ADD "弱点の欠片データベース", "弱点キー"
DT_COLUMN_ADD "弱点の欠片データベース", "数量", 3

@フレーバー素質初期設定
#DIM 全体設定
#DIM 残りキャラ設定

DRAWLINE
PRINTL 陰毛の濃さや乳輪の大きさなど、快楽発生量に直接関わらない素質を設定します。
PRINTL （口上の変動やコマンド発生条件には関わる可能性があります）
PRINTL 
PRINTL またこれらの設定にこだわりがない場合、［全て表示しない］を選ぶことでキャラのイメージを壊さないことが可能です。
DRAWLINE
PRINTL 
PRINT 現在の設定：
SELECTCASE 全体設定
	CASE 0
		PRINTBUTTON "[全て表示しない]", 100
		PRINTL
	CASE 1
		PRINTBUTTON "[初対面時に設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[初対面時に設定]", 101
		ENDIF
		PRINTL
	CASE 2
		PRINTBUTTON "[初うふふ時に設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[初うふふ時に設定]", 101
		ENDIF
		PRINTL
	CASE 3
		PRINTBUTTON "[ランダムに設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[ランダムに設定]", 101
		ENDIF
		PRINTL
ENDSELECT

PRINTL
PRINTBUTTON "[998] 口上側の設定があるキャラの一覧を見る", 998
PRINTL
PRINTBUTTON "[999] 設定を終える", 999

BINPUT
SELECTCASE RESULT
	CASE 100
		DRAWLINE
		PRINTBUTTON "[全て表示しない]", 0
		PRINTL
		PRINTBUTTON "[初対面時に設定]", 1
		PRINTL
		PRINTBUTTON "[初うふふ時に設定]", 2
		PRINTL
		PRINTBUTTON "[ランダムに設定]", 3
		PRINTL
		DRAWLINE
		PRINTBUTTON "[戻る]", 999
		BINPUT
		IF RESULT == 999
			RESTART
		ENDIF
		残りキャラ設定 = 0
		全体設定 = RESULT
		RESTART
	CASE 101
		DRAWLINE
		SELECTCASE 全体設定
			CASE 1
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[初対面時に設定]", 1
				PRINTL
			CASE 2
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[初うふふ時に設定]", 1
				PRINTL
			CASE 3
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[ランダムに設定]", 1
				PRINTL
		ENDSELECT
		DRAWLINE
		PRINTBUTTON "[戻る]", 999
		BINPUT
		IF RESULT == 999
			RESTART
		ENDIF
		残りキャラ設定 = RESULT
		RESTART
	CASE 998
		CALL フレーバー素質口上設定者表示
		RESTART
ENDSELECT

SELECTCASE 全体設定
	CASE 0
		PRINTL [全て表示しない]の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			フレーバー素質:COUNT:素質表示設定 = 0
		NEXT
		フレーバー素質設定 = 1
	CASE 1
		PRINT [初対面時に設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[初対面時に設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 2
		NEXT
		フレーバー素質設定 = 2
	CASE 2
		PRINT [初うふふ時に設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[初うふふ時に設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 3
		NEXT
		フレーバー素質設定 = 3
	CASE 3
		PRINT [ランダムに設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[ランダムに設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 1
			フレーバー素質:COUNT:陰毛濃さ = RAND:5 + 1
			フレーバー素質:COUNT:乳首大きさ = RAND:5 + 1
			フレーバー素質:COUNT:乳輪大きさ = RAND:5 + 1
			IF IS_FEMALE(COUNT)
				フレーバー素質:COUNT:性器種類 = RAND:5 + 1
				フレーバー素質:COUNT:クリ大きさ = RAND:3 + 1
			ENDIF
			IF TALENT:COUNT:性別 == 3
				フレーバー素質:COUNT:ふたなり時_玉ありなし = RAND:2 + 1
			ENDIF
			;ほくろはbit管理でいくつでも設定可能
			フレーバー素質:COUNT:ほくろ位置 |= !RAND:5 << 0                        ;乳首付近
			フレーバー素質:COUNT:ほくろ位置 |= (!RAND:5 && HAS_VAGINA(COUNT)) << 1 ;女性器付近
			フレーバー素質:COUNT:ほくろ位置 |= !RAND:5 << 2                        ;アナル付近
			フレーバー素質:COUNT:ほくろ位置 |= (!RAND:5 && HAS_PENIS(COUNT)) << 3  ;陰茎
		NEXT
ENDSELECT


@フレーバー素質個別設定画面(キャラ番号)
#DIM キャラ番号
#DIM フレーバー素質数値保存, 10
VARSET フレーバー素質数値保存

FOR COUNT, 1, 10
	IF フレーバー素質:キャラ番号:COUNT
		フレーバー素質数値保存:COUNT = フレーバー素質:キャラ番号:COUNT
	ENDIF
NEXT

$描画_LOOP
DRAWLINE
PRINTL キャラクターのフレーバー素質を手動で設定します。
DRAWLINE
LOCALS = 
;画像があるなら突っ込んでおく
SIF CSTR:キャラ番号:画像フォルダ == ""
	CSTR:キャラ番号:画像フォルダ = {NO:キャラ番号}%CALLNAME:キャラ番号%


LOCALS += @"<div rect='4300,0,1500,3000'>%短冊グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += "<div rect='62,0,4175,3750' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='137,125,3975,3750'>"

LOCALS += @"■%NAME:キャラ番号%<br>"
IF 装備ステータス補正保存:キャラ番号:属性
	LOCALS:1 = %属性数値文字色変換_HTML(装備ステータス補正保存:キャラ番号:属性 - 10)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(装備ステータス補正保存:キャラ番号:属性 - 10)%</font>"
ELSE
	LOCALS:1 = %属性数値文字色変換_HTML(基礎BATTLE_STATE:キャラ番号:属性)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(基礎BATTLE_STATE:キャラ番号:属性)%</font>"
ENDIF
LOCALS += @"　得意武器：%得意武器表示文字列(基礎BATTLE_STATE:キャラ番号:得意武器)%　"
LOCALS += @"　タイプ：%タイプ数値文字列変換(基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存)%"
LOCALS += "<br><br>"
LOCALS += "□ 情報 □---------------------------------------------------------------------<br>"
LOCALS += @"　種族：%GET_TALENTNAME(200,TALENT:キャラ番号:種族,,キャラ番号)%"
IF CSTR:キャラ番号:特殊種族 != ""
	LOCALS +=  @"[%CSTR:キャラ番号:特殊種族%]"
ENDIF
SIF TALENT:キャラ番号:角あり
	LOCALS += " （角あり）"
SIF TALENT:キャラ番号:尻尾あり
	LOCALS += " （尻尾）"
SIF TALENT:キャラ番号:エルーン耳
	LOCALS += " （エルーン耳）"
SIF TALENT:キャラ番号:ハーヴィン耳
	LOCALS += " （ハーヴィン耳）"
SIF TALENT:キャラ番号:発情期あり > 0
	LOCALS += " （発情期あり）"
SIF TALENT:キャラ番号:発情期あり < 0
	LOCALS += " （発情期なし）"
LOCALS += "<br>"
LOCALS += "　性別："
IF TALENT:キャラ番号:性別 == 1
	LOCALS += "女性"
	SIF TALENT:キャラ番号:処女
		LOCALS += "（処女）"
ELSEIF TALENT:キャラ番号:性別 == 2
	LOCALS += "男性"
	SIF TALENT:キャラ番号:非童貞 == 0
		LOCALS += "（童貞）"
ELSEIF TALENT:キャラ番号:性別 == 3
	LOCALS += "ふたなり"
	SIF TALENT:キャラ番号:非童貞 == 0
		LOCALS += "(童貞)"
	SIF TALENT:キャラ番号:処女
		LOCALS += "（処女）"
ELSE
	LOCALS += "その他"
ENDIF
IF GETBIT(TALENT:キャラ番号:性別, 0)
LOCALS += @"バストサイズ：%GET_TALENTNAME(205,TALENT:キャラ番号:バストサイズ,,キャラ番号)%"
ENDIF
LOCALS += "<br>"

LOCALS += @"　性知識レベル：%性知識段階表示(知識素質:キャラ番号:性知識, キャラ番号), 17, LEFT%"
LOCALS += "<br>"

IF TALENT:キャラ番号:性別 & 2
	LOCALS += @"　男性器サイズ：%GET_TALENTNAME(162,TALENT:キャラ番号:男性器サイズ,,キャラ番号)%"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"

CALL PRINT_STATE_TALENT(キャラ番号)
LOCALS += @"%RESULTS%"
LOCALS += "<br>"

LOCALS += "□ フレーバー素質 □-----------------------------------------------------------<br>"

FOR COUNT, 1, 6
	SIF ERDNAME(フレーバー素質, COUNT) == ""
		CONTINUE
	SIF IS_MALE(キャラ番号) && COUNT >= 1
		BREAK
	LOCALS += @"　%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
	FOR LOCAL:1, 0, 6
		LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
		IF LOCAL:1 == 0
			LOCALS:2 = 非表示
		ENDIF
		IF COUNT == 5 && LOCAL:1 == 4
			CONTINUE
		ELSEIF LOCALS:2 == ""
			CONTINUE
		ENDIF
		IF フレーバー素質数値保存:COUNT == LOCAL:1
			LOCALS:1 = C0C0C0
		ELSE
			LOCALS:1 = 333333
		ENDIF
		LOCALS += @"<font color='#%LOCALS:1%'><button value='{COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
	NEXT
	LOCALS += "<br>"
NEXT
LOCALS += @"　%ERDNAME(フレーバー素質, フレーバー素質_ほくろ位置), 12, LEFT%：　　　　"
FOR LOCAL:1, 0, 4
	LOCALS:2 = %GET_フレーバー素質NAME(フレーバー素質_ほくろ位置, POWER(2, LOCAL:1))%
	SIF LOCALS:2 == ""
		CONTINUE
	SIF LOCAL:1 == 1 && !HAS_VAGINA(キャラ番号) ;ビット0：乳首付近　1：女性器付近　2：アナル付近　3：陰茎（ふたなり時のみ）
		CONTINUE
	SIF LOCAL:1 == 3 && !HAS_PENIS(キャラ番号)
		CONTINUE
	IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
		LOCALS:1 = C0C0C0
	ELSE
		LOCALS:1 = 333333
	ENDIF
	LOCALS += @"<font color='#%LOCALS:1%'><button value='{60 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
NEXT
LOCALS += "<br>"
IF TALENT:キャラ番号:性別 == 3 && OPTION変数:ふたなり玉設定 == 0
	LOCALS += @"　%ERDNAME(フレーバー素質, フレーバー素質_ふたなり時_玉ありなし), 12, LEFT%：　　　　"
	FOR LOCAL:1, 0, 3
		LOCALS:2 = %GET_フレーバー素質NAME(フレーバー素質_ふたなり時_玉ありなし, LOCAL:1)%
		IF LOCALS:2 == ""
			CONTINUE
		ENDIF
		IF フレーバー素質数値保存:7 == LOCAL:1
			LOCALS:1 = C0C0C0
		ELSE
			LOCALS:1 = 333333
		ENDIF
		LOCALS += @"<font color='#%LOCALS:1%'><button value='{70 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
	NEXT
ENDIF
LOCALS += "</div>"
LOCALS += "<div rect='2812,3562,1875,312'><button value='999'>[999] これで決定</button></div>"

HTML_PRINT LOCALS, 1
FOR COUNT, 0, 35
	PRINTL
NEXT

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0 TO 59, 70 TO 79
		フレーバー素質数値保存:(LOCAL / 10) = LOCAL % 10
		GOTO 描画_LOOP
	CASE 60 TO 69
		INVERTBIT フレーバー素質数値保存:(LOCAL / 10), LOCAL % 10
		GOTO 描画_LOOP
	CASE 999
		LOCAL:1 = 0
		FOR COUNT, 1, フレーバー素質_項目数 + 1
			IF フレーバー素質数値保存:COUNT
				フレーバー素質:キャラ番号:COUNT = フレーバー素質数値保存:COUNT
				LOCAL:1 = 1
			ENDIF
		NEXT
		IF LOCAL:1
			フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 = 1
		ELSE
			フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 = 0
		ENDIF
ENDSELECT



@フレーバー素質口上設定者表示
#DIM 現在ページ
#DIM 表示候補, 500
#DIM 候補人数
現在ページ = 0

$最初から
;表示候補選定＆ソート処理
LOCAL:1 = 0
VARSET 表示候補
VARSET LOCALS
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	SIF フレーバー素質:LOCAL:素質表示設定 != 1
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
候補人数 = LOCAL:1
LOCAL:1 = 1

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, 候補人数
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME:(表示候補:LOCAL)), "]"),41,LEFT%</button>"
	LOCALS += "　　　　　　　　"
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 29
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [900]前のページへ
ELSE
	PRINTLC  
ENDIF
IF 現在ページ < 候補人数 / 30
	PRINTLC  [901]次のページへ
ENDIF
PRINTL
DRAWLINE
PRINTBUTTONLC "[1000]戻る", 1000
PRINTL

$INPUT_LOOP
BINPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 900
	現在ページ = MAX(現在ページ - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 901
	現在ページ = MIN(現在ページ + 1, 候補人数 / 30)
	GOTO 表示処理_LOOP
ENDIF
LOCAL = RESULT
$再表示
IF 候補人数 == 1
	CALL PRINT_STATE(LOCAL, 0)
ELSEIF 表示候補:0 == LOCAL
	CALL PRINT_STATE(LOCAL, 2)
ELSEIF 表示候補:(候補人数 - 1) == LOCAL
	CALL PRINT_STATE(LOCAL, 1)
ELSE
	CALL PRINT_STATE(LOCAL, 3)
ENDIF
IF RESULT == 100
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 - 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ELSEIF RESULT == 101
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 + 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ENDIF

GOTO 最初から


; @初期従業員選択(追加人数)
; #DIM 追加人数
; #DIM 追加候補, 10
; #DIM 表示候補, 5000
; #DIM 配列番号
; #DIM 表示最大数
; #DIM 現在ページ
; #DIM 対象キャラ

; VARSET 追加候補, -1
; VARSET 表示候補

; 表示最大数 = 0
; FOR 配列番号, 1, CHARANUM
; 	SIF CFLAG:配列番号:招待不可フラグ == 2
; 		CONTINUE
; 	表示候補:表示最大数 = 配列番号
; 	表示最大数 ++
; NEXT

; $表示LOOP
; DRAWLINE
; PRINTL 初期追加する従業員を選択してください。
; DRAWLINE

; FOR 配列番号, 現在ページ * 30, 現在ページ * 30 + 30
; 	SIF 表示候補:配列番号 <= 0
; 		BREAK
; 	SIF MATCH(追加候補, 表示候補:配列番号)
; 		SETCOLOR カラーパレット("黄")
; 	PRINTBUTTON @"[{配列番号}]%NAME:(表示候補:配列番号), 41, LEFT%", 配列番号
; 	RESETCOLOR
; 	SIF 配列番号 % 2
; 		PRINTL 
; NEXT
; PRINTL
; PRINTL 
; IF 現在ページ > 0
; 	PRINTBUTTONLC "[-1] 前ページへ", -1
; ELSE
; 	PRINTLC  
; ENDIF
; IF 表示候補:(現在ページ * 30 + 30) > 0
; 	PRINTBUTTONLC "[-2] 次ページへ", -2
; ENDIF
; PRINTL 
; DRAWLINE
; PRINTL ・
; DRAWLINE
; PRINTL ・現在の選択キャラ
; FOR LOCAL, 0, 追加人数
; 	IF 追加候補:LOCAL < 0
; 		PRINTL 未選択
; 	ELSE
; 		PRINTFORML %NAME:(追加候補:LOCAL)%
; 	ENDIF
; NEXT
; PRINTL 
; IF 10 - MATCH(追加候補, -1) == 追加人数
; 	PRINTBUTTON "[9999] これで決定する", 9999
; ELSE
; 	SETCOLOR カラーパレット("グレーアウト")
; 	PRINTPLAIN [9999] これで決定する
; 	RESETCOLOR
; ENDIF
; PRINTL 
; DRAWLINE
; BINPUT
; LOCAL = RESULT
; SELECTCASE LOCAL
; 	CASE -1
; 		現在ページ --
; 	CASE -2
; 		現在ページ ++
; 	CASE 9999
; 		IF 10 - MATCH(追加候補, -1) != 追加人数
; 			PRINTL 設定人数が合っていません
; 			GOTO 表示LOOP
; 		ENDIF
; 		FOR 配列番号, 0, 追加人数
; 			対象キャラ = 追加候補:配列番号
; 			SIF 対象キャラ < 1
; 				CONTINUE
; 			CFLAG:対象キャラ:信頼度 = 5000
; 			CFLAG:対象キャラ:招待不可フラグ = 0
; 			TALENT:対象キャラ:従業員 = 1
; 			CFLAG:対象キャラ:滞在期間 = 999
; 			CFLAG:対象キャラ:探索参加許可フラグ = 1
; 			FOR LOCAL, 0, 従業員部屋数
; 				IF 従業員部屋割り配列:LOCAL < 1
; 					従業員部屋割り配列:LOCAL = 対象キャラ
; 					BREAK
; 				ENDIF
; 			NEXT
; 			初体験日:対象キャラ:初対面 = -1
; 			CFLAG:対象キャラ:現在位置 = キャラクター部屋検索(対象キャラ)
; 			CFLAG:対象キャラ:現在マップ種別 = 0
; 			;初期状態雑務
; 			現在仕事:対象キャラ:0 = 1
; 		NEXT
; 		PRINTW 初期従業員キャラを設定しました。
; 		RETURN 0
; 	CASEELSE
		
; 		;- キャラ招待ループ
; 		WHILE 1
; 			CALL 初期従業員セット確認(表示候補:LOCAL, MATCH(追加候補, 表示候補:LOCAL))
; 			SELECTCASE RESULT
; 				CASE 0
; 					;- キャンセル
; 					; リスタートしてリスト表示へ戻る
; 					BREAK
; 				CASE 1
; 					;- キャラ追加後
; 					; リスタートしてリスト表示へ戻る
; 					IF MATCH(追加候補, 表示候補:LOCAL)
; 						追加候補:(FINDELEMENT(追加候補, 表示候補:LOCAL)) = -1
; 					ELSE
; 						IF 10 - MATCH(追加候補, -1) < 追加人数
; 							追加候補:(FINDELEMENT(追加候補, -1)) = 表示候補:LOCAL
; 						ELSE
; 							PRINTW 追加枠が一杯です。
; 						ENDIF
; 					ENDIF
; 					BREAK
; 				CASE 10
; 					;- 前のキャラ
; 					; 候補番号を更新、ループして前のキャラを表示
; 					LOCAL -=  1
; 					SIF LOCAL < 0
; 						LOCAL = 表示最大数 - 1
; 				CASE 11
; 					;- 次のキャラ
; 					; 候補番号を更新、ループして次のキャラを表示
; 					LOCAL +=  1
; 					SIF LOCAL >= 表示最大数
; 						LOCAL = 0
; 			ENDSELECT
; 		WEND
; ENDSELECT

; GOTO 表示LOOP

@初期従業員セット確認(対象キャラ, セットフラグ)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 返り値
#DIM 描画開始行
#DIM 対象画像数
#DIM リロールボタン有効
#DIM 実行結果
#DIM セットフラグ
#DIM リスタートフラグ
#DIM TFLAG保存
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	; 所持ZPチェック
	; ・ZPが足りなくても簡易紹介を表示したいのでZPの確認は後に移動
;	IF CFLAG:対象キャラ:ZPショップ値段 > FLAG:ZP所持量
;		PRINTW ZPが足りません
;		RETURN
;	ENDIF
	描画開始行 = LINECOUNT
ENDIF

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
IF セットフラグ
	PRINTFORML %NAME:対象キャラ%を初期従業員から外しますか？
ELSE
	PRINTFORML %NAME:対象キャラ%を初期従業員にセットしますか？
ENDIF
; ボタン表示
PRINTBUTTONLC "[0]はい", 0
PRINTBUTTONLC "[1]いいえ", 1
PRINTBUTTONLC "[10]詳細能力を表示", 10
PRINTL
PRINTBUTTONLC "[2]前のキャラ", 2
PRINTBUTTONLC "[3]次のキャラ", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]画像のランダム表示をリロール", 4
PRINTL

; 入力ループ
WHILE 1
	BINPUT
	SELECTCASE RESULT
		CASE 0
			返り値 = 1
			BREAK
		CASE 1
			返り値 = 0
			BREAK
		CASE 4
			RESTART
		CASE 2
			;- [2]前のキャラ
			返り値 = 10
			BREAK
		CASE 3
			;- [3]次のキャラ
			返り値 = 11
			BREAK
		CASE 10
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					返り値 = 10
					BREAK
				CASE 101
					;次のキャラへ
					返り値 = 11
					BREAK
				CASE 1
					;はい
					返り値 = 1
					BREAK
				CASE 0
					;いいえ
					返り値 = 0
					BREAK
			ENDSELECT
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)

RETURN 返り値




@初期従業員選択(追加人数)
#DIM 追加人数
#DIM 追加候補, 10
#DIM 現在ページ
#DIM 表示候補, キャラクタ数上限
#DIM ソート基準値, キャラクタ数上限
#DIM 候補人数
#DIM 口上ファイル数
#DIM アビ奥義関数
#DIM 対象キャラ
#DIM スキップフラグ
#DIM 得意武器番号
#DIM 配列番号
現在ページ = 0
VARSET 追加候補, -1

$最初から
;表示候補選定＆ソート処理

VARSET 表示候補
VARSET ソート基準値
VARSET LOCALS

LOCAL:1 = 0
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	CALL 装備再計算処理(LOCAL)
	SIF CFLAG:LOCAL:招待不可フラグ == 2
		CONTINUE
	SIF 滞在者のみフィルタ == 1 && CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:性別, @"性別：%GET_TALENTNAME(2, TALENT:LOCAL:性別)%")
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:種族, @"種族：%GET_TALENTNAME(200, TALENT:LOCAL:種族)%")
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:バストサイズ, @"バストサイズ：%GET_TALENTNAME(205, TALENT:LOCAL:バストサイズ)%") && TALENT:LOCAL:性別 != 2
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:バストサイズ, "バストサイズ：なし") && TALENT:LOCAL:性別 == 2
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:男性器サイズ, @"男性器サイズ：%GET_TALENTNAME(162, TALENT:LOCAL:男性器サイズ)%") && GETBIT(TALENT:LOCAL:性別, 1) != 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:男性器サイズ, "男性器サイズ：なし") && GETBIT(TALENT:LOCAL:性別, 1) == 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:性知識Lv, @"性知識Lv：%性知識段階表示(知識素質:LOCAL:性知識, LOCAL)%")
		CONTINUE
	IF 装備ステータス補正保存:LOCAL:装備耳飾り番号 > 0 && DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:LOCAL:装備耳飾り番号, "変更属性", 1) != ""
		SIF STRCOUNT(キャラ一覧フィルタ:属性, @"属性：%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:LOCAL:装備耳飾り番号, "変更属性", 1)%")
			CONTINUE
	ELSE
		SIF STRCOUNT(キャラ一覧フィルタ:属性, @"属性：%属性数値文字列変換(基礎BATTLE_STATE:LOCAL:属性)%")
			CONTINUE
	ENDIF
	スキップフラグ = 0
	FOR 得意武器番号, 0, 10
		IF GETBIT(基礎BATTLE_STATE:LOCAL:得意武器, 得意武器番号) && STRCOUNT(キャラ一覧フィルタ:得意武器, @"得意武器：%得意武器数値文字列変換(得意武器番号)%")
			スキップフラグ = 1
			BREAK
		ENDIF
	NEXT
	SIF スキップフラグ
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:タイプ, @"タイプ：%タイプ数値文字列変換(基礎BATTLE_STATE:LOCAL:ステータスタイプ保存)%")
		CONTINUE
	;発情期はちょっと面倒だけどオール手打ち
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情期なし") && !発情期判定(LOCAL)
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情中") && 発情期判定(LOCAL) && CFLAG:LOCAL:発情期フラグ < 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情期ではない") && 発情期判定(LOCAL) && CFLAG:LOCAL:発情期フラグ > -1
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:妊娠, @"妊娠フラグ：妊娠中") && TALENT:LOCAL:妊娠
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:妊娠, @"妊娠フラグ：妊娠していない") && !TALENT:LOCAL:妊娠
		CONTINUE
	口上ファイル数 = ENUMFILES(@"ERB/口上_キャラ個別ERB/{NO:LOCAL}_%CALLNAME:LOCAL%", "口上_*.ERB", 1)
	SIF STRCOUNT(キャラ一覧フィルタ:口上, @"口上：口上あり") && 口上ファイル数 > 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:口上, @"口上：口上なし") && 口上ファイル数 < 1
		CONTINUE
	アビ奥義関数 = ENUMFUNCBEGINSWITH(@"固有アビ_{NO:LOCAL}_") + ENUMFUNCENDSWITH(@"固有奥義_{NO:LOCAL}")
	SIF STRCOUNT(キャラ一覧フィルタ:固有アビ奥義, @"固有アビ奥義：あり") && アビ奥義関数 > 0
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:固有アビ奥義, @"固有アビ奥義：なし") && アビ奥義関数 < 1
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, @"陥落：恋慕") && TALENT:LOCAL:恋慕
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, @"陥落：セフレ") && TALENT:LOCAL:セフレ
		CONTINUE
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, @"陥落：なし") && !TALENT:LOCAL:セフレ && !TALENT:LOCAL:恋慕
		CONTINUE
	;条件を満たす場合はLOCAL:3に１以上が入る
	LOCAL:3 = 1
	FOR LOCAL:2, 0, 99
		SIF キャラ一覧素質絞り込み:(LOCAL:2) == ""
			BREAK
		SPLIT キャラ一覧素質絞り込み:(LOCAL:2), "_", LOCALS
		IF キャラ一覧素質絞り込み:99 == "AND"
			IF TALENT:LOCAL:(TOINT(LOCALS:0)) != TOINT(LOCALS:1)
				;ANDの場合は一個でもアウトだとNG
				LOCAL:3 = 0
				BREAK
			ENDIF
		ELSE
			LOCAL:3 = 0
			IF TALENT:LOCAL:(TOINT(LOCALS:0)) == TOINT(LOCALS:1)
				;OR条件の場合は一個当てはまればOK
				LOCAL:3 = 1
				BREAK
			ENDIF
		ENDIF
	NEXT
	SIF LOCAL:3 == 0
		CONTINUE

	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
候補人数 = LOCAL:1
LOCAL:1 = 1
;ソート処理
IF キャラ一覧ソート:0 != ""
	IF STRCOUNT(キャラ一覧ソート:1, "昇順")
		LOCAL:1 = 1
	ELSEIF STRCOUNT(キャラ一覧ソート:1, "降順")
		LOCAL:1 = -1
	ENDIF
	IF STRCOUNT(キャラ一覧ソート:0, "好感度レベル")
		LOCALS = CFLAG
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = ((CFLAG:(表示候補:LOCAL):好感度レベル * 1000) * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル")
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = ((CFLAG:(表示候補:LOCAL):恋慕レベル * 1000) * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF GETNUM(ABL, キャラ一覧ソート:0) >= 0
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = ((ABL:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000) * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF GETNUM(装備ステータス補正保存, キャラ一覧ソート:0) >= 0
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = ((基礎BATTLE_STATE:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000 + 装備ステータス補正保存:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000) * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "Lv")
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = (基礎BATTLE_STATE:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000 * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF GETNUM(知識素質, キャラ一覧ソート:0) >= 0
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = (知識素質:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000 * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "コマンド回数")
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = (CFLAG:(表示候補:LOCAL):コマンドタゲ回数記録 * 1000 * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF GETNUM(性癖素質, キャラ一覧ソート:0) > 0
		FOR LOCAL, 0, 候補人数
			ソート基準値:LOCAL = (性癖素質:(表示候補:LOCAL):(キャラ一覧ソート:0) * 1000 * LOCAL:1) + LOCAL + 1
		NEXT
		ARRAYMSORT ソート基準値, 表示候補
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "画像数")
			FOR LOCAL, 0, 候補人数
				ソート基準値:LOCAL = (ENUMFILES(@"resources/%CSTR:(表示候補:LOCAL):画像フォルダ%") * 1000 * LOCAL:1) + LOCAL + 1
			NEXT
			ARRAYMSORT ソート基準値, 表示候補
	ENDIF
ENDIF

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, 候補人数
	IF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル") && CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
		LOCALS += "<font color='#FF3399'>"
	ENDIF
	LOCALS += @"<button value = '{LOCAL}'>[{LOCAL,3}] %NAME表示省略(ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]", 1)),41,LEFT%</button>"
	IF STRCOUNT(キャラ一覧ソート:0, "好感度レベル")
		LOCALS += @"好感度Lv：{CFLAG:(表示候補:LOCAL):好感度レベル, 3}　 "
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル")
		LOCALS += @"　恋慕Lv：{CFLAG:(表示候補:LOCAL):恋慕レベル, 3}　 "
		SIF CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
			LOCALS += "</font>"
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "コマンド回数")
		LOCALS += @"　回数：{CFLAG:(表示候補:LOCAL):コマンドタゲ回数記録, 5}　 "
	ELSEIF GETNUM(ABL, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{ABL:(表示候補:LOCAL):(キャラ一覧ソート:0), 3}　 "
	ELSEIF GETNUM(装備ステータス補正保存, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{基礎BATTLE_STATE:(表示候補:LOCAL):(キャラ一覧ソート:0) + 装備ステータス補正保存:(表示候補:LOCAL):(キャラ一覧ソート:0), 3}　 "
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "Lv")
		LOCALS += @"Lv：{基礎BATTLE_STATE:(表示候補:LOCAL):Lv, 3}　 "
	ELSEIF GETNUM(知識素質, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{知識素質:(表示候補:LOCAL):(キャラ一覧ソート:0)}　 "
	ELSEIF GETNUM(性癖素質, キャラ一覧ソート:0) >= 0
		SELECTCASE 性癖素質:(表示候補:LOCAL):(キャラ一覧ソート:0)
			CASE 0
				LOCALS += @"%キャラ一覧ソート:0, 8%：なし　 "
			CASE 1
				LOCALS += @"%キャラ一覧ソート:0, 8%：興味　 "
			CASE 2
				LOCALS += @"%キャラ一覧ソート:0, 8%：愛好　 "
			CASE 3
				LOCALS += @"%キャラ一覧ソート:0, 8%：中毒　 "
		ENDSELECT
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "画像数")
		LOCALS += @"画像数：{MAX(ENUMFILES(@"resources/%CSTR:(表示候補:LOCAL):画像フォルダ%"), 0), 4}　"
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "チョロさ")
		LOCALS += @"チョロさ：{COM353_SUB(表示候補:LOCAL), 3}　 "
	ELSE
		IF CFLAG:(表示候補:LOCAL):長期雇用
			LOCALS += "　　長期雇用中　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 1
			LOCALS += @"　　　<font color='#%カラーパレット_HTML("黄")%'>本日帰宅</font>　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 999
			LOCALS += "　　　常時滞在　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 1
			LOCALS += @"　残滞在：{CFLAG:(表示候補:LOCAL):滞在期間 - 1, 2}日　"
		ELSE
			LOCALS += "　　　　　　　　"
		ENDIF
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 29
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [-1]前のページへ
ELSE
	PRINTLC  
ENDIF
IF 現在ページ < 候補人数 / 30
	PRINTLC  [-2]次のページへ
ENDIF
PRINTL
DRAWLINE

PRINTBUTTON "[2000]フィルタ機能", 2000
PRINTL
PRINTBUTTON "[3000]ソート機能", 3000
PRINT 　　　　　現在のソート設定：
IF キャラ一覧ソート:0 != ""
	PRINTFORM %キャラ一覧ソート:0%
ENDIF
IF STRCOUNT(キャラ一覧ソート:1, "昇順")
	PRINT (昇順)
	PRINT 　　　　　
	PRINTBUTTON "[降順にする]", 3100
ELSEIF STRCOUNT(キャラ一覧ソート:1, "降順")
	PRINT (降順)
	PRINT 　　　　　
	PRINTBUTTON "[昇順にする]", 3200
ELSE
	PRINT なし(キャラ番号順)
ENDIF
PRINTL
PRINTBUTTON "[4000]素質絞り込み機能", 4000
PRINT 　　現在の絞り込み設定：
IF キャラ一覧素質絞り込み:0 != ""
	IF キャラ一覧素質絞り込み:99 == "AND"
		PRINT (AND条件)：
	ELSE
		PRINT (OR条件)：
	ENDIF
	FOR LOCAL, 0, 99
		SIF キャラ一覧素質絞り込み:LOCAL == ""
			BREAK
		LOCALS:0 = 
		LOCALS:1 = 
		SPLIT キャラ一覧素質絞り込み:LOCAL, "_", LOCALS
		PRINTFORM [%GET_TALENTNAME(TOINT(LOCALS:0), TOINT(LOCALS:1))%]
	NEXT
ELSE
	PRINT なし
ENDIF
PRINTL

DRAWLINE
PRINTL ・現在の選択キャラ
FOR LOCAL, 0, 追加人数
	IF 追加候補:LOCAL < 0
		PRINTL 未選択
	ELSE
		PRINTBUTTON "[外す]", 9900 + LOCAL
		PRINTFORML 　%NAME:(追加候補:LOCAL), 24, LEFT%
	ENDIF
NEXT
PRINTL 
PRINTBUTTON "[9998] おすすめセットを選ぶ", 9998
PRINTL 
IF 10 - MATCH(追加候補, -1) == 追加人数
	PRINTBUTTON "[9999] これで決定する", 9999
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [9999] これで決定する
	RESETCOLOR
ENDIF

PRINTL 
DRAWLINE
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9900 TO 9910
		追加候補:(LOCAL - 9900) = -1
	CASE 2000
		CALL SHOW_CHARADATA_FILTA
		現在ページ = 0
		GOTO 最初から
	CASE 2001
		INVERTBIT 滞在者のみフィルタ, 0
		現在ページ = 0
		GOTO 最初から
	CASE 3000
		CALL SHOW_CHARADATA_SORT
		現在ページ = 0
		GOTO 最初から
	CASE 3100
		キャラ一覧ソート:1 = 降順
		現在ページ = 0
		GOTO 最初から
	CASE 3200
		キャラ一覧ソート:1 = 昇順
		現在ページ = 0
		GOTO 最初から
	CASE 4000
		CALL SHOW_CHARADATA_素質絞り込み
		現在ページ = 0
		GOTO 最初から
	CASE -1
		現在ページ --
	CASE -2
		現在ページ ++
	CASE 9998
		CALL 初期従業員オススメセット(追加候補)
	CASE 9999
		IF 10 - MATCH(追加候補, -1) != 追加人数
			PRINTL 設定人数が合っていません
			GOTO 表示処理_LOOP
		ENDIF
		FOR 配列番号, 0, 追加人数
			対象キャラ = 追加候補:配列番号
			SIF 対象キャラ < 1
				CONTINUE
			CFLAG:対象キャラ:信頼度 = 5000
			CFLAG:対象キャラ:招待不可フラグ = 0
			TALENT:対象キャラ:従業員 = 1
			CFLAG:対象キャラ:滞在期間 = 999
			CFLAG:対象キャラ:探索参加許可フラグ = 1
			FOR LOCAL, 0, 従業員部屋数
				IF 従業員部屋割り配列:LOCAL < 1
					従業員部屋割り配列:LOCAL = 対象キャラ
					BREAK
				ENDIF
			NEXT
			初体験日:対象キャラ:初対面 = -1
			CFLAG:対象キャラ:現在位置 = キャラクター部屋検索(対象キャラ)
			CFLAG:対象キャラ:現在マップ種別 = 0
			;初期状態雑務
			現在仕事:対象キャラ:0 = 1
		NEXT
		PRINTW 初期従業員キャラを設定しました。
		RETURN 0
	CASEELSE
		
		;- キャラ招待ループ
		WHILE 1
			CALL 初期従業員セット確認(表示候補:LOCAL, MATCH(追加候補, 表示候補:LOCAL))
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスタートしてリスト表示へ戻る
					BREAK
				CASE 1
					;- キャラ追加後
					; リスタートしてリスト表示へ戻る
					IF MATCH(追加候補, 表示候補:LOCAL)
						追加候補:(FINDELEMENT(追加候補, 表示候補:LOCAL)) = -1
					ELSE
						IF 10 - MATCH(追加候補, -1) < 追加人数
							追加候補:(FINDELEMENT(追加候補, -1)) = 表示候補:LOCAL
						ELSE
							PRINTW 追加枠が一杯です。
						ENDIF
					ENDIF
					BREAK
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補人数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補人数
						LOCAL = 0
			ENDSELECT
		WEND
ENDSELECT

GOTO 表示処理_LOOP


@初期従業員オススメセット(追加候補)
#DIM REF 追加候補
#DIM 選択セット番号
#DIM 関数数
#DIMS 関数配列, 100

選択セット番号 = -1
VARSET RESULTS
VARSET 関数配列
ENUMFUNCBEGINSWITH "オススメセット処理_"
ARRAYCOPY "RESULTS", "関数配列"
関数数 = RESULT

$表示ループ
DRAWLINE
PRINTL 任意のセットを選択してください。
DRAWLINE

;左側
LOCALS = <div rect='81,31,1937,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>
FOR LOCAL, 0, 関数数
	LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %REPLACE(関数配列:LOCAL, "オススメセット処理_", "")%</button><br>"
NEXT
LOCALS += "</div>"

;右側
LOCALS += "<div rect='2050,31,3875,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
RSTR:コマンド結果受渡し文字列 = 
SIF 選択セット番号 >= 0
	CALLFORM %関数配列:選択セット番号%("説明文表示", 追加候補)
LOCALS += @"%RSTR:コマンド結果受渡し文字列%"
LOCALS += "</div>"

;戻る
LOCALS += "<div rect='131,2631,1000,200'><button value='999'>[999] 戻る</button></div>"

HTML_PRINT LOCALS

FOR LOCAL, 0, 27
	PRINTL
NEXT

BINPUT 999
SELECTCASE RESULT
	CASE 998
		CALLFORM %関数配列:選択セット番号%("キャラセット", 追加候補)
		PRINTFORMW %REPLACE(関数配列:選択セット番号, "オススメセット処理_", "")%を適用しました。
		RETURN 0
	CASE 999
		RETURN 0
	CASE IS >= 1000
		; キャラ確認用の簡易紹介
		CALL キャラ簡易紹介(RESULT - 1000)
		WAIT
	CASEELSE
		選択セット番号 = RESULT
ENDSELECT
GOTO 表示ループ

@オススメセット処理_主人公たちセット(動作モード, 追加候補)
#DIM REF 追加候補
#DIMS 動作モード

SELECTCASE 動作モード
	CASE "説明文表示"
		RSTR:コマンド結果受渡し文字列 += "■主人公たちセット<br>"
		RSTR:コマンド結果受渡し文字列 += "<br>"
		RSTR:コマンド結果受渡し文字列 += "グランブルーファンタジー原作でメインの位置にいる４人のキャラ。<br>"
		RSTR:コマンド結果受渡し文字列 += "グラン・ジータ・ルリアは[命のリンク]素質でZPが稼ぎやすく、初心者向け。<br>"
		RSTR:コマンド結果受渡し文字列 += "反面、加入キャラの性別がバラけているのでハーレム等には適していないセット。<br>"
		RSTR:コマンド結果受渡し文字列 += "<br>"
		RSTR:コマンド結果受渡し文字列 += "・加入キャラ<br>"
		RSTR:コマンド結果受渡し文字列 += @"<button value='{キャラ検索("[夢抱く者]グラン") + 1000}'>[夢抱く者]グラン</button><br>"
		RSTR:コマンド結果受渡し文字列 += @"<button value='{キャラ検索("[碧く麗しき十天の輝星]ジータ") + 1000}'>[碧く麗しき十天の輝星]ジータ</button><br>"
		RSTR:コマンド結果受渡し文字列 += @"<button value='{キャラ検索("[蒼の少女]ルリア") + 1000}'>[蒼の少女]ルリア</button><br>"
		RSTR:コマンド結果受渡し文字列 += @"<button value='{キャラ検索("[小さき赤き竜]ビィ") + 1000}'>[小さき赤き竜]ビィ</button><br>"
		RSTR:コマンド結果受渡し文字列 += "<br>"
		RSTR:コマンド結果受渡し文字列 += "<button value='998'>[998] このセットを適用する</button><br>"
	CASE "キャラセット"
		追加候補:0 = キャラ検索("[夢抱く者]グラン")
		追加候補:1 = キャラ検索("[碧く麗しき十天の輝星]ジータ")
		追加候補:2 = キャラ検索("[蒼の少女]ルリア")
		追加候補:3 = キャラ検索("[小さき赤き竜]ビィ")
ENDSELECT


@QUICK_START(ARG)
#DIM 初期レベル
#DIM 追加信頼度

;ARG:クイックスタート番号
;どこらへんから始めるか
;1:人食い鳥の島クリア
;2:温泉解放

SELECTCASE ARG
	CASE 1
		初期レベル = 4
		追加信頼度 = 1500
	CASE 2
		初期レベル = 8
		追加信頼度 = 2500
	CASEELSE
		RETURN
ENDSELECT



FOR LOCAL,1, CHARANUM
	IF TALENT:LOCAL:従業員
		CFLAG:LOCAL:信頼度 += 追加信頼度
		;非戦闘員なのにレベル上がるのはバグの元になりそうなのでひとまず上げない
		IF !TALENT:LOCAL:非戦闘キャラ
			基礎BATTLE_STATE:LOCAL:蓄積EXP = 戦闘経験値テーブル:初期レベル
			CALL レベルアップ処理(LOCAL, 1)
		ENDIF
	ENDIF
	
NEXT
;沿岸部の森クリア
CALL イベント完了フラグ汎用処理("森に住み着いた魔物")
CALL イベント完了フラグ汎用処理("航路開拓計画")
空艇港初来訪イベントフラグ = 1
ダンジョン出現フラグ_沿岸部の森 = 1
ダンジョンクリアフラグ_沿岸部の森 = 1
CALL 汎用指輪取得処理("栄冠の指輪・力", 2)
CALL 汎用耳飾り取得処理("火の耳飾り")


;人食い鳥の島クリア
SETVAR @"ダンジョン構造配列_人食い鳥の島:0:0", 3
ダンジョン出現フラグ_人食い鳥の島 = 1
ダンジョンクリアフラグ_人食い鳥の島 = 1
CALL 汎用指輪取得処理("飛翔の指輪", 2)
CALL 汎用耳飾り取得処理("猛禽の耳飾り")
残り巣の数_人食い鳥の島 = 0

SETBIT ダンジョン構造配列_人食い鳥の島:14:0, 4
SETBIT ダンジョン構造配列_人食い鳥の島:21:3, 4
SETBIT ダンジョン構造配列_人食い鳥の島:31:1, 4
SETBIT ダンジョン構造配列_人食い鳥の島:30:5, 4
;ジョブの証
SETBIT ダンジョン構造配列_人食い鳥の島:31:1, 1
CALL 汎用証取得処理("ファイターの証")
CALL 汎用証取得処理("ナイトの証")
CALL 汎用証取得処理("プリーストの証")
CALL 汎用証取得処理("ウィザードの証")
;門
SETBIT ダンジョン構造配列_人食い鳥の島:11:4, 2

;人食い鳥の島クリアから開始ならここで抜ける
IF ARG == 1
	CALL RESET_GRAPH_0
	CALL RESET_GRAPH_1
	RETURN
ENDIF

;前提のあれこれ
CALL イベント完了フラグ汎用処理("渡りに船の定期便")
CALL イベント完了フラグ汎用処理("大拡張！空艇港")

;ゆぐゆぐダンジョンクリア
SETVAR @"ダンジョン構造配列_名前を亡くした遺跡:0:4", 3
ダンジョン出現フラグ_名前を亡くした遺跡 = 1
ダンジョンクリアフラグ_名前を亡くした遺跡 = 1
SETBIT ダンジョン構造配列_名前を亡くした遺跡:22:5, 4
CALL 汎用耳飾り取得処理("猛禽の耳飾り")

;ゆぐゆぐ解禁
LOCAL = GETCHARA(124)
CFLAG:LOCAL:招待不可フラグ = 0
CFLAG:LOCAL:信頼度 = 1000


;温泉追加
温泉作成許可フラグ = 1

施設改造度:7:0 = 1
施設改造度:8:0 = 1
CALL 滞在可能キャラ数再計算
CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"大浴場を温泉施設Lv1に改造した","実績")
CALL イベント完了フラグ汎用処理("温泉を作るために")
CALL イベント完了フラグ汎用処理("【条件確認】温泉を作るために")
CALL RESET_GRAPH_0
CALL RESET_GRAPH_1

;維持費が辛くなるだろうからお金と人気度追加
MONEY += 100000
リゾート人気度 = 1000
