;****************************************
;サイド領域の複数人一枚絵を登録するための関数
;****************************************
;・使い方
;「XXXX」を任意の文字列に書き換え、キャラの組み合わせと表示条件を入れる
;「XXXX」は既存の関数と被らないこと
;キャラの組み合わせは既存の関数と被らないこと、同じ組み合わせがないかどうか事前にチェックしてください
;処理的に、組み合わせのキャラが全員同じ場所にいないと条件は読まれません
;画像のサイズはおおよそ650*650程度が限度です
;****************************************

;--------------------------------------------------
;登録データ
;--------------------------------------------------
@複数人一枚絵_登録データ_たきなと千束(動作モード, 指定キャラ)
#DIMS 動作モード
#DIM REF 指定キャラ

SELECTCASE 動作モード
	CASE "初期登録"
		;キャラの組み合わせを指定、5キャラまで指定可能
		指定キャラ:0 = キャラ検索("[不器用なセカンドリコリス]井ノ上たきな")
		指定キャラ:1 = キャラ検索("[喫茶リコリコの看板娘]錦木千束")
		; 指定キャラ:2 = キャラ検索("")
		; 指定キャラ:3 = キャラ検索("")
		; 指定キャラ:4 = キャラ検索("")
	CASE "画像指定"
		;表示条件を分岐しつつ画像名を指定する
		;キャラ0とキャラ1が同時フェラチオしている場合
		IF MODE_確認_完全一致("フェラチオ系", 指定キャラ:0, PLAYER) >= 0 && MODE_確認_完全一致("フェラチオ系", 指定キャラ:1, PLAYER) >= 0
			;画像の位置を指定する
			;指定キャラ:0のフォルダに指定
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/たきな_千束_ダブルフェラチオ
		;キャラ0と後背位系でキャラ1が存在
		ELSEIF (MODE_存在判定_完全一致("後背位", MASTER, 指定キャラ:0) || MODE_存在判定_完全一致("立ちバック", MASTER, 指定キャラ:0) || MODE_存在判定_完全一致("背面座位", MASTER, 指定キャラ:0)|| MODE_存在判定_完全一致("背面駅弁", MASTER, 指定キャラ:0)) && !MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:1, MASTER) 
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/たきな_千束_後背位
		;キャラ1と後背位系でキャラ0が存在
		ELSEIF (MODE_存在判定_完全一致("後背位", MASTER, 指定キャラ:1) || MODE_存在判定_完全一致("立ちバック", MASTER, 指定キャラ:1) || MODE_存在判定_完全一致("背面座位", MASTER, 指定キャラ:1)|| MODE_存在判定_完全一致("背面駅弁", MASTER, 指定キャラ:1)) && !MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:0, MASTER) 
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/千束_たきな_後背位
		;キャラ0と後背位系以外のV挿入でキャラ1が存在
		ELSEIF MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:0, MASTER)  && !MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:1, MASTER) 
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/たきな_千束_正常位
		;キャラ1と後背位系以外のV挿入でキャラ0が存在		
		ELSEIF MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:1, MASTER)  && !MODE_存在判定_順不同("Ｖ挿入系", 指定キャラ:0, MASTER) 
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/千束_たきな_正常位
		ENDIF
ENDSELECT
