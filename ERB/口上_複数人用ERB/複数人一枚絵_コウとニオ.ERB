;****************************************
;サイド領域の複数人一枚絵を登録するための関数
;****************************************
;・使い方
;「XXXX」を任意の文字列に書き換え、キャラの組み合わせと表示条件を入れる
;「XXXX」は既存の関数と被らないこと
;キャラの組み合わせは既存の関数と被らないこと、同じ組み合わせがないかどうか事前にチェックしてください
;処理的に、組み合わせのキャラが全員同じ場所にいないと条件は読まれません
;画像のサイズはおおよそ650*650程度が限度です
;****************************************

;--------------------------------------------------
;登録データ
;--------------------------------------------------
@複数人一枚絵_登録データ_コウとニオ(動作モード, 指定キャラ)
#DIM 配列番号
#DIM 差分数
#DIM ランダム決定
#DIM 服確認
#DIMS 動作モード
#DIMS 射精指定
#DIMS 服指定
#DIMS コマンド指定
#DIM REF 指定キャラ

SELECTCASE 動作モード
	CASE "初期登録"
		指定キャラ:0 = キャラ検索("[千年を語る者]コウ")
		指定キャラ:1 = キャラ検索("[十天衆・九]ニオ")
	CASE "画像指定"
		
		コマンド指定 = %MODE_直近取得_順不同(指定キャラ:0, 指定キャラ:1)%
		IF コマンド指定 == ""
			コマンド指定 = 待機
		ENDIF

		射精指定 = 
		IF TCVAR:(指定キャラ:1):前ターン被射精場所 > 0
			射精指定 = 射精
		ENDIF

        服指定 = %CSTR:(指定キャラ:1):着せ替え服%
		服確認 = ENUMFILES(@"resources/%CSTR:(指定キャラ:0):画像フォルダ%/ニオ/%服指定%", @"*")
        IF  服確認 < 0 || 服指定 == ""
            服指定 = デフォルト
        ENDIF

		差分数 = ENUMFILES(@"resources/%CSTR:(指定キャラ:0):画像フォルダ%/ニオ/%服指定%/%射精指定%", @"%コマンド指定%*.*")
		IF 差分数 > 0
			ランダム決定 = RAND:差分数
			RESULTS = %REPLACE(RESULTS:ランダム決定, ESCAPE("resources\\"), "")%
		ELSEIF 差分数 == 0
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/ニオ/%服指定%/%射精指定%/待機
		ELSE
			RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/ニオ/%服指定%/%射精指定%/%コマンド指定%
		ENDIF
ENDSELECT


;キャラたちが特定の場所にいる、場合の分岐は以下のようになる

	; IF GETPLACENAME(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) == "任意の場所名"
	; 	RESULTS = %CSTR:(指定キャラ:0):画像フォルダ%/任意の画像名　
	; ENDIF
