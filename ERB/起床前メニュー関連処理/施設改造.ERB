;---------------------------------------------------------------
;リゾート施設の改造、増築、開拓関連
;---------------------------------------------------------------

@施設改装メニュー処理
#DIM 画面表示用変数

LOCALS = <div rect='13px,0px,306px,55px' border='5px' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='25px,10px,306px,45px'>所持ルピ：{MONEY}<br>所持ZP　：%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='13px,60px,310px,450px' border='5px' bcolor='#C0C0C0'></div><div rect='328px,60px,620px,450px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='25px,80px,310px,450px'>"
LOCALS += "<button value='100'>[100] 庭系列を改装</button>"
LOCALS += "<br>"
LOCALS += "<button value='101'>[101] 遊戯室系列を改装</button>"
LOCALS += "<br>"
LOCALS += "<button value='102'>[102] プール系列を改装</button>"
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 従業員部屋を増築</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"
LOCALS += "<div rect='340px,80px,620px,450px'>"
SELECTCASE 画面表示用変数
	CASE 0
		LOCALS += "リゾート施設の改装・改造を行います。<br>"
		LOCALS += "改造にはルピなどのリソースが必要ですが、<br>"
		LOCALS += "滞在可能人数やリゾートの人気上限などが上昇します。<br>"
	CASE 100
		LOCALS += 庭系列改造メニュー()
	CASE 101
		LOCALS += 遊戯室系列改造メニュー()
	CASE 102
		LOCALS += プール系列改造メニュー()
	CASE 110
		LOCALS += 従業員部屋増築メニュー()
ENDSELECT
LOCALS += "</div>"




HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT

SELECTCASE LOCAL
	CASE 100
		画面表示用変数 = 100
		RESTART
	CASE 101
		画面表示用変数 = 101
		RESTART
	CASE 102
		画面表示用変数 = 102
		RESTART
	CASE 110
		画面表示用変数 = 110
		RESTART
	CASE 999
		画面表示用変数 = 0
		RETURN
	CASEELSE
		SELECTCASE 画面表示用変数
			CASE 100
				CALL 庭系列改造実行関数(LOCAL)
			CASE 101
				CALL 遊戯室系列改造実行関数(LOCAL)
			CASE 102
				CALL プール系列改造実行関数(LOCAL)
			CASE 110
				CALL 従業員部屋増築実行関数(LOCAL)
		ENDSELECT
		RESTART
ENDSELECT


@従業員部屋増築メニュー()
#FUNCTIONS

LOCALS = 従業員部屋を増築し、更に従業員を雇えるようにします。<br><br>
IF 従業員キャラ上限 < 20
	LOCALS += @"現在従業員部屋数：{従業員キャラ上限}　　増築上限：20<br>"
	LOCALS += @"■<button value='200'>[従業員部屋増築]</button> （必要:{従業員キャラ上限 * 10000}ルピ）<br>"
ELSE
	LOCALS += @"※既に最大数まで増築されています<br>"
ENDIF

RETURNF LOCALS

@従業員部屋増築実行関数(ARG)

SELECTCASE ARG
	CASE 200
		IF MONEY < 従業員キャラ上限 * 10000
			PRINTW ルピが足りません。
			RETURN 0
		ENDIF
		PRINTW 従業員部屋を一部屋増築しました。
		MONEY -= 従業員キャラ上限 * 10000
		従業員キャラ上限 += 1
ENDSELECT
