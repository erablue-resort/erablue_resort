;---------------------------------------------------------------
;リゾート施設の改造、増築、開拓関連
;---------------------------------------------------------------

@施設改装メニュー処理
#DIM 画面表示用変数
;100~199の範囲でボタン数値を収めること

LOCALS = <div rect='81,0,1912,344' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='156,62,1912,281'>所持ルピ：%NUM_FORMAT(MONEY)%<br>所持ZP　：%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,500,1937,2812'>"
LOCALS += "<br>"
LOCALS += "<button value='100'>[100] 庭系列を改装</button>"
LOCALS += "<br>"
LOCALS += "<button value='101'>[101] 遊戯室系列を改装</button>"
LOCALS += "<br>"
LOCALS += "<button value='102'>[102] プール系列を改装</button>"
LOCALS += "<br>"
IF 温泉作成許可フラグ
	LOCALS += "<button value='103'>[103] 温泉系列を改装</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"
IF 施設改造度:20:0
	LOCALS += "<button value='109'>[109] 自宅を改装</button>"
ELSE
	LOCALS += "<button value='109'>[109] 自宅を建設する</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 従業員部屋を増築</button>"
LOCALS += "<br>"
IF 鍛冶場作成許可フラグ
	LOCALS += "<button value='120'>[120] 空艇港エリアを発展させる</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"
LOCALS += "<div rect='2125,500,3875,2812'>"
SELECTCASE 画面表示用変数
	CASE 0
		LOCALS += "リゾート施設の改装・改造を行います。<br>"
		LOCALS += "改造にはルピなどのリソースが必要ですが、<br>"
		LOCALS += "滞在可能人数やリゾートの人気上限などが上昇します。<br>"
	CASE 100
		LOCALS += 庭系列改造メニュー()
	CASE 101
		LOCALS += 遊戯室系列改造メニュー()
	CASE 102
		LOCALS += プール系列改造メニュー()
	CASE 103
		LOCALS += 風呂場系列改造メニュー()
	CASE 109
		LOCALS += 自宅改造メニュー()
	CASE 110
		LOCALS += 従業員部屋増築メニュー()
	CASE 120
		LOCALS += 空艇港改造メニュー()
ENDSELECT
LOCALS += "</div>"




HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT

SELECTCASE LOCAL
	CASE 100 TO 199
		画面表示用変数 = LOCAL
		RESTART
	CASE 999
		画面表示用変数 = 0
		RETURN
	CASEELSE
		SELECTCASE 画面表示用変数
			CASE 100
				CALL 庭系列改造実行関数(LOCAL)
			CASE 101
				CALL 遊戯室系列改造実行関数(LOCAL)
			CASE 102
				CALL プール系列改造実行関数(LOCAL)
			CASE 103
				CALL 風呂場系列改造実行関数(LOCAL)
			CASE 109
				CALL 自宅改造実行関数(LOCAL)
			CASE 110
				CALL 従業員部屋増築実行関数(LOCAL)
			CASE 120
				CALL 空艇港改造実行関数(LOCAL)
		ENDSELECT
		RESTART
ENDSELECT


@従業員部屋増築メニュー()
#FUNCTIONS
;100~199と999は大本のメニュー操作で使うため、使用しないこと

LOCALS = 従業員部屋を増築し、更に従業員を雇えるようにします。<br><br>
IF 従業員部屋数 < 従業員部屋拡張上限
	LOCALS += @"現在従業員部屋数：{従業員部屋数}　　増築上限：40<br>"
	IF 従業員部屋数 < 20
		LOCALS += @"■<button value='200'>[従業員部屋増築]</button> （必要:%NUM_FORMAT(従業員部屋数 * 5000)%ルピ）<br>"
	ELSE
		LOCALS += @"■<button value='200'>[従業員部屋増築]</button> （必要:%NUM_FORMAT(従業員部屋数 * 10000)%ルピ）<br>"
	ENDIF
ELSE
	LOCALS += @"※既に最大数まで増築されています<br>"
ENDIF

RETURNF LOCALS

@従業員部屋増築実行関数(ARG)
#DIM 必要費用

SELECTCASE ARG
	CASE 200
		IF 従業員部屋数 < 20
			必要費用 = 従業員部屋数 * 5000
		ELSE
			必要費用 = 従業員部屋数 * 10000
		ENDIF
		IF MONEY < 必要費用
			PRINTW ルピが足りません。
			RETURN 0
		ENDIF
		PRINTW 従業員部屋を一部屋増築しました。
		MONEY -= 必要費用
		従業員部屋数 += 1
ENDSELECT
