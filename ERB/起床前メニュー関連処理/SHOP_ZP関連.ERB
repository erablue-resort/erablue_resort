

@ZP_取得処理(キャラ番号, 取得基礎値)
#DIM キャラ番号
#DIM 取得基礎値

;あなたはZP算出しない
SIF キャラ番号 < 1
	RETURN

;キャラの素質などにより取得料補正
LOCAL = 取得基礎値

;今は未実装


;ZP加算
RCVAR:キャラ番号:ZP取得量 += LOCAL
RFLAG:ZP取得量 += LOCAL
FLAG:ZP所持量 += LOCAL


@ZP_SHOP
DRAWLINE
PRINTL ■ZPショップへようこそ
PRINTFORML 所持ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
PRINTBUTTON "[0]特別な能力を得る", 0
PRINTL 
PRINTBUTTON "[1]特別なアイテムを得る", 1
PRINTL 
PRINTBUTTON "[2]特別なキャラを招待候補に加える", 2
PRINTL 
PRINTBUTTON "[3]キャラの素質を変化させる", 3
PRINTL 
PRINTBUTTON "[4]リゾート全体の環境を変化させる", 4
PRINTL 
PRINTL
IF !FLAG:エンディング後フラグ
	PRINTBUTTON "[100]目標分のZPを献上する（エンディングへ）", 100
	PRINTL 
	PRINTL
ENDIF
PRINTBUTTONLC "[999]戻る", 999
PRINTBUTTONLC "[998]説明", 998
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		CALL ZP_SHOP_能力
		RESTART
	CASE 1
		CALL ZP_SHOP_アイテム
		RESTART
	CASE 2
		CALL ZP_SHOP_EXキャラ
		RESTART
	CASE 3
		CALL ZP_SHOP_素質変化
		RESTART
	CASE 4
		CALL ZP_SHOP_環境変化
		RESTART
	CASE 100
		IF FLAG:エンディング後フラグ
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		CALL ZP_SHOP_汎用エンディング
		RESTART
	CASE 999
		RETURN 0
	CASE 998
		DRAWLINE
		PRINTL ZP（絶頂ポイント）を消費して特別な効果を得るためのショップです。
		PRINTL 任意のキャラが絶頂した際にZPを取得することが出来ます。（あなたの絶頂では取得出来ません）
		PRINTW お気に入りの相手をガンガン絶頂させてドンドンZPを手に入れましょう！
		RESTART
ENDSELECT

REUSELASTLINE 
GOTO INPUT_LOOP


@ZP_SHOP_能力
DRAWLINE
PRINTL どの能力を手に入れますか？
PRINTFORML 所持ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
IF !あなた特殊能力:ハーレム
	PRINTBUTTON "[0]ハーレム入門を取得する", 0
	PRINTL 　　　　　値段：50ZP
	PRINTL 　　　　　…一度に複数人を恋慕対象にするための能力です。取得すると同時に３人まで対象に取ることが出来ます。
	PRINTL 　　　　　　また、恋慕素質を獲得した人数に応じてこの能力は成長します。
	PRINTL
ENDIF
IF !あなた特殊能力:ギャンブラー
	PRINTBUTTON "[1]ギャンブラーを取得する", 1
	PRINTL 　　　　　値段：30ZP
	PRINTL 　　　　　…賭け事を多く経験するための能力です。取得するとカードや卓球勝負の際、確実に賭け事を行うことが出来ます。
	PRINTL 　　　　　　別にゲーム自体に強くなるわけではない点には注意してください。
	PRINTL
ENDIF
IF ABL:PLAYER:技巧 < 10
	PRINTBUTTON "[2]技巧レベルを上昇させる", 2
	PRINTFORML 　　　　　値段：{ABL:PLAYER:技巧 * ABL:PLAYER:技巧 * 5}ZP　　現在Lv:{ABL:PLAYER:技巧}
	PRINTL 　　　　　…相手により大きな快楽を与えるための能力です。技巧レベルが高まるたびに値段は上昇します。
	PRINTL
ENDIF
IF !あなた特殊能力:発情鑑定術
	PRINTBUTTON "[3]発情鑑定術を取得する", 3
	PRINTL 　　　　　値段：20ZP
	PRINTL 　　　　　…キャラクターの発情期の周期を詳細に知ることが出来る能力です。
	PRINTL 　　　　　　いつ頃が発情期なのか知ることが出来れば、より効率よく仲を深めることが出来るでしょう。
	PRINTL 　　　　　　能力表示画面の「性的能力表示」で見ることが出来ます。
	PRINTL
ENDIF
IF !あなた特殊能力:危険日感知
	PRINTBUTTON "[4]危険日感知を取得する", 4
	PRINTL 　　　　　値段：20ZP
	PRINTL 　　　　　…キャラクターの危険日を詳細に知ることが出来る能力です。
	PRINTL 　　　　　　仲を深めたキャラクターの危険日を把握すれば、妊娠の回避や或いはその逆の事も容易になるでしょう。
	PRINTL 　　　　　　現在の状態がキャラ名の横に常に表示される他、詳細な危険日周期は能力表示画面で確認できます。
	PRINTL
ENDIF
IF !あなた特殊能力:触手変化術
	PRINTBUTTON "[5]触手変化術を取得する", 5
	PRINTL 　　　　　値段：80ZP
	PRINTL 　　　　　…肉体の一部を触手に変化させ、自在に操る魔法です。様々な触手コマンドが使用可能になります。
	PRINTL 　　　　　　取得直後では触手種類も与える快楽量も少ないですが、触手変化術のLvと技巧を上げることにより増加します。
	PRINTL 　　　　　　ただし対象が性癖素質[触手プレイ]を持っていない場合、触手関連のコマンドは実行出来ません。
	PRINTL
ENDIF
PRINTBUTTON "[999]戻る", 999
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		IF FLAG:ZP所持量 < 50
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF !あなた特殊能力:ハーレム
			PRINTL 50ZPを支払い、ハーレム入門を取得しますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 50
				あなた特殊能力:ハーレム = 1
				PRINTW 能力[ハーレム入門]を取得しました。
			ENDIF
			RESTART
			
		ENDIF
	CASE 1
		IF FLAG:ZP所持量 < 30
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF !あなた特殊能力:ギャンブラー
			PRINTL 30ZPを支払い、ギャンブラーを取得しますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 30
				あなた特殊能力:ギャンブラー = 1
				PRINTW 能力[ギャンブラー]を取得しました。
			ENDIF
			RESTART
		ENDIF
	CASE 2
		IF FLAG:ZP所持量 < (ABL:PLAYER:技巧 * ABL:PLAYER:技巧 * 5)
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF ABL:PLAYER:技巧 < 10
			PRINTFORML {ABL:PLAYER:技巧 * ABL:PLAYER:技巧 * 5}ZPを支払い、技巧を上昇させますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= (ABL:PLAYER:技巧 * ABL:PLAYER:技巧 * 5)
				ABL:PLAYER:技巧 += 1
				PRINTFORMW 技巧がLv{ABL:PLAYER:技巧}に上昇しました。
			ENDIF
			RESTART
		ENDIF
	CASE 3
		IF FLAG:ZP所持量 < 20
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF !あなた特殊能力:発情鑑定術
			PRINTL 20ZPを支払い、発情鑑定術を取得しますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 20
				あなた特殊能力:発情鑑定術 = 1
				PRINTW 能力[発情鑑定術]を取得しました。
			ENDIF
			RESTART
		ENDIF
	CASE 4
		IF FLAG:ZP所持量 < 20
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF !あなた特殊能力:危険日感知
			PRINTL 20ZPを支払い、危険日感知を取得しますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 20
				あなた特殊能力:危険日感知 = 1
				PRINTW 能力[危険日感知]を取得しました。
			ENDIF
			RESTART
		ENDIF
	CASE 5
		IF FLAG:ZP所持量 < 80
			PRINTW ZPが足りません
			RESTART
		ENDIF
		IF !あなた特殊能力:触手変化術
			PRINTL 80ZPを支払い、触手変化術を取得しますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 80
				あなた特殊能力:触手変化術 = 1
				PRINTW 能力[触手変化術 Lv1]を取得しました。
			ENDIF
			RESTART
		ENDIF
	CASE 999
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

REUSELASTLINE 
GOTO INPUT_LOOP

@ZP_SHOP_アイテム
DRAWLINE
PRINTL ZPを消費し、特別なアイテムを入手できます。
PRINTL どのアイテムを購入しますか？
PRINTFORML 所持ZP:{FLAG:ZP所持量}
DRAWLINE
PRINTBUTTON "[0]サプチケを購入する", 0
PRINTL 　　　　　値段：20ZP
PRINTL 　　　　　…任意のキャラを必ず招待するチケット。開発中Ver専用の特売品です。
PRINTL 　　　　　　開発が進み、正規の入手手段が実装された段階でこの項目は削除されます。
PRINTL
PRINTBUTTON "[1]発情誘発剤を購入する", 1
PRINTL 　　　　　値段：5ZP
PRINTL 　　　　　…陥落済みのキャラに対して、即座に発情期を開始させる薬。消耗品。
PRINTL 　　　　　　発情期が存在しないキャラに対しては使用出来ません。
PRINTL
PRINTBUTTON "[2]危険日変動薬を購入する", 2
PRINTL 　　　　　値段：5ZP
PRINTL 　　　　　…陥落済みのキャラに対して使用可能な、危険日を薬を服用した日に変化させる薬。消耗品。
PRINTL

PRINTBUTTON "[999]戻る", 999
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		IF FLAG:ZP所持量 < 20
			PRINTW ZPが足りません
			RESTART
		ENDIF
		PRINTL 20ZPを支払い、サプチケを一枚取得しますか？
		PRINTBUTTONLC "[0]はい", 0
		PRINTBUTTONLC "[1]いいえ", 1
		$INPUT_LOOP2
		INPUT
		SELECTCASE RESULT
			CASE 0
				FLAG:ZP所持量 -= 20
				ITEM:サプチケ += 1
				PRINTW サプチケを一枚購入しました。
			CASE 1
				RESTART
			CASEELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP2
		ENDSELECT
	CASE 1
		IF FLAG:ZP所持量 < 5
			PRINTW ZPが足りません
			RESTART
		ENDIF
		PRINTL 5ZPを支払い、発情誘発剤を取得しますか？
		PRINTBUTTONLC "[0]はい", 0
		PRINTBUTTONLC "[1]いいえ", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 5
			ITEM:発情誘発剤 += 1
			PRINTW 発情誘発剤を一回分購入しました。
		ENDIF
		RESTART
	CASE 2
		IF FLAG:ZP所持量 < 5
			PRINTW ZPが足りません
			RESTART
		ENDIF
		PRINTL 5ZPを支払い、危険日変動薬を取得しますか？
		PRINTBUTTONLC "[0]はい", 0
		PRINTBUTTONLC "[1]いいえ", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 5
			ITEM:危険日変動薬 += 1
			PRINTW 危険日変動薬を一回分購入しました。
		ENDIF
		RESTART
	CASE 999
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT


@ZP_SHOP_EXキャラ
;現在招待不可フラグが１なキャラ一覧
#DIM 表示候補, 200
#DIM 現在ページ
#DIM 広告対象

VARSET 表示候補, -1
現在ページ = 0

;広告対象の表示
DRAWLINE
PRINTL ZPを消費し、本来ならばリゾートを訪れることが出来ないキャラを導きます。
PRINTL どのキャラを導きますか？
DRAWLINE

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;フラグがないキャラは出さない
	SIF CFLAG:LOCAL:招待不可フラグ == 0
		CONTINUE
	SIF CFLAG:LOCAL:招待不可フラグ == 2
		CONTINUE
	;該当イベをクリアしてない場合出さない
	SIF CFLAG:LOCAL:招待不可フラグ == 3 && イベント完了フラグ確認("「平行世界」という場所") == 0
		CONTINUE
	;特殊条件がある、かつ条件未達成のキャラは出さない
	TRYCCALLFORM EXキャラ_表示条件_{NO:LOCAL}
		SIF RESULT == 0
			CONTINUE
	CATCH
	ENDCATCH

	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	SIF CFLAG:(表示候補:LOCAL):ZPショップ値段 == 0
		CFLAG:(表示候補:LOCAL):ZPショップ値段 = 50
	PRINTBUTTON @"[{LOCAL}]%NAME:(表示候補:LOCAL), 20, LEFT%：消費ZP{CFLAG:(表示候補:LOCAL):ZPショップ値段, 4, RIGHT}", LOCAL
	PRINTL 
NEXT

DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]次ページへ", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]戻る", 999

$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASEELSE
		IF 表示候補:RESULT >= CHARANUM
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 表示候補:LOCAL < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		IF CFLAG:(表示候補:LOCAL):ZPショップ値段 > FLAG:ZP所持量
			PRINTW ZPが足りません
			RESTART
		ENDIF
		PRINTFORML %NAME:(表示候補:LOCAL)%を導きます。
		PRINTL よろしいですか？
		PRINTBUTTONLC "[0]はい", 0 
		PRINTBUTTONLC "[1]いいえ", 1
		PRINTL
		$INPUT_LOOP_2
		INPUT
		IF RESULT == 0
			PRINTFORML %NAME:(表示候補:LOCAL)%が導かれました。
			IF 滞在キャラ上限 - 滞在キャラ数 - 集客人数 > 0
				PRINTW 起床後にリゾートを訪れます。
				FOR LOCAL:1, 0, 100
					IF 解放キャラ招待:(LOCAL:1) == 0
						解放キャラ招待:(LOCAL:1) = 表示候補:LOCAL
						滞在キャラ数 += 1
						BREAK
					ENDIF
				NEXT
			ELSE
				PRINTW 滞在人数がいっぱいのため、今後の招待対象に追加します。
			ENDIF
			CFLAG:(表示候補:LOCAL):招待不可フラグ = 0
			FLAG:ZP所持量 -= CFLAG:(表示候補:LOCAL):ZPショップ値段
		ELSEIF RESULT == 1
			RESTART
		ELSE
			REUSELASTLINE 
			GOTO INPUT_LOOP_2
		ENDIF
		SIF RESULT == -1
			RESTART
ENDSELECT


@ZP_SHOP_素質変化
;現在滞在しているキャラ一覧
#DIM 表示候補, 200
#DIM 現在ページ
#DIM 広告対象

VARSET 表示候補, -1
現在ページ = 0

;広告対象の表示
DRAWLINE
PRINTL 誰の素質を強化しますか？
DRAWLINE

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;滞在してないキャラは出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	PRINTBUTTON @"[{LOCAL}]%NAME:(表示候補:LOCAL)%", LOCAL
	PRINTL 
NEXT

DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]次ページへ", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]戻る", 999

$INPUT_LOOP
INPUT

LOCAL = 0
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASEELSE
		IF 表示候補:RESULT >= CHARANUM
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		CALL ZP_素質変化(表示候補:RESULT)
		SIF RESULT == -1
			RESTART
ENDSELECT

@ZP_素質変化(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTL ZPを消費してキャラの素質を変化させることが出来ます。
PRINTL どの素質を変化させますか？
DRAWLINE
PRINTBUTTONLC "[1]感覚系素質を強化する", 1
PRINTL
PRINTBUTTONLC "[2]乳の大きさを変化させる", 2
PRINTL
PRINTBUTTONLC "[3]種族を変化させる", 3
PRINTL
PRINTBUTTONLC "[999]戻る", 999
PRINTL

$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 1
	CASE 2
	CASE 3
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT



IF RESULT == 1
	DRAWLINE
	PRINTL ZPを消費してキャラに敏感系素質を付与することが出来ます。
	PRINTL どの素質を敏感化しますか？
	DRAWLINE
	FOR LOCAL:1 , 101, 106,
		IF TALENT:キャラ番号:(LOCAL:1) > 0
			PRINTFORML %TALENTNAME:(LOCAL:1)%は既に敏感です
			CONTINUE
		ENDIF
		IF TALENT:キャラ番号:(LOCAL:1) == -1
			PRINTBUTTON @"[{LOCAL:1}]%TALENTNAME:(LOCAL:1)%:鈍感 > 通常", LOCAL:1
			PRINTL 　　　必要ZP:100
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%TALENTNAME:(LOCAL:1)%:通常 > 敏感", LOCAL:1
			PRINTL 　　　必要ZP:300
		ENDIF
	NEXT
	PRINTBUTTON "[999]戻る", 999
	$INPUT_LOOP2
	INPUT
	LOCAL:1 = RESULT
	SELECTCASE RESULT
		CASE 999
			RESTART
		CASE 101 TO 105
			;敏感なのに入ってきてない？
			IF TALENT:キャラ番号:(LOCAL:1) > 0
				REUSELASTLINE 
				GOTO INPUT_LOOP2
			ENDIF
			;ZP足りる？
			IF TALENT:キャラ番号:(LOCAL:1) == -1 & FLAG:ZP所持量 < 100
				PRINTW ZPが足りません
				RESTART
			ELSEIF TALENT:キャラ番号:(LOCAL:1) == 0 & FLAG:ZP所持量 < 300
				PRINTW ZPが足りません
				RESTART
			ENDIF
			IF TALENT:キャラ番号:(LOCAL:1) == -1
				PRINT 100
			ELSEIF TALENT:キャラ番号:(LOCAL:1) == 0
				PRINT 300
			ENDIF
			PRINTFORML ZPを支払い、%TALENTNAME:(LOCAL:1)%素質を上昇させますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				IF TALENT:キャラ番号:(LOCAL:1) == -1
					FLAG:ZP所持量 -= 100
					PRINTFORMW %TALENTNAME:(LOCAL:1)%が鈍感から通常に変化しました。
				ELSEIF TALENT:キャラ番号:(LOCAL:1) == 0
					FLAG:ZP所持量 -= 300
					PRINTFORMW %TALENTNAME:(LOCAL:1)%が通常から敏感に変化しました。
				ENDIF
				TALENT:キャラ番号:(LOCAL:1) += 1
			ENDIF
			RESTART
		CASEELSE
			REUSELASTLINE 
			GOTO INPUT_LOOP2

	ENDSELECT
ELSEIF RESULT == 2
	DRAWLINE
	PRINTL ZPを消費してキャラの乳の大きさを変化させることができます。
	PRINTL 任意の大きさを選択してください
	DRAWLINE
	FOR LOCAL:1 , -2, 4
		IF TALENT:キャラ番号:バストサイズ == LOCAL:1
			SETCOLOR 0x666666
			PRINTFORML [{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1)%（現在の大きさ）
			RESETCOLOR
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1)%（{ABS(TALENT:キャラ番号:バストサイズ - LOCAL:1) * 20}）", LOCAL:1
			PRINTL
		ENDIF
	NEXT
	PRINTBUTTON "[999]戻る", 999
	$INPUT_LOOP3
	INPUT
	LOCAL:1 = RESULT
	SELECTCASE LOCAL:1
		CASE 999
			RESTART
		CASE TALENT:キャラ番号:バストサイズ
			REUSELASTLINE 
			GOTO INPUT_LOOP3
		CASE -2 TO 3
			;ZP足りる？
			IF FLAG:ZP所持量 < ABS(TALENT:キャラ番号:バストサイズ - LOCAL:1) * 20
				PRINTW ZPが足りません
			GOTO INPUT_LOOP3
			ENDIF
			PRINTFORM {ABS(TALENT:キャラ番号:バストサイズ - LOCAL:1) * 20}
			PRINTFORML ZPを支払い、バストサイズを変化させますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			INPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= ABS(TALENT:キャラ番号:バストサイズ - LOCAL:1) * 20
				PRINTFORMW %CALLNAME:キャラ番号%のバストサイズが%GET_TALENTNAME(205, TALENT:キャラ番号:バストサイズ)%から%GET_TALENTNAME(205, LOCAL:1)%に変化しました。
				TALENT:キャラ番号:バストサイズ = LOCAL:1
			ENDIF
			RESTART
		CASEELSE
			REUSELASTLINE 
			GOTO INPUT_LOOP3
	ENDSELECT
ELSEIF RESULT == 3
	DRAWLINE
	PRINTL ZPを消費してキャラの種族を変化させることができます。
	PRINTL 任意の種族を選択してください
	DRAWLINE
	FOR LOCAL:1 , 1, 7
		IF TALENT:キャラ番号:種族 == LOCAL:1
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(200, LOCAL:1)%（現在の種族）
			PRINTL
			RESETCOLOR
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(200, LOCAL:1), 14, LEFT%必要ZP:200", LOCAL:1
			PRINTL
		ENDIF
	NEXT
	PRINTBUTTON "[999]戻る", 999
	$INPUT_LOOP4
	BINPUT
	LOCAL:1 = RESULT
	SELECTCASE LOCAL:1
		CASE 999
			RESTART
		CASE 1 TO 6
			;ZP足りる？
			IF FLAG:ZP所持量 < 200
				PRINTW ZPが足りません
				GOTO INPUT_LOOP4
			ENDIF
			PRINTFORML 200ZPを支払い、種族を変化させますか？
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			BINPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= 200
				PRINTFORMW %CALLNAME:キャラ番号%の種族が%GET_TALENTNAME(200, TALENT:キャラ番号:種族)%から%GET_TALENTNAME(200, LOCAL:1)%に変化しました。
				CALL ZP_SHOP_種族特徴変換(キャラ番号, TALENT:キャラ番号:種族, LOCAL:1)
				TALENT:キャラ番号:種族 = LOCAL:1
			ENDIF
			RESTART
	ENDSELECT
ENDIF

@ZP_SHOP_種族特徴変換(キャラ番号, 変更元種族, 変更先種族)
#DIM キャラ番号
#DIM 変更元種族
#DIM 変更先種族

CFLAG:キャラ番号:発情期フラグ = 0
BASE:キャラ番号:母乳 = 0
IF TALENT:キャラ番号:母乳体質 == 2
	TALENT:キャラ番号:母乳体質 = 0
ENDIF

IF 変更元種族 == 4 && 変更先種族 != 5
	;元がハーヴィンの場合、ランダムでバスト変化
	SELECTCASE RAND:100
		CASE 0 TO 16
			TALENT:キャラ番号:バストサイズ = -2
		CASE 17 TO 33
			TALENT:キャラ番号:バストサイズ = -1
		CASE 34 TO 50
			TALENT:キャラ番号:バストサイズ = 0
		CASE 51 TO 67
			TALENT:キャラ番号:バストサイズ = 1
		CASE 68 TO 84
			TALENT:キャラ番号:バストサイズ = 2
		CASE 85 TO 99
			TALENT:キャラ番号:バストサイズ = 3
	ENDSELECT
	IF TALENT:キャラ番号:ランダムキャラ
		SELECTCASE RAND:100
			CASE 0 TO 59
				TALENT:キャラ番号:体格 = -1
			CASE 60 TO 94
				TALENT:キャラ番号:体格 = 0
			CASE 95 TO 99
				TALENT:キャラ番号:体格 = 1
		ENDSELECT
	ENDIF
ELSEIF 変更元種族 == 3 && 変更先種族 != 5
	;元がドラフの場合、ランダムでバスト変化
	SELECTCASE RAND:100
		CASE 0 TO 16
			TALENT:キャラ番号:バストサイズ = -2
		CASE 17 TO 33
			TALENT:キャラ番号:バストサイズ = -1
		CASE 34 TO 50
			TALENT:キャラ番号:バストサイズ = 0
		CASE 51 TO 67
			TALENT:キャラ番号:バストサイズ = 1
		CASE 68 TO 84
			TALENT:キャラ番号:バストサイズ = 2
		CASE 85 TO 99
			TALENT:キャラ番号:バストサイズ = 3
	ENDSELECT
	IF TALENT:キャラ番号:ランダムキャラ
		SELECTCASE RAND:100
			CASE 0 TO 59
				TALENT:キャラ番号:体格 = -1
			CASE 60 TO 94
				TALENT:キャラ番号:体格 = 0
			CASE 95 TO 99
				TALENT:キャラ番号:体格 = 1
		ENDSELECT
	ENDIF
ENDIF

;種族に応じた変化
SELECTCASE 変更先種族
	CASE 1
		;ヒューマン
		TALENT:キャラ番号:角あり = 0
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 0
	CASE 2
		;エルーン
		TALENT:キャラ番号:角あり = 0
		IF RAND:10 == 0
			TALENT:キャラ番号:尻尾あり = 1
		ELSE
			TALENT:キャラ番号:尻尾あり = 0
		ENDIF
		TALENT:キャラ番号:エルーン耳 = 1
		TALENT:キャラ番号:ハーヴィン耳 = 0
	CASE 3
		;ドラフ
		IF TALENT:キャラ番号:ランダムキャラ
			TALENT:キャラ番号:体格 = -1
		ENDIF
		TALENT:キャラ番号:角あり = 1
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 0
		TALENT:キャラ番号:バストサイズ = 3
	CASE 4
		;ハーヴィン
		IF TALENT:キャラ番号:ランダムキャラ
			TALENT:キャラ番号:体格 = -2
		ENDIF
		TALENT:キャラ番号:角あり = 0
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 1
		TALENT:キャラ番号:バストサイズ = -2
	CASE 5
		;星晶獣は変更なし
	CASE 6
		;その他は変更なし
ENDSELECT

@ZP_SHOP_環境変化
#DIM 妊娠期間変化値
#DIM 消費ZP
妊娠期間変化値 = 妊娠期間
DRAWLINE
PRINTL ZPを消費し、リゾート内の滞在者全員に特殊な影響を与えることができます。
PRINTL どの機能を導入しますか？
PRINTFORML 所持ZP:{FLAG:ZP所持量}
DRAWLINE
IF !妊娠切り替えOPTION記憶
	PRINTBUTTON "[0]妊娠期間を変動させる", 0
ELSE
	SETCOLOR 0x666666
	PRINTPLAINFORM [0]妊娠期間を変動させる
ENDIF
PRINTL 　　　　　値段：（変動させる日数×1）ZP
PRINTL 　　　　　…リゾート内において、子供を出産するまでにかかる時間を変動させます。
PRINTL
RESETCOLOR
PRINTBUTTON "[999]戻る", 999
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		$INPUT_BEFORE2
		PRINTFORML 妊娠期間として適用したい数値を入力してください。（現在の妊娠期間：{妊娠期間}日）
		PRINTBUTTON "[<< -10] ",-999
		PRINTBUTTON "[< -1] ",-998
		PRINTFORM  [{妊娠期間変化値}]決定  
		PRINTBUTTON "[+1 >] ",-997
		PRINTBUTTON "[+10 >>] " ,-996
		PRINTBUTTON "      [0]戻る", 0
		PRINTL
		$INPUT_LOOP2
		INPUT
		消費ZP = ABS(RESULT - 妊娠期間)
		IF RESULT == 0
			RESTART
		ELSEIF INRANGE(RESULT,-999,-996)
			SELECTCASE RESULT
				CASE -996
					妊娠期間変化値 += 10
				CASE -997
					妊娠期間変化値 += 1
				CASE -998
					妊娠期間変化値 -= 1
				CASE -999
					妊娠期間変化値 -= 10
			ENDSELECT
			CLEARLINE 3
			REUSELASTLINE 
			GOTO INPUT_BEFORE2
		ELSEIF RESULT < 0
			CLEARLINE 1
			REUSELASTLINE 0以下の値は指定できません
			GOTO INPUT_LOOP2
		ELSEIF 消費ZP > FLAG:ZP所持量
			CLEARLINE 1
			REUSELASTLINE ZPが足りません
			GOTO INPUT_LOOP2
		ELSEIF RESULT == 妊娠期間
			CLEARLINE 1
			REUSELASTLINE 今の妊娠期間と同じ値です
			GOTO INPUT_LOOP2
		ELSE
			妊娠期間変化値 = RESULT
			PRINTFORML {消費ZP}ZPを支払い、出産までにかかる期間を{妊娠期間}日から{妊娠期間変化値}日へと変動させますか？
			PRINTL （現状のキャラごとの妊娠の進行度は再計算され引き継がれますが、多少ずれる可能性があります）
			PRINTBUTTONLC "[0]はい", 0
			PRINTBUTTONLC "[1]いいえ", 1
			$INPUT_LOOP2_2
			INPUT
			SELECTCASE RESULT
				CASE 0
					FLAG:ZP所持量 -= 消費ZP
					FOR LOCAL,0,CHARANUM
						SIF CFLAG:LOCAL:妊娠経過日
							CFLAG:LOCAL:妊娠経過日 = CFLAG:LOCAL:妊娠経過日 * 妊娠期間変化値 / 妊娠期間
					NEXT
					妊娠期間 = 妊娠期間変化値
					PRINTFORMW 出産までにかかる期間を{妊娠期間}日へと変動させました。
					RESTART
				CASE 1
					GOTO INPUT_BEFORE2
				CASEELSE
					REUSELASTLINE 
					GOTO INPUT_LOOP2_2
			ENDSELECT
		ENDIF
	CASE 999
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT
