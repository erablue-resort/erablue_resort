@従業員雇用_V2
#LOCALSIZE 2
#LOCALSSIZE 1
#DIMS 表示文字列
#DIMS 追加表示格納
#DIMS 素質行格納
#DIM メニュー調理法一時保存, 6
#DIM 管理番号
#DIM ワーウルフフラグ
#DIM Ｒキャラ性別
#DIM 改行フラグ
#DIM 追加キャラ番号
#DIM 画像フラグ, 2
#DIM 候補キャラ

VARSET メニュー調理法一時保存
FOR LOCAL, 1, 6
	メニュー調理法一時保存:LOCAL = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:LOCAL, "調理器具ID", 1)
NEXT
CALL 料理メニューコンボ判定処理
SIF !RESULT
	発動中コンボ名 '= ""
IF そっくりさん画像枚数_閾値 < 1
	そっくりさん画像枚数_閾値 = 10
ENDIF
IF そっくりさんフラグ
	VARSET LOCAL
	FOR 候補キャラ, 1, CHARANUM
		;ランダムキャラは対象にしない
		SIF NO:候補キャラ == 999
			CONTINUE
		;画像枚数OK？
		SIF GET_RESOURCES_FILES(CSTR:候補キャラ:画像フォルダ, "*.webp", 1) < そっくりさん画像枚数_閾値
			CONTINUE
		IF CSVTALENT(NO:候補キャラ, GETNUM(TALENT, "性別")) != 2
			そっくりさん候補配列_非男:LOCAL = 候補キャラ
			LOCAL ++
		ELSE
			そっくりさん候補配列_男:(LOCAL:1) = 候補キャラ
			LOCAL:1 ++
		ENDIF
	NEXT
ENDIF

$従業員_LOOP
DRAWLINE
PRINTL 従業員（ランダムキャラ）を雇います。
PRINTPLAIN [110] 別の候補を見る　を選んだ場合、前のキャラには戻れないので注意してください。
PRINTL
DRAWLINE

;左パネル (メニュー)
表示文字列 = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
表示文字列 += "<div rect='156,156,1937,2812'>"
表示文字列 += "<button value='100'>[100] このキャラを雇用する</button>"
表示文字列 += "<br>"
表示文字列 += "<button value='110'>[110] 別の候補を見る</button>"
表示文字列 += "<br>"
表示文字列 += "<button value='120'>[120] 汎用立ち絵機能を切り替える</button>"
表示文字列 += "<br>"
表示文字列 += "<button value='130'>[130] ワーウルフの立ち絵を変更する</button>"
表示文字列 += "<br>"
表示文字列 += "<button value='131'>[131] そっくりさん立ち絵を変更する</button>"
表示文字列 += "<br>"
表示文字列 += "<br>"
表示文字列 += "<button value='999'>[999] 戻る</button>"
表示文字列 += "</div>"

;キャラ生成処理
CALL ランダムキャラ生成準備開始(Ｒキャラ追加候補記録, 1)
管理番号 = RESULT

;従業員雇用固有の仕様
;属性：食堂のメニューの調理法によって出やすいものがある
SELECTCASE メニュー調理法一時保存:(RAND:6)
	CASE 1
		;包丁は水・風属性が出やすい
		CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性", (RAND:2 ? 1 # 2))
	CASE 2
		;オーブンは火・風属性が出やすい
		CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性", (RAND:2 ? 0 # 2))
	CASE 3
		;鍋は光・闇属性が出やすい
		CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性", (RAND:2 ? 4 # 5))
	CASE 4
		;フライパンは水・土属性が出やすい
		CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性", (RAND:2 ? 1 # 3))
	CASE 5
		;蒸し器は火・土属性が出やすい
		CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性", (RAND:2 ? 0 # 3))
ENDSELECT

;戦闘タイプ：食堂のメニューのコンボで出やすいものがある
IF RAND:3
	SELECTCASE 発動中コンボ名
		CASE "新鮮海鮮レストラン"
			;海鮮だと連撃・回避が出やすい
			CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", (RAND:2 ? 5 # 6))
		CASE "肉食べ放題レストラン"
			;肉だと攻撃・奥義が出やすい
			CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", (RAND:2 ? 1 # 7))
		CASE "健康野菜レストラン"
			;野菜だと回復・サポートが出やすい
			CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", (RAND:2 ? 3 # 4))
		CASE "満腹穀物レストラン"
			;穀物だと防御が出やすい
			SIF RAND:2
				CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", 2)
		CASEELSE
			;バランスの良い食事処だとバランスが出やすい
			SIF RAND:2
				CALL ランダムキャラデータ_SET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", 0)
	ENDSELECT
ENDIF

;残りは標準設定でランダム生成実行
CALL ランダムキャラ生成(Ｒキャラ追加候補記録, 管理番号)

;そっくりさん処理
IF そっくりさんフラグ && RAND:3 == 0
	CALL ランダムキャラそっくりさん適用(Ｒキャラ追加候補記録, 管理番号)
ENDIF

;メインパネル (キャラデータ表示処理)
表示文字列 += "<div rect='2125,156,3675,2812'>"

LOCALS '= ランダムキャラデータ_GETS(Ｒキャラ追加候補記録, 管理番号, "呼び名")
表示文字列 += @"■[従業員]%LOCALS, 18, LEFT%"

LOCAL = ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "属性")
表示文字列 += @"<font color='#%属性数値文字色変換_HTML(LOCAL)%'>"
表示文字列 += @"属性：%属性数値文字列変換(LOCAL)%</font>"

表示文字列 += @"　得意武器：%得意武器表示文字列(ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "得意武器"))%"
表示文字列 += @"　タイプ：%成長タイプ:(ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存"))%"
表示文字列 += "<br><br>"

表示文字列 += "□ 情報 □---------------------------------------------------------------<br>"

表示文字列 += @"　種族：%GET_TALENTNAME(200, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 200))%"
LOCALS '= ランダムキャラデータ_GETS_B(Ｒキャラ追加候補記録, 管理番号, "CSTR", "特殊種族")
ワーウルフフラグ = 0
IF LOCALS != ""
	表示文字列 += @" [%LOCALS%]"
	SIF LOCALS == "ワーウルフ"
		ワーウルフフラグ = 1
ENDIF

追加表示格納 '= ""
SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "角あり")
	追加表示格納 += "（角あり）"
SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "尻尾あり")
	追加表示格納 += "（尻尾）"
SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "エルーン耳")
	追加表示格納 += "（エルーン耳）"
SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "ハーヴィン耳")
	追加表示格納 += "（ハーヴィン耳）"
SELECTCASE ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "発情期あり")
	CASE IS > 0
		追加表示格納 += "（発情期あり）"
	CASE IS < 0
		追加表示格納 += "（発情期なし）"
ENDSELECT
表示文字列 += REPLACE(追加表示格納, "）（", "／")
表示文字列 += "<br>"

表示文字列 += "　性別："
追加表示格納 '= ""
Ｒキャラ性別 = ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "性別")
SELECTCASE Ｒキャラ性別
	CASE 1
		表示文字列 += "女性"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "処女")
			追加表示格納 += "（処女）"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "Ａ処女")
			追加表示格納 += "（Ａ処女）"
	CASE 2
		表示文字列 += "男性"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "非童貞") == 0
			追加表示格納 += "（童貞）"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "Ａ処女")
			追加表示格納 += "（Ａ処女）"
	CASE 3
		表示文字列 += "ふたなり"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "非童貞") == 0
			追加表示格納 += "（童貞）"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "処女")
			追加表示格納 += "（処女）"
		SIF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "Ａ処女")
			追加表示格納 += "（Ａ処女）"
	CASEELSE
		表示文字列 += "その他"
ENDSELECT
表示文字列 += REPLACE(追加表示格納, "）（", "／")
表示文字列 += "<br>"

IF GETBIT(Ｒキャラ性別, 0)
	;女性・ふたなりはバストサイズの表示
	表示文字列 += @"　バストサイズ：%GET_TALENTNAME(205, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 205))%　　"
ENDIF

表示文字列 += @"　性知識Ｌｖ："
;性知識：処女/童貞、年齢層、エルーンかどうかで分布が異なる
SELECTCASE ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "知識素質", "性知識")
	CASE -2
		表示文字列 += "無知"
	CASE -1
		表示文字列 += "獣並み"
	CASE 0
		IF ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "性的興味") > 0
			表示文字列 += "耳年増"
		ELSE
			表示文字列 += "疎い"
		ENDIF
	CASE 1
		表示文字列 += "必要十分"
	CASE 2
		表示文字列 += "有識者"
ENDSELECT
表示文字列 += "<br>"

表示文字列 += @"　容姿：%GET_TALENTNAME(160, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 160)), 14, LEFT%"

LOCAL:0 = ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "年齢層")
LOCAL:1 = ランダムキャラデータ_GET_B(Ｒキャラ追加候補記録, 管理番号, "素質", "外見年齢")
表示文字列 += @"年齢：%GET_年齢文字列(Ｒキャラ性別, LOCAL:0, LOCAL:1), 17, LEFT%"
表示文字列 += "<br>"

表示文字列 += @"　体格：%GET_TALENTNAME(161, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 161)), 14, LEFT%"

表示文字列 += @"体型：%GET_TALENTNAME(163, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 163))%"
表示文字列 += "<br>"

IF Ｒキャラ性別 == 3
	;男性器サイズの表示はふたなりのみ
	表示文字列 += @"　男性器サイズ：%GET_TALENTNAME(162, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", 162))%"
ENDIF
表示文字列 += "<br><br>"

表示文字列 += "□ 素質 □---------------------------------------------------------------<br>"

素質行格納 '= "　"
FOR LOCAL, 6, 160
	LOCALS '= GET_TALENTNAME(LOCAL, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", LOCAL))
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	;男の時、V感覚はスキップ
	SIF LOCAL == 102 && !GETBIT(Ｒキャラ性別, 0)
		CONTINUE

	IF (STRLENS(HTML_TOPLAINTEXT(素質行格納 + LOCALS)) + 2) / (3625 / 50) > 0
		表示文字列 += 素質行格納 + "<br>"
		素質行格納 '= "　"
	ENDIF
	素質行格納 += @"[%LOCALS%]"
NEXT
FOR LOCAL, 220, 300
	LOCALS '= GET_TALENTNAME(LOCAL, ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "素質", LOCAL))
	SIF STRLENS(LOCALS) <= 0
		CONTINUE

	IF (STRLENS(HTML_TOPLAINTEXT(素質行格納 + LOCALS)) + 2) / (3625 / 50) > 0
		表示文字列 += 素質行格納 + "<br>"
		素質行格納 '= "　"
	ENDIF
	素質行格納 += @"[%LOCALS%]"
NEXT
FOR LOCAL, 0, 10
	LOCALS '= ランダムキャラデータ_GETS_B(Ｒキャラ追加候補記録, 管理番号, "CSTR", @"固有素質{LOCAL}")
	SIF STRLENS(LOCALS) <= 0
		CONTINUE

	IF (STRLENS(HTML_TOPLAINTEXT(素質行格納 + LOCALS)) + 2) / (3625 / 50) > 0
		表示文字列 += 素質行格納 + "<br>"
		素質行格納 '= "　"
	ENDIF
	素質行格納 += @"[%LOCALS%]"
NEXT
FOR LOCAL, 1, 100
	LOCAL:1 = ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "知識素質", LOCAL)
	SIF LOCAL:1 == 0
		CONTINUE
	LOCALS '= ERDNAME(知識素質, LOCAL)
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCALS += @"Lv{LOCAL:1}"

	IF (STRLENS(HTML_TOPLAINTEXT(素質行格納 + LOCALS)) + 2) / (3625 / 50) > 0
		表示文字列 += 素質行格納 + "<br>"
		素質行格納 '= "　"
	ENDIF
	素質行格納 += @"[%LOCALS%]"
NEXT
表示文字列 += 素質行格納 + "<br><br>"

表示文字列 += "□ 能力 □---------------------------------------------------------------<br>"
改行フラグ = 0
FOR LOCAL, 0, 30
	;非表示
	SIF GROUPMATCH(LOCAL, 16, 17, 21, 22, 23, 24, 25, 26, 27, 28)
		CONTINUE

	LOCAL:1 = ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "能力", LOCAL)
	SIF LOCAL:1 == 0
		CONTINUE
	IF 改行フラグ == 3
		表示文字列 += "<br>"
		改行フラグ = 0
	ENDIF
	表示文字列 += @"%ABLNAME:LOCAL, 16%:{LOCAL:1, 3, LEFT} "
	改行フラグ ++
NEXT
FOR LOCAL, 21, 30
	;非表示
	SIF GROUPMATCH(LOCAL, 23, 25, 26, 28)
		CONTINUE

	LOCAL:1 = ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "能力", LOCAL)
	SIF LOCAL:1 == 0
		CONTINUE
	IF 改行フラグ == 3
		表示文字列 += "<br>"
		改行フラグ = 0
	ENDIF
	表示文字列 += @"%ABLNAME:LOCAL, 16%:{LOCAL:1, 3, LEFT} "
	改行フラグ ++
NEXT
表示文字列 += "<br><br>"

表示文字列 += "□ 経験 □---------------------------------------------------------------<br>"
改行フラグ = 0
FOR LOCAL, 0, 200
	;肉体熟知は別枠
	SIF LOCAL == 89
		CONTINUE

	LOCAL:1 = ランダムキャラデータ_GET(Ｒキャラ追加候補記録, 管理番号, "経験", LOCAL)
	SIF LOCAL:1 == 0
		CONTINUE
	IF 改行フラグ == 3
		表示文字列 += "<br>"
		改行フラグ = 0
	ENDIF
	表示文字列 += @"%EXPNAME:LOCAL, 16%:{LOCAL:1, 3, LEFT} "
	改行フラグ ++
NEXT
表示文字列 += "</div>"

$表示_LOOP
;上パネル (汎用立ち絵表示)
LOCALS '= ランダムキャラデータ_GETS_B(Ｒキャラ追加候補記録, 管理番号, "CSTR", "汎用立ち絵登録文字列")
追加表示格納 '= "<div rect='81,31,5860,1200' padding='6' border='31' bcolor='#C0C0C0'>"
追加表示格納 += "<br>"
IF OPTION変数:汎用立ち絵制御
	追加表示格納 += @"<button value='120'>[120] 汎用立ち絵 ON / <font color='#%カラーパレット_HTML("グレーアウト")%'>OFF</font></button>"
	追加表示格納 += "<br>"
	追加表示格納 += @"<button value='130'>[130] ワーウルフの立ち絵を変更する<br>　　　（現在：\@ OPTION変数:ワーウルフ画像ケモ度? ケモより# 人より\@）</button>"
	追加表示格納 += "<br>"
ELSE
	追加表示格納 += @"<button value='120'>[120] 汎用立ち絵 <font color='#%カラーパレット_HTML("グレーアウト")%'>ON</font> / OFF</button>"
	追加表示格納 += "<br>"
ENDIF
追加表示格納 += "</div>"
IF OPTION変数:汎用立ち絵制御
	追加表示格納 += "<div rect='2050,31,2400,1200' padding='6' border='31' bcolor='#C0C0C0'>"
	IF LOCALS == ""
		追加表示格納 += "<img src='顔ノーイメ' height='1125'>"
	ELSE
		SPRITEDISPOSE @"顔グラ3000"
		GDISPOSE 3000
		IF STRFIND(LOCALS, "そっくりさん設定_") == 0
			CALL GCREATE_拡張子(3000, @"%REPLACE(LOCALS, "そっくりさん設定_", "")%/顔_デフォルト")
		ELSE
			CALL GCREATE_拡張子(3000, @"汎用グラフィック/顔_%LOCALS%")
		ENDIF
		画像フラグ:0 = RESULT
		SPRITEDISPOSE @"短冊グラ3000"
		GDISPOSE 3001
		IF STRFIND(LOCALS, "そっくりさん設定_") == 0
			CALL GCREATE_拡張子(3001, @"%REPLACE(LOCALS, "そっくりさん設定_", "")%/短冊_デフォルト")
		ELSE
			CALL GCREATE_拡張子(3001, @"汎用グラフィック/短冊_%LOCALS%")
		ENDIF
		画像フラグ:1 = RESULT
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF !ワーウルフフラグ
			追加表示格納 += "<clearbutton>"
		SIF 画像フラグ:0 && スプライト作成(@"顔グラ3000", 3000, 0)
			追加表示格納 += @"<button value='130' title='%LOCALS%'><img src='顔グラ3000'height='1125'></button>"
		SIF 画像フラグ:1 && スプライト作成(@"短冊グラ3000", 3001, 0)
			追加表示格納 += @"<button value='130' title='%LOCALS%'><img src='短冊グラ3000'height='1125'></button>"
		SIF !ワーウルフフラグ
			追加表示格納 += "</clearbutton>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
	ENDIF
	追加表示格納 += "</div>"
ENDIF
HTML_PRINT 追加表示格納
FOR LOCAL, 0, 10
	PRINTL
NEXT

HTML_PRINT 表示文字列
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 100
		追加キャラ番号 = -1
		FOR LOCAL, 0, 従業員部屋数
			IF 従業員部屋割り配列:LOCAL < 1
				追加キャラ番号 = CHARANUM
				従業員部屋割り配列:LOCAL = 追加キャラ番号
				BREAK
			ENDIF
		NEXT
		IF 追加キャラ番号 < 0
			PRINTW 従業員部屋が一杯です。
			RETURN 0
		ENDIF

		ADDCHARA 999
		CALL ランダムキャラデータ復元(Ｒキャラ追加候補記録, 追加キャラ番号, 管理番号)

		NAME:追加キャラ番号 = [従業員]%CALLNAME:追加キャラ番号%
		TALENT:追加キャラ番号:従業員 = 1
		PRINTFORMW %NAME:追加キャラ番号%を雇用しました。

		CALL ADD_PERSON_FROM_CHARA(追加キャラ番号)
		CALL コマンド存在チェック
		CALL キャラクター初期化(追加キャラ番号)

		CFLAG:追加キャラ番号:滞在期間 = 999
		CFLAG:追加キャラ番号:招待不可フラグ = 0
		CFLAG:追加キャラ番号:探索参加許可フラグ = 1
		CALL ステータス記録_ランダムキャラデータベース(Ｒキャラ追加候補記録, 管理番号, CFLAG:追加キャラ番号:人物番号)
		CALL 従業員人数チェック()
		RETURN 0
	CASE 110
		GOTO 従業員_LOOP
	CASE 120
		INVERTBIT OPTION変数:汎用立ち絵制御, 0
		GOTO 表示_LOOP
	CASE 130
		INVERTBIT OPTION変数:ワーウルフ画像ケモ度, 0
		GOTO 表示_LOOP
	CASE 131
		$そっくりさんLOOP
		DRAWLINE
		PRINTL ランダムキャラの立ち絵に既存キャラの立ち絵を適用することが出来ます。
		PRINTL 使用する場合、ランダムキャラ生成時に33％の確率でそっくりさんが出現します。
		PRINTL
		PRINT ・そっくりさん立ち絵を使用 
		IF そっくりさんフラグ == 0
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[する]", 0
			RESETCOLOR
			PRINTPLAIN  / [しない]
		ELSE
			PRINTPLAIN [する] / 
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[しない]", 0
			RESETCOLOR
		ENDIF
		PRINTL
		IF そっくりさんフラグ
			PRINT ・そっくりさん候補になる画像枚数
			PRINTFORM 　　{そっくりさん画像枚数_閾値, 2}枚　
			PRINTBUTTON "[変更]", 1
			PRINTL
		ENDIF
		PRINTBUTTON "[999] 戻る", 999
		PRINTL

		PRINTL
		BINPUT
		PRINTL
		PRINTL
		SELECTCASE RESULT
			CASE 0
				INVERTBIT そっくりさんフラグ, 0
				GOTO そっくりさんLOOP
			CASE 1
				$枚数LOOP
				DRAWLINE
				PRINTL そっくりさん候補になる画像の最低枚数を設定出来ます。
				PRINTL 1以上の数値を入力してください。デフォルトは10枚以上です。
				PRINTL
				INPUT
				PRINTL
				IF RESULT < 1
					PRINTW 0以下の数値は入力出来ません。
					GOTO 枚数LOOP
				ENDIF
				そっくりさん画像枚数_閾値 = RESULT
				GOTO そっくりさんLOOP
			CASE 999
				IF そっくりさんフラグ
					VARSET LOCAL
					FOR 候補キャラ, 1, CHARANUM
						;ランダムキャラは対象にしない
						SIF NO:候補キャラ == 999
							CONTINUE
						;画像枚数OK？
						SIF GET_RESOURCES_FILES(CSTR:候補キャラ:画像フォルダ, "*.webp", 1) < そっくりさん画像枚数_閾値
							CONTINUE
						IF CSVTALENT(NO:候補キャラ, GETNUM(TALENT, "性別")) != 2
							そっくりさん候補配列_非男:LOCAL = 候補キャラ
							LOCAL ++
						ELSE
							そっくりさん候補配列_男:(LOCAL:1) = 候補キャラ
							LOCAL:1 ++
						ENDIF
					NEXT
				ENDIF
				GOTO 従業員_LOOP
		ENDSELECT
	CASE 999
		RETURN 0
	CASEELSE
ENDSELECT


@ステータス記録_ランダムキャラデータベース(登録元ＤＢ名, 登録元管理番号, 人物番号)
;引き継ぎ処理時に使う初期値の記録用
#DIMS 登録元ＤＢ名
#DIM 登録元管理番号
#DIM 人物番号
#DIM 登録先管理番号
#DIM ランダムキャラレコード, ランダムキャラレコード最大数
#DIM ランダムキャラレコード行数
#DIM ループ用

CALL ランダムキャラデータ_NEW(Ｒキャラ初期値記録, 人物番号)
登録先管理番号 = RESULT

ランダムキャラレコード行数 = DT_SELECT(登録元ＤＢ名, @"管理番号 = {登録元管理番号} And カテゴリ <> '人物番号'", , ランダムキャラレコード)
SIF ランダムキャラレコード行数 > ランダムキャラレコード最大数
	THROW ランダムキャラレコードの行数が多すぎます。開発者に連絡し、要素数の拡張を依頼してください。

FOR ループ用, 0, ランダムキャラレコード行数
	IF DT_CELL_ISNULL(登録元ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1) == 0
		{
		DT_ROW_ADD Ｒキャラ初期値記録, "管理番号", 登録先管理番号,
			"カテゴリ", DT_CELL_GETS(登録元ＤＢ名, ランダムキャラレコード:ループ用, "カテゴリ", 1),
			"項目", DT_CELL_GET(登録元ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1),
			"値_数値", DT_CELL_GET(登録元ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		}
	ELSEIF DT_CELL_ISNULL(登録元ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1) == 0
		{
		DT_ROW_ADD Ｒキャラ初期値記録, "管理番号", 登録先管理番号,
			"カテゴリ", DT_CELL_GETS(登録元ＤＢ名, ランダムキャラレコード:ループ用, "カテゴリ", 1),
			"項目", DT_CELL_GET(登録元ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1),
			"値_文字列", DT_CELL_GETS(登録元ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		}
	ENDIF
NEXT
