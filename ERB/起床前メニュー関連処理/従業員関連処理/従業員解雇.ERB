@従業員解雇
#DIM ループ用
#DIM 従業員一覧, 500
#DIM ページ数
#DIM 選択従業員番号
#DIM 降順ソート用配列, 200
#DIM 降順ソート用番号配列, 200
#DIMS Arrays = "降順ソート用配列", "降順ソート用番号配列"

VARSET 従業員一覧

DRAWLINE
PRINTL 従業員を解雇します。
PRINTL 解雇したキャラは好感度要素：信頼度が下がりますので注意してください。
PRINTL
DRAWLINE

;現在いる従業員を一覧表示
LOCAL:1 = 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
FOR LOCAL, ページ数 * 17, ページ数 * 17 + 17
	IF 従業員一覧:LOCAL < 1
		LOCALS += "<br>"
		CONTINUE
	ENDIF
	LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[%現在仕事数字文字列変換(現在仕事:(従業員一覧:LOCAL):0, 0, 従業員一覧:LOCAL)%]</button>"
	LOCALS += "<br>"
NEXT
IF ページ数 > 0
	LOCALS += "<button value='1001'>　＜ 前ページ</button>"
ELSE
	LOCALS += "　　　　　　　 "
ENDIF
LOCALS += "　　　　"
IF 従業員一覧:(ページ数 * 17 + 17) > 0
	LOCALS += "<button value='1002'>次ページ ＞</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,156,3675,2812'>"
IF 選択従業員番号 > 0
	LOCALS += @"■%NAME表示(従業員一覧:選択従業員番号), 26, LEFT%"
	LOCALS:1 = %属性数値文字色変換_HTML(基礎BATTLE_STATE:(従業員一覧:選択従業員番号):属性)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(基礎BATTLE_STATE:(従業員一覧:選択従業員番号):属性)%</font>"
	LOCALS += @"　得意武器：%得意武器表示文字列(基礎BATTLE_STATE:(従業員一覧:選択従業員番号):得意武器)%"
	SIF 基礎BATTLE_STATE:(従業員一覧:選択従業員番号):ステータスタイプ保存 > -1
		LOCALS += @"　タイプ：%成長タイプ:(基礎BATTLE_STATE:(従業員一覧:選択従業員番号):ステータスタイプ保存)%"
	LOCALS += "<br><br>"
	LOCALS += "□ 情報 □---------------------------------------------------------------<br>"
	LOCALS += @"　種族：%GET_TALENTNAME(200, TALENT:(従業員一覧:選択従業員番号):種族,,従業員一覧:選択従業員番号)%"
	IF TALENT:(従業員一覧:選択従業員番号):エルーン耳
		LOCALS += "（エルーン耳）"
	ENDIF
	IF TALENT:(従業員一覧:選択従業員番号):尻尾あり
		LOCALS += "（尻尾）"
	ENDIF
	IF TALENT:(従業員一覧:選択従業員番号):角あり
		LOCALS += "（角あり）"
	ENDIF
	IF TALENT:(従業員一覧:選択従業員番号):ハーヴィン耳
		LOCALS += "（ハーヴィン耳）"
	ENDIF
	LOCALS += "<br>"
	LOCALS += "　性別："
	SELECTCASE TALENT:(従業員一覧:選択従業員番号):性別
		CASE 1
			LOCALS += "女性"
			IF TALENT:(従業員一覧:選択従業員番号):処女 == 0
				LOCALS += "　　　　　　　　　"
			ELSE
				LOCALS += "（処女）　　　　　"
			ENDIF
		CASE 3
			LOCALS += "ふたなり"
			IF TALENT:(従業員一覧:選択従業員番号):処女 == 0
				LOCALS += "　　　　"
			ELSE
				LOCALS += "（処女）"
			ENDIF
			IF TALENT:(従業員一覧:選択従業員番号):非童貞 == 1
				LOCALS += "　　　　　"
			ELSE
				LOCALS += "（童貞）　"
			ENDIF
	ENDSELECT
	LOCALS += @"バストサイズ：%GET_TALENTNAME(205, TALENT:(従業員一覧:選択従業員番号):バストサイズ,,従業員一覧:選択従業員番号)%"
	LOCALS += "<br>"
	LOCALS += @"　性知識レベル：%性知識段階表示(知識素質:(従業員一覧:選択従業員番号):性知識, 従業員一覧:選択従業員番号)%"
	IF TALENT:(従業員一覧:選択従業員番号):ランダムキャラ
		LOCALS += @"<br>　容姿：%GET_TALENTNAME(160, TALENT:(従業員一覧:選択従業員番号):容姿,,従業員一覧:選択従業員番号)%　　"
		LOCALS += @"体格：%GET_TALENTNAME(161,TALENT:(従業員一覧:選択従業員番号):体格,,従業員一覧:選択従業員番号)%"
		IF TALENT:(従業員一覧:選択従業員番号):性別 == 3
			LOCALS += @"<br>　男性器サイズ：%GET_TALENTNAME(162, TALENT:(従業員一覧:選択従業員番号):男性器サイズ,,従業員一覧:選択従業員番号)%"
		ENDIF
	ENDIF
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "□ 素質 □---------------------------------------------------------------<br>"
	
	FOR ループ用,8,160
		IF STRLENS(GET_TALENTNAME(ループ用,TALENT:(従業員一覧:選択従業員番号):ループ用, 0,従業員一覧:選択従業員番号))
			LOCALS += @"[%GET_TALENTNAME(ループ用,TALENT:(従業員一覧:選択従業員番号):ループ用, 0,従業員一覧:選択従業員番号)%]"
		ENDIF
	NEXT
	FOR ループ用,220,300
		IF STRLENS(GET_TALENTNAME(ループ用,TALENT:(従業員一覧:選択従業員番号):ループ用,,従業員一覧:選択従業員番号))
			LOCALS += @"[%GET_TALENTNAME(ループ用,TALENT:(従業員一覧:選択従業員番号):ループ用,,従業員一覧:選択従業員番号)%]"
		ENDIF
	NEXT
	FOR ループ用,1,100
		IF 知識素質:(従業員一覧:選択従業員番号):ループ用
			LOCALS += @"[%ERDNAME(知識素質,ループ用)%Lv{知識素質:(従業員一覧:選択従業員番号):ループ用}]"
		ENDIF
	NEXT

	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "□ 能力 □---------------------------------------------------------------<br>"
	LOCAL:1 = 0
	LOCAL:2 = 0
	VARSET 降順ソート用配列
	VARSET 降順ソート用番号配列
	FOR LOCAL, 0, 40
		SIF LOCAL >= 21 && LOCAL <= 27
			CONTINUE
		IF ABL:(従業員一覧:選択従業員番号):LOCAL > 0
			降順ソート用配列:(LOCAL:1) = ABL:(従業員一覧:選択従業員番号):LOCAL
			降順ソート用番号配列:(LOCAL:1) = LOCAL
			LOCAL:1 += 1
		ENDIF
	NEXT
	ARRAYMSORTEX 降順ソート用配列, Arrays, 0
	
	FOR LOCAL, 0, 6
		IF 降順ソート用配列:LOCAL < 1
			SIF LOCAL:2
				LOCALS += "<br>"
			BREAK
		ENDIF
		LOCALS += @"%ABLNAME:(降順ソート用番号配列:LOCAL), 16%:{ABL:(従業員一覧:選択従業員番号):(降順ソート用番号配列:LOCAL), 2}   "
		LOCAL:2 += 1
		IF LOCAL == 5
			LOCALS += "他<br>"
		ELSEIF LOCAL:2 == 3
			LOCALS += "<br>"
			LOCAL:2 = 0
		ENDIF
	NEXT
	LOCALS += "<br>"
	LOCALS += "□ 経験 □---------------------------------------------------------------<br>"
	LOCAL:1 = 0
	LOCAL:2 = 0
	VARSET 降順ソート用配列
	VARSET 降順ソート用番号配列
	FOR LOCAL, 0, 95
		SIF LOCAL == 89
			CONTINUE
		IF EXP:(従業員一覧:選択従業員番号):LOCAL > 0
			降順ソート用配列:(LOCAL:1) = EXP:(従業員一覧:選択従業員番号):LOCAL
			降順ソート用番号配列:(LOCAL:1) = LOCAL
			LOCAL:1 += 1
		ENDIF
	NEXT
	ARRAYMSORTEX 降順ソート用配列, Arrays, 0
	
	FOR LOCAL, 0, 6
		IF 降順ソート用配列:LOCAL < 1
			SIF LOCAL:2
				LOCALS += "<br>"
			BREAK
		ENDIF
		LOCALS += @"%GET_EXPNAME(降順ソート用番号配列:LOCAL, 従業員一覧:選択従業員番号), 16%:{EXP:(従業員一覧:選択従業員番号):(降順ソート用番号配列:LOCAL), 3}   "
		LOCAL:2 += 1
		IF LOCAL == 5
			LOCALS += "他<br>"
		ELSEIF LOCAL:2 == 3
			LOCALS += "<br>"
			LOCAL:2 = 0
		ENDIF
	NEXT
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<button value='1000'>[1000]このキャラを解雇する</button>"
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1 TO 499
		選択従業員番号 = LOCAL
		RESTART
	CASE 999
		選択従業員番号 = 0
		ページ数 = 0
		RETURN 0
	CASE 1001
		ページ数 -= 1
		RESTART
	CASE 1002
		ページ数 += 1
		RESTART
	CASE 1000
		PRINTL
		PRINTFORML 本当に%NAME表示((従業員一覧:選択従業員番号))%を解雇しますか？
		PRINTBUTTON "[0]はい", 0
		PRINTL
		PRINTBUTTON "[1]いいえ", 1
		PRINTL
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CALL 解雇実処理(従業員一覧:選択従業員番号)
				選択従業員番号 = 0
				RETURN 0
			CASE 1
				RESTART
		ENDSELECT
ENDSELECT


@解雇実処理(キャラ番号)
#DIM キャラ番号
#DIM ループ用
#DIM 要素最大値
#DIM 計算後レベル

IF TALENT:キャラ番号:従業員 == 0
	PRINTFORMW %CALLNAME:キャラ番号%は従業員ではありません。
	RETURN -1
ENDIF

; IF なんか解雇不可の条件があったら後からここに付け足す
; 	PRINTFORMW %CALLNAME:キャラ番号%は解雇できません。
; 	RETURN 0
; ENDIF

PRINTFORMW %NAME表示(キャラ番号)%を解雇しました。
TRYCALLFORM KOJO_EVENT_従業員解雇_{NO:キャラ番号}(キャラ番号)

TALENT:キャラ番号:従業員 = 0
IF TALENT:キャラ番号:定住者
ELSE
	;残り１日にすることで、起床時に滞在を終えて帰っていく
	;なので部屋割りは考えなくてヨシ
	CFLAG:キャラ番号:滞在期間 = 1
	IF STRFIND(NAME:キャラ番号, "[従業員]") > -1
		NAME:キャラ番号 = [一般人]%CALLNAME:キャラ番号%
	ENDIF
	FOR LOCAL, 0, 従業員部屋数
		IF 従業員部屋割り配列:LOCAL == キャラ番号
			従業員部屋割り配列:LOCAL = 0
			BREAK
		ENDIF
	NEXT
ENDIF
現在仕事:キャラ番号:0 = 0
現在仕事:キャラ番号:1 = 0
現在制服:キャラ番号:0 = 0
CFLAG:キャラ番号:招待不可フラグ = 0

;信頼度低下10000
CFLAG:キャラ番号:信頼度 = MAX(CFLAG:キャラ番号:信頼度 - 10000, 0)
PRINTFORML %CALLNAME:キャラ番号%の信頼度がダウンしました…
;好感度レベルの再計算

要素最大値 = MAX(CFLAG:キャラ番号:友情度, CFLAG:キャラ番号:信頼度, CFLAG:キャラ番号:恋心度, CFLAG:キャラ番号:性愛度)
FOR ループ用, 0, 100
	IF 経験値テーブル:ループ用 >= 要素最大値
		BREAK
	ENDIF
NEXT
計算後レベル = ループ用

IF CFLAG:キャラ番号:好感度レベル > 計算後レベル
	IF あなた特殊能力:フレンドリーセンス
		PRINTFORML %CALLNAME:キャラ番号%の好感度レベルが{CFLAG:キャラ番号:好感度レベル - 計算後レベル}ダウンしました…
	ELSE
		PRINTFORML %CALLNAME:キャラ番号%の好感度レベルが下がりました…
	ENDIF
	CFLAG:キャラ番号:好感度レベル = 計算後レベル
ENDIF

CALL 従業員人数チェック()
CALL ユニーク客人数チェック()

WAIT
RETURN 1
