@従業員仕事割り振り
#DIM 従業員一覧, 500
#DIM ソート配列, 500
#DIM ページ数
#DIM 変更キャラ
#DIMS 屋号保存
#DIM ソートタイプ
#DIMS 改行判定文字列
#DIM 空き店舗あり
#DIMS 常駐業務名
#DIM 固有仕事数
#DIMS 固有仕事配列, 50
#DIMS 固有仕事名
#DIM 固有仕事キー

変更キャラ = -1

$表示ループ
VARSET 従業員一覧, -1
VARSET ソート配列
VARSET 仕事人数表示用

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,3762' border='31' bcolor='#C0C0C0'></div><div rect='2050,710,3875,3080' border='31' bcolor='#C0C0C0'></div>

;現在いる従業員を配列へ
従業員一覧:0 = 0
LOCAL:1 = 1
仕事人数表示用:(現在仕事:0:0) += 1
ソート配列:0 = 現在仕事:0:0 * 100 + 現在仕事:0:1 + 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	SIF 現在仕事:LOCAL:0 >= 0 && 現在仕事:LOCAL:0 < 10000
		仕事人数表示用:(現在仕事:(従業員一覧:(LOCAL:1)):0) += 1
	IF 現在仕事:LOCAL:0 == -1
		ソート配列:(LOCAL:1) = 99999
	ELSE
		ソート配列:(LOCAL:1) = 現在仕事:LOCAL:0 * 100 + 現在仕事:LOCAL:1 + 1
	ENDIF
	LOCAL:1 += 1
NEXT
IF ソートタイプ
	ARRAYMSORT ソート配列, 従業員一覧
ENDIF

;一覧表示
LOCALS += "<div rect='156,156,1737,3662'><nobr>"
FOR LOCAL, ページ数 * 17, ページ数 * 17 + 17
	IF 従業員一覧:LOCAL < 0
		LOCALS += "<br>"
		CONTINUE
	ENDIF
	IF 現在仕事:(従業員一覧:LOCAL):0 == -1
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'><nonbutton>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[商店経営]</nonbutton></font>"
	ELSE
		SIF 変更キャラ == 従業員一覧:LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>"
		LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[%現在仕事数字文字列変換(現在仕事:(従業員一覧:LOCAL):0, 0, 従業員一覧:LOCAL)%"
		IF 現在仕事:(従業員一覧:LOCAL):0 == 2
			LOCALS = %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 4)%
			IF 現在仕事:(従業員一覧:LOCAL):1 >= 10003 && 現在仕事:(従業員一覧:LOCAL):1 <= 10010
				屋号保存 = 『%空艇港店舗屋号記録:(現在仕事:(従業員一覧:LOCAL):1 % 10000)%』
			ELSE
				屋号保存 = 
			ENDIF
			LOCALS += @"：%MAP_GET("常駐業務名", REPLACE(GETPLACENAME(現在仕事:(従業員一覧:LOCAL):1 / 10000, 現在仕事:(従業員一覧:LOCAL):1 % 10000), "『.*』", ""))%%屋号保存%"
		ELSEIF (現在仕事:(従業員一覧:LOCAL):0 == 8 || 現在仕事:(従業員一覧:LOCAL):0 == 7)
			LOCALS += "："
			SIF 現在仕事:(従業員一覧:LOCAL):1 > 0
				LOCALS += @"%ERDNAME(知識素質, 現在仕事:(従業員一覧:LOCAL):1)%"
		ENDIF
		LOCALS += "]</button>"
		SIF 変更キャラ == 従業員一覧:LOCAL
			LOCALS += @"</font>"
	ENDIF
	LOCALS += "<br>"
NEXT
IF ページ数 > 0
	LOCALS += "<button value='1000'>　＜ 前ページ</button>"
ELSE
	LOCALS += "　　　　　　　 "
ENDIF
LOCALS += "　　　　"
IF 従業員一覧:(ページ数 * 17 + 17) > -1
	LOCALS += "<button value='1001'>次ページ ＞</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>　　"
IF ソートタイプ
	LOCALS += "仕事順 <button value='998'>[切替]</button>"
ELSE
	LOCALS += "番号順 <button value='998'>[切替]</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "</nobr></div>"


;仕事の種類と現在そこに従事している人数を表示
LOCALS += "<div rect='2125,770,3675,2812'><nobr>"
IF 変更キャラ > -1
	LOCALS += @"現在従事している業務：%NAME:変更キャラ%<br>"
	LOCALS += @"■%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 0, 変更キャラ), 16, LEFT%<br><br>"
	LOCALS += @"%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 1, 変更キャラ)%<br><br>"
	;仕事レベルが存在するなら表示
	IF 現在仕事:変更キャラ:0 == 2 && 現在仕事:変更キャラ:1 > 0
		常駐業務名 '= MAP_GET("常駐業務名", REPLACE(GETPLACENAME(現在仕事:変更キャラ:1 / 10000, 現在仕事:変更キャラ:1 % 10000), "『.*』", ""))
		LOCALS += @"現在の%常駐業務名%レベル：{常駐業務レベル:変更キャラ:(GETNUM(常駐業務レベル, 常駐業務名))}"
	ELSEIF 仕事レベル存在チェック(現在仕事:変更キャラ:0)
		LOCALS += @"現在の%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 0, 変更キャラ)%レベル：{仕事レベル:変更キャラ:(現在仕事:変更キャラ:0)}"
	ELSEIF 現在仕事:変更キャラ:0 == 9
		LOCALS += @"現在の知識素質：料理人レベル：{知識素質:変更キャラ:料理人}"
	ELSE
		LOCALS += @"仕事レベルなし"
	ENDIF
ELSE
	LOCALS += "仕事内容と従事人数一覧<br><br>"
	FOR LOCAL, 0, 50
		IF 現在仕事数字文字列変換(LOCAL) != ""
			LOCALS += @"■%現在仕事数字文字列変換(LOCAL), 16, LEFT%：従事人数{仕事人数表示用:LOCAL,3}人<br>"
		ENDIF
	NEXT
ENDIF
LOCALS += "</nobr></div>"

;仕事切り替え用枠
LOCALS += "<div rect='2050,31,3875,650' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
IF 変更キャラ > -1
	改行判定文字列 = 
	FOR LOCAL, 0, 50
		IF 現在仕事数字文字列変換(LOCAL) != ""
			SIF 仕事条件判定関数(変更キャラ, LOCAL) == 0
				CONTINUE
			IF LOCAL > 0
				IF STRLENS(改行判定文字列 + 現在仕事数字文字列変換(LOCAL)) > 75
					LOCALS += "<br>"
					改行判定文字列 = 
				ELSE
					LOCALS += " / "
					改行判定文字列 += " / "
				ENDIF
			ENDIF
			IF 現在仕事:変更キャラ:0 == LOCAL
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[%現在仕事数字文字列変換(LOCAL)%]</font>"
				改行判定文字列 += @"[%現在仕事数字文字列変換(LOCAL)%]"
			ELSE
				LOCALS += @"<button value='{LOCAL + 2000}'>[%現在仕事数字文字列変換(LOCAL)%]</button>"
				改行判定文字列 += @"[%現在仕事数字文字列変換(LOCAL)%]"
			ENDIF
		ENDIF
	NEXT

	VARSET RESULTS
	固有仕事数 = ENUMFUNCBEGINSWITH(@"固有仕事設定_{NO:変更キャラ}_")
	ARRAYCOPY "RESULTS", "固有仕事配列"
	IF 固有仕事数 > 0
		FOR LOCAL, 0, 固有仕事数
			SIF GETMETH(固有仕事配列:LOCAL, 0, 変更キャラ, "条件判定") == 0
				CONTINUE
			CALLFORMF %固有仕事配列:LOCAL%(変更キャラ, "仕事名")
			固有仕事名 '= TSTR:コマンド名受渡
			固有仕事キー = TOINT(REPLACE(固有仕事配列:LOCAL, @"固有仕事設定_{NO:変更キャラ}_", ""))
			SIF 固有仕事キー < 0
				CONTINUE
			IF STRLENS(改行判定文字列 + 固有仕事名) > 75
				LOCALS += "<br>"
				改行判定文字列 = 
			ELSE
				LOCALS += " / "
				改行判定文字列 += " / "
			ENDIF
			IF 現在仕事:変更キャラ:0 == 固有仕事キー + 10000
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[%固有仕事名%]</font>"
				改行判定文字列 += @"[%固有仕事名%]"
			ELSE
				LOCALS += @"<button value='{固有仕事キー + 10000}'>[%固有仕事名%]</button>"
				改行判定文字列 += @"[%固有仕事名%]"
			ENDIF
		NEXT
	ENDIF

	空き店舗あり = 0
	;空き店舗を検索
	FOR LOCAL, 0, VARSIZE("商業区店割り配列", 1)
		IF 商業区店割り配列:LOCAL:店舗名 == "" || 商業区店割り配列:LOCAL:店員配列 == "-1_"
			空き店舗あり = 1
			BREAK
		ENDIF
	NEXT
	IF 空き店舗あり && 港改造度:0:0 && TALENT:変更キャラ:定住者 && EXISTFUNCTION(@"特殊店舗_建設条件_{NO:変更キャラ}")
		LOCALS += "<br><button value='997'>[商店を開かせる]</button>"
	ENDIF
ENDIF
LOCALS += "</div>"



HTML_PRINT LOCALS
FOR LOCAL, 0, 34
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE IS >= 10000
		現在仕事:変更キャラ:0 = LOCAL
		現在仕事:変更キャラ:1 = 0
	CASE IS >= 3000
		CALL 仕事追加処理関数(変更キャラ, LOCAL)
	CASE IS >= 2000
		現在仕事:変更キャラ:0 = LOCAL - 2000
		現在仕事:変更キャラ:1 = 0
	CASE 1000
		ページ数 -= 1
	CASE 1001
		ページ数 += 1
	CASE 999
		CALL 仕事切替時補正(変更キャラ)
		ページ数 = 0
		IF 高速船記録:派遣先登録 != ""
			CALLFORM 高速船派遣先関数_%高速船記録:派遣先登録%("派遣条件")
			IF RESULT == 0
				CALLFORM 高速船派遣先関数_%高速船記録:派遣先登録%("解説文")
				DRAWLINE
				HTML_PRINT TSTR:ツールチップ受渡
				PRINTL
				DRAWLINE
				PRINTL 以上の高速船派遣の条件を満たさなくなったため、派遣を中止します。
				PRINTL よろしいですか？
				PRINTBUTTONLC "[0] はい", 0
				PRINTBUTTONLC "[1] いいえ", 1
				PRINTL
				BINPUT
				IF RESULT == 0
					高速船記録:派遣先登録 = 
					高速船記録:派遣状態 = 
					PRINTL
					PRINTL
					PRINTW 派遣を中止しました。
				ELSE
					GOTO 表示ループ
				ENDIF
			ENDIF
		ENDIF
		RETURN 0
	CASE 998
		INVERTBIT ソートタイプ, 0
	CASE 997
		PRINTL
		PRINTL
		DRAWLINE
		PRINTL ※※※※※注意※※※※※
		PRINTFORML %CALLNAME:変更キャラ%が商業区に店舗を開くようになります。
		PRINTL 今後、通常の業務に就くことは出来なくなります。
		PRINTL 特殊店舗が用意されているキャラのみこの選択肢が出ますが、
		PRINTL 店舗を開く条件が整っているかどうかは別なのでご注意ください。
		DRAWLINE
		PRINTL
		PRINTFORML %NAME:変更キャラ%に商店を開くよう促しますか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTFORMW %NAME:変更キャラ%に商店を開く業務を命じました。
			現在仕事:変更キャラ:0 = -1
			RETURN 0
		ELSE
			RESTART
		ENDIF
	CASE 0 TO 499
		CALL 仕事切替時補正(変更キャラ)
		変更キャラ = 従業員一覧:LOCAL
		;CALL 従業員仕事割り振り_切り替え(従業員一覧:LOCAL)
ENDSELECT

GOTO 表示ループ

@仕事切替時補正(対象キャラ)
#DIM 対象キャラ

SIF 対象キャラ < 0
	RETURN 0

;キャラ切り替え時や戻るボタンを押した時、設定必須の項目が設定されてなかった場合
;仕事をなしに戻す
IF 現在仕事:対象キャラ:0 == 2 && 現在仕事:対象キャラ:1 == 0
	現在仕事:対象キャラ:0 = 0
ENDIF
IF 現在仕事:対象キャラ:0 == 7
	SIF 現在仕事:対象キャラ:1 == 0
		現在仕事:対象キャラ:0 = 0
	IF 知識素質:対象キャラ:(現在仕事:対象キャラ:1) > 1
		現在仕事:対象キャラ:0 = 0
		現在仕事:対象キャラ:1 = 0
	ENDIF
ENDIF
IF 現在仕事:対象キャラ:0 == 8
	SIF 現在仕事:対象キャラ:1 == 0
		現在仕事:対象キャラ:0 = 0
	IF 知識素質:対象キャラ:(現在仕事:対象キャラ:1) < 2
		現在仕事:対象キャラ:0 = 0
		現在仕事:対象キャラ:1 = 0
	ENDIF
ENDIF

@従業員仕事割り振り_切り替え(ARG)
#DIM 空き店舗あり

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += @"■%CALLNAME:ARG%の業務を<br>"
FOR LOCAL, 0, 50
	IF 現在仕事数字文字列変換(LOCAL) != ""
		SIF 仕事条件判定関数(ARG, LOCAL) == 0
			CONTINUE
		IF 現在仕事:ARG:0 == LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%</font><br>"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%に変更</button><br>"
		ENDIF
	ENDIF
NEXT
LOCALS += "<br>"

RESULT = 0
RESULTS = 
空き店舗あり = 0
;空き店舗を検索
FOR LOCAL, 0, VARSIZE("商業区店割り配列", 1)
	IF 商業区店割り配列:LOCAL:店舗名 == "" || 商業区店割り配列:LOCAL:店員配列 == "-1_"
		空き店舗あり = 1
		BREAK
	ENDIF
NEXT
IF 空き店舗あり && 港改造度:0:0 && TALENT:ARG:定住者 && EXISTFUNCTION(@"特殊店舗_建設条件_{NO:ARG}")
	LOCALS += "<button value='998'>[998] 商店を開かせる</button><br>"
	LOCALS += "<br>"
ENDIF

LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の説明を表示
LOCALS:1 = <div rect='2125,156,3675,2812'>
LOCALS:1 += "現在従事している業務<br>"
LOCALS:1 += @"■%現在仕事数字文字列変換(現在仕事:ARG:0, 0, ARG), 16, LEFT%"
LOCALS:1 += "</div>"
LOCALS:1 += "<div rect='2312,437,3675,2812'>"
LOCALS:1 += @"<nobr>%現在仕事数字文字列変換(現在仕事:ARG:0, 1, ARG)%</nobr>"
LOCALS:1 += "<br><br>"
;仕事レベルが存在するなら表示
IF 仕事レベル存在チェック(現在仕事:ARG:0)
	LOCALS:1 += @"現在の%現在仕事数字文字列変換(現在仕事:ARG:0, 0, ARG)%レベル：{仕事レベル:ARG:(現在仕事:ARG:0)}"
ELSEIF 現在仕事:ARG:0 == 9
	LOCALS:1 += @"現在の知識素質：料理人レベル：{知識素質:ARG:料理人}"
ELSE
	LOCALS:1 += @"仕事レベルなし"
ENDIF
LOCALS:1 += "</div>"


HTML_PRINT LOCALS, 1
HTML_PRINT LOCALS:1, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		IF 現在仕事:ARG:0 == 2 && 現在仕事:ARG:1 == 0
			現在仕事:ARG:0 = 0
		ENDIF
		IF 現在仕事:ARG:0 == 7
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) > 1
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		IF 現在仕事:ARG:0 == 8
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) < 2
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		RETURN 0
	CASE 998
		PRINTL
		PRINTL
		DRAWLINE
		PRINTL ※※※※※注意※※※※※
		PRINTFORML %CALLNAME:ARG%が商業区に店舗を開くようになります。
		PRINTL 今後、通常の業務に就くことは出来なくなります。
		PRINTL 特殊店舗が用意されているキャラのみこの選択肢が出ますが、
		PRINTL 店舗を開く条件が整っているかどうかは別なのでご注意ください。
		DRAWLINE
		PRINTL
		PRINTFORML %NAME:ARG%に商店を開くよう促しますか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTFORMW %NAME:ARG%に商店を開く業務を命じました。
			現在仕事:ARG:0 = -1
			RETURN 0
		ELSE
			RESTART
		ENDIF
	CASE 0 TO 11
		IF 仕事条件判定関数(ARG, LOCAL) == 0
			PRINTFORMW %CALLNAME:ARG%は%現在仕事数字文字列変換(LOCAL)%の仕事に付くことは出来ません。
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		現在仕事:ARG:0 = LOCAL
		現在仕事:ARG:1 = 0
		RESTART
	CASEELSE
		CALL 仕事追加処理関数(ARG, LOCAL)
		RESTART
ENDSELECT

@現在仕事数字文字列変換(ARG, ARG:1 = 0, キャラ番号 = 0)
#FUNCTIONS
#DIM キャラ番号

;固有業務
IF ARG >= 10000
	IF ARG:1
		CALLFORMF 固有仕事設定_{NO:キャラ番号}_{ARG - 10000}(キャラ番号, "仕事解説")
		RETURNF TSTR:ツールチップ受渡
	ENDIF
	CALLFORMF 固有仕事設定_{NO:キャラ番号}_{ARG - 10000}(キャラ番号, "仕事名")
	RETURNF TSTR:コマンド名受渡
ENDIF

SELECTCASE ARG
	CASE -1
		IF ARG:1
			RETURNF "港エリアにて、商店を経営してもらっている状態。<br>効果：特になし<br>　　　通常の店舗持ちキャラと同じように行動する。"
		ENDIF
		RETURNF "商店経営"
	CASE 0
		IF ARG:1
			RETURNF "特に仕事を申し付けず、待機している状態。<br>効果：特になし<br>　　　通常の客と同じように行動する。"
		ENDIF
		RETURNF "なし"
	CASE 1
		IF ARG:1
			RETURNF "リゾート内の細々とした雑務を処理する業務。<br>効果：一日の最後に、全ての場所の雑務ゲージが<br>　　　［（仕事レベル＋１）＾２×１００］減少する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "雑務"
	CASE 2
		IF ARG:1
			LOCALS = 特定の場所で専門的な仕事を行う業務。<br>効果：一日の最後に、ルピが<br>　　　［滞在者数×（仕事レベル＋１）＾３］増加する。
			LOCALS += "<br><br>"
			IF 現在仕事:キャラ番号:1 == 2
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3002] ロビー ：現在常駐場所</font><br>"
			ELSE
				LOCALS += @"<button value='3002'>[3002] ロビー でロビー受付として働く</button><br>"
			ENDIF
			IF 施設改造度:5:0
				IF 現在仕事:キャラ番号:1 == 503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3503] バーエリア ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='3503'>[3503] バーエリア でバーテンダーとして働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:5:6
				IF 現在仕事:キャラ番号:1 == 506
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3506] 図書室 ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='3506'>[3506] 図書室 で司書として働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:15:0
				IF 現在仕事:キャラ番号:1 == 1503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[4503] プールサイド ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='4503'>[4503] プールサイド で監視員として働く</button><br>"
				ENDIF
			ELSE
				IF 現在仕事:キャラ番号:1 == 15
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3015] プール ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='3015'>[3015] プール で監視員として働く</button><br>"
				ENDIF
			ENDIF
			IF 空艇港初来訪イベントフラグ
				FOR LOCAL, 3, 9
					IF 空艇港店舗状況:LOCAL:0 > 0
						CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:LOCAL:0}(LOCAL)
						IF 現在仕事:キャラ番号:1 == LOCAL + 10000
							LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% ：現在常駐場所</font><br>"
						ELSE
							LOCALS += @"<button value='{10000 + LOCAL}'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% で店員として働く</button><br>"
						ENDIF
					ENDIF
				NEXT
			ENDIF
			RETURNF LOCALS
		ENDIF
		RETURNF "常駐業務"
	CASE 3
		IF ARG:1
			RETURNF @"%CALLNAME:PLAYER%の補佐を行う業務。<br>効果：一日の最後に、好感度要素：信頼度が<br>　　　［（仕事レベル＋１）＾２×２００］増加する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "秘書"
	CASE 4
		IF ARG:1
			RETURNF @"手紙や広告などでリゾートを宣伝する業務。<br>効果：一日の最後に、リゾートの人気度が<br>　　　［（仕事レベル＋１）＾２×１］増加する。<br>　　　従業員部屋の自室でチラシ作りなどを行う。"
		ENDIF
		RETURNF "宣伝"
	CASE 5
		IF 施設改造度:3:8 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"訓練場で戦闘能力を鍛える業務。<br>効果：一日の最後にEXPを獲得する。<br>　　　獲得量は全キャラ中最も高いレベルに応じて変動する。<br>　　　（全キャラ中最も高いレベルより上には成長しない）<br>　　　訓練場に常駐する。"
		ENDIF
		RETURNF "訓練"
	CASE 6
		IF あなた特殊能力:ダンジョン探索PRO < 1
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"クリア済みのダンジョンに赴き、素材を集めてくる業務。<br>効果：一日の最後に素材やEXPを獲得する。<br>　　　獲得量は赴いたダンジョンによって変動する。<br>　　　（赴くダンジョンの設定は「探索へ出かける」から行う）<br>　　　%CALLNAME:MASTER%が探索をしていない日は効果が増加する。<br>　　　日中はリゾートにおらず、夕方以降は自室にいる。<br>　　　この業務に業務レベルは存在しない。"
		ENDIF
		RETURNF "自動探索"
	CASE 7
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 学校に赴き、指定された知識について学習する業務。<br>効果：午前と午後の学習終了時に知識経験値を得る。<br>　　　学校にいない時間帯は自室にいる。<br>　　　Lv2に到達した知識は選択出来ない。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL > 1
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})</font>　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})選択</font>　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 3000}'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})</button>　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "知識習得"
	CASE 8
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 学校に赴き、指定された知識について講義を行う業務。<br>効果：学校で学ぶキャラの学習効率が上昇する。<br>　　　学校にいない時間帯は自室にいる。<br>　　　Lv2以上の知識のみ選択可能。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL < 2
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})</font>　　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})選択</font>　　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 3000}'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})</button>　　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "教師"
	CASE 9
		IF ARG:1
			RETURNF "リゾート内の食堂で料理を担当する業務。<br>効果：料理メニューの効果によるZP収入が<br>　　　［（知識素質：料理人レベル＋１）✕５］％増加する。<br>　　　複数人の効果は加算されるが、上限は<br>　　　［滞在人数÷１０］％まで。（モブ・ユニーク合算）<br>　　　料理人レベルがマイナスのキャラをこの業務に就けてはいけない。"
		ENDIF
		RETURNF "調理師"
	CASE 10
		IF ARG:1
			RETURNF "リゾート内の食堂で給仕を担当する業務。<br>効果：料理メニューの効果によるルピ収入が<br>　　　［（仕事レベル＋１）✕５］％増加する。<br>　　　複数人の効果は加算されるが、上限は<br>［滞在人数÷１０］％まで。（モブ・ユニーク合算）"
		ENDIF
		RETURNF "給仕"
	CASE 11
		IF ARG:1
			RETURNF @"高速船に乗り込み、派遣先に向かう業務。<br>効果：派遣先により変化。"
		ENDIF
		RETURNF "高速船員"
ENDSELECT

@仕事条件判定関数(キャラ番号, 仕事番号)
#FUNCTION
#DIM キャラ番号
#DIM 仕事番号

SELECTCASE 仕事番号
	CASE 2,3,11
		SIF キャラ番号 == PLAYER
			RETURNF 0
	CASE 5,6
		SIF キャラ番号 == PLAYER
			RETURNF 0
		SIF TALENT:キャラ番号:非戦闘キャラ
			RETURNF 0
ENDSELECT

RETURNF 1


@仕事追加処理関数(キャラ番号, 選択番号)
#DIM キャラ番号
#DIM 選択番号

SELECTCASE 現在仕事:キャラ番号:0
	CASE 2
		;常駐する店舗を選べる
		IF 選択番号 > 10000
			現在仕事:キャラ番号:1 = 選択番号
		ELSE
			現在仕事:キャラ番号:1 = 選択番号 - 3000
		ENDIF
	CASE 7
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 3000
	CASE 8
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 3000
ENDSELECT

@仕事レベル存在チェック(チェック番号)
#FUNCTION
#DIM チェック番号

IF GROUPMATCH(チェック番号, 1, 2, 3, 4, 8, 10)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@従業員休暇判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ

;ゲーム中に変動しない数値として、人物番号を使う
IF DAY % 7 == CFLAG:対象キャラ:人物番号 % 7
	RETURNF 1
ENDIF

RETURNF 0
