@ITEM_BUY
PRINTFORML {ITEMSALES:62}
CALL ITEM_SALES
PRINT_ITEM
PRINTFORML 購入するアイテムを選んでください 所持金:{MONEY}%MONEYLABEL%
CALL SHOW_ITEM
PRINT [100] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF ITEMSTOCK(RESULT) == 1 || ITEMSTOCK(RESULT) == 4 || LOCAL < 0 || LOCAL > 2000
	RESTART
ELSEIF ITEMSTOCK(RESULT) == 2
	PRINTW 売切です
	RESTART
ELSEIF ITEMSTOCK(RESULT) == 3
	PRINTW 所持金が足りません
	RESTART
ELSEIF ITEMSTOCK(RESULT) == 5
	PRINTW 既に十分持っています
	RESTART
ENDIF
LOCAL = RESULT

IF LOCAL == 70
	CALL CHOICE("【元気になる薬】を購入しますか？", "はい", "いいえ")
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？
	PRINTL [0] - 戻る
	FOR LOCAL:1,1,CHARANUM
		IF BASE:(LOCAL:1):0 < MAXBASE:(LOCAL:1):0
			PRINTFORM [{LOCAL:1}] - %CALLNAME:(LOCAL:1)% 
			CALL PRINT_BASE("体力",LOCAL:1,0,32)
			PRINTL
		ENDIF
	NEXT
	$INPUT_LOOP_70
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF RESULT <= 0 || RESULT >= CHARANUM || BASE:RESULT:0 == MAXBASE:RESULT:0
		GOTO INPUT_LOOP_70
	ELSE
		PRINTFORMW %CALLNAME:RESULT%の体力+300
		MONEY -= ITEMPRICE:LOCAL
		BASE:RESULT:0 += 300
		SIF BASE:RESULT:0 > MAXBASE:RESULT:0
			BASE:RESULT:0 = MAXBASE:RESULT:0
	ENDIF
	RESTART
ENDIF
IF LOCAL == 71
	CALL CHOICE("【薬の材料(双)】を購入しますか？", "はい", "いいえ")
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？
	PRINTL [0] - 戻る
	FOR LOCAL:1,1,CHARANUM
		SIF TALENT:(LOCAL:1):2 == 1
			PRINTFORML [{LOCAL:1}] - %CALLNAME:(LOCAL:1)% 
	NEXT
	$INPUT_LOOP_71
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF RESULT <= 0 || RESULT >= CHARANUM || TALENT:RESULT:2 != 1
		GOTO INPUT_LOOP_71
	ELSE
		PRINTFORMW %CALLNAME:RESULT%はふたなりになりました
		MONEY -= ITEMPRICE:LOCAL
		TALENT:RESULT:2 |= 2
		MAXBASE:RESULT:2 = 10000
	ENDIF
	RESTART
ENDIF
IF LOCAL == 72
	CALL CHOICE("【薬の材料(消)】を購入しますか？", "はい", "いいえ")
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？
	PRINTL [0] - 戻る
	FOR LOCAL:1,1,CHARANUM
		SIF TALENT:(LOCAL:1):2 == 3
			PRINTFORML [{LOCAL:1}] - %CALLNAME:(LOCAL:1)% 
	NEXT
	$INPUT_LOOP_72
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF RESULT <= 0 || RESULT >= CHARANUM || TALENT:RESULT:2 != 3
		GOTO INPUT_LOOP_72
	ELSE
		PRINTFORMW %CALLNAME:RESULT%は女の子になりました
		MONEY -= ITEMPRICE:LOCAL
		TALENT:RESULT:2 = 1
		MAXBASE:RESULT:射精 = 0
	ENDIF
	RESTART
ENDIF
IF LOCAL == 73
	CALL CHOICE("【青いキャンディ】を購入しますか？", "はい", "いいえ")
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？
	PRINTL [0] - 戻る
	FOR LOCAL:1,1,CHARANUM
		SIF !TALENT:(LOCAL:1):母乳体質
			PRINTFORML [{LOCAL:1}] - %CALLNAME:(LOCAL:1)% 
	NEXT
	$INPUT_LOOP_73
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF RESULT <= 0 || RESULT >= CHARANUM || TALENT:RESULT:149
		GOTO INPUT_LOOP_73
	ELSE
		PRINTFORMW %CALLNAME:RESULT%は[母乳体質]を得ました
		MONEY -= ITEMPRICE:LOCAL
		TALENT:RESULT:母乳体質 = 1
	ENDIF
	RESTART
ENDIF
IF LOCAL == 74
	CALL CHOICE("【赤いキャンディ】を購入しますか？", "はい", "いいえ")
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？
	PRINTL [0] - 戻る
	FOR LOCAL:1,1,CHARANUM
		SIF !TALENT:(LOCAL:1):0 && !TALENT:(LOCAL:1):1
			PRINTFORML [{LOCAL:1}] - %CALLNAME:(LOCAL:1)% 
	NEXT
	$INPUT_LOOP_74
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF RESULT <= 0 || RESULT >= CHARANUM || (TALENT:RESULT:0 || TALENT:RESULT:1)
		GOTO INPUT_LOOP_74
	ELSE
		PRINTFORMW %CALLNAME:RESULT%の処女膜が再生しました
		MONEY -= ITEMPRICE:LOCAL
		TALENT:RESULT:0 = 2
	ENDIF
	RESTART
ENDIF
IF LOCAL == 41
	MONEY -= ITEMPRICE:LOCAL
	ITEM:LOCAL ++
	PRINTW 媚薬を精製しました
	RESTART
ENDIF
;複数所持アイテム
SELECTCASE LOCAL
	CASE 40 TO 60, 91, 92,
		PRINTFORM %ITEMNAME:LOCAL%をいくつ購入しますか？
		SIF LOCAL == 50
			PRINTFORM (あと{ITEMSALES:LOCAL}個)
		PRINTFORML 
		;金欠、アイテム数の超過はあとで合わせる
		PRINTL [0] - やめる
		PRINT [1] - 1　
		PRINT [5] - 5　
		PRINT [10] - 10　
		PRINT [20] - 20　
		PRINT [50] - 50　
		PRINT [99] - 買えるだけ
		INPUT
		SIF RESULT <= 0
			RESTART
		SIF RESULT * ITEMPRICE:LOCAL > MONEY
			RESULT = MONEY / ITEMPRICE:LOCAL
		;個数限定
		IF ITEMSALES:LOCAL
			IF RESULT >= ITEMSALES:LOCAL
				RESULT = ITEMSALES:LOCAL
				ITEMSALES:LOCAL = -1
			ELSE
				ITEMSALES:LOCAL -= RESULT
			ENDIF
		ELSE
			ITEM:LOCAL += RESULT
			IF ITEM:LOCAL > 99
				RESULT = 99 - (ITEM:LOCAL - RESULT)
				ITEM:LOCAL = 99
			ENDIF
		ENDIF
		PRINTFORMW %ITEMNAME:LOCAL%を{RESULT}個、{RESULT * ITEMPRICE:LOCAL}%MONEYLABEL%で購入しました
		MONEY -= RESULT * ITEMPRICE:LOCAL
		RESTART
;単品アイテム
	CASE 0 TO 39
		PRINTFORML %ITEMNAME:LOCAL%を購入しますか？
		PRINTL [0] - はい  [1] - いいえ
		INPUT
		SIF RESULT
			RESTART
		ITEM:LOCAL ++
		ITEMSALES:LOCAL --
		MONEY -= ITEMPRICE:LOCAL
		RESTART
	CASE 1000 TO 2000
		PRINTFORML %ITEMNAME:LOCAL%を購入しますか？
		PRINTL [0] - はい  [1] - いいえ
		INPUT
		SIF RESULT
			RESTART
		ITEM:LOCAL ++
		ITEMSALES:LOCAL --
		MONEY -= ITEMPRICE:LOCAL
		RESTART
ENDSELECT

@SHOW_ITEM
DRAWLINE
LOCAL:2 = 0
FOR LOCAL,0,100
	;表示しない
	SIF ITEMSTOCK(LOCAL) == 1
		CONTINUE
	SIF ITEMSTOCK(LOCAL) == 4
		CONTINUE
	LOCALS = [{LOCAL}]%ITEMNAME:(LOCAL)% ({ITEMPRICE:(LOCAL)}%MONEYLABEL%)
	;売り切れ
	IF ITEMSTOCK(LOCAL) == 2
		LOCALS = [{LOCAL}]%ITEMNAME:LOCAL% (売切)
		SETCOLOR 150,150,150
	ELSEIF ITEMSTOCK(LOCAL) == 5
		LOCALS = [{LOCAL}]%ITEMNAME:LOCAL%
		SETCOLOR 150,150,150
	ENDIF
	PRINTFORM %LOCALS,28,LEFT%
	RESETCOLOR
	LOCAL:2 += 1
	SIF LOCAL:2 % 3 == 0
		PRINTL
NEXT
PRINTL

;条件付きアイテムの処理
@ITEM_SALES
;ITEMSALES = 0 購入可
;ITEMSALES = -1 売切
;ITEMSALES = -2 封印
;技巧
IF ABL:MASTER:技巧 < 5
	ITEMSALES:63 = 0
ELSE
	ITEMSALES:63 = -2
ENDIF
IF ABL:MASTER:技巧
	ITEMSALES:64 = 0
ELSE
	ITEMSALES:64 = -2
ENDIF

SIF TALENT:MASTER:禁断の知識
	ITEMSALES:62 = -1
SIF 知識素質:MASTER:調合知識
	ITEMSALES:61 = -1
SIF TALENT:MASTER:汚臭耐性 == 2
	ITEMSALES:60 = -1
;媚薬
	IF ITEM:41
		ITEMSALES:41 = -1
	ELSE
		ITEMSALES:41 = 0
	ENDIF

;禁断
FOR LOCAL,70,100
	;売切の場合はそのまま
	SIF ITEMSALES:LOCAL == -1
		CONTINUE
	ITEMSALES:LOCAL = -2
	;とりあえず排卵誘発剤と避妊薬は条件無しに
	SIF LOCAL == 91 || LOCAL == 92
		ITEMSALES:LOCAL = 0
	;禁断の知識
	IF (LOCAL > 70 && LOCAL < 77) || LOCAL == 90
		SIF TALENT:MASTER:禁断の知識
			ITEMSALES:LOCAL = 0
	;調合知識
	ELSEIF LOCAL == 70
		SIF 知識素質:MASTER:調合知識
			ITEMSALES:LOCAL = 0
	ENDIF
NEXT

