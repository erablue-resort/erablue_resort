;==================================================================================================
@性癖開拓チャレンジ処理_ゆきずりセックス(ARG)
;-------------------------------------------------
;ゆきずりセックスを取得するための処理
;--------------------------------------------------------------------------------------------------
#DIM 興味度
;--------------------------------------------------------------------------------------------------
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「ゆきずりの性交を愉しんでいる女性」を描いたエロ本が渡るように小細工した…<br><br>"

;固有処理
RESULT = 0
CALL 口上変数初期化
TRYCALLFORM 性癖開拓_固有条件_ゆきずりセックス_{NO:ARG}(ARG)
LOCAL = RESULT
SIF 口上有無確認(0, 1)
	CSTR:ARG:性癖開拓報告_記録文字列 += @"性癖開拓_固有条件"
IF LOCAL == 1
	CSTR:ARG:性癖開拓報告_記録文字列 += @"<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[ゆきずりセックス(興味)]を取得した<br>"
	性癖素質:ARG:ゆきずりセックス = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSEIF LOCAL == -1
	RETURN 0
ENDIF


;---------------------------------------
;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 性知識条件_性癖開拓チャレンジ()
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は10%
LOCAL = 10

;興味度を初期化
興味度 = 10

;--- プラス補正 ------------------------
;[性欲強い]             … +10%
;[好奇心]               … +10%
;現在の性欲値が1500以上 … +10%
;[ABL:欲望]             … 欲望 * 10%
;一番多い性経験         … 経験数10につき+1%

;マイナス補正が激しい性癖の為、調教による補正が高め


;好奇心が影響
SIF TALENT:ARG:性的興味 > 0
	興味度 += 10

;性欲強いが影響
SIF TALENT:ARG:性欲 > 0
	興味度 += 10

;現在の性欲値が影響
SIF BASE:ARG:性欲 > 1500
	興味度 += 10

;欲望が影響
興味度 += ABL:ARG:欲望 * 10

;それぞれの性交経験のうち一番多い経験が影響
興味度 += MAX(EXP:ARG:Ｖ性交経験, EXP:ARG:Ａ性交経験, EXP:ARG:手淫経験, EXP:ARG:口淫経験, EXP:ARG:Ｃ性交経験_Ｖ挿入, EXP:ARG:Ｃ性交経験_Ａ挿入) / 10


;--- マイナス補正 ----------------------
;[プライドが高い] …  -5%
;[恥じらい]       … -10%
;[保守的]         … -10%
;[性欲薄い]       … -10%
;[自制心]         … -20%
;[抵抗]           … -20%
;[一線を越えない] … -20%
;[貞操観念]       … -30%
;全部盛で-125%

;プライドが影響
SIF TALENT:ARG:プライド > 0
	興味度 -= 5

;恥じらいが影響
SIF TALENT:ARG:羞恥心 > 0
	興味度 -= 10

;保守的が影響
SIF TALENT:ARG:性的興味 < 0
	興味度 -= 10

;性欲薄いが影響
SIF TALENT:ARG:性欲 == -1
	興味度 -= 10

;自制心なら大きく影響
SIF TALENT:ARG:自制心 > 1
	興味度 -= 20

;抵抗持ちなら大きく影響
SIF TALENT:ARG:抵抗
	興味度 -= 20

;一線を越えない持ちなら大きく影響
SIF TALENT:ARG:一線越えない
	興味度 -= 20

;貞操観念ならかなり大きく影響
SIF TALENT:ARG:貞操
	興味度 -= 30


;--- 最終補正 --------------------------

;最低確率1割は確保
LOCAL = MAX(LOCAL, 興味度)

;[処女][恋人持ち][人妻][未亡人][想い人あり]なら最終確率を半減させる
SIF TALENT:ARG:処女 || TALENT:ARG:恋人持ち
	LOCAL /= 2

;叡智の殿堂なら最終確率に直接加算
SIF 高速船記録:派遣先登録 == "叡智の殿堂"
	LOCAL += 10

;経験豊富、淫乱であるなら確定
SIF TALENT:ARG:経験豊富 || TALENT:ARG:淫乱
	LOCAL = 100

;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
	
;現在は女性しか読まない
SIF TALENT:ARG:性別 != 1
	LOCAL = -1


;---------------------------------------

;補正確率より低ければ読む
IF RAND:100 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	
	;性別で分岐
	IF TALENT:ARG:性別 == 1
		CSTR:ARG:性癖開拓報告_記録文字列 += @"そこには出会ったばかりの見ず知らずの男に股を開き、インスタントに快楽を享受する雌の姿が描かれていた。<br>"
	ELSE  
		CSTR:ARG:性癖開拓報告_記録文字列 += @"そこには出会ったばかりの見ず知らずの雌をオナホ代わりに、インスタントに性欲を発散する男の姿が描かれていた。<br>"
	ENDIF
	
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	
	;貞操観念で分岐
	IF TALENT:ARG:貞操 == 1
		CSTR:ARG:性癖開拓報告_記録文字列 += @"貞操を重んじる%CALLNAME:ARG%は、初めこそその姿に嫌悪感を抱いたが……<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"気軽に性を貪るその姿にどこか憧れに似た惹かれるものを感じ、ページをめくる手を止められずにいた。<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"ページをめくる毎に%CALLNAME:ARG%の心の根底にあった貞操観念が崩れていく……<br>"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += @"ページをめくる毎に%CALLNAME:ARG%の価値観が淫靡な方向へアップデートされていく……<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"そして%CALLNAME:ARG%は気付いてしまった。<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"セックスは「何時でも」「何処でも」「誰とでも」出来る行為だという事に……！<br>"
	ENDIF
	
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[ゆきずりセックス(興味)]を取得した<br>"
	性癖素質:ARG:ゆきずりセックス = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]を取得した</font>","うふふ")
	RETURN 1
	
ELSEIF 興味度 <= 0
	;興味が0もしくはマイナスなら読みもしない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
	
ELSE
	;興味が少しでもあれば読むけど癖の扉は開かない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"手に取って愉しんだようだが、新たな性癖になるまでには至らなかったようだ……<br>"

ENDIF

RETURN 0

;==================================================================================================
@性癖段階非同室成長_ゆきずりセックス(キャラ番号)
;-------------------------------------------------
;ゆきずりセックスの性癖レベルの上昇処理
;--------------------------------------------------------------------------------------------------
#DIM キャラ番号
;--------------------------------------------------------------------------------------------------

SELECTCASE 性癖素質:キャラ番号:ゆきずりセックス
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:ゆきずりセックス経験 < 30
			RETURN 0
		性癖素質:キャラ番号:ゆきずりセックス = 2
		;取得メッセージ
		DRAWLINE
		IF 高貴知識_初期所持判定(キャラ番号) >= 1
			PRINTFORML 立場のある身でありながら、ほんのひとときの快楽のために軽率に体を許し血を穢す行い。
			PRINTFORML 見ず知らずの相手に本来秘すべき玉体を晒し、激しく貪られることで得る高貴なカラダの価値の再認識。
			PRINTL
			PRINTFORML 尊い%TEXTR("血統/先祖/血筋")%への冒涜。その背徳快楽の味を知ってしまった%CALLNAME:キャラ番号%は、もはやただの興味とはいえない程にこの行いにハマりつつあるようだ…
		ELSEIF アイドル判定(キャラ番号)
			PRINTFORML ファンがどれだけの財を貢ごうとも拝むことすら叶わない体。
			PRINTFORML その触れ難き柔肌を、垂涎の唇を、極上の雌穴を見ず知らずの相手に晒し、差し出し、隅々まで貪らせる放蕩。
			PRINTFORML アイドルとしてファンに夢を見せる立場でありながら、ほんのひとときの快楽のために股を開く裏切り行為。
			PRINTL
			PRINTFORML アイドルとしての商品を棄損し、女としての価値すらをも打ち捨てる破滅的な陶酔快楽の味を知ってしまった%CALLNAME:キャラ番号%は、
			PRINTFORML もはやただの興味とはいえない程にこの行いにハマりつつあるようだ…
		ELSE
			PRINTFORML %CALLNAME:キャラ番号%はゆきずりの相手とのお手軽交尾を繰り返したことで、初対面の相手であろうと股を開くことに抵抗感が無くなってしまったようだ…
		ENDIF
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[ゆきずりセックス(興味)]は[ゆきずりセックス(愛好)]に成長した
		IF TALENT:キャラ番号:貞操 == 1
			PRINTFORML %CALLNAME:キャラ番号%は[貞操観念]を失った…
			TALENT:キャラ番号:貞操 = 0
		ENDIF
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]が[ゆきずりセックス(愛好)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:ゆきずりセックス経験 < 100
			RETURN 0
		性癖素質:キャラ番号:ゆきずりセックス = 3
		;取得メッセージ
		DRAWLINE
		IF 高貴知識_初期所持判定(キャラ番号) >= 1
			PRINTFORML 故郷から離れた地であるのをいいことに、立場を脱ぎ捨てその血の責務をすら放り投げて。
			PRINTFORML 羽目を外して誰彼構わずハメまくる奔放な快楽の虜になってしまった%CALLNAME:キャラ番号%は、
			PRINTFORML その高貴な唇や貴い雌穴を、身分も立場もはるかに劣る民草が欲望を吐き散らすための便器さながらに差し出すことにすら陶酔した快楽を覚えるようになってしまった…
		ELSEIF アイドル判定(キャラ番号)
			PRINTFORML ファンの目がないのをいいことに、立場を脱ぎ捨てアイドルとしてのプロ意識すら放り投げて。
			PRINTFORML ファンから貢がれ稼いだ財を、ときに相手に差し出してまで快楽を貪る淫蕩。
			PRINTL
			PRINTFORML すっかりこの刹那的な快楽の虜になってしまった%CALLNAME:キャラ番号%にとって、もはやアイドルとして磨きぬいてきたその体は
			PRINTFORML ファンではなく一夜の相手を魅了し誘う、性快楽の誘蛾灯となり果ててしまった…
		ELSE
			PRINTFORML もはや%CALLNAME:キャラ番号%は、手頃な相手で一期一会の快楽を貪る行為が生活の一部になってしまっている…
		ENDIF
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[ゆきずりセックス(愛好)]は[ゆきずりセックス(中毒)]に成長した
		IF TALENT:キャラ番号:貞操 == 0
			PRINTFORML ゆきずりで持った関係ももはや百を数え、客の間でも「誰にでも股を開く」などと噂される%CALLNAME:キャラ番号%は[貞操無頓着]を得た
			TALENT:キャラ番号:貞操 = -1
		ENDIF
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(愛好)]が[ゆきずりセックス(中毒)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

