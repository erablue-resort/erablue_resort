;==================================================================================================
@ゆきずりセックス立ち絵更新(ARG, TYPE="顔_")
#FUNCTIONS
;-------------------------------------------------
;ゆきずりセックスを行っているか確認し、グラ表示用一時文字列を生成する。
;--------------------------------------------------------------------------------------------------
#DIM 立ち絵乱数
#DIM 物陰エフェクト
#DIMS TYPE
#DIMS 切り替え用文字列
#DIMS 文字列記憶
#DIMS D_NAME
#DIMS 体位
;--------------------------------------------------------------------------------------------------
VARSET LOCAL
VARSET 差分エフェクト

立ち絵乱数 = RAND:2
D_NAME '= ""

;顔グラか短冊か確認
切り替え用文字列 '= CSTR:ARG:顔グラ表示切り替え用文字列
SIF TYPE == "短冊_"
	切り替え用文字列 '= CSTR:ARG:短冊グラ表示切り替え用文字列

;ゆきずりセックス中であるか確認
IF IS_ゆきずりセックス中(ARG)
	;体位を受け渡す
	体位 = %立ち絵存在確認＆代用差替(ARG, 切り替え用文字列, CSTR:ARG:ゆきずりセックス_プレイ内容, TYPE)%
	
	;ゆきずりセックス中は常に欲情エフェクト
	;差分エフェクト:0:0 '= "エフェクト_" + TYPE + "欲情"
	;SIF 物陰エフェクト
	;	差分エフェクト:0:1 '= "エフェクト_" + TYPE + "隠密"

	SIF 差分抽出(ARG, TYPE, 切り替え用文字列, 体位) && D_NAME == ""
		D_NAME '= RESULTS
	;差分が見つかればそれを、なければ差分無しを返す
	SIF D_NAME != ""
		RETURNF D_NAME
	RETURNF ""
	
ELSE
	;ゆきずりセックス中ではない場合は何も返さない
	RETURNF ""
ENDIF

;==================================================================================================
@立ち絵存在確認＆代用差替(ARG, 切り替え用文字列, 体位, TYPE)
#FUNCTIONS
;-------------------------------------------------
;体位に対して画像が存在するか確認し、無いなら代用の画像に差し替える
;--------------------------------------------------------------------------------------------------
#DIMS 切り替え用文字列
#DIMS 体位
#DIMS TYPE
;--------------------------------------------------------------------------------------------------
;覗き差分、覗きバレ差分があればそれを返す
SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
	CASE 1 ;Ｖ性交
		IF TCVAR:ARG:覗きバレ && ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗きバレセックス*.*")
			RETURNF "覗きバレセックス"
		ELSEIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗きセックス*.*")
			RETURNF "覗きセックス"
		ENDIF
	CASE 2 ;Ａ性交
		IF TCVAR:ARG:覗きバレ && ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗きバレＡセックス*.*")
			RETURNF "覗きバレＡセックス"
		ELSEIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗きＡセックス*.*")
			RETURNF "覗きＡセックス"
		ENDIF
	CASEELSE ;奉仕
		IF TCVAR:ARG:覗きバレ && ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗きバレ奉仕*.*")
			RETURNF "覗きバレ奉仕"
		ELSEIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_覗き奉仕*.*")
			RETURNF "覗き奉仕"
		ENDIF
ENDSELECT


;覗き差分、覗きバレ差分がない場合は既存の体位の画像を探す

;画像有無の確認、ないなら体位を差し替える
IF !ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%*.*")
	SELECTCASE 体位
		;Ｖ正常位グループ
		CASE "対面座位", "騎乗位", "屈曲位", "駅弁", "対面立位"
			体位 = 正常位
			
		;Ｖ後背位グループ
		CASE "寝バック", "背面座位", "側位", "背面駅弁", "立ちバック"
			体位 = 後背位
			
		;Ａ正常位グループ
		CASE  "Ａ対面座位", "Ａ騎乗位", "Ａ屈曲位", "Ａ駅弁", "Ａ対面立位"
			体位 = Ａ正常位
				
		;Ａ後背位グループ
		CASE "Ａ寝バック", "Ａ背面座位", "Ａ側位", "Ａ背面駅弁", "Ａ立ちバック"
			体位 = Ａ後背位
		
	ENDSELECT
ENDIF

;Ｖ性交で画像がなければエフェクトを表示
IF TCVAR:ARG:ゆきずりセックス_体位 == 1
	SIF !ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%*.*")
		差分エフェクト:0 '= "エフェクト_" + TYPE + "挿入"
ENDIF


;射精差分
IF !TCVAR:ARG:ゆきずりセックス_絶頂値
	SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
		CASE 1 ;Ｖ性交
			;体位ごとの射精差分がなければ汎用画像を選択
			SIF !ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%膣内射精*.*")
				体位 = 膣内射精
			;汎用画像もないならエフェクトを表示
			IF !ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%*.*")
				IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2
					差分エフェクト:0 '= "エフェクト_" + TYPE + "妊娠膣内射精"
				ELSE
					差分エフェクト:0 '= "エフェクト_" + TYPE + "膣内射精"
				ENDIF
			ENDIF
				
		CASE 2 ;Ａ性交
			SIF !ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%腸内射精*.*")
				体位 = 腸内射精
			
		CASEELSE ;奉仕
			SELECTCASE 体位
				CASE "手コキ"
					SIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_手に射精*.*")
						体位 = 手に射精
					
				CASE "フェラチオ"
					IF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_口内射精*.*")
						体位 = 口内射精
					ELSE
						;射精エフェクトを表示
						差分エフェクト:0 '= "エフェクト_" + TYPE + "口内射精"
					ENDIF
			ENDSELECT
			
	ENDSELECT
	
ENDIF


;最終的に現在の体位がないなら欲情にする
SIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%切り替え用文字列%_差分_%体位%*.*") == 0
	体位 = 欲情

RETURNF 体位
