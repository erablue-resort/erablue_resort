
@指輪個別文章表示_大盗賊の指輪
LOCALS = 
LOCALS += @"■大盗賊の指輪<br><br>"
LOCALS += "伝説の大盗賊が装着していたとされる曰く付きの希少な指輪。<br>"
LOCALS += "通常攻撃時、まれにその対象にドロップ率UP効果を付与することがある。<br>"
LOCALS += "装備していると、権力者や常識に反抗したくなる…気がする。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 75000

@指輪基礎効果文章表示_大盗賊の指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+300 最大ＭＰ+300 攻撃力+300 行動速度+600<br>"
LOCALS += "TA率+30% 命中率+10% ときどきドロップ率UP<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_大盗賊の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 4
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＳＲ
ENDSELECT

@指輪ステータス決定_大盗賊の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_300_最大ＭＰ_300_攻撃力_300_行動速度_600_TA率_30_命中率_10

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 3
ENDIF

;オプション数に応じてオプション効果をランダムで付加

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(3)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "大盗賊の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)


@固有通常攻撃後処理_指輪_大盗賊の指輪(全滅フラグ, 連撃数)
#DIM 全滅フラグ
#DIM 連撃数

SIF RAND:3
	RETURN

ARRAYCOPY "アビテンプレート用_通常攻撃使用先保存", "アビテンプレート用_対象保存"
CALL アビ汎用変数文字列リセット
CALL アビテンプレート_不利効果_トレハン
