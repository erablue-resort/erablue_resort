
@指輪個別文章表示_玄冬の指輪
LOCALS = 
LOCALS += @"■玄冬の指輪<br><br>"
LOCALS += "ある国の巫女が水行の気を込めて作った指輪。<br>"
LOCALS += "冬に装備することで、高い防御性能を発揮する。<br>"
LOCALS += "芽吹きを待つ幼い人物や、静かに余生を過ごす老齢の人物の装備にも適しそうだ。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_玄冬の指輪(指輪番号 = 0, 効果番号 = -1)
#DIM 効果番号
#DIM 指輪番号
IF 効果番号 > -1
	;効果を指定されているのでランダム部分固定
	LOCALS = 
	LOCALS += "【基礎効果】<br>"
	LOCALS += @"最大ＨＰ+800 防御力+400 回復力+150 行動速度+150 %属性数値文字列変換(効果番号 - 1)%耐性+20 ※条件あり<br>"
ELSEIF 指輪番号 < 1
	;所持してない指輪なのでランダム表記
	LOCALS = 
	LOCALS += "【基礎効果】<br>"
	LOCALS += "最大ＨＰ+800 防御力+400 回復力+150 行動速度+150 ランダム属性耐性+20 ※条件あり<br>"
ELSE
	;所持してる指輪なのでランダム部分を引っ張ってくる
	LOCALS = 
	LOCALS += "【基礎効果】<br>"
	LOCALS += "最大ＨＰ+800 防御力+400 回復力+150 行動速度+150 "
	LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "基礎ステータス補正")%
	LOCALS:1 = %SUBSTRING(LOCALS:1, 58, 2)%
	LOCALS += @"%LOCALS:1%耐性+20 ※条件あり<br>"
ENDIF

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%
;ランダム効果数を返す
RETURN 6

@指輪出現率_玄冬の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_玄冬の指輪(対象キャラ)
#DIM 対象キャラ
;冬なら無条件でOK
SIF (DAY / 30) % 4 == 3
	RETURN 1
;幼い・長命者・ロリババア
RETURN (TALENT:対象キャラ:年齢 == -2 || TALENT:対象キャラ:年齢 == 3 || TALENT:対象キャラ:年齢 == 4)

@指輪ステータス決定_玄冬の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_800_防御力_400_回復力_150_行動速度_150
IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF
IF 効果指定 > -1
	LOCAL = 効果指定
ELSE
	LOCAL = RAND:6
ENDIF
基本補正文字列 += @"_%属性数値文字列変換(LOCAL)%耐性_20"

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "玄冬の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)





