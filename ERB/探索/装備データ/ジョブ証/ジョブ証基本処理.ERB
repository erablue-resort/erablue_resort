

@汎用証取得処理(証名一時保存)
#DIMS 証名一時保存
	
;アンロック式なのでフラグ立てるだけ
;ERDいじったりしなくていいようにデータベース管理
ENUMFUNCBEGINSWITH @"証関数_%証名一時保存%"
IF RESULT
	FOR LOCAL, 1, DT_ROW_LENGTH("所持ジョブ証データベース")
		IF DT_CELL_GETS("所持ジョブ証データベース", LOCAL, "ジョブ証名") == 証名一時保存
			;すでに追加済みの場合は-2を返す
			RETURN -2
		ENDIF
	NEXT
	;抜けてきたら登録
	CALLFORM 証関数_%証名一時保存%("ジョブ証種別")
	DT_ROW_ADD "所持ジョブ証データベース", "ジョブ証名", 証名一時保存, "ジョブ証種別", 証関連文字列受け渡し
	RETURN 1
ELSE
	;そもそも証の関数そのものが無い時は-1を返す
	PRINTW 指定の証が見つかりません。
	RETURN -1
ENDIF

@証付け替え処理画面(キャラ番号)
#DIM キャラ番号
#DIM ページ番号
#DIMS 表示証種別
#DIM 表示アビ番号
#DIM 関数総数

IF 表示証種別 == ""
	表示証種別 = 通常ジョブ
ENDIF

;表示部分
;装備中ジョブ証表示
DRAWLINE
TSTR:コマンド名受渡 = 
詳細文文字列受け渡し変数 = 
TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:キャラ番号:装備証番号, "ジョブ証名", 1)%("証名")
LOCALS = <div rect='200,0,5662,1250' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='262, 62, 1125, 1125'>%顔グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += @"<div rect='1387,62,5537,1125'>■%CALLNAME:キャラ番号%が装備中のジョブ証<br>"
IF TSTR:コマンド名受渡 == ""
	LOCALS += "なし</div>"
ELSE
	RESULT = 0
	TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:キャラ番号:装備証番号, "ジョブ証名", 1)%("クラス名", キャラ番号)
	IF RESULT
		LOCALS += @"%TSTR:コマンド名受渡%　%RESULTS:1%：%RESULTS%<br>"
	ELSE
		LOCALS += @"%TSTR:コマンド名受渡%<br>"
	ENDIF
	TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:キャラ番号:装備証番号, "ジョブ証名", 1)%("フレーバー")
	LOCALS += @"説明：%詳細文文字列受け渡し変数%</div>"
ENDIF
;所持証リスト表示
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
CALL 所持証リスト表示(装備ステータス補正保存:キャラ番号:装備証番号, ページ番号, 表示証種別)

;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 装備ステータス補正保存:キャラ番号:装備証番号 > 0
	LOCALS += "　　　　　　　　　　　　　<button value='1002'>[1002]装備を外す</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 45
	PRINTL
NEXT

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9999
		ページ番号 = 0
		表示証種別 = 通常ジョブ
		RETURN 0
	CASE 1002
		装備ステータス補正保存:キャラ番号:装備証番号 = 0
		PRINTFORMW %CALLNAME:キャラ番号%は探索証を外した。
		ページ番号 = 0
		RETURN 0
	CASE IS < 500
		装備ステータス補正保存:キャラ番号:装備証番号 = DT_CELL_GET("所持ジョブ証データベース", LOCAL, "id")
		PRINTFORMW %CALLNAME:キャラ番号%は%DT_CELL_GETS("所持ジョブ証データベース", LOCAL, "ジョブ証名")%を装備した。
		表示証種別 = 通常ジョブ
		ページ番号 = 0
		RETURN 0
	CASE IS < 1000
		TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL - 500, "ジョブ証名")%("フレーバー")
		LOCALS = %詳細文文字列受け渡し変数%<br>
		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL - 500, "ジョブ証名")%_")
		FOR 表示アビ番号, 0, 関数総数
			TSTR:コマンド名受渡 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ名", キャラ番号)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文", キャラ番号)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		LOCALS += "<br>"
		HTML_PRINT LOCALS
		WAIT
		RESTART
ENDSELECT
LOOP 1

@所持証リスト表示(装備中証番号, ページ番号, 表示証種別 = "通常ジョブ", y軸位置 = 1375)
#DIM ページ番号
#DIM 装備中証番号
#DIMS 表示証種別
#DIM y軸位置

;証はアンロック式なので一覧だと名前だけ表示でいいはず

VARSET LOCAL
VARSET LOCALS
LOCALS = <div rect='262,{y軸位置},5537,3625'><nobr>
FOR LOCAL, 1, DT_ROW_LENGTH("所持ジョブ証データベース")
	DT_CELL_GETS "所持ジョブ証データベース", LOCAL, "ジョブ証名"
	IF RESULTS == ""
		CONTINUE
	ENDIF
	;証名を一旦保存
	LOCALS:10 = %RESULTS%
	DT_CELL_GETS "所持ジョブ証データベース", LOCAL, "ジョブ証種別"
	IF RESULTS == 表示証種別
		;表示種別が合うなら表示
		IF 装備中証番号 == DT_CELL_GET("所持ジョブ証データベース", LOCAL, "id")
			LOCALS += "<font color='#CCCC00'>"
		ENDIF
		LOCALS += @"<button value='{LOCAL}'>"
		TRYCALLFORM 証関数_%LOCALS:10%("証名")
		LOCALS += @"%TSTR:コマンド名受渡, 30, LEFT%</button>"
		IF 装備中証番号 == DT_CELL_GET("所持ジョブ証データベース", LOCAL, "id")
			LOCALS += "</font>"
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>"
		LOCALS += "[詳細]</button>"
		LOCAL:1 += 1
		IF LOCAL:1 % 2 == 0
			LOCALS += "<br>"
		ELSE
			LOCALS += "　　　"
		ENDIF
	ENDIF
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS, 1

@所持証リスト表示_USERCOM
#DIM ページ番号
#DIM 表示アビ番号
#DIM 関数総数

LOCALS = 
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
HTML_PRINT LOCALS, 1
CALL 所持証リスト表示(0, ページ番号)
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 45
	PRINTL
NEXT
BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	ページ番号 = 0
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN -1
	CASE IS < 500
		LOCALS = --------------------------------------------------------------------
		TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL, "ジョブ証名")%("証名")
		TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL, "ジョブ証名")%("フレーバー")
		LOCALS += @"■%TSTR:コマンド名受渡%<br><br>"
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"

		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL, "ジョブ証名")%_")
		FOR 表示アビ番号, 0, 関数総数
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		HTML_PRINT LOCALS
		WAIT
	CASE IS < 1000
		TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL - 500, "ジョブ証名")%("フレーバー")
		LOCALS = %詳細文文字列受け渡し変数%<br>

		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%DT_CELL_GETS("所持ジョブ証データベース", LOCAL - 500, "ジョブ証名")%_")
		FOR 表示アビ番号, 0, 関数総数
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		HTML_PRINT LOCALS
		WAIT
ENDSELECT


@GET_JOBSKILL_LEVEL(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM キャラLEVEL

SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
	CASE IS >= 40
		キャラLEVEL = 5
	CASE IS >= 32
		キャラLEVEL = 4
	CASE IS >= 24
		キャラLEVEL = 3
	CASE IS >= 16
		キャラLEVEL = 2
	CASEELSE
		キャラLEVEL = 1
ENDSELECT

RETURNF MIN(GET_JOBTOKEN_LEVEL(), キャラLEVEL)


@GET_JOBTOKEN_LEVEL()
#FUNCTION
#DIM 所持LEVEL

;ここはなんか、「一人前の印」「熟練の印」「達人の印」みたいなアイテムを輸入品店で買うと増えるイメージ
;１個買えば証全部のレベルが上がる的な…
所持LEVEL = 1

RETURNF 所持LEVEL
