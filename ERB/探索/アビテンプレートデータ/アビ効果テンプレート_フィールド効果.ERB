;フィールド効果とは基本的に味方全体、あるいは敵全体に効果を発揮するもの
;行動時にはフィールドを展開するだけで、効果そのものは別のタイミングで発揮される
;なお、基本的に「全ての効果はフィールドごとに独自の効果でテンプレートは用意しない」ものとする
;また、フィールドは基本的に先出し有利である
;強制書き換えも可能だが、基本的にエネミーにのみ持たせること

@アビテンプレート_フィールド効果セット(強制書き換え = 0)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:フィールド枠
;------------------------------------------------------------------------
;返り値-1:キャンセル 0:既にフィールドが貼られている 1:成功
;------------------------------------------------------------------------
#DIM CONST 対象番号 = 0
#DIM バフ番号
#DIM 強制書き換え
#DIMS 名前一時格納
#DIMS 効果量一時格納
#DIM データベース行数

CALL アビテンプレート_効果共通処理(1)
SIF RESULT == -1
	RETURN -1

IF アビ汎用文字列:フィールド枠 == ""
	PRINTW フィールド名が不正です
	RETURN -1
ENDIF

IF 戦闘行動キャラ < 10
	名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%
ELSE
	名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%
ENDIF

;既存のフィールドがないかチェック
IF 強制書き換え == 0
	IF 展開中フィールド名格納 != ""
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = 既に別のフィールドが張られている！
		RETURN 0
	ENDIF
ENDIF

;新しくデータベースにセット
展開中フィールド名格納 '= アビ汎用文字列:フィールド枠
{
DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:フィールド枠, "バフ・デバフフラグ", "フィールド", "適用範囲", "フィールド",
	"効果枠", アビ汎用文字列:フィールド枠, "対象能力", アビ汎用文字列:フィールド枠,
	"効果量_割合", 0, "効果量_固定値", 0,
	"持続ターン", アビ汎用変数:持続ターン, "持続回数", 0, "持続行動回数", 0, "持続被ダメ回数", 0,
	"消去不可フラグ", 1, "特殊表示オプション", アビ汎用変数:特殊表示オプション,
}
アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%展開中フィールド名格納%フィールドを展開した！


@アビテンプレート_フィールド効果セット_強制上書き
JUMP アビテンプレート_フィールド効果セット(1)
