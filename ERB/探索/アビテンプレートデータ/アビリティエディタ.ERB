
@アビエディタデータベースセット()
DT_CREATE "アビエディタデータベース"
DT_COLUMN_ADD "アビエディタデータベース", "アビ効果種別"
DT_COLUMN_ADD "アビエディタデータベース", "アビ対象"
DT_COLUMN_ADD "アビエディタデータベース", "効果順番", 1
DT_COLUMN_ADD "アビエディタデータベース", "アビ段階", 1
DT_COLUMN_ADD "アビエディタデータベース", "アビ段階条件"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列１"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列２"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列３"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列４"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列５"
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値１", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値２", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値３", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値４", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値５", 3

@アビリティエディタ()
#DIM 対象NO
#DIM 既存アビ数
#DIM 表示Lv
#DIM アビ作成中フラグ
#DIM アビID, 100
#DIMS HTML文字列
#DIM 枠配置縦位置
#DIM アビ段階
#DIMS アビ名, 5
#DIM 消費ＭＰ, 5
#DIMS 取得条件, 5
#DIM 表示候補数
#DIM 選択番号

#DIM 取得条件_Lv
#DIMS 取得条件_陥落

;初期化部分

;アビ名を呼び出すためにキャラを１人追加する
ADDCHARA 0
;アビ作成中フラグ = 0
;開発中のためいきなり作成中に飛ぶ
アビ作成中フラグ = 1
表示Lv = 1
基礎BATTLE_STATE:0:Lv = 表示Lv
VARSET アビ名, "（未定）"
VARSET 消費ＭＰ, -1
VARSET 取得条件
アビ段階 = 0
CALL アビエディタデータベースセット()

対象NO = 1

$動作LOOP

DRAWLINE
PRINTL 固有アビリティを作成するためのエディタです。
PRINTL 現在開発中のため、作成出来る機能に制限があります。
DRAWLINE

HTML文字列 = 
;選択キャラ表示枠
HTML文字列 += "<div rect='50, 0, 5000, 1200' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
IF 対象NO > 0
	HTML文字列 += @"%CSVNAME(対象NO)%<br>"
	;現在あるアビリティを呼んでくる
	既存アビ数 = ENUMFUNCBEGINSWITH(@"固有アビ_{対象NO}_")
	IF 既存アビ数 > 0
		HTML文字列 += @"Lv{表示Lv, 2}時アビリティ："
		FOR LOCAL, 0, 既存アビ数
			TSTR:コマンド名受渡 = 
			詳細文文字列受け渡し変数 = 
			CALLFORM %RESULTS:LOCAL%("アビ名", 0)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			CALLFORM %RESULTS:LOCAL%("アビ説明文_詳細", 0)
			SIF 詳細文文字列受け渡し変数 == ""
				CALLFORM %RESULTS:LOCAL%("アビ説明文", 0)
			HTML文字列 += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>■%TSTR:コマンド名受渡%</nonbutton>　　"
		NEXT
	ENDIF
	HTML文字列 += "<br>"
	HTML文字列 += "<br>"
ELSE
	HTML文字列 += "（選択なし）<br>"
	HTML文字列 += "<br>"
	HTML文字列 += "<br>"
ENDIF
HTML文字列 += "<button value='200'>[作成キャラを変更する]</button> / <button value='201'>[表示レベルを変更する]</button>"
HTML文字列 += "</div>"
IF 対象NO > 0 && EXIST画像FILE(@"resources/{対象NO}%CSVCALLNAME(対象NO)%/顔_デフォルト")
	;何番でもいいけどとりあえず20000以降を使用
	GDISPOSE 20000
	SPRITEDISPOSE @"口上用画像0"
	CALL GCREATE_拡張子(20000, @"{対象NO}%CSVCALLNAME(対象NO)%/顔_デフォルト")
	SPRITECREATE @"口上用画像0", 20000
	HTML文字列 += @"<div rect='3900, 50, 1500, 1500'><img src='口上用画像0' height='{GHEIGHT(20000) / 16 * 100}' width='{GWIDTH(20000) / 16 * 100}'></div>"
ENDIF

;操作パネル
HTML文字列 += "<div rect='50, 1250, 1600, 4000' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
IF アビ作成中フラグ == 0
	HTML文字列 += "<button value='900'>[900] 新規アビリティ作成</button><br>"
	;作成中
	;HTML文字列 += "<button value='101'>[101] アビデータ読込</button><br>"
ELSE
	;アビパーツの選択肢を並べる
	HTML文字列 += "<button value='0'>[ 0] アビ名を変更する</button><br>"
	HTML文字列 += "<button value='1'>[ 1] ダメージ効果を追加</button><br>"
ENDIF
HTML文字列 += "<br>"
HTML文字列 += "<br>"
SIF アビ作成中フラグ
	HTML文字列 += "<button value='998'>[998] アビリティERBを生成する</button><br>"
HTML文字列 += "<button value='999'>[999] 戻る</button><br>"
HTML文字列 += "</div>"

;作成アビリティ効果表示枠
HTML文字列 += "<div rect='1700, 1250, 4050, 4000' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
HTML文字列 += @"<button value='0'>■%アビ名:アビ段階, 30, LEFT%</button>取得条件：%アビエディタ_取得条件文章化(取得条件)%　　<button value='202'>[取得条件変更]</button><br>"
HTML文字列 += @"消費ＭＰ："
IF 消費ＭＰ:アビ段階 < 0
	HTML文字列 += @"自動設定（現在{ABS(消費ＭＰ:アビ段階)}）　　<button value='203'>[手動で設定する]</button><br>"
ELSE
	HTML文字列 += @"{消費ＭＰ:アビ段階}　　<button value='203'>[自動で設定する]</button><br>"
ENDIF
HTML文字列 += "-----------------------------------------------------------------------------------<br>"
HTML文字列 += "</div>"
IF アビ作成中フラグ
	枠配置縦位置 = 0
	VARSET RESULT
	表示候補数 = DT_SELECT("アビエディタデータベース", @"アビ段階 = {アビ段階}", "効果順番 ASC")
	;配列に保存
	ARRAYCOPY "RESULT", "アビID"
	ARRAYSHIFT アビID, -1, 0
	FOR LOCAL, 0, 表示候補数
		SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ効果種別", 1)
			CASE "ダメージ効果"
				HTML文字列 += @"<div rect='1800, {1650 + 枠配置縦位置}, 3850, 380' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
				HTML文字列 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ対象", 1)%に"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
					HTML文字列 += @"［%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)%✕{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1)}％］の"
				ELSE
					HTML文字列 += @"［{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1)}］の"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1) < 0
					HTML文字列 += "自属性ダメージを"
				ELSE
					HTML文字列 += @"<font color='#%カラーパレット_HTML(@"%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1))%属性")%'>%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1))%属性</font>ダメージを"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値４", 1) < 2
					HTML文字列 += "与える。"
				ELSE
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値４", 1)}回与える。"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += @"<button value='{LOCAL + 100}'>[効果を変更する]</button>　　<button value='{LOCAL + 300}'>[効果を削除する]</button>　　"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "効果順番", 1) > 0
					HTML文字列 += @"<button value='{LOCAL + 400}'>[↑]</button>"
				ELSE
					HTML文字列 += "　　"
				ENDIF
				HTML文字列 += "　"
				IF 表示候補数 > LOCAL + 1
					HTML文字列 += @"<button value='{LOCAL + 500}'>[↓]</button>"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += "</div>"
				枠配置縦位置 += 430
		ENDSELECT
	NEXT
ENDIF


HTML_PRINT HTML文字列, 1
FOR LOCAL, 0, 47
	PRINTL
NEXT

BINPUT
PRINTL
PRINTL

選択番号 = RESULT
SELECTCASE 選択番号
	CASE 200
		CALL CSVコピー処理(2)
		SIF 選択番号 > 0
			対象NO = 選択番号
	CASE 201
		DRAWLINE
		PRINTL 1～80の範囲でLvを設定してください。
		INPUT
		IF 選択番号 < 1 || 選択番号 > 80
			PRINTW 数値が不正です。
		ELSE
			表示Lv = 選択番号
			基礎BATTLE_STATE:0:Lv = 表示Lv
		ENDIF
	CASE 202
		;取得条件を解析

		VARSET RESULTS
		取得条件_Lv = 0
		取得条件_陥落 = 
		SPLIT 取得条件:アビ段階, "***", RESULTS
		FOR LOCAL, 0, 選択番号
			IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
				取得条件_Lv = TOINT(REPLACE(RESULTS:LOCAL, "Lv_", ""))
			ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
				取得条件_陥落 '= REPLACE(RESULTS:LOCAL, "陥落_", "")
			ENDIF
		NEXT

		$取得LOOP

		DRAWLINE
		PRINTL 取得条件を設定してください。
		PRINTFORM 現在の取得条件：
		IF 取得条件_Lv > 0
			PRINTFORM Lv{取得条件_Lv}　　
		ENDIF
		IF 取得条件_陥落 != ""
			PRINTFORM 陥落条件：%取得条件_陥落%　　
		ENDIF
		PRINTL
		DRAWLINE
		IF 取得条件_Lv > 0
			PRINTBUTTON @"[0] Lv条件を設定する　　現在：Lv{取得条件_Lv}条件", 0
			PRINTL
			PRINTBUTTON "[1] Lv条件を削除する", 1
		ELSE
			PRINTBUTTON "[0] Lv条件を設定する　　現在：Lv条件なし", 0
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "陥落"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[10] 陥落：種類不問", 10
			RESETCOLOR
		ELSE
			PRINTBUTTON "[10] 陥落：種類不問", 10
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "恋慕"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[11] 陥落：恋慕系", 11
			RESETCOLOR
		ELSE
			PRINTBUTTON "[11] 陥落：恋慕系", 11
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "セフレ"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[12] 陥落：セフレ系", 12
			RESETCOLOR
		ELSE
			PRINTBUTTON "[12] 陥落：セフレ系", 12
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "非エロ"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[13] 陥落：非エロ系", 13
			RESETCOLOR
		ELSE
			PRINTBUTTON "[13] 陥落：非エロ系", 13
		ENDIF
		PRINTL
		PRINTL
		PRINTBUTTON "[999] これで決定", 999
		PRINTL
		PRINTL
		BINPUT
		PRINTL
		PRINTL
		SELECTCASE RESULT
			CASE 999
				取得条件 = 
				IF 取得条件_Lv > 0
					取得条件 += @"Lv_{取得条件_Lv}"
				ENDIF
				IF 取得条件_陥落 != ""
					SIF 取得条件 != ""
						取得条件 += "***"
					取得条件 += @"陥落_%取得条件_陥落%"
				ENDIF
				SIF 消費ＭＰ < 0
					CALL ＭＰ自動計算(消費ＭＰ, 取得条件)
				GOTO 動作LOOP
			CASE 0
				$LvLOOP
				DRAWLINE
				PRINTL 取得Lvを入力してください。
				DRAWLINE
				PRINTL
				INPUT
				IF RESULT < 1 || RESULT > 80
					PRINTW １未満、あるいは８０を越える数値は入力できません。
					GOTO LvLOOP
				ELSE
					取得条件_Lv = RESULT
				ENDIF
			CASE 1
				取得条件_Lv = 0
			CASE 10
				IF 取得条件_陥落 == "陥落"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 陥落
				ENDIF
			CASE 11
				IF 取得条件_陥落 == "恋慕"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 恋慕
				ENDIF
			CASE 12
				IF 取得条件_陥落 == "セフレ"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = セフレ
				ENDIF
			CASE 13
				IF 取得条件_陥落 == "非エロ"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 非エロ
				ENDIF
		ENDSELECT

		GOTO 取得LOOP
	CASE 203
		IF 消費ＭＰ < 0
			;自動＞手動
			$MPLOOP
			DRAWLINE
			PRINTL 消費するＭＰの値を入力してください。
			INPUT
			IF 選択番号 < 0
				PRINTW 負の値は指定出来ません。
				GOTO MPLOOP
			ELSE
				消費ＭＰ = 選択番号
			ENDIF
		ELSE
			;手動＞自動
			消費ＭＰ = -1
		ENDIF
	CASE 900
		アビ作成中フラグ = 1
		PRINTW 新しいアビリティを作成します。
	CASE 998
		IF DT_ROW_LENGTH("アビエディタデータベース") < 1
			PRINTW 効果が設定されていません。
			GOTO 動作LOOP
		ENDIF
		IF アビ名 == "" || アビ名 == "（未定）"
			PRINTW アビ名が入力されていません。
			GOTO 動作LOOP
		ENDIF
		CALL アビエディタ_ERB生成(対象NO, アビ名, 消費ＭＰ, 取得条件)

		;追加したキャラを削除
		DELCHARA CHARANUM - 1
		;データベースの削除
		DT_RELEASE "アビエディタデータベース"
		RETURN 0
	CASE 999
		;追加したキャラを削除
		DELCHARA CHARANUM - 1
		;データベースの削除
		DT_RELEASE "アビエディタデータベース"
		RETURN 0
	CASE 0
		$NAMELOOP
		DRAWLINE
		PRINTL アビ名を入力してください。
		INPUTS
		IF RESULTS == ""
			PRINTW アビ名は空白に出来ません。
			GOTO NAMELOOP
		ELSE
			アビ名 '= RESULTS
		ENDIF
	CASE 1
		;ダメージ効果
		CALL アビエディタ_ダメージ効果追加(0, アビ段階)
	CASE 100 TO 199
		;効果変更
		SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 100), "アビ効果種別", 1)
			CASE "ダメージ効果"
				CALL アビエディタ_ダメージ効果追加(アビID:(選択番号 - 100))
		ENDSELECT
	CASE 300 TO 399
		;効果削除
		DT_ROW_REMOVE "アビエディタデータベース", アビID:(選択番号 - 300)
	CASE 400 TO 499
		;上に移動
		LOCAL = DT_CELL_GET("アビエディタデータベース", アビID:(選択番号 - 400), "効果順番", 1)
		DT_SELECT "アビエディタデータベース", @"効果順番 = {LOCAL - 1}"
		DT_CELL_SET "アビエディタデータベース", RESULT:1, "効果順番", LOCAL, 1
		DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "効果順番", LOCAL - 1, 1
		;もしも下に移動した効果が二番目以降不可の対象だった時、入れ替え対象だったものと効果対象を入れ替える
		IF GROUPMATCH(DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), "味方単体", "敵単体", "敵横列", "敵縦列")
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", LOCALS, 1
		ENDIF
		;もしも一番上に来た効果の位置が最初、かつ対象が「上と同じ」だった場合、入れ替え対象だったものと効果対象を入れ替える
		LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1)
		IF LOCAL - 1 == 0 && LOCALS == "上と同じ相手"
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", LOCALS, 1
		ENDIF
	CASE 500 TO 599
		;下に移動
		LOCAL = DT_CELL_GET("アビエディタデータベース", アビID:(選択番号 - 500), "効果順番", 1)
		DT_SELECT "アビエディタデータベース", @"効果順番 = {LOCAL + 1}"
		DT_CELL_SET "アビエディタデータベース", RESULT:1, "効果順番", LOCAL, 1
		DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "効果順番", LOCAL + 1, 1
		;もしも下に移動した効果が二番目以降不可の対象だった時、入れ替え対象だったものと効果対象を入れ替える
		IF GROUPMATCH(DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1), "味方単体", "敵単体", "敵横列", "敵縦列")
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", LOCALS, 1
		ENDIF
		;もしも一番上に来た効果の位置が最初、かつ対象が「上と同じ」だった場合、入れ替え対象だったものと効果対象を入れ替える
		LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
		IF LOCAL - 1 == 0 && LOCALS == "上と同じ相手"
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", LOCALS, 1
		ENDIF
ENDSELECT

SIF 消費ＭＰ < 0
	CALL ＭＰ自動計算(消費ＭＰ, 取得条件)
GOTO 動作LOOP


@アビエディタ_ダメージ効果追加(変更ID = 0, アビ段階 = 0)
;アビ文字列１ ダメージ倍率に使用する能力値
;アビ文字列２
;アビ文字列３
;アビ文字列４
;アビ文字列５
;アビ数値１ ダメージ倍率
;アビ数値２ ダメージ固定値
;アビ数値３ ダメージ属性
;アビ数値４ ダメージ回数
;アビ数値５
#DIM 効果順番
#DIM DYNAMIC ダメージ倍率
#DIM DYNAMIC ダメージ固定値
#DIM DYNAMIC ダメージ属性
#DIM DYNAMIC ダメージ回数
#DIMS DYNAMIC ダメージ能力値
#DIMS DYNAMIC ダメージ対象
#DIM アビ段階
#DIM ダメージモード
#DIM 変更ID
#DIMS HTML文字列

;効果の最初かどうかを判定
IF 変更ID == 0
	;アビ段階が同じ効果がある、かつ新規効果追加状態＝最初ではない
	LOCAL = DT_SELECT("アビエディタデータベース", @"アビ段階 = {アビ段階}")
	効果順番 = LOCAL
ELSE
	;既存効果変更の場合、行数を見る
	FOR LOCAL, 0, DT_ROW_LENGTH("アビエディタデータベース")
		IF DT_CELL_GET("アビエディタデータベース", LOCAL, "id") == 変更ID
			効果順番 = LOCAL
			BREAK
		ENDIF
	NEXT
ENDIF

IF 変更ID
	ダメージ対象 '= DT_CELL_GETS("アビエディタデータベース", 変更ID, "アビ対象", 1)
	ダメージ倍率 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値１", 1)
	ダメージ固定値 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値２", 1)
	ダメージ属性 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値３", 1)
	ダメージ回数 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値４", 1)
	ダメージ能力値 '= DT_CELL_GETS("アビエディタデータベース", 変更ID, "アビ文字列１", 1)
	アビ段階 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ段階", 1)
ENDIF
IF ダメージ対象 == ""
	IF 効果順番 < 1
		ダメージ対象 = 敵単体
	ELSE
		ダメージ対象 = 上と同じ相手
	ENDIF
ENDIF
SIF ダメージ回数 < 1
	ダメージ回数 = 1
SIF ダメージ能力値 == ""
	ダメージ能力値 = 攻撃力
SIF ダメージ倍率 == 0
	ダメージ倍率 = 100

$動作LOOP

DRAWLINE
PRINTL ダメージ効果の設定を選択してください。
DRAWLINE

PRINTL ・現在の効果
PRINT 　　
PRINTFORM %ダメージ対象%に
IF ダメージモード == 0
	PRINTFORM ［%ダメージ能力値%✕{ダメージ倍率}％］の
ELSE
	PRINTFORM ［{ダメージ固定値}］の
ENDIF
IF ダメージ属性 < 0
	PRINT 自属性ダメージを
ELSE
	SETCOLOR カラーパレット(@"%属性数値文字列変換(ダメージ属性)%属性")
	PRINTFORM %属性数値文字列変換(ダメージ属性)%属性
	RESETCOLOR
	PRINT ダメージを
ENDIF
IF ダメージ回数 < 2
	PRINT 与える。
ELSE
	PRINTFORM {ダメージ回数}回与える。
ENDIF
PRINTL
DRAWLINE

HTML文字列 = 
HTML文字列 += "<div rect='50, 0, 5000, 3000' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
HTML文字列 += @"<button value='1'>[1] ダメージ対象を変更する</button>　　　　現在：%ダメージ対象%<br>"
IF ダメージモード == 0
	HTML文字列 += @"<button value='2'>[2] 使用する能力値を変更する</button>　　　現在：%ダメージ能力値%<br>"
	HTML文字列 += @"<button value='3'>[3] ダメージ倍率を変更する</button>　　　　現在：{ダメージ倍率}％<br>"
ELSE
	HTML文字列 += @"<button value='3'>[3] ダメージ値を変更する</button>　　　　　現在：{ダメージ固定値}<br>"
ENDIF
IF ダメージ属性 < 0
	HTML文字列 += @"<button value='4'>[4] ダメージの属性を変更する</button>　　　現在：自属性<br>"
ELSE
	HTML文字列 += @"<button value='4'>[4] ダメージの属性を変更する</button>　　　現在：<font color='#%カラーパレット_HTML(@"%属性数値文字列変換(ダメージ属性)%属性")%'>%属性数値文字列変換(ダメージ属性)%属性</font><br>"
ENDIF
HTML文字列 += @"<button value='5'>[5] ダメージの回数を変更する</button>　　　現在：{ダメージ回数}回<br>"
HTML文字列 += "<br>"
HTML文字列 += "<br>"
HTML文字列 += @"<button value='998'>[998] この設定で効果を決定する</button><br>"
HTML文字列 += @"<button value='999'>[999] 保存せず戻る</button><br>"
HTML文字列 += "</div>"


HTML_PRINT HTML文字列, 1
FOR LOCAL, 0, 35
	PRINTL
NEXT

BINPUT
PRINTL
PRINTL
SELECTCASE RESULT
	CASE 1
		CALL アビエディタ_対象選択(ダメージ対象, 効果順番)
	CASE 2
		DRAWLINE
		PRINTFORML 使用する能力値を選択してください。 現在：%ダメージ能力値%
		PRINTL
		FOR LOCAL, 6, 10
			PRINTBUTTON @"[{LOCAL}] %ERDNAME(基礎BATTLE_STATE, LOCAL)%", LOCAL
			PRINTL
		NEXT
		BINPUT
		PRINTL
		PRINTL
		ダメージ能力値 '= ERDNAME(基礎BATTLE_STATE, RESULT)
	CASE 3
		DRAWLINE
		IF ダメージモード == 0
			PRINTFORML ダメージ倍率を入力してください。 現在：{ダメージ倍率}
		ELSE
			PRINTFORML ダメージ値を入力してください。 現在：{ダメージ固定値}
		ENDIF
		PRINTL
		INPUT
		IF RESULT < 1
			PRINTW ０や負の値は指定出来ません
		ENDIF
		IF ダメージモード == 0
			ダメージ倍率 = RESULT
		ELSE
			ダメージ固定値 = RESULT
		ENDIF
	CASE 4
		DRAWLINE
		PRINT ダメージの属性を選択してください。 現在：
		IF ダメージ属性 < 0
			PRINT 自属性
		ELSE
			PRINTFORM %属性数値文字列変換(ダメージ属性)%属性
		ENDIF
		PRINTL
		PRINTL
			PRINTBUTTON @"[-1] 自属性", -1
			PRINTL
		FOR LOCAL, 0, 7
			SETCOLOR カラーパレット(@"%属性数値文字列変換(LOCAL)%属性")
			PRINTBUTTON @"[{LOCAL}] %属性数値文字列変換(LOCAL)%属性", LOCAL
			PRINTL
		NEXT
		RESETCOLOR
		BINPUT
		PRINTL
		PRINTL
		ダメージ属性 = RESULT
	CASE 5
		DRAWLINE
		PRINTFORML ダメージの回数を入力してください。 現在：{ダメージ回数}回
		PRINTL
		INPUT
		IF RESULT < 1
			PRINTW ０や負の値は指定出来ません
		ENDIF
		ダメージ回数 = RESULT
	CASE 998
		IF 変更ID
			;既存効果変更
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ対象", ダメージ対象, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値３", ダメージ属性, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値４", ダメージ回数, 1
			IF ダメージモード == 0
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ文字列１", ダメージ能力値, 1
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値１", ダメージ倍率, 1
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値２", 0, 1
			ELSE
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値２", ダメージ固定値, 1
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ文字列１", "", 1
				DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値１", 0, 1
			ENDIF
			PRINTW ダメージ効果を変更しました。
		ELSE
			;新規効果追加
			IF ダメージモード == 0
				DT_ROW_ADD "アビエディタデータベース", "効果順番", 効果順番, "アビ段階", 0, "アビ効果種別", "ダメージ効果", "アビ対象", ダメージ対象, "アビ文字列１", ダメージ能力値, "アビ数値１", ダメージ倍率, "アビ数値３", ダメージ属性, "アビ数値４", ダメージ回数
			ELSE
				DT_ROW_ADD "アビエディタデータベース", "効果順番", 効果順番, "アビ段階", 0, "アビ効果種別", "ダメージ効果", "アビ対象", ダメージ対象, "アビ数値２", ダメージ固定値, "アビ数値３", ダメージ属性, "アビ数値４", ダメージ回数
			ENDIF
			PRINTW 新しいダメージ効果を追加しました。
		ENDIF
		RETURN 1
	CASE 999
		RETURN 0
ENDSELECT

GOTO 動作LOOP



@アビエディタ_対象選択(ダメージ対象, 効果順番 = 0)
#DIMS REF ダメージ対象
#DIM ランダム被りなし
#DIM ランダム体数記録
#DIM 効果順番

SIF ランダム体数記録 < 1
	ランダム体数記録 = 1


DRAWLINE
PRINTFORML 対象を選択してください。 現在：%ダメージ対象%
IF 効果順番 > 0
	PRINTL 最初の効果以外の時、対象指定操作を挟む対象は選択出来ません。
ENDIF
PRINTL
IF 効果順番 < 1
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [-1] 直前の効果と同じ
	RESETCOLOR
ELSE
	PRINTBUTTON "[-1] 直前の効果と同じ", -1
ENDIF
PRINTL
PRINTBUTTON "[0] 自身", 0
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [1] 味方単体
	RESETCOLOR
ELSE
	PRINTBUTTON "[1] 味方単体", 1
ENDIF
PRINTL
PRINTBUTTON "[2] 味方全体", 2
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [10] 敵単体
	RESETCOLOR
ELSE
	PRINTBUTTON "[10] 敵単体", 10
ENDIF
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [11] 敵横列
	RESETCOLOR
ELSE
	PRINTBUTTON "[11] 敵横列", 11
ENDIF
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [12] 敵縦列
	RESETCOLOR
ELSE
	PRINTBUTTON "[12] 敵縦列", 12
ENDIF
PRINTL
PRINTBUTTON "[13] 敵全体", 13
PRINTL
PRINTBUTTON "[14] 敵ランダム◯体", 14
PRINTL
PRINTL
BINPUT
PRINTL
PRINTL
SELECTCASE RESULT
	CASE -1
		ダメージ対象 = 上と同じ相手
	CASE 0
		ダメージ対象 = 自身
	CASE 1
		ダメージ対象 = 味方単体
	CASE 2
		ダメージ対象 = 味方全体
	CASE 10
		ダメージ対象 = 敵単体
	CASE 11
		ダメージ対象 = 敵横列
	CASE 12
		ダメージ対象 = 敵縦列
	CASE 13
		ダメージ対象 = 敵全体
	CASE 14
		$ランダムLOOP
		DRAWLINE
		PRINTL ランダムで選ぶ対象数を入力してください。
		IF ランダム被りなし
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[ターゲット重複あり]", 1
			RESETCOLOR
			PRINT  / 
			PRINTPLAIN [ターゲット重複なし]
		ELSE
			PRINTPLAIN [ターゲット重複あり]
			PRINT  / 
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[ターゲット重複なし]", 1
			RESETCOLOR
		ENDIF
		PRINTBUTTON "[0] 対象数を変更する", 0
		PRINTFORM 　　現在：{ランダム体数記録}体
		PRINTL
		PRINTBUTTON "[9] これで決定", 9
		PRINTL
		PRINTL
		BINPUT
		PRINTL
		PRINTL
		SELECTCASE RESULT
			CASE 1
				INVERTBIT ランダム被りなし, 0
			CASE 0
				DRAWLINE
				PRINTL 対象数を入力してください
				INPUT
				IF RESULT < 1
					PRINTW １未満の数は入力出来ません
				ELSE
					ランダム体数記録 = RESULT
				ENDIF
			CASE 9
				IF ランダム被りなし
					ダメージ対象 = 敵ランダム{ランダム体数記録}体（重複なし）
				ELSE
					ダメージ対象 = 敵ランダム{ランダム体数記録}体
				ENDIF
				RETURN 0
		ENDSELECT
		GOTO ランダムLOOP
ENDSELECT


@アビエディタ_ERB生成(対象No, アビ名, 消費ＭＰ, 取得条件)
#DIM DB行数
#DIMS アビERB記述
#DIMS アビ説明記述, 10
#DIMS アビ効果記述, 10
#DIM アビ段階
#DIM 最大アビ段階
#DIM 対象No
#DIM 消費ＭＰ
#DIMS アビ名
#DIMS 取得条件
#DIMS 取得条件_文字列
#DIM アビID, 100

VARSET アビ説明記述
VARSET アビ効果記述

アビERB記述 = @固有アビ_{対象No}_YYYY(ARGS, キャラ番号)<br>
アビERB記述 += "#DIM キャラ番号<br>"
アビERB記述 += "#DIM 消費ＭＰ<br>"
アビERB記述 += "#DIMS アビ名<br>"
アビERB記述 += "<br>"

;ここに習得条件入れる
IF 取得条件 != ""
	;取得条件を解析

	VARSET RESULTS
	取得条件_文字列 = 
	SPLIT 取得条件, "***", RESULTS
	FOR LOCAL, 0, RESULT
		IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
			SIF 取得条件_文字列 != ""
				取得条件_文字列 += " &amp;&amp; "
			取得条件_文字列 += @"基礎BATTLE_STATE:キャラ番号:Lv >= %REPLACE(RESULTS:LOCAL, "Lv_", "")%"
		ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
			SIF 取得条件_文字列 != ""
				取得条件_文字列 += " &amp;&amp; "
			SELECTCASE REPLACE(RESULTS:LOCAL, "陥落_", "")
				CASE "陥落"
					取得条件_文字列 += "陥落チェック(キャラ番号)"
				CASE "恋慕"
					取得条件_文字列 += "陥落チェック_恋慕系(キャラ番号)"
				CASE "セフレ"
					取得条件_文字列 += "GETBIT(陥落チェック(キャラ番号), 1)"
				CASE "非エロ"
					取得条件_文字列 += "陥落チェック_非性的(キャラ番号)"
			ENDSELECT
		ENDIF
	NEXT
	アビERB記述 += @"IF %取得条件_文字列%<br>"
	アビERB記述 += @"	アビ名 = %アビ名%<br>"
	アビERB記述 += @"	消費ＭＰ = {ABS(消費ＭＰ)}<br>"
	アビERB記述 += "ENDIF<br><br>"
ELSE
	アビERB記述 += @"アビ名 = %アビ名%<br>"
	アビERB記述 += @"消費ＭＰ = {ABS(消費ＭＰ)}<br><br>"
ENDIF

VARSET RESULT
DT_SELECT "アビエディタデータベース", "", "アビ段階 ASC, 効果順番 ASC"
;配列に保存
ARRAYCOPY "RESULT", "アビID"
ARRAYSHIFT アビID, -1, 0
アビ段階 = 0

FOR DB行数, 0, DT_ROW_LENGTH("アビエディタデータベース")
	SIF アビ段階 != DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ段階", 1)
		アビ段階 = DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ段階", 1)
	SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ効果種別", 1)
		CASE "ダメージ効果"
			アビ説明記述:アビ段階 += "<br>"
			アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1)%に"
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ説明記述:アビ段階 += @"［%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%✕{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}％］の"
			ELSE
				アビ説明記述:アビ段階 += @"［{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}］の"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1) < 0
				アビ説明記述:アビ段階 += "自属性ダメージを"
			ELSE
				アビ説明記述:アビ段階 += @"<font color='#%カラーパレット_HTML(@"%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性")%'>%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性</font>ダメージを"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) < 2
				アビ説明記述:アビ段階 += "与える。"
			ELSE
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}回与える。"
			ENDIF

			アビ効果記述:アビ段階 += "CALL アビ汎用変数文字列リセット<br>"
			アビ効果記述:アビ段階 += アビエディタ_対象CALL変換(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1))
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果割合 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}<br>"
				アビ効果記述:アビ段階 += @"アビ汎用文字列:攻撃側使用能力値 = %DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%<br>"
			ELSE
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果量 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}<br>"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1) < 0
				LOCALS = CALL アビテンプレート_ダメージ処理_自属性
			ELSE
				LOCALS = CALL アビテンプレート_ダメージ処理_%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) > 1
				アビ効果記述:アビ段階 += @"FOR LOCAL, 0, {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}<br>"
				アビ効果記述:アビ段階 += @"	%LOCALS%<br>"
				アビ効果記述:アビ段階 += @"NEXT<br>"
			ELSE
				アビ効果記述:アビ段階 += @"%LOCALS%<br>"
			ENDIF
	ENDSELECT
	SIF 最大アビ段階 < アビ段階
		最大アビ段階 = アビ段階
NEXT
FOR LOCAL, 0, 最大アビ段階 + 1
	アビ説明記述:アビ段階 '= @"消費ＭＰ：{ABS(消費ＭＰ)}%アビ説明記述:アビ段階%"
NEXT

CLEARLINE MAX(LINECOUNT, 1000)
TWAIT 100, 1

アビERB記述 += "SELECTCASE ARGS<br>"
アビERB記述 += "	CASE \"アビ名\"<br>"
アビERB記述 += "		TSTR:コマンド名受渡 = \%アビ名\%<br>"
アビERB記述 += "	CASE \"アビ説明文\"<br>"
アビERB記述 += @"		詳細文文字列受け渡し変数 = "
HTML_PRINT アビERB記述, 1
VARSET RESULTS
SPLIT アビ説明記述:アビ段階, "<br>", RESULTS
PRINTFORML %RESULTS:0%
FOR LOCAL, 1, RESULT
	PRINTFORML 		詳細文文字列受け渡し変数 += "<br>%RESULTS:LOCAL%"
NEXT
アビERB記述 = 
アビERB記述 += "	CASE \"アビ効果\"<br>"
アビERB記述 += @"		%REPLACE(アビ効果記述:アビ段階, "<br>", "<br>		")%"
アビERB記述 '= SUBSTRING(アビERB記述, 0, STRLENS(アビERB記述) - 2)
アビERB記述 += "ENDSELECT<br><br><br>"


HTML_PRINT アビERB記述
PRINTL 
OUTPUTLOG @"ERB_EDITFILE/固有アビリティ_%CSVCALLNAME(対象No)%.ERB", 1
PRINTL ERB_EDITFILEフォルダに出力しました。
PRINTW 関数名のYYYYの部分を任意の文字列に変更し、ERB/口上_キャラ個別ERBのキャラクターフォルダに移動して再起動してください


@アビエディタ_対象CALL変換(対象文字列)
#FUNCTIONS
#DIMS 対象文字列
#DIM 対象数

SELECTCASE 対象文字列
	CASE "上と同じ相手"
		;直前と同じなので記述しない
	CASE "自身"
		RETURNF "CALL アビ対象選択テンプレート_自己のみ<br>"
	CASE "味方単体"
		RETURNF "CALL アビ対象選択テンプレート_味方単体<br>"
	CASE "味方全体"
		RETURNF "CALL アビ対象選択テンプレート_味方全体<br>"
	CASE "敵単体"
		RETURNF "CALL アビ対象選択テンプレート_敵単体<br>"
	CASE "敵横列"
		RETURNF "CALL アビ対象選択テンプレート_敵横列<br>"
	CASE "敵縦列"
		RETURNF "CALL アビ対象選択テンプレート_敵縦列<br>"
	CASE "敵全体"
		RETURNF "CALL アビ対象選択テンプレート_敵全体_生者のみ<br>"
	CASEELSE
		IF STRFIND(対象文字列, "敵ランダム") >= 0
			対象数 = TOINT(REPLACE(REPLACE(REPLACE(対象文字列, "敵ランダム", ""), "体", ""), "（重複なし）", ""))
			IF STRFIND(対象文字列, "（重複なし）") >= 0
				RETURNF @"CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し({対象数})<br>"
			ELSE
				RETURNF @"CALL アビ対象選択テンプレート_敵ランダムＸ体({対象数})<br>"
			ENDIF
		ENDIF
ENDSELECT


@ＭＰ自動計算(消費ＭＰ, 取得条件)
#DIM REF 消費ＭＰ
#DIM ダメージ総倍率
#DIM ダメージ総値
#DIM DB行数
#DIM 基礎消費MP
#DIM 補正回数
#DIM ループ用
#DIM レベル補正
#DIMS 取得条件
#DIM 取得条件_Lv
#DIMS 取得条件_陥落

;取得条件を解析

VARSET RESULTS
取得条件_Lv = 0
取得条件_陥落 = 
SPLIT 取得条件, "***", RESULTS
FOR LOCAL, 0, RESULT
	IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
		取得条件_Lv = TOINT(REPLACE(RESULTS:LOCAL, "Lv_", ""))
	ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
		取得条件_陥落 '= REPLACE(RESULTS:LOCAL, "陥落_", "")
	ENDIF
NEXT

;陥落は２５Lv分として計算
SIF 取得条件_陥落 != ""
	取得条件_Lv += 25

レベル補正 = 取得条件_Lv / 10 + 1

ダメージ総倍率 = 0
ダメージ総値 = 0
FOR DB行数, 0, DT_ROW_LENGTH("アビエディタデータベース")
	SELECTCASE DT_CELL_GETS("アビエディタデータベース", DB行数, "アビ効果種別")
		CASE "ダメージ効果"
			ダメージ総倍率 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１") * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値４")
			ダメージ総値 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２") * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値４")
	ENDSELECT
NEXT

基礎消費MP = 5
補正回数 = ダメージ総倍率 / 50 + 1
FOR ループ用, 0, 補正回数
	IF ループ用 == 補正回数 - 1
		基礎消費MP += (ダメージ総倍率 % 50) * POWER(2, MAX(0, ループ用 - レベル補正)) / 40
	ELSE
		基礎消費MP += 50 * POWER(2, MAX(0, ループ用 - レベル補正)) / 40
	ENDIF
NEXT


消費ＭＰ = 基礎消費MP * -1


@アビエディタ_取得条件文章化(取得条件)
#FUNCTIONS
#DIMS 取得条件
#DIMS 文章化

SIF 取得条件 == ""
	RETURNF ""

文章化 = 
VARSET RESULTS
SPLIT 取得条件, "***", RESULTS
FOR LOCAL, 0, RESULT
	IF RESULTS:LOCAL != ""
		文章化 += REPLACE(RESULTS:LOCAL, "_", ":")
		文章化 += "　"
	ENDIF
NEXT

RETURNF 文章化

