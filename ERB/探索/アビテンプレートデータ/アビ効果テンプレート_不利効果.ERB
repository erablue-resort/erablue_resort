;不利効果とは基本的に行動キャラが敵陣営のキャラに掛けるもの
;そのためマウントなどの効果を受ける
;スキルのデメリットによって自分が受ける効果などは弱体耐性貫通オプションをつけること

@アビテンプレート_不利効果_デバフ効果セット(累積フラグ = 0)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用文字列:バフ・デバフ対象能力
;アビ汎用変数:効果割合
;アビ汎用変数:効果量
;アビ汎用変数:持続ターン
;アビ汎用変数:持続回数
;アビ汎用変数:基礎成功確率
;アビ汎用文字列:付随先設定
;アビ汎用文字列:付随先_隊列設定
;アビ汎用変数:付随割合
;アビ汎用変数:付随量
;アビ汎用文字列:バフ・デバフ名
;アビ汎用変数:パッシブ付随先NO
;------------------------------------------------------------------------
;◯強化、◯耐性、連続攻撃率、状態異常耐性_○○については初期値が０のため効果割合を入れても効果がないことに注意
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じ枠で効果高いバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM デバフ番号
#DIM スキップフラグ
#DIM 弱体無効フラグ
#DIMS 名前一時格納
#DIMS 効果量一時格納
#DIM 成功率一時格納
#DIM 累積フラグ
#DIM バフ番号
#DIM 累積量
#DIMS 適用範囲保存
#DIM 効果変動フラグ

IF アビ汎用文字列:付随先設定 == "パッシブ"
	;範囲が入ってないなら自分のNOを指定
	SIF アビ汎用文字列:適用範囲 == ""
		アビ汎用文字列:適用範囲 = NO{NO:(BATTLE_STATE:戦闘行動キャラ:0)}
	SIF アビ汎用変数:パッシブ付随先NO == 0
		アビ汎用変数:パッシブ付随先NO = NO:(BATTLE_STATE:戦闘行動キャラ:0)
ELSEIF アビ汎用文字列:適用範囲 != ""
	CALL アビテンプレート_効果共通処理(1)
	アビテンプレート用_対象保存:0 = 戦闘行動キャラ
ELSE
	CALL アビテンプレート_効果共通処理
ENDIF
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

IF アビ汎用文字列:付随先設定 != "" && アビ汎用文字列:付随先設定 != "パッシブ"
	;付随バフの場合は表示なしとする。説明は付随元のバフや特性に記載すること
	アビ汎用変数:特殊表示オプション = -1
ENDIF

SIF アビテンプレート用_アビ名 == ""
	アビテンプレート用_アビ名 '= アビ汎用文字列:バフ・デバフ対象能力

; SIF アビ汎用文字列:バフ・デバフ名 == ""
; 	アビ汎用文字列:バフ・デバフ名 '= アビテンプレート用_アビ名

;付与予定のデバフ対象を保存
SIF アビ汎用文字列:付随先設定 != "パッシブ"
	アビテンプレート用_付与デバフ保存 += @"%アビ汎用文字列:バフ・デバフ対象能力%_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	効果変動フラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号
	
	IF アビ汎用文字列:適用範囲 != ""
		適用範囲保存 '= アビ汎用文字列:適用範囲
	ELSE
		適用範囲保存 = {戦闘行動対象}
	ENDIF

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にデバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF アビ汎用変数:弱体耐性貫通オプション == 0 && アビ汎用文字列:付随先設定 != "パッシブ"
		弱体無効フラグ = バフ・デバフ効果検索("バフ", "能力", "弱体無効", 戦闘行動対象)
		IF 弱体無効フラグ > -1
			;弱体無効バフがあると弾く
			スキップフラグ = 3
			;回数持続の効果の回数を減少
			CALL バフ回数経過処理(戦闘行動対象, "弱体無効")
			GOTO メッセージ表示部
		ELSEIF アビ汎用変数:基礎成功確率
			;基礎成功率が設定されてる場合は成功率判定
			成功率一時格納 = アビ汎用変数:基礎成功確率
			CALL バフ・デバフ測定(戦闘行動キャラ, "弱体成功率")
			成功率一時格納 += RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "弱体耐性")
			成功率一時格納 -= RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "状態異常耐性_" + アビ汎用文字列:バフ・デバフ対象能力)
			成功率一時格納 -= RESULT
			IF RAND:100 > 成功率一時格納
				スキップフラグ = 2
				GOTO メッセージ表示部
			ELSEIF 戦闘行動対象 >= 10 && 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
				;ボスに基礎成功率のあるデバフを掛けることに成功した場合、ボスはそのデバフの状態異常耐性を得る
				累積量 = ボス耐性累積量(アビ汎用文字列:バフ・デバフ対象能力, 敵BATTLE_STATE:(戦闘行動対象 - 10):Lv)
				IF 累積量 > 0
					バフ番号 = バフ番号取得_枠("ボス累積耐性_" + アビ汎用文字列:バフ・デバフ対象能力, 戦闘行動対象)
					IF バフ番号 < 0
						{
						DT_ROW_ADD "戦闘効果データベース", "バフ・デバフフラグ", "バフ", "適用範囲", @"{戦闘行動対象}",
							"効果枠", "ボス累積耐性_" + アビ汎用文字列:バフ・デバフ対象能力,
							"対象能力", "状態異常耐性_" + アビ汎用文字列:バフ・デバフ対象能力,
							"効果量_固定値", 累積量, "効果名", "ボス累積耐性",
							"持続ターン", -1, "消去不可フラグ", 1, "特殊表示オプション", -1, 
						}
					ELSE
						累積量 += DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
						DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 累積量
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;既存のデバフと枠が被ってないかチェック
	FOR デバフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", デバフ番号, "適用範囲") == 適用範囲保存 && DT_CELL_GETS("戦闘効果データベース", デバフ番号, "バフ・デバフフラグ") == "デバフ"
			;すでに同じ枠のデバフがある場合、累積フラグで分岐
			IF 累積フラグ
				;表示文章用にスキップフラグ設定
				スキップフラグ = 10
				IF DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_割合") < アビ汎用変数:累積上限_割合
					;上限に達してない場合は足してターン伸ばす
					累積量 = MIN(DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_割合") + アビ汎用変数:効果割合, アビ汎用変数:累積上限_割合)
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "効果量_割合", 累積量
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続行動回数", アビ汎用変数:持続行動回数
					効果変動フラグ = 1
					スキップフラグ = 11
				ELSE
					;達してる場合はターン伸ばすだけ
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続行動回数", アビ汎用変数:持続行動回数
				ENDIF
				;固定値の方も同様に処理
				IF DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_固定値") < アビ汎用変数:累積上限_固定値
					;上限に達してない場合は足してターン伸ばす
					累積量 = MIN(DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_固定値") + アビ汎用変数:効果量, アビ汎用変数:累積上限_固定値)
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "効果量_固定値", 累積量
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続行動回数", アビ汎用変数:持続行動回数
					効果変動フラグ = 1
					スキップフラグ = 11
				ELSE
					;達してる場合はターン伸ばすだけ
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", デバフ番号, "持続行動回数", アビ汎用変数:持続行動回数
				ENDIF
			ELSE
				IF DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_割合") == 0 && アビ汎用変数:効果割合 == 0
					;大本デバフも追加デバフも割合が０の時、固定値を比較
					IF DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_固定値") <= アビ汎用変数:効果量
						;追加する方の割合が高いか同じ場合は消して新しく追加
						DT_ROW_REMOVE "戦闘効果データベース", DT_CELL_GET("戦闘効果データベース", デバフ番号, "id")
					ELSE
						;そうでない場合は終了
						スキップフラグ = 1
					ENDIF
				ELSEIF DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_割合") <= アビ汎用変数:効果割合
					;追加する方の割合が高いか同じ場合は消して新しく追加
					DT_ROW_REMOVE "戦闘効果データベース", DT_CELL_GET("戦闘効果データベース", デバフ番号, "id")
				ELSE
					;そうでない場合は終了
					スキップフラグ = 1
				ENDIF
			ENDIF
			BREAK
		ENDIF
	NEXT

	$メッセージ表示部

	IF アビ汎用変数:特殊表示オプション == -1
		IF スキップフラグ == 0
			;累積時でないなら新しくデータベースにセット
			{
			DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ名, "バフ・デバフフラグ", "デバフ", "適用範囲", 適用範囲保存,
				"効果枠", アビ汎用文字列:バフ・デバフ枠, "対象能力", アビ汎用文字列:バフ・デバフ対象能力,
				"効果量_割合", アビ汎用変数:効果割合, "効果量_固定値", アビ汎用変数:効果量,
				"持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数, "持続被ダメ回数", アビ汎用変数:持続被ダメ回数,
				"消去不可フラグ", アビ汎用変数:消去不可オプション, "特殊表示オプション", アビ汎用変数:特殊表示オプション,
				"付随効果枠", アビ汎用文字列:付随先設定, "付随効果枠_対象隊列", アビ汎用文字列:付随先_隊列設定, "付随効果量_割合", アビ汎用変数:付随割合, "付随効果量_固定値", アビ汎用変数:付随量,
				"パッシブ付随先NO", アビ汎用変数:パッシブ付随先NO, "参照効果枠", アビ汎用文字列:参照先設定, "参照効果枠_対象隊列", アビ汎用文字列:参照先_隊列設定,
				"アイコン", アビ汎用文字列:バフ・デバフアイコン
			}
			効果変動フラグ = 1
		ENDIF
	ELSEIF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果が無いようだ…
	ELSEIF スキップフラグ == 2
		IF アビ汎用文字列:バフ・デバフ名 == ""
			IF GETNUM(BATTLE_STATE, アビ汎用文字列:バフ・デバフ対象能力, 2) > 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%減少効果を受けなかった…
			ELSEIF アビ汎用文字列:バフ・デバフ対象能力 == "連続攻撃率"
				効果量一時格納 = {アビ汎用変数:効果量}
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%減少効果を受けなかった…
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%効果を受けなかった…
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ名%効果を受けなかった…
		ENDIF
	ELSEIF スキップフラグ == 3
		IF アビ汎用文字列:バフ・デバフ名 == ""
			IF GETNUM(BATTLE_STATE, アビ汎用文字列:バフ・デバフ対象能力, 2) > 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は弱体無効効果により%アビ汎用文字列:バフ・デバフ対象能力%減少効果を弾いた！
			ELSEIF アビ汎用文字列:バフ・デバフ対象能力 == "連続攻撃率"
				効果量一時格納 = {アビ汎用変数:効果量}
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は弱体無効効果により%アビ汎用文字列:バフ・デバフ対象能力%減少効果を弾いた！
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は弱体無効効果により%アビ汎用文字列:バフ・デバフ対象能力%効果を弾いた！
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は弱体無効効果により%アビ汎用文字列:バフ・デバフ名%効果を弾いた！
		ENDIF
	ELSEIF スキップフラグ == 10
		IF アビ汎用文字列:バフ・デバフ名 == ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の累積%アビ汎用文字列:バフ・デバフ対象能力%デバフ効果が効果時間が更新された！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の累積%アビ汎用文字列:バフ・デバフ名%デバフ効果が効果時間が更新された！
		ENDIF
	ELSE
		IF アビ汎用変数:効果割合
			効果量一時格納 = {アビ汎用変数:効果割合}\%
			IF アビ汎用変数:効果量
				効果量一時格納 += @" + {アビ汎用変数:効果割合}"
			ENDIF
		ELSE
			効果量一時格納 = {アビ汎用変数:効果量}
		ENDIF
		IF アビ汎用文字列:バフ・デバフ名 == ""
			IF GETNUM(BATTLE_STATE, アビ汎用文字列:バフ・デバフ対象能力, 2) > 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%下がった！
			ELSEIF アビ汎用文字列:バフ・デバフ対象能力 == "連続攻撃率"
				効果量一時格納 = {アビ汎用変数:効果量}
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%\%下がった！
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%効果を受けた！
			ENDIF
		ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ名%効果を受けた！
		ENDIF
		IF スキップフラグ < 10
			;累積時でないなら新しくデータベースにセット
			{
			DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ名, "バフ・デバフフラグ", "デバフ", "適用範囲", 適用範囲保存,
				"効果枠", アビ汎用文字列:バフ・デバフ枠, "対象能力", アビ汎用文字列:バフ・デバフ対象能力,
				"効果量_割合", アビ汎用変数:効果割合, "効果量_固定値", アビ汎用変数:効果量,
				"持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数, "持続被ダメ回数", アビ汎用変数:持続被ダメ回数,
				"消去不可フラグ", アビ汎用変数:消去不可オプション, "特殊表示オプション", アビ汎用変数:特殊表示オプション,
				"付随効果枠", アビ汎用文字列:付随先設定, "付随効果枠_対象隊列", アビ汎用文字列:付随先_隊列設定, "付随効果量_割合", アビ汎用変数:付随割合, "付随効果量_固定値", アビ汎用変数:付随量,
				"パッシブ付随先NO", アビ汎用変数:パッシブ付随先NO, "参照効果枠", アビ汎用文字列:参照先設定, "参照効果枠_対象隊列", アビ汎用文字列:参照先_隊列設定,
				"アイコン", アビ汎用文字列:バフ・デバフアイコン
			}
			効果変動フラグ = 1
		ENDIF
	ENDIF
	
	;適用範囲が入ってるときは対象変わっても意味ないので一回でBREAKする
	SIF アビ汎用文字列:適用範囲 != ""
		BREAK
NEXT

SIF 効果変動フラグ
	CALL 戦闘中最大ＨＰＭＰ変動処理()

@アビテンプレート_不利効果_バフ消去(消去枠 = "")
#DIMS 消去枠
#DIM スキップフラグ
#DIM メッセージ非表示フラグ
#DIMS 名前一時格納
#DIM 対象番号
#DIM 消滅番号
#DIM バフ番号

;特殊な適用範囲のバフは消去されないので注意

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "ディスペル_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	メッセージ非表示フラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF 消去枠 == ""
		;枠指定無しの場合、最新１個を消す
		消滅番号 = -1
		FOR バフ番号, DT_ROW_LENGTH("戦闘効果データベース") - 1, -1, -1
			SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") != "バフ"
				CONTINUE
			SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") != @"{戦闘行動対象}"
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "特殊表示オプション") == -1
				CONTINUE
			IF DT_CELL_GET("戦闘効果データベース", バフ番号, "消去不可フラグ") == 0 || アビ汎用変数:消去不可バフデバフ削除オプション
				消滅番号 = バフ番号
				BREAK
			ENDIF
		NEXT
		IF 消滅番号 >= 0
			IF バフ番号取得("ディスペルガード", 戦闘行動対象) >= 0 && !アビ汎用変数:阻害無視オプション
				;ディスペルガードバフがあると弾く
				スキップフラグ = 3
				;回数持続の効果の回数を減少
				CALL バフ回数経過処理(戦闘行動対象, "ディスペルガード")
			ENDIF
		ELSE
			スキップフラグ = 1
		ENDIF
	ELSE
		;枠指定がある場合、その枠を消す。ないなら何もしない
		;枠指定は完全一致なので一個しかないはず
		消滅番号 = バフ番号取得_枠(消去枠)
		IF DT_CELL_GET("戦闘効果データベース", 消滅番号, "消去不可フラグ") && アビ汎用変数:消去不可バフデバフ削除オプション == 0
			消滅番号 = -1
		ELSEIF DT_CELL_GET("戦闘効果データベース", 消滅番号, "特殊表示オプション") == -1
			メッセージ非表示フラグ = 1
		ENDIF
		IF 消滅番号 < 0
			スキップフラグ = 1
		ENDIF
	ENDIF

	IF メッセージ非表示フラグ
		IF スキップフラグ == 0
			CALL 戦闘効果削除(消滅番号)
		ENDIF
	ELSEIF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
	ELSEIF スキップフラグ == 3
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は効果を弾いた！
	ELSE
		IF DT_CELL_GETS("戦闘効果データベース", 消滅番号, "効果名") == ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS("戦闘効果データベース", 消滅番号, "対象能力")%バフが消え去った！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS("戦闘効果データベース", 消滅番号, "効果名")%バフが消え去った！
		ENDIF
		CALL 戦闘効果削除(消滅番号)
	ENDIF
NEXT

CALL 戦闘中最大ＨＰＭＰ変動処理()

@アビテンプレート_不利効果_バフ全て消去(消去能力 = "")
#DIM スキップフラグ
#DIM メッセージ非表示フラグ
#DIMS 名前一時格納
#DIMS 消去能力
#DIM 対象番号
#DIM 消滅番号
#DIM バフ番号
#DIM 消滅配列番号
#DIM 消滅番号保存, 100

;特殊な適用範囲のバフは消去されないので注意

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "ディスペル_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	VARSET 消滅番号保存
	スキップフラグ = 0
	消滅配列番号 = 0
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからないのでスキップ
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;回数制以外のディスペルガードがある場合はスキップ
	バフ番号 = バフ番号取得("ディスペルガード", 戦闘行動対象) && !アビ汎用変数:阻害無視オプション
	IF バフ番号 >= 0 && DT_CELL_GET("戦闘効果データベース", バフ番号, "持続回数") <= 0
		スキップフラグ = 1
	ENDIF

	IF スキップフラグ == 0
		;全部見る
		FOR LOCAL, 0, DT_ROW_LENGTH("戦闘効果データベース")
			SIF DT_CELL_GETS("戦闘効果データベース", LOCAL, "バフ・デバフフラグ") != "バフ"
				CONTINUE
			SIF DT_CELL_GETS("戦闘効果データベース", LOCAL, "適用範囲") != @"{戦闘行動対象}"
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", LOCAL, "特殊表示オプション") == -1
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", LOCAL, "消去不可フラグ") != 0 && アビ汎用変数:消去不可バフデバフ削除オプション == 0
				CONTINUE
			IF 消去能力 != ""
				;消去能力が設定されている場合、特定の能力のみ消す
				SIF 消去能力 != DT_CELL_GETS("戦闘効果データベース", LOCAL, "対象能力")
					CONTINUE
			ENDIF
			;ここで削除しちゃうと行がズレるので一旦配列に退避
			IF バフ番号取得("ディスペルガード", 戦闘行動対象) >= 0 && !アビ汎用変数:阻害無視オプション
				;ディスペルガードバフがあると弾く
				スキップフラグ = 3
				;回数持続の効果の回数を減少
				CALL バフ回数経過処理(戦闘行動対象, "ディスペルガード")
			ELSE
				消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", LOCAL, "id")
				消滅配列番号 += 1
			ENDIF
		NEXT
		
		FOR LOCAL, 0, 消滅配列番号
			CALL 戦闘効果削除(消滅番号保存:LOCAL, 1)
		NEXT
	ENDIF

	IF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
	ELSE
		IF アビ汎用変数:消去不可バフデバフ削除オプション
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%消去能力%バフが全て消え去った！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%消去能力%バフが{消滅配列番号, 2}個消え去った！
		ENDIF
	ENDIF
NEXT

CALL 戦闘中最大ＨＰＭＰ変動処理()

@アビテンプレート_不利効果_スロウ
#DIM 対象番号
#DIM スキップフラグ
#DIM 減少チャージ
#DIM 成功率一時格納
#DIM バフ番号
#DIM 累積量

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "スロウ_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		;味方はスロウ効果なし
		CONTINUE
	ELSE
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF アビ汎用変数:弱体耐性貫通オプション == 0
		IF アビ汎用変数:基礎成功確率
			;基礎成功率が設定されてる場合は成功率判定
			成功率一時格納 = アビ汎用変数:基礎成功確率
			CALL バフ・デバフ測定(戦闘行動キャラ, "弱体成功率")
			成功率一時格納 += RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "弱体耐性")
			成功率一時格納 -= RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "状態異常耐性_スロウ")
			成功率一時格納 -= RESULT
			IF RAND:100 > 成功率一時格納
				スキップフラグ = 2
			ELSEIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
				;ボスに基礎成功率のあるデバフを掛けることに成功した場合、ボスはそのデバフの状態異常耐性を得る
				累積量 = ボス耐性累積量("スロウ", 敵BATTLE_STATE:(戦闘行動対象 - 10):Lv)
				IF 累積量 > 0
					バフ番号 = バフ番号取得_枠("ボス累積耐性_スロウ", 戦闘行動対象)
					IF バフ番号 < 0
						{
						DT_ROW_ADD "戦闘効果データベース", "バフ・デバフフラグ", "バフ", "適用範囲", @"{戦闘行動対象}",
							"効果枠", "ボス累積耐性_スロウ",
							"対象能力", "状態異常耐性_スロウ",
							"効果量_固定値", 累積量, "効果名", "ボス累積耐性",
							"持続ターン", -1, "消去不可フラグ", 1, "特殊表示オプション", -1
						}
					ELSE
						累積量 += DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
						DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 累積量
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン < 1
		;そもそもチャージ溜まってない
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%には効果が無いようだ…
	ELSEIF スキップフラグ == 2
		;スロウ外した
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%はスロウ効果を受けなかった…
	ELSE
		減少チャージ = MIN(敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン, アビ汎用変数:効果量)
		敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン = MAX(敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン - アビ汎用変数:効果量, 0)
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のチャージが{減少チャージ}下がった！
	ENDIF
NEXT



@アビテンプレート_不利効果_トレハン
#DIM 対象番号
#DIM スキップフラグ
#DIM 現在トレハンLv
#DIM データベース行数
#DIM 成功率一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "トレハン_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		;味方はトレハン効果なし
	ELSE
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
		データベース行数 = デバフ番号取得("ドロップ率UP")
		IF データベース行数 == -1
			現在トレハンLv = 0
		ELSE
			現在トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")
		ENDIF
		;最大9
		SIF 現在トレハンLv > 8
			スキップフラグ = 1

		IF アビ汎用変数:弱体耐性貫通オプション == 0
			IF アビ汎用変数:基礎成功確率
				;基礎成功率が設定されてる場合は成功率判定
				成功率一時格納 = アビ汎用変数:基礎成功確率
				CALL バフ・デバフ測定(戦闘行動キャラ, "弱体成功率")
				成功率一時格納 += RESULT
				CALL バフ・デバフ測定(戦闘行動対象, "弱体耐性")
				成功率一時格納 -= RESULT
				CALL バフ・デバフ測定(戦闘行動対象, "状態異常耐性_トレハン")
				成功率一時格納 -= RESULT
				IF RAND:100 > 成功率一時格納
					スキップフラグ = 2
				ENDIF
			ELSE
				;されてない場合は現在のトレハン数値に応じて成功率判定
				成功率一時格納 = 99 - 現在トレハンLv * (現在トレハンLv + 1)
				CALL バフ・デバフ測定(戦闘行動キャラ, "弱体成功率")
				成功率一時格納 += RESULT
				CALL バフ・デバフ測定(戦闘行動対象, "弱体耐性")
				成功率一時格納 -= RESULT
				CALL バフ・デバフ測定(戦闘行動対象, "状態異常耐性_トレハン")
				成功率一時格納 -= RESULT
				IF RAND:100 > 成功率一時格納
					スキップフラグ = 2
				ENDIF
			ENDIF
		ENDIF

		IF スキップフラグ == 1
			;すでにトレハン９
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%にはこれ以上効果が無いようだ…
		ELSEIF スキップフラグ == 2
			;外した
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%にトレハン効果が外れた…
		ELSE
			IF データベース行数 == -1
				DT_ROW_ADD "戦闘効果データベース", "効果枠", "ドロップ率UP", "バフ・デバフフラグ", "デバフ", "適用範囲", @"{戦闘行動対象}", "対象能力", "ドロップ率UP", "効果量_固定値", 1, "効果名", "トレジャーハント", "持続ターン", -1, "消去不可フラグ", 1, "特殊表示オプション", 1
			ELSE
				DT_CELL_SET "戦闘効果データベース", データベース行数, "効果量_固定値", 現在トレハンLv + 1
			ENDIF
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のアイテムドロップ率が上がった！
		ENDIF
	ENDIF
NEXT

@バフ・デバフ特殊表示_ドロップ率UP(対象キャラ隊列, バフ・デバフ番号)
#DIM 対象キャラ隊列
#DIM バフ・デバフ番号

バフ・デバフ番号 = デバフ番号取得("ドロップ率UP", 対象キャラ隊列)
PRINT ドロップ率UP：
PRINTFORML Lv{DT_CELL_GET("戦闘効果データベース", バフ・デバフ番号, "効果量_固定値")}

@アビテンプレート_不利効果_即死
#DIM 対象番号
#DIM スキップフラグ
#DIM 減少チャージ
#DIM 成功率一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "即死_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	;基本的にデバフは死者にはかからないのでスキップ
	IF 戦闘行動対象 < 10
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF
	IF アビ汎用変数:弱体耐性貫通オプション == 0
		IF アビ汎用変数:基礎成功確率
			;基礎成功率が設定されてる場合は成功率判定
			成功率一時格納 = アビ汎用変数:基礎成功確率
			CALL バフ・デバフ測定(戦闘行動キャラ, "弱体成功率")
			成功率一時格納 += RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "弱体耐性")
			成功率一時格納 -= RESULT
			CALL バフ・デバフ測定(戦闘行動対象, "状態異常耐性_即死")
			成功率一時格納 -= RESULT
			IF RAND:100 > 成功率一時格納
				スキップフラグ = 2
			ENDIF
		ENDIF
	ENDIF
	IF 戦闘行動対象 >= 10 && 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
		;ボスには効かない
		スキップフラグ = 2
	ENDIF

	IF スキップフラグ == 2
		;即死外した
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%は即死効果を受けなかった…
	ELSE
		IF 戦闘行動対象 < 10
			BATTLE_STATE:戦闘行動対象:ＨＰ = 0
		ELSE
			敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ = 0
		ENDIF
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%は倒れた！
		CALL 戦闘不能者登録(戦闘行動対象, 戦闘行動キャラ)
	ENDIF
NEXT

CALL 全滅チェック

@アビテンプレート_不利効果_累積デバフ効果セット
JUMP アビテンプレート_不利効果_デバフ効果セット(1)

@アビテンプレート_不利効果_奥義ゲージDOWN
#DIM ゲージ減少量
#DIM 対象番号

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

;付与予定のデバフ対象を保存
アビテンプレート用_付与デバフ保存 += "奥義ゲージDOWN_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	;基本的にデバフは死者にはかからない
	SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
		CONTINUE
	IF 戦闘行動対象 < 10
		;味方対象
		ゲージ減少量 = BATTLE_STATE:戦闘行動対象:奥義ゲージ * アビ汎用変数:効果割合 / 100
		ゲージ減少量 += アビ汎用変数:効果量

		ゲージ減少量 = MIN(ゲージ減少量, BATTLE_STATE:戦闘行動対象:奥義ゲージ)
		IF ゲージ減少量 > 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%の奥義ゲージが{ゲージ減少量}％減少した！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%には効果がなかった…
		ENDIF
		BATTLE_STATE:戦闘行動対象:奥義ゲージ = MAX(BATTLE_STATE:戦闘行動対象:奥義ゲージ - ゲージ減少量, 0)
	ENDIF
	;奥義ゲージは味方にしか存在しないので、敵対象時の分岐は無し
NEXT

@アビテンプレート_ＭＰ減少処理_現在値依存()
#DIM 対象番号
#DIM 減少量計算値

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	IF アビテンプレート用_対象保存:対象番号 < 10
		;味方対象
		戦闘行動対象 = アビテンプレート用_対象保存:対象番号
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
		減少量計算値 = BATTLE_STATE:戦闘行動対象:ＭＰ * アビ汎用変数:効果割合 / 100
		減少量計算値 = 減少量計算値 + アビ汎用変数:効果量
		減少量計算値 = MIN(減少量計算値, BATTLE_STATE:戦闘行動対象:ＭＰ)

		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%のＭＰが{減少量計算値}減少した！
		BATTLE_STATE:戦闘行動対象:ＭＰ = MAX(BATTLE_STATE:戦闘行動対象:ＭＰ - 減少量計算値, 0)

	ELSE
		;敵にはMPがないのでなんもしない
	ENDIF
NEXT


@ボス耐性累積量(バフ・デバフ種類, レベル)
#FUNCTION
#DIMS CONST 能力系デバフタイプ = "連続攻撃率", "与ダメージ", "被ダメージ", "全強化", "全耐性"
#DIMS CONST チャージ系デバフタイプ = "最大ＣＴ増加", "スロウ"
#DIMS バフ・デバフ種類
#DIM レベル

IF バフ・デバフ種類 == "弱体耐性" || REGEXPMATCH(バフ・デバフ種類, "^状態異常耐性_.*$") > 0
	;弱体の成功率を増減させるデバフには干渉しない
	RETURNF 0
ELSEIF MATCH(チャージ系デバフタイプ, バフ・デバフ種類)
	;チャージターンを操作する系のデバフはボスくらいにしか出番がないので累積量が低い
	SELECTCASE レベル
		CASE IS >= 75
			RETURNF 15
		CASE IS >= 65
			RETURNF 10
		CASE IS >= 55
			RETURNF 5
		CASE IS >= 45
			RETURNF 3
		CASE IS >= 35
			RETURNF 2
		CASEELSE
			RETURNF 0
	ENDSELECT
ELSEIF GETNUM(BATTLE_STATE, バフ・デバフ種類, 2) > 0 || MATCH(能力系デバフタイプ, バフ・デバフ種類)
	;基礎能力操作系のデバフはちょっと通りにくくなる
	SELECTCASE レベル
		CASE IS >= 75
			RETURNF 20
		CASE IS >= 65
			RETURNF 15
		CASE IS >= 55
			RETURNF 10
		CASE IS >= 45
			RETURNF 7
		CASE IS >= 35
			RETURNF 5
		CASE IS >= 25
			RETURNF 3
		CASEELSE
			RETURNF 0
	ENDSELECT
ELSE
	;行動を縛ったりする特殊なデバフはすぐ全然効かなくなる
	SELECTCASE レベル
		CASE IS >= 55
			RETURNF 100000
		CASE IS >= 45
			RETURNF 80
		CASE IS >= 35
			RETURNF 50
		CASE IS >= 25
			RETURNF 34
		CASEELSE
			RETURNF 10
	ENDSELECT
ENDIF
RETURNF 0


@アビテンプレート_不利効果_付随デバフ効果セット(付随先枠)
#DIMS 付随先枠

アビ汎用文字列:付随先設定 '= 付随先枠
JUMP アビテンプレート_不利効果_デバフ効果セット()



@アビテンプレート_不利効果_バフ効果短縮(短縮種別)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用変数:効果量
;アビ汎用変数:基礎成功確率
;------------------------------------------------------------------------
;効果量の分だけ効果種別の回数を減らす。１,０や－１は変動しない。
;適用範囲は対象のバフのみ、全体バフなどは変動しない
;アビ汎用文字列:バフ・デバフ枠にALLを入れると全部縮める
;------------------------------------------------------------------------
#DIM 対象番号
#DIMS 短縮種別
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 効果量一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のバフと枠をチェック
	スキップフラグ = 1
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") != アビ汎用文字列:バフ・デバフ枠 && アビ汎用文字列:バフ・デバフ枠 != "ALL"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") != @"{戦闘行動対象}"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") != "バフ"
			CONTINUE
		SIF アビ汎用変数:基礎成功確率 > 0 && RAND:100 >= アビ汎用変数:基礎成功確率
			CONTINUE

		;短縮処理、1より上の場合だけ
		IF DT_CELL_GET("戦闘効果データベース", バフ番号, 短縮種別) > 1
			効果量一時格納 = DT_CELL_GET("戦闘効果データベース", バフ番号, 短縮種別) - アビ汎用変数:効果量
			DT_CELL_SET "戦闘効果データベース", バフ番号, 短縮種別, 効果量一時格納
			SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "特殊表示オプション") != -1
				スキップフラグ = 0
		ENDIF
	NEXT

	IF スキップフラグ == 0
		IF アビ汎用文字列:バフ・デバフ枠 == "ALL"
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のバフが短縮された！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ枠%が短縮された！
		ENDIF
	ENDIF
NEXT


@アビテンプレート_不利効果_バフ効果短縮_持続ターン

JUMP アビテンプレート_不利効果_バフ効果短縮("持続ターン")

@アビテンプレート_不利効果_バフ効果短縮_持続回数

JUMP アビテンプレート_不利効果_バフ効果短縮("持続回数")

@アビテンプレート_不利効果_バフ効果短縮_持続行動回数

JUMP アビテンプレート_不利効果_バフ効果短縮("持続行動回数")

@アビテンプレート_不利効果_バフ効果短縮_持続被ダメ回数

JUMP アビテンプレート_不利効果_バフ効果短縮("持続被ダメ回数")



@アビテンプレート_不利効果_デバフ効果延長(延長種別)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用変数:効果量
;アビ汎用変数:基礎成功確率
;------------------------------------------------------------------------
;効果量の分だけ効果種別の回数を増やす。０や－１は変動しない。
;適用範囲は対象のデバフのみ、全体デバフなどは変動しない
;アビ汎用文字列:バフ・デバフ枠にALLを入れると全部伸ばす
;------------------------------------------------------------------------
#DIM 対象番号
#DIMS 延長種別
#DIM デバフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 効果量一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のバフと枠をチェック
	スキップフラグ = 1
	FOR デバフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "効果枠") != アビ汎用文字列:バフ・デバフ枠 && アビ汎用文字列:バフ・デバフ枠 != "ALL"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "適用範囲") != @"{戦闘行動対象}"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "バフ・デバフフラグ") != "デバフ"
			CONTINUE
		SIF アビ汎用変数:基礎成功確率 > 0 && RAND:100 >= アビ汎用変数:基礎成功確率
			CONTINUE

		;延長処理、0より上の場合だけ
		IF DT_CELL_GET("戦闘効果データベース", デバフ番号, 延長種別) > 0
			効果量一時格納 = DT_CELL_GET("戦闘効果データベース", デバフ番号, 延長種別) + アビ汎用変数:効果量
			DT_CELL_SET "戦闘効果データベース", デバフ番号, 延長種別, 効果量一時格納
			SIF DT_CELL_GET("戦闘効果データベース", デバフ番号, "特殊表示オプション") != -1
				スキップフラグ = 0
		ENDIF
	NEXT

	IF スキップフラグ == 0
		IF アビ汎用文字列:バフ・デバフ枠 == "ALL"
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のバフが延長された！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ枠%が延長された！
		ENDIF
	ENDIF
NEXT


@アビテンプレート_不利効果_デバフ効果延長_持続ターン

JUMP アビテンプレート_不利効果_デバフ効果延長("持続ターン")

@アビテンプレート_不利効果_デバフ効果延長_持続回数

JUMP アビテンプレート_不利効果_デバフ効果延長("持続回数")

@アビテンプレート_不利効果_デバフ効果延長_持続行動回数

JUMP アビテンプレート_不利効果_デバフ効果延長("持続行動回数")

@アビテンプレート_不利効果_デバフ効果延長_持続被ダメ回数

JUMP アビテンプレート_不利効果_デバフ効果延長("持続被ダメ回数")

@アビテンプレート_不利効果_パッシブデバフ効果セット()

アビ汎用文字列:付随先設定 = パッシブ
アビ汎用変数:消去不可オプション = 1
JUMP アビテンプレート_不利効果_デバフ効果セット()
