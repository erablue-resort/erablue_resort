
@アビエディタ_その他有利効果追加(変更ID = 0, アビ段階 = 0)
;アビ文字列１ 有利効果の種類
;アビ文字列２ 
;アビ文字列３ 
;アビ文字列４ 
;アビ文字列５
;アビ数値１ 効果倍率
;アビ数値２ 効果固定値
;アビ数値３ 
;アビ数値４ 基礎成功率
;アビ数値５
#DIM 効果順番
#DIM DYNAMIC 効果倍率
#DIM DYNAMIC 効果固定値
#DIMS DYNAMIC 有利効果
#DIMS DYNAMIC 効果対象
#DIM DYNAMIC 効果成功率
#DIM アビ段階
#DIM 変更ID
#DIMS HTML文字列
#DIMS 計算式タイプ

;効果の最初かどうかを判定
IF 変更ID == 0
	;アビ段階が同じ効果がある、かつ新規効果追加状態＝最初ではない
	LOCAL = DT_SELECT("アビエディタデータベース", @"アビ段階 = {アビ段階}")
	効果順番 = LOCAL
ELSE
	;既存効果変更の場合、行数を見る
	FOR LOCAL, 0, DT_ROW_LENGTH("アビエディタデータベース")
		IF DT_CELL_GET("アビエディタデータベース", LOCAL, "id") == 変更ID
			効果順番 = LOCAL
			BREAK
		ENDIF
	NEXT
ENDIF

IF 変更ID
	効果対象 '= DT_CELL_GETS("アビエディタデータベース", 変更ID, "アビ対象", 1)
	有利効果 '= DT_CELL_GETS("アビエディタデータベース", 変更ID, "アビ文字列１", 1)
	効果倍率 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値１", 1)
	効果固定値 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値２", 1)
	効果成功率 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ数値４", 1)
	アビ段階 = DT_CELL_GET("アビエディタデータベース", 変更ID, "アビ段階", 1)
ENDIF
IF 効果対象 == ""
	IF 効果順番 < 1
		効果対象 = 味方単体
	ELSE
		効果対象 = 上と同じ相手
	ENDIF
ENDIF
SIF 有利効果 == ""
	有利効果 = 奥義ゲージUP

$動作LOOP
;バフの計算式タイプ取得
計算式タイプ '= アビエディタ_ＭＰ自動計算_有利効果種類(有利効果)

DRAWLINE
PRINTL 有利効果の設定を選択してください。
DRAWLINE

PRINTL ・現在の効果
PRINT 　　
PRINTFORM %効果対象%に
IF 有利効果 == "ウェポンバースト"
	PRINTFORM ウェポンバースト効果を与える。
ELSE
	PRINT ［
	IF 効果倍率 > 0
		PRINTFORM {効果倍率}％
	ENDIF
	IF 効果固定値 > 0
		SIF 効果倍率 > 0
			PRINT ＋
		PRINTFORM {効果固定値}
	ENDIF
	PRINTFORM ］
	PRINTFORM の%有利効果%効果を与える。
ENDIF
PRINTL
DRAWLINE

HTML文字列 = 
HTML文字列 += "<div rect='50, 0, 5000, 3000' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
HTML文字列 += @"<button value='1'>[1] 効果対象を変更する</button>　　　　　　現在：%効果対象%<br>"
HTML文字列 += @"<button value='2'>[2] 有利効果を変更する</button>　　　　　　現在：%有利効果%<br>"
IF STRFIND(計算式タイプ, "割合") >= 0
	HTML文字列 += @"<button value='3'>[3] 効果倍率を変更する</button>　　　　　　現在：{効果倍率}％<br>"
ELSE
	HTML文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[3] 効果倍率を変更する</font><br>"
ENDIF
IF STRFIND(計算式タイプ, "固定値") >= 0
	HTML文字列 += @"<button value='4'>[4] 効果固定値を変更する</button>　　　　　現在：{効果固定値}<br>"
ELSE
	HTML文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[4] 効果固定値を変更する</font><br>"
ENDIF
HTML文字列 += "<br>"
HTML文字列 += "<br>"
HTML文字列 += @"<button value='998'>[998] この設定で効果を決定する</button><br>"
HTML文字列 += @"<button value='999'>[999] 保存せず戻る</button><br>"
HTML文字列 += "</div>"


HTML_PRINT HTML文字列, 1
FOR LOCAL, 0, 35
	PRINTL
NEXT

BINPUT
PRINTL
PRINTL
SELECTCASE RESULT
	CASE 1
		CALL アビエディタ_対象選択(効果対象, 効果順番)
	CASE 2
		CALL アビエディタ_その他有利効果種類選択(有利効果)
	CASE 3
		DRAWLINE
		PRINTFORML 効果倍率を入力してください。 現在：{効果倍率}％
		PRINTL
		INPUT
		IF RESULT < 0
			PRINTW 負の値は指定出来ません
		ENDIF
		効果倍率 = RESULT
	CASE 4
		DRAWLINE
		PRINTFORML 効果固定値を入力してください。 現在：{効果固定値}
		PRINTL
		INPUT
		IF RESULT < 0
			PRINTW 負の値は指定出来ません
		ENDIF
		効果固定値 = RESULT
	CASE 998
		IF STRFIND(計算式タイプ, "割合") < 0
			効果倍率 = 0
		ENDIF
		IF STRFIND(計算式タイプ, "固定値") < 0
			効果固定値 = 0
		ENDIF
		IF 変更ID
			;既存効果変更
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ対象", 効果対象, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値１", 効果倍率, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値２", 効果固定値, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ数値４", 効果成功率, 1
			DT_CELL_SET "アビエディタデータベース", 変更ID, "アビ文字列１", 有利効果, 1
			PRINTW バフ効果を変更しました。
		ELSE
			;新規効果追加
			{
			DT_ROW_ADD 
			"アビエディタデータベース", "効果順番", 効果順番, "アビ段階", アビ段階, "アビ効果種別", "その他有利効果", "アビ対象", 効果対象, 
			"アビ数値１", 効果倍率, "アビ数値２", 効果固定値, "アビ数値４", 効果成功率,
			"アビ文字列１", 有利効果
			}
			PRINTW 新しい有利効果を追加しました。
		ENDIF
		RETURN 1
	CASE 999
		RETURN 0
ENDSELECT

GOTO 動作LOOP



@アビエディタ_その他有利効果種類選択(有利効果)
#DIMS REF 有利効果
#DIM 表示数

DRAWLINE
PRINTFORML 設定する有利効果を選択してください。 現在：%有利効果%
DRAWLINE
PRINTBUTTONLC @"[0] 奥義ゲージUP", 0
PRINTL
PRINTBUTTONLC @"[1] ウェポンバースト", 1
PRINTL
PRINTBUTTONLC @"[2] デバフ消去", 2
PRINTL
; PRINTBUTTONLC @"[3] バフ効果延長", 3
; PRINTL
; PRINTBUTTONLC @"[4] デバフ効果短縮", 4
; PRINTL
PRINTL
PRINTBUTTONLC "[499] その他有利効果解説", 499
PRINTL
DRAWLINE
PRINTL
	
BINPUT
PRINTL
PRINTL
SELECTCASE RESULT
	CASE 0
		有利効果 = 奥義ゲージUP
	CASE 1
		有利効果 = ウェポンバースト
	CASE 2
		有利効果 = デバフ消去
	CASE 499
		DRAWLINE
		PRINTL ・奥義ゲージUP
		PRINTL 　　効果量だけ奥義ゲージを上昇させます。
		PRINTL 　　効果固定値のみ効果を発揮します。
		PRINTL
		PRINTL ・ウェポンバースト
		PRINTL 　　奥義ゲージを元の値にかかわらず100％にします。
		PRINTL 　　元の値が100％以上の時、効果を発揮しません。
		PRINTL 　　効果割合、効果固定値どちらも効果を発揮しません。
		PRINTL
		PRINTL ・デバフ消去
		PRINTL 　　最も新しいデバフを効果固定値の数だけ削除します。
		PRINTL 　　消去不可のデバフは飛ばします。消去数にカウントされません。
		PRINTL 　　効果固定値のみ効果を発揮します。
		PRINTL
		WAIT
		RESTART
ENDSELECT


@アビエディタ_ＭＰ自動計算_有利効果種類(有利効果)
#FUNCTIONS
#DIMS 有利効果

;自動計算に使う係数と、計算式タイプを返す

SELECTCASE 有利効果
	CASE "奥義ゲージUP"
		RETURNF "固定値"
	CASE "ウェポンバースト"
		RETURNF ""
	CASE "デバフ消去"
		RETURNF "固定値"
ENDSELECT
