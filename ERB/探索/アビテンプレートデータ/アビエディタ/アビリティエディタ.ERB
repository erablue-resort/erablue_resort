
@アビエディタデータベースセット()
DT_CREATE "アビエディタデータベース"
DT_COLUMN_ADD "アビエディタデータベース", "アビ効果種別"
DT_COLUMN_ADD "アビエディタデータベース", "アビ対象"
DT_COLUMN_ADD "アビエディタデータベース", "効果順番", 1
DT_COLUMN_ADD "アビエディタデータベース", "アビ段階", 1
DT_COLUMN_ADD "アビエディタデータベース", "アビ段階条件"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列１"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列２"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列３"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列４"
DT_COLUMN_ADD "アビエディタデータベース", "アビ文字列５"
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値１", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値２", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値３", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値４", 3
DT_COLUMN_ADD "アビエディタデータベース", "アビ数値５", 3

@アビリティエディタ()
#DIM 対象NO
#DIM 既存アビ数
#DIM 表示Lv
#DIM アビ作成中フラグ
#DIM アビID, 100
#DIMS HTML文字列
#DIM 枠配置縦位置
#DIM アビ段階
#DIM 最大アビ段階
#DIMS アビ名, 5
#DIM 消費ＭＰ, 5
#DIMS 取得条件, 5
#DIM 表示候補数
#DIM 選択番号

#DIM 取得条件_Lv
#DIMS 取得条件_陥落

;初期化部分

;アビ名を呼び出すためにキャラを１人追加する
ADDCHARA 0
;アビ作成中フラグ = 0
;開発中のためいきなり作成中に飛ぶ
アビ作成中フラグ = 1
表示Lv = 1
基礎BATTLE_STATE:0:Lv = 表示Lv
VARSET アビ名, "（未定）"
VARSET 消費ＭＰ, -1
VARSET 取得条件
アビ段階 = 0
最大アビ段階 = 0
CALL アビエディタデータベースセット()

対象NO = 1

$動作LOOP

DRAWLINE
PRINTL 固有アビリティを作成するためのエディタです。
PRINTL 現在開発中のため、作成出来る機能に制限があります。
DRAWLINE

HTML文字列 = 
;選択キャラ表示枠
HTML文字列 += "<div rect='50, 0, 5000, 1200' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
IF 対象NO > 0
	HTML文字列 += @"%CSVNAME(対象NO)%<br>"
	;現在あるアビリティを呼んでくる
	既存アビ数 = ENUMFUNCBEGINSWITH(@"固有アビ_{対象NO}_")
	IF 既存アビ数 > 0
		HTML文字列 += @"Lv{表示Lv, 2}時アビリティ："
		FOR LOCAL, 0, 既存アビ数
			TSTR:コマンド名受渡 = 
			詳細文文字列受け渡し変数 = 
			CALLFORM %RESULTS:LOCAL%("アビ名", 0)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			CALLFORM %RESULTS:LOCAL%("アビ説明文_詳細", 0)
			SIF 詳細文文字列受け渡し変数 == ""
				CALLFORM %RESULTS:LOCAL%("アビ説明文", 0)
			HTML文字列 += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>■%TSTR:コマンド名受渡%</nonbutton>　　"
		NEXT
	ENDIF
	HTML文字列 += "<br>"
	HTML文字列 += "<br>"
ELSE
	HTML文字列 += "（選択なし）<br>"
	HTML文字列 += "<br>"
	HTML文字列 += "<br>"
ENDIF
HTML文字列 += "<button value='200'>[作成キャラを変更する]</button> / <button value='201'>[表示レベルを変更する]</button>"
HTML文字列 += "</div>"
IF 対象NO > 0 && EXIST画像FILE(@"resources/{対象NO}%CSVCALLNAME(対象NO)%/顔_デフォルト")
	;何番でもいいけどとりあえず20000以降を使用
	GDISPOSE 20000
	SPRITEDISPOSE @"口上用画像0"
	CALL GCREATE_拡張子(20000, @"{対象NO}%CSVCALLNAME(対象NO)%/顔_デフォルト")
	SPRITECREATE @"口上用画像0", 20000
	HTML文字列 += @"<div rect='3900, 50, 1500, 1500'><img src='口上用画像0' height='{GHEIGHT(20000) / 16 * 100}' width='{GWIDTH(20000) / 16 * 100}'></div>"
ENDIF

;操作パネル
HTML文字列 += "<div rect='50, 1250, 1600, 4200' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
IF アビ作成中フラグ == 0
	HTML文字列 += "<button value='900'>[900] 新規アビリティ作成</button><br>"
	;作成中
	;HTML文字列 += "<button value='101'>[101] アビデータ読込</button><br>"
ELSE
	;アビパーツの選択肢を並べる
	HTML文字列 += "<button value='0'>[ 0] アビ名を変更する</button><br>"
	HTML文字列 += "<button value='1'>[ 1] ダメージ効果を追加</button><br>"
	HTML文字列 += "<button value='2'>[ 2] バフ効果を追加</button><br>"
	HTML文字列 += "<button value='3'>[ 3] その他有利効果を追加</button><br>"
ENDIF
HTML文字列 += "<br>"
HTML文字列 += "<br>"
IF アビ作成中フラグ
	HTML文字列 += "<button value='997'>[997] アビ段階を増やす</button><br>"
	HTML文字列 += "<br>"
	HTML文字列 += "<button value='998'>[998] アビリティERBを生成する</button><br>"
ENDIF
HTML文字列 += "<button value='999'>[999] 戻る</button><br>"
HTML文字列 += "</div>"

;作成アビリティ効果表示枠
HTML文字列 += "<div rect='1700, 1425, 5050, 4025' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
HTML文字列 += @"<button value='0'>■%アビ名:アビ段階, 30, LEFT%</button>取得条件：%アビエディタ_取得条件文章化(取得条件:アビ段階)%　　<button value='202'>[取得条件変更]</button><br>"
HTML文字列 += @"消費ＭＰ："
IF 消費ＭＰ:アビ段階 < 0
	HTML文字列 += @"自動設定（現在{ABS(消費ＭＰ:アビ段階)}）　　<button value='203'>[手動で設定する]</button><br>"
ELSE
	HTML文字列 += @"{消費ＭＰ:アビ段階}　　<button value='203'>[自動で設定する]</button><br>"
ENDIF
HTML文字列 += "------------------------------------------------------------------------------------------------------<br>"
HTML文字列 += "</div>"
IF アビ作成中フラグ
	枠配置縦位置 = 0
	VARSET RESULT
	表示候補数 = DT_SELECT("アビエディタデータベース", @"アビ段階 = {アビ段階}", "効果順番 ASC")
	;配列に保存
	ARRAYCOPY "RESULT", "アビID"
	ARRAYSHIFT アビID, -1, 0
	FOR LOCAL, 0, 表示候補数
		SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ効果種別", 1)
			CASE "ダメージ効果"
				HTML文字列 += @"<div rect='1800, {1825 + 枠配置縦位置}, 4850, 380' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
				HTML文字列 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ対象", 1)%に"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
					HTML文字列 += @"［%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)%✕{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1)}％］の"
				ELSE
					HTML文字列 += @"［{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1)}］の"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1) < 0
					HTML文字列 += "自属性ダメージを"
				ELSE
					HTML文字列 += @"<font color='#%カラーパレット_HTML(@"%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1))%属性")%'>%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1))%属性</font>ダメージを"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値４", 1) < 2
					HTML文字列 += "与える。"
				ELSE
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値４", 1)}回与える。"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += @"<button value='{LOCAL + 100}'>[効果を変更する]</button>　　<button value='{LOCAL + 300}'>[効果を削除する]</button>　　"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "効果順番", 1) > 0
					HTML文字列 += @"<button value='{LOCAL + 400}'>[↑]</button>"
				ELSE
					HTML文字列 += "　　"
				ENDIF
				HTML文字列 += "　"
				IF 表示候補数 > LOCAL + 1
					HTML文字列 += @"<button value='{LOCAL + 500}'>[↓]</button>"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += "</div>"
				枠配置縦位置 += 430
			CASE "バフ効果"
				HTML文字列 += @"<div rect='1800, {1825 + 枠配置縦位置}, 4850, 380' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
				HTML文字列 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ対象", 1)%に［"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1)}％"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1) > 0
					SIF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
						HTML文字列 += "＋"
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1)}"
				ENDIF
				HTML文字列 += "］（"
				IF STRFIND(DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列２", 1), DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)) == 0
					HTML文字列 += @"%TOFULL(REPLACE(DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列２", 1), DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1), ""))%枠"
				ELSE
					HTML文字列 += "独自枠"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1) > 0
					HTML文字列 += @"・{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値３", 1)}%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列３", 1)%"
				ENDIF
				IF STRFIND(DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列４", 1), "消去不可") >= 0
					HTML文字列 += "・消去不可"
				ENDIF
				IF DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1) == "ストレングス" || DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1) == "逆境"
					HTML文字列 += @"）の%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)%効果を与える。"
				ELSE
					HTML文字列 += @"）の%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)%上昇効果を与える。"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += @"<button value='{LOCAL + 100}'>[効果を変更する]</button>　　<button value='{LOCAL + 300}'>[効果を削除する]</button>　　"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "効果順番", 1) > 0
					HTML文字列 += @"<button value='{LOCAL + 400}'>[↑]</button>"
				ELSE
					HTML文字列 += "　　"
				ENDIF
				HTML文字列 += "　"
				IF 表示候補数 > LOCAL + 1
					HTML文字列 += @"<button value='{LOCAL + 500}'>[↓]</button>"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += "</div>"
				枠配置縦位置 += 430
			CASE "その他有利効果"
				HTML文字列 += @"<div rect='1800, {1825 + 枠配置縦位置}, 4850, 380' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
				HTML文字列 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ対象", 1)%に［"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1)}％"
				ENDIF
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1) > 0
					SIF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値１", 1) > 0
						HTML文字列 += "＋"
					HTML文字列 += @"{DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "アビ数値２", 1)}"
				ENDIF
				HTML文字列 += "］"
				HTML文字列 += @"の%DT_CELL_GETS("アビエディタデータベース", アビID:LOCAL, "アビ文字列１", 1)%効果を与える。"
				HTML文字列 += "<br>"
				HTML文字列 += @"<button value='{LOCAL + 100}'>[効果を変更する]</button>　　<button value='{LOCAL + 300}'>[効果を削除する]</button>　　"
				IF DT_CELL_GET("アビエディタデータベース", アビID:LOCAL, "効果順番", 1) > 0
					HTML文字列 += @"<button value='{LOCAL + 400}'>[↑]</button>"
				ELSE
					HTML文字列 += "　　"
				ENDIF
				HTML文字列 += "　"
				IF 表示候補数 > LOCAL + 1
					HTML文字列 += @"<button value='{LOCAL + 500}'>[↓]</button>"
				ENDIF
				HTML文字列 += "<br>"
				HTML文字列 += "</div>"
				枠配置縦位置 += 430
		ENDSELECT
	NEXT
ENDIF

;アビ段階タブ表示
FOR LOCAL, 0, 最大アビ段階 + 1
	IF LOCAL == アビ段階
		HTML文字列 += @"<div rect='{1700 + LOCAL * 350}, 1250, 300, 200' padding='25' color='#101010' border='31,31,0' bcolor='#C0C0C0'><nobr>"
	ELSE
		HTML文字列 += @"<div rect='{1700 + LOCAL * 350}, 1250, 300, 200' padding='25' color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
	ENDIF
	HTML文字列 += @"<button value='{LOCAL + 1000}'> {LOCAL}  </button>"
	HTML文字列 += "</div>"
NEXT


HTML_PRINT HTML文字列, 1
FOR LOCAL, 0, 49
	PRINTL
NEXT

BINPUT
PRINTL
PRINTL

選択番号 = RESULT
SELECTCASE 選択番号
	CASE 200
		CALL CSVコピー処理(2)
		SIF 選択番号 > 0
			対象NO = 選択番号
	CASE 201
		DRAWLINE
		PRINTL 1～80の範囲でLvを設定してください。
		INPUT
		IF 選択番号 < 1 || 選択番号 > 80
			PRINTW 数値が不正です。
		ELSE
			表示Lv = 選択番号
			基礎BATTLE_STATE:0:Lv = 表示Lv
		ENDIF
	CASE 202
		;取得条件を解析

		VARSET RESULTS
		取得条件_Lv = 0
		取得条件_陥落 = 
		SPLIT 取得条件:アビ段階, "***", RESULTS
		FOR LOCAL, 0, 選択番号
			IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
				取得条件_Lv = TOINT(REPLACE(RESULTS:LOCAL, "Lv_", ""))
			ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
				取得条件_陥落 '= REPLACE(RESULTS:LOCAL, "陥落_", "")
			ENDIF
		NEXT

		$取得LOOP

		DRAWLINE
		PRINTL 取得条件を設定してください。
		PRINTFORM 現在の取得条件：
		IF 取得条件_Lv > 0
			PRINTFORM Lv{取得条件_Lv}　　
		ENDIF
		IF 取得条件_陥落 != ""
			PRINTFORM 陥落条件：%取得条件_陥落%　　
		ENDIF
		PRINTL
		DRAWLINE
		IF 取得条件_Lv > 0
			PRINTBUTTON @"[0] Lv条件を設定する　　現在：Lv{取得条件_Lv}条件", 0
			PRINTL
			PRINTBUTTON "[1] Lv条件を削除する", 1
		ELSE
			PRINTBUTTON "[0] Lv条件を設定する　　現在：Lv条件なし", 0
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "陥落"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[10] 陥落：種類不問", 10
			RESETCOLOR
		ELSE
			PRINTBUTTON "[10] 陥落：種類不問", 10
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "恋慕"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[11] 陥落：恋慕系", 11
			RESETCOLOR
		ELSE
			PRINTBUTTON "[11] 陥落：恋慕系", 11
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "セフレ"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[12] 陥落：セフレ系", 12
			RESETCOLOR
		ELSE
			PRINTBUTTON "[12] 陥落：セフレ系", 12
		ENDIF
		PRINTL
		IF 取得条件_陥落 == "非エロ"
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON "[13] 陥落：非エロ系", 13
			RESETCOLOR
		ELSE
			PRINTBUTTON "[13] 陥落：非エロ系", 13
		ENDIF
		PRINTL
		PRINTL
		PRINTBUTTON "[999] これで決定", 999
		PRINTL
		PRINTL
		BINPUT
		PRINTL
		PRINTL
		SELECTCASE RESULT
			CASE 999
				取得条件:アビ段階 = 
				IF 取得条件_Lv > 0
					取得条件:アビ段階 += @"Lv_{取得条件_Lv}"
				ENDIF
				IF 取得条件_陥落 != ""
					SIF 取得条件:アビ段階 != ""
						取得条件:アビ段階 += "***"
					取得条件:アビ段階 += @"陥落_%取得条件_陥落%"
				ENDIF
				SIF 消費ＭＰ < 0
					CALL ＭＰ自動計算(消費ＭＰ, 取得条件:アビ段階, アビ段階)
				GOTO 動作LOOP
			CASE 0
				$LvLOOP
				DRAWLINE
				PRINTL 取得Lvを入力してください。
				DRAWLINE
				PRINTL
				INPUT
				IF RESULT < 1 || RESULT > 80
					PRINTW １未満、あるいは８０を越える数値は入力できません。
					GOTO LvLOOP
				ELSE
					取得条件_Lv = RESULT
				ENDIF
			CASE 1
				取得条件_Lv = 0
			CASE 10
				IF 取得条件_陥落 == "陥落"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 陥落
				ENDIF
			CASE 11
				IF 取得条件_陥落 == "恋慕"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 恋慕
				ENDIF
			CASE 12
				IF 取得条件_陥落 == "セフレ"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = セフレ
				ENDIF
			CASE 13
				IF 取得条件_陥落 == "非エロ"
					取得条件_陥落 = 
				ELSE
					取得条件_陥落 = 非エロ
				ENDIF
		ENDSELECT

		GOTO 取得LOOP
	CASE 203
		IF 消費ＭＰ < 0
			;自動＞手動
			$MPLOOP
			DRAWLINE
			PRINTL 消費するＭＰの値を入力してください。
			INPUT
			IF 選択番号 < 0
				PRINTW 負の値は指定出来ません。
				GOTO MPLOOP
			ELSE
				消費ＭＰ = 選択番号
			ENDIF
		ELSE
			;手動＞自動
			消費ＭＰ = -1
		ENDIF
	CASE 900
		アビ作成中フラグ = 1
		PRINTW 新しいアビリティを作成します。
	CASE 997
		最大アビ段階 ++
	CASE 998
		FOR LOCAL, 0, 最大アビ段階 + 1
			IF アビ名:LOCAL == "" || アビ名:LOCAL == "（未定）"
				PRINTFORMW 段階{LOCAL}のアビ名が入力されていません。
				GOTO 動作LOOP
			ENDIF
			IF DT_SELECT("アビエディタデータベース", @"アビ段階 = {LOCAL}") < 1
				PRINTFORMW 段階{LOCAL}の効果が設定されていません。
				GOTO 動作LOOP
			ENDIF
		NEXT
		CALL アビエディタ_ERB生成(対象NO, アビ名, 消費ＭＰ, 取得条件)

		;追加したキャラを削除
		DELCHARA CHARANUM - 1
		;データベースの削除
		DT_RELEASE "アビエディタデータベース"
		RETURN 0
	CASE 999
		;追加したキャラを削除
		DELCHARA CHARANUM - 1
		;データベースの削除
		DT_RELEASE "アビエディタデータベース"
		RETURN 0
	CASE 0
		$NAMELOOP
		DRAWLINE
		PRINTL アビ名を入力してください。
		INPUTS
		IF RESULTS == ""
			PRINTW アビ名は空白に出来ません。
			GOTO NAMELOOP
		ELSE
			アビ名:アビ段階 '= RESULTS
		ENDIF
	CASE 1
		;ダメージ効果
		CALL アビエディタ_ダメージ効果追加(0, アビ段階)
	CASE 2
		;バフ効果
		CALL アビエディタ_バフ効果追加(0, アビ段階)
	CASE 3
		;その他有利効果
		CALL アビエディタ_その他有利効果追加(0, アビ段階)
	CASE 100 TO 199
		;効果変更
		SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 100), "アビ効果種別", 1)
			CASE "ダメージ効果"
				CALL アビエディタ_ダメージ効果追加(アビID:(選択番号 - 100))
			CASE "バフ効果"
				CALL アビエディタ_バフ効果追加(アビID:(選択番号 - 100))
		ENDSELECT
	CASE 300 TO 399
		;効果削除
		DT_ROW_REMOVE "アビエディタデータベース", アビID:(選択番号 - 300)
	CASE 400 TO 499
		;上に移動
		LOCAL = DT_CELL_GET("アビエディタデータベース", アビID:(選択番号 - 400), "効果順番", 1)
		DT_SELECT "アビエディタデータベース", @"効果順番 = {LOCAL - 1}"
		DT_CELL_SET "アビエディタデータベース", RESULT:1, "効果順番", LOCAL, 1
		DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "効果順番", LOCAL - 1, 1
		;もしも下に移動した効果が二番目以降不可の対象だった時、入れ替え対象だったものと効果対象を入れ替える
		IF GROUPMATCH(DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), "味方単体", "敵単体", "敵横列", "敵縦列")
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", LOCALS, 1
		ENDIF
		;もしも一番上に来た効果の位置が最初、かつ対象が「上と同じ」だった場合、入れ替え対象だったものと効果対象を入れ替える
		LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1)
		IF LOCAL - 1 == 0 && LOCALS == "上と同じ相手"
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 400), "アビ対象", LOCALS, 1
		ENDIF
	CASE 500 TO 599
		;下に移動
		LOCAL = DT_CELL_GET("アビエディタデータベース", アビID:(選択番号 - 500), "効果順番", 1)
		DT_SELECT "アビエディタデータベース", @"効果順番 = {LOCAL + 1}"
		DT_CELL_SET "アビエディタデータベース", RESULT:1, "効果順番", LOCAL, 1
		DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "効果順番", LOCAL + 1, 1
		;もしも下に移動した効果が二番目以降不可の対象だった時、入れ替え対象だったものと効果対象を入れ替える
		IF GROUPMATCH(DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1), "味方単体", "敵単体", "敵横列", "敵縦列")
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", LOCALS, 1
		ENDIF
		;もしも一番上に来た効果の位置が最初、かつ対象が「上と同じ」だった場合、入れ替え対象だったものと効果対象を入れ替える
		LOCALS '= DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1)
		IF LOCAL - 1 == 0 && LOCALS == "上と同じ相手"
			LOCALS '= DT_CELL_GETS("アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", 1)
			DT_CELL_SET "アビエディタデータベース", アビID:(選択番号 - 500), "アビ対象", DT_CELL_GETS("アビエディタデータベース", RESULT:1, "アビ対象", 1), 1
			DT_CELL_SET "アビエディタデータベース", RESULT:1, "アビ対象", LOCALS, 1
		ENDIF
	CASE 1000 TO 1010
		アビ段階 = 選択番号 - 1000
ENDSELECT

SIF 消費ＭＰ < 0
	CALL ＭＰ自動計算(消費ＭＰ, 取得条件:アビ段階, アビ段階)
GOTO 動作LOOP



@アビエディタ_対象選択(ダメージ対象, 効果順番 = 0)
#DIMS REF ダメージ対象
#DIM ランダム被りなし
#DIM ランダム体数記録
#DIM 効果順番

SIF ランダム体数記録 < 1
	ランダム体数記録 = 1


DRAWLINE
PRINTFORML 対象を選択してください。 現在：%ダメージ対象%
IF 効果順番 > 0
	PRINTL 最初の効果以外の時、対象指定操作を挟む対象は選択出来ません。
ENDIF
PRINTL
IF 効果順番 < 1
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [-1] 直前の効果と同じ
	RESETCOLOR
ELSE
	PRINTBUTTON "[-1] 直前の効果と同じ", -1
ENDIF
PRINTL
PRINTBUTTON "[0] 自身", 0
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [1] 味方単体
	RESETCOLOR
ELSE
	PRINTBUTTON "[1] 味方単体", 1
ENDIF
PRINTL
PRINTBUTTON "[2] 味方全体", 2
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [10] 敵単体
	RESETCOLOR
ELSE
	PRINTBUTTON "[10] 敵単体", 10
ENDIF
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [11] 敵横列
	RESETCOLOR
ELSE
	PRINTBUTTON "[11] 敵横列", 11
ENDIF
PRINTL
IF 効果順番 > 0
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [12] 敵縦列
	RESETCOLOR
ELSE
	PRINTBUTTON "[12] 敵縦列", 12
ENDIF
PRINTL
PRINTBUTTON "[13] 敵全体", 13
PRINTL
PRINTBUTTON "[14] 敵ランダム◯体", 14
PRINTL
PRINTL
BINPUT
PRINTL
PRINTL
SELECTCASE RESULT
	CASE -1
		ダメージ対象 = 上と同じ相手
	CASE 0
		ダメージ対象 = 自身
	CASE 1
		ダメージ対象 = 味方単体
	CASE 2
		ダメージ対象 = 味方全体
	CASE 10
		ダメージ対象 = 敵単体
	CASE 11
		ダメージ対象 = 敵横列
	CASE 12
		ダメージ対象 = 敵縦列
	CASE 13
		ダメージ対象 = 敵全体
	CASE 14
		$ランダムLOOP
		DRAWLINE
		PRINTL ランダムで選ぶ対象数を入力してください。
		IF ランダム被りなし
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[ターゲット重複あり]", 1
			RESETCOLOR
			PRINT  / 
			PRINTPLAIN [ターゲット重複なし]
		ELSE
			PRINTPLAIN [ターゲット重複あり]
			PRINT  / 
			SETCOLOR カラーパレット("グレーアウト")
			PRINTBUTTON "[ターゲット重複なし]", 1
			RESETCOLOR
		ENDIF
		PRINTBUTTON "[0] 対象数を変更する", 0
		PRINTFORM 　　現在：{ランダム体数記録}体
		PRINTL
		PRINTBUTTON "[9] これで決定", 9
		PRINTL
		PRINTL
		BINPUT
		PRINTL
		PRINTL
		SELECTCASE RESULT
			CASE 1
				INVERTBIT ランダム被りなし, 0
			CASE 0
				DRAWLINE
				PRINTL 対象数を入力してください
				INPUT
				IF RESULT < 1
					PRINTW １未満の数は入力出来ません
				ELSE
					ランダム体数記録 = RESULT
				ENDIF
			CASE 9
				IF ランダム被りなし
					ダメージ対象 = 敵ランダム{ランダム体数記録}体（重複なし）
				ELSE
					ダメージ対象 = 敵ランダム{ランダム体数記録}体
				ENDIF
				RETURN 0
		ENDSELECT
		GOTO ランダムLOOP
ENDSELECT


@アビエディタ_ERB生成(対象No, アビ名, 消費ＭＰ, 取得条件)
#DIM DB行数
#DIMS アビERB記述
#DIMS アビ説明記述, 10
#DIMS アビ効果記述, 10
#DIM アビ段階
#DIM 最大アビ段階
#DIM 対象No
#DIM REF 消費ＭＰ
#DIMS REF アビ名
#DIMS REF 取得条件
#DIMS 取得条件_文字列, 10
#DIM アビID, 100

VARSET アビ説明記述
VARSET アビ効果記述

;アビ段階は連番であること、全てにアビ名が設定されていることを前提とする
FOR アビ段階, 0, VARSIZE("アビ名")
	SIF アビ名:アビ段階 != "" && アビ名:アビ段階 != "（未定）"
		最大アビ段階 = アビ段階
NEXT

アビERB記述 = @固有アビ_{対象No}_YYYY(ARGS, キャラ番号)<br>
アビERB記述 += "#DIM キャラ番号<br>"
アビERB記述 += "#DIM 消費ＭＰ<br>"
アビERB記述 += "#DIMS アビ名<br>"
アビERB記述 += "<br>"

;ここに習得条件入れる
IF MATCH(取得条件, "") < VARSIZE("取得条件") || MATCH(アビ名, "（未定）") < VARSIZE("アビ名") - 1
	;取得条件存在、もしくはアビ名複数時にIF文を記述する
	VARSET 取得条件_文字列 
	FOR アビ段階, 最大アビ段階, -1, -1
		;取得条件を解析
		VARSET RESULTS
		SPLIT 取得条件:アビ段階, "***", RESULTS
		FOR LOCAL, 0, RESULT
			IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
				SIF 取得条件_文字列:アビ段階 != ""
					取得条件_文字列:アビ段階 += " &amp;&amp; "
				取得条件_文字列:アビ段階 += @"基礎BATTLE_STATE:キャラ番号:Lv >= %REPLACE(RESULTS:LOCAL, "Lv_", "")%"
			ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
				SIF 取得条件_文字列:アビ段階 != ""
					取得条件_文字列:アビ段階 += " &amp;&amp; "
				SELECTCASE REPLACE(RESULTS:LOCAL, "陥落_", "")
					CASE "陥落"
						取得条件_文字列:アビ段階 += "陥落チェック(キャラ番号)"
					CASE "恋慕"
						取得条件_文字列:アビ段階 += "陥落チェック_恋慕系(キャラ番号)"
					CASE "セフレ"
						取得条件_文字列:アビ段階 += "GETBIT(陥落チェック(キャラ番号), 1)"
					CASE "非エロ"
						取得条件_文字列:アビ段階 += "陥落チェック_非性的(キャラ番号)"
				ENDSELECT
			ENDIF
		NEXT
		SIF 取得条件_文字列:アビ段階 == ""
			取得条件_文字列:アビ段階 = 1
		IF アビ段階 == 最大アビ段階
			アビERB記述 += @"IF %取得条件_文字列:アビ段階%<br>"
		ELSE
			アビERB記述 += @"ELSEIF %取得条件_文字列:アビ段階%<br>"
		ENDIF
		アビERB記述 += @"	アビ名 = %アビ名:アビ段階%<br>"
		アビERB記述 += @"	消費ＭＰ = {ABS(消費ＭＰ:アビ段階)}<br>"
	NEXT
	アビERB記述 += "ENDIF<br><br>"
ELSE
	アビERB記述 += @"アビ名 = %アビ名:アビ段階%<br>"
	アビERB記述 += @"消費ＭＰ = {ABS(消費ＭＰ:アビ段階)}<br><br>"
ENDIF

VARSET RESULT
DT_SELECT "アビエディタデータベース", "", "アビ段階 ASC, 効果順番 ASC"
;配列に保存
ARRAYCOPY "RESULT", "アビID"
ARRAYSHIFT アビID, -1, 0
アビ段階 = 0

FOR DB行数, 0, DT_ROW_LENGTH("アビエディタデータベース")
	SIF アビ段階 != DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ段階", 1)
		アビ段階 = DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ段階", 1)
	SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ効果種別", 1)
		CASE "ダメージ効果"
			アビ説明記述:アビ段階 += "<br>"
			IF DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1) != "上と同じ相手"
				アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1)%に"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ説明記述:アビ段階 += @"［%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%✕{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}％］の"
			ELSE
				アビ説明記述:アビ段階 += @"［{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}］の"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1) < 0
				アビ説明記述:アビ段階 += "自属性ダメージを"
			ELSE
				アビ説明記述:アビ段階 += @"<font color='#%カラーパレット_HTML(@"%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性")%'>%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性</font>ダメージを"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) < 2
				アビ説明記述:アビ段階 += "与える。"
			ELSE
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}回与える。"
			ENDIF

			アビ効果記述:アビ段階 += "CALL アビ汎用変数文字列リセット<br>"
			アビ効果記述:アビ段階 += アビエディタ_対象CALL変換(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1))
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果割合 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}<br>"
				アビ効果記述:アビ段階 += @"アビ汎用文字列:攻撃側使用能力値 = %DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%<br>"
			ELSE
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果量 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}<br>"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1) < 0
				LOCALS = CALL アビテンプレート_ダメージ処理_自属性
			ELSE
				LOCALS = CALL アビテンプレート_ダメージ処理_%属性数値文字列変換(DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1))%属性
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) > 1
				アビ効果記述:アビ段階 += @"FOR LOCAL, 0, {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}<br>"
				アビ効果記述:アビ段階 += @"	%LOCALS%<br>"
				アビ効果記述:アビ段階 += @"NEXT<br>"
			ELSE
				アビ効果記述:アビ段階 += @"%LOCALS%<br>"
			ENDIF
		CASE "バフ効果"
			アビ説明記述:アビ段階 += "<br>"
			IF DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1) != "上と同じ相手"
				アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1)%に"
			ENDIF
			アビ説明記述:アビ段階 += "［"
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}％"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1) > 0
				SIF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
					アビ説明記述:アビ段階 += "＋"
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}"
			ENDIF
			アビ説明記述:アビ段階 += "］（"
			IF STRFIND(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列２", 1), DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)) == 0
				アビ説明記述:アビ段階 += @"%TOFULL(REPLACE(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列２", 1), DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1), ""))%枠"
			ELSE
				アビ説明記述:アビ段階 += "独自枠"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1) > 0
				アビ説明記述:アビ段階 += @"・{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1)}%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列３", 1)%"
			ENDIF
			IF STRFIND(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列４", 1), "消去不可") >= 0
				アビ説明記述:アビ段階 += @"・消去不可"
			ENDIF
			アビ説明記述:アビ段階 += "）の"
			IF DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1) == "ストレングス" || DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1) == "逆境"
				アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%効果を与える。"
			ELSE
				アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%上昇効果を与える。"
			ENDIF

			アビ効果記述:アビ段階 += "CALL アビ汎用変数文字列リセット<br>"
			アビ効果記述:アビ段階 += アビエディタ_対象CALL変換(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1))

			アビ効果記述:アビ段階 += @"アビ汎用文字列:バフ・デバフ枠 = %DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列２", 1)%<br>"
			アビ効果記述:アビ段階 += @"アビ汎用文字列:バフ・デバフ対象能力 = %DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%<br>"

			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果割合 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}<br>"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果量 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}<br>"
			ENDIF

			SELECTCASE DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列３", 1)
				CASE "ターン"
					アビ効果記述:アビ段階 += @"アビ汎用変数:持続ターン = "
				CASE "回"
					アビ効果記述:アビ段階 += @"アビ汎用変数:持続回数 = "
				CASE "行動"
					アビ効果記述:アビ段階 += @"アビ汎用変数:持続行動回数 = "
				CASE "被ダメ"
					アビ効果記述:アビ段階 += @"アビ汎用変数:持続被ダメ回数 = "
			ENDSELECT
			アビ効果記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値３", 1)}<br>"

			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:基礎成功確率 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}<br>"
			ENDIF

			アビ効果記述:アビ段階 += "CALL アビテンプレート_有利効果_バフ効果セット<br>"
		CASE "その他有利効果"
			アビ説明記述:アビ段階 += "<br>"
			IF DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1) != "上と同じ相手"
				アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1)%に"
			ENDIF
			アビ説明記述:アビ段階 += "［"
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}％"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1) > 0
				SIF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
					アビ説明記述:アビ段階 += "＋"
				アビ説明記述:アビ段階 += @"{DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}"
			ENDIF
			アビ説明記述:アビ段階 += "］"
			アビ説明記述:アビ段階 += "の"
			アビ説明記述:アビ段階 += @"%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%効果を与える。"

			アビ効果記述:アビ段階 += "CALL アビ汎用変数文字列リセット<br>"
			アビ効果記述:アビ段階 += アビエディタ_対象CALL変換(DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ対象", 1))

			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果割合 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値１", 1)}<br>"
			ENDIF
			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:効果量 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値２", 1)}<br>"
			ENDIF

			IF DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1) > 0
				アビ効果記述:アビ段階 += @"アビ汎用変数:基礎成功確率 = {DT_CELL_GET("アビエディタデータベース", アビID:DB行数, "アビ数値４", 1)}<br>"
			ENDIF

			アビ効果記述:アビ段階 += @"CALL アビテンプレート_有利効果_%DT_CELL_GETS("アビエディタデータベース", アビID:DB行数, "アビ文字列１", 1)%<br>"
	ENDSELECT
	SIF 最大アビ段階 < アビ段階
		最大アビ段階 = アビ段階
NEXT
FOR アビ段階, 0, 最大アビ段階 + 1
	アビ説明記述:アビ段階 '= @"消費ＭＰ：{ABS(消費ＭＰ:アビ段階)}%アビ説明記述:アビ段階%"
NEXT

CLEARLINE MAX(LINECOUNT, 1000)
TWAIT 100, 1

アビERB記述 += "SELECTCASE ARGS<br>"
アビERB記述 += "	CASE \"アビ名\"<br>"
アビERB記述 += "		TSTR:コマンド名受渡 = \%アビ名\%<br>"
アビERB記述 += "	CASE \"アビ説明文\"<br>"
HTML_PRINT アビERB記述, 1
FOR アビ段階, 最大アビ段階, -1, -1
	IF アビ段階 == 最大アビ段階
		PRINTFORML 		IF %取得条件_文字列:アビ段階%
	ELSE
		PRINTFORML 		ELSEIF %取得条件_文字列:アビ段階%
	ENDIF
	PRINTFORM 			詳細文文字列受け渡し変数 = 
	VARSET RESULTS
	SPLIT アビ説明記述:アビ段階, "<br>", RESULTS
	PRINTFORML %RESULTS:0%
	FOR LOCAL, 1, RESULT
		PRINTFORML 			詳細文文字列受け渡し変数 += "<br>%RESULTS:LOCAL%"
	NEXT
NEXT
PRINTFORML 		ENDIF
アビERB記述 = 
アビERB記述 += "	CASE \"アビ効果\"<br>"
FOR アビ段階, 最大アビ段階, -1, -1
	IF アビ段階 == 最大アビ段階
		アビERB記述 += @"		IF %取得条件_文字列:アビ段階%<br>"
	ELSE
		アビERB記述 += @"		ELSEIF %取得条件_文字列:アビ段階%<br>"
	ENDIF
	アビERB記述 += @"			%REPLACE(アビ効果記述:アビ段階, "<br>", "<br>			")%"
	アビERB記述 '= SUBSTRING(アビERB記述, 0, STRLENS(アビERB記述) - 3)
NEXT
	アビERB記述 += "		ENDIF<br>"
アビERB記述 += "ENDSELECT<br><br><br>"


HTML_PRINT アビERB記述
PRINTL 
OUTPUTLOG @"ERB_EDITFILE/固有アビリティ_%CSVCALLNAME(対象No)%.ERB", 1
PRINTL ERB_EDITFILEフォルダに出力しました。
PRINTW 関数名のYYYYの部分を任意の文字列に変更し、ERB/口上_キャラ個別ERBのキャラクターフォルダに移動して再起動してください


@アビエディタ_対象CALL変換(対象文字列)
#FUNCTIONS
#DIMS 対象文字列
#DIM 対象数

SELECTCASE 対象文字列
	CASE "上と同じ相手"
		;直前と同じなので記述しない
	CASE "自身"
		RETURNF "CALL アビ対象選択テンプレート_自己のみ<br>"
	CASE "味方単体"
		RETURNF "CALL アビ対象選択テンプレート_味方単体<br>"
	CASE "味方全体"
		RETURNF "CALL アビ対象選択テンプレート_味方全体<br>"
	CASE "敵単体"
		RETURNF "CALL アビ対象選択テンプレート_敵単体<br>"
	CASE "敵横列"
		RETURNF "CALL アビ対象選択テンプレート_敵横列<br>"
	CASE "敵縦列"
		RETURNF "CALL アビ対象選択テンプレート_敵縦列<br>"
	CASE "敵全体"
		RETURNF "CALL アビ対象選択テンプレート_敵全体_生者のみ<br>"
	CASEELSE
		IF STRFIND(対象文字列, "敵ランダム") >= 0
			対象数 = TOINT(REPLACE(REPLACE(REPLACE(対象文字列, "敵ランダム", ""), "体", ""), "（重複なし）", ""))
			IF STRFIND(対象文字列, "（重複なし）") >= 0
				RETURNF @"CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し({対象数})<br>"
			ELSE
				RETURNF @"CALL アビ対象選択テンプレート_敵ランダムＸ体({対象数})<br>"
			ENDIF
		ENDIF
ENDSELECT


@ＭＰ自動計算(消費ＭＰ, 取得条件, アビ段階 = 0)
#DIM REF 消費ＭＰ
#DIM ダメージ総倍率
#DIM ダメージ総値
#DIM バフ総倍率, 3
#DIM バフ総値, 3
#DIM DB行数
#DIM 基礎消費MP
#DIM 補正回数
#DIM ループ用
#DIM アビ段階
#DIM レベル補正
#DIM 対象補正
#DIM 対象数
#DIMS 対象文字列
#DIMS 取得条件
#DIM 取得条件_Lv
#DIMS 取得条件_陥落
#DIM バフ量総計
#DIMS 計算式タイプ
#DIM 持続係数

;取得条件を解析

VARSET RESULTS
取得条件_Lv = 0
取得条件_陥落 = 
SPLIT 取得条件, "***", RESULTS
FOR LOCAL, 0, RESULT
	IF STRFIND(RESULTS:LOCAL, "Lv") >= 0
		取得条件_Lv = TOINT(REPLACE(RESULTS:LOCAL, "Lv_", ""))
	ELSEIF STRFIND(RESULTS:LOCAL, "陥落") >= 0
		取得条件_陥落 '= REPLACE(RESULTS:LOCAL, "陥落_", "")
	ENDIF
NEXT

;陥落は２５Lv分として計算
SIF 取得条件_陥落 != ""
	取得条件_Lv += 25

レベル補正 = 取得条件_Lv / 10 + 1

ダメージ総倍率 = 0
ダメージ総値 = 0
VARSET バフ総倍率
VARSET バフ総値
基礎消費MP = 5
バフ量総計 = 0

FOR DB行数, 0, DT_ROW_LENGTH("アビエディタデータベース")
	SIF DT_CELL_GET("アビエディタデータベース", DB行数, "アビ段階") != アビ段階
		CONTINUE
	対象文字列 '= DT_CELL_GETS("アビエディタデータベース", DB行数, "アビ対象")
	SELECTCASE 対象文字列
		CASE "敵横列"
			対象補正 = 2
		CASE "敵縦列"
			対象補正 = 3
		CASE "敵全体"
			対象補正 = 4
		CASE "味方全体"
			対象補正 = 4
		CASEELSE
			IF STRFIND(対象文字列, "敵ランダム") >= 0
				対象数 = TOINT(REPLACE(REPLACE(REPLACE(対象文字列, "敵ランダム", ""), "体", ""), "（重複なし）", ""))
				対象補正 = 対象数 / 2 + 1
			ELSE
				対象補正 = 1
			ENDIF
	ENDSELECT
	SELECTCASE DT_CELL_GETS("アビエディタデータベース", DB行数, "アビ効果種別")
		CASE "ダメージ効果"
			;全てのダメージ効果を合算して最後に計算
			ダメージ総倍率 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１") * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値４") * 対象補正
			ダメージ総値 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２") * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値４") * 対象補正
		CASE "バフ効果"
			;ちゃんとしたバランスではないが、とりあえず仮置き
			; SIF DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１") > 0
			; 	基礎消費MP += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１") / (レベル補正 + 10) * 4 * 対象補正 * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値３")
			; SIF DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２") > 0
			; 	基礎消費MP += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２") / (レベル補正 * 100) * 2 * 対象補正 * DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値３")

			RFLAG:アビMP係数 = 100
			計算式タイプ '= アビエディタ_ＭＰ自動計算_バフ種類(DT_CELL_GETS("アビエディタデータベース", DB行数, "アビ文字列１"))
			持続係数 = DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値３")
			;永続は6ターン分とする、それと同様に7ターン以上は6ターン分まで
			SIF 持続係数 < 1 || 持続係数 > 6
				持続係数 = 6
			IF STRFIND(計算式タイプ, "青天井") >= 0
				バフ総倍率:0 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１")
				バフ総値:0 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２")

				補正回数 = 1
				IF バフ総倍率:0 > 0
					補正回数 += バフ総倍率:0 / 10
					LOCAL = 0
					FOR ループ用, 0, 補正回数
						IF ループ用 == 補正回数 - 1
							LOCAL += (バフ総倍率:0 % 10) * POWER(2, MAX(0, ループ用 - (レベル補正))) / 5
						ELSE
							LOCAL += 10 * POWER(2, MAX(0, ループ用 - (レベル補正))) / 5
						ENDIF
					NEXT
				ENDIF

				補正回数 = 1
				IF バフ総値:0  > 0
					補正回数 += (バフ総値:0 / (レベル補正 * 250))
					FOR ループ用, 0, 補正回数
						IF ループ用 == 補正回数 - 1
							LOCAL += (バフ総値:0 % (レベル補正 * 250)) * POWER(2, MAX(0, ループ用 - (レベル補正))) / 150
						ELSE
							LOCAL += (レベル補正 * 250) * POWER(2, MAX(0, ループ用 - (レベル補正))) / 150
						ENDIF
					NEXT
				ENDIF

				基礎消費MP += LOCAL * 対象補正 * RFLAG:アビMP係数 / 100 * 持続係数
			ELSEIF STRFIND(計算式タイプ, "最大100") >= 0
				バフ総倍率:1 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値１")
				バフ総値:1 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２")

				LOCAL = MAX(1, 100 - (バフ総倍率:1 + バフ総値:1))
				基礎消費MP += RFLAG:アビMP係数 * (15 - レベル補正) / LOCAL * 対象補正 * 持続係数
			ELSEIF STRFIND(計算式タイプ, "テンション") >= 0
				バフ総値:2 += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２")

				基礎消費MP += (バフ総値:2 * バフ総値:2 * バフ総値:2 * バフ総値:2) * 5 * 対象補正 * 持続係数
			ENDIF
		CASE "その他有利効果"
			;更に効果で分岐
			SELECTCASE DT_CELL_GETS("アビエディタデータベース", DB行数, "アビ文字列１")
				CASE "奥義ゲージUP"
					;単純に１％＝１ＭＰ、レベル補正ごとに0.05軽減
					基礎消費MP += DT_CELL_GET("アビエディタデータベース", DB行数, "アビ数値２") * 対象補正 * (100 - ((レベル補正 - 1) * 5)) / 100
			ENDSELECT
	ENDSELECT
NEXT

補正回数 = 1
SIF ダメージ総倍率 > 0
	補正回数 += ダメージ総倍率 / 50
SIF ダメージ総値 > 0
	補正回数 += (ダメージ総値 / (レベル補正 * 200))
FOR ループ用, 0, 補正回数
	IF ループ用 == 補正回数 - 1
		基礎消費MP += (ダメージ総倍率 % 50) * POWER(2, MAX(0, ループ用 - レベル補正)) / 40
	ELSE
		基礎消費MP += 50 * POWER(2, MAX(0, ループ用 - レベル補正)) / 40
	ENDIF
NEXT


消費ＭＰ:アビ段階 = 基礎消費MP * -1


@アビエディタ_取得条件文章化(取得条件)
#FUNCTIONS
#DIMS 取得条件
#DIMS 文章化

SIF 取得条件 == ""
	RETURNF ""

文章化 = 
VARSET RESULTS
SPLIT 取得条件, "***", RESULTS
FOR LOCAL, 0, RESULT
	IF RESULTS:LOCAL != ""
		文章化 += REPLACE(RESULTS:LOCAL, "_", ":")
		文章化 += "　"
	ENDIF
NEXT

RETURNF 文章化

