;有利効果とは基本的に行動キャラが自陣営のキャラに掛けるもの
;そのためかばう・マウントなどの効果を無視する


;------------------------------------------------------------------------
;設定可能なバフ効果一覧
;各ステータス数値：BATTLE_STATE@2.ERDを参照
;弱体無効：デバフを弾く
;追撃：通常攻撃時にダメージ追加
;再生：行動終了時に回復
;回避反撃：回避に成功した時攻撃
;カウンター：ダメージを受けた時攻撃
;------------------------------------------------------------------------

@アビテンプレート_有利効果_バフ効果セット(累積フラグ = 0)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用文字列:バフ・デバフ対象能力
;アビ汎用変数:効果割合
;アビ汎用変数:効果量
;アビ汎用変数:持続ターン
;アビ汎用変数:持続回数
;アビ汎用文字列:付随先設定
;アビ汎用文字列:付随先_隊列設定
;アビ汎用変数:付随割合
;アビ汎用変数:付随量
;アビ汎用文字列:バフ・デバフ名
;アビ汎用変数:パッシブ付随先NO
;------------------------------------------------------------------------
;◯強化、◯耐性、連続攻撃率については初期値が０のため効果割合を入れても効果がないことに注意
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じ枠で効果高いバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIMS 効果量一時格納
#DIM 累積フラグ
#DIM 累積量
#DIMS 適用範囲保存
#DIM 効果変動フラグ

IF アビ汎用文字列:付随先設定 == "パッシブ"
	;範囲が入ってないなら自分のNOと対象のNOを指定
	SIF アビ汎用文字列:適用範囲 == ""
		アビ汎用文字列:適用範囲 = NO{NO:(BATTLE_STATE:戦闘行動キャラ:0)}
	SIF アビ汎用変数:パッシブ付随先NO == 0
		アビ汎用変数:パッシブ付随先NO = NO:(BATTLE_STATE:戦闘行動キャラ:0)
ELSEIF アビ汎用文字列:適用範囲 != ""
	CALL アビテンプレート_効果共通処理(1)
	アビテンプレート用_対象保存:0 = 戦闘行動キャラ
ELSE
	CALL アビテンプレート_効果共通処理
ENDIF
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

IF アビ汎用文字列:付随先設定 != "" && アビ汎用文字列:付随先設定 != "パッシブ"
	;付随バフの場合は表示なしとする。説明は付随元のバフや特性に記載すること
	アビ汎用変数:特殊表示オプション = -1
ENDIF

SIF アビテンプレート用_アビ名 == ""
	アビテンプレート用_アビ名 '= アビ汎用文字列:バフ・デバフ枠

; SIF アビ汎用文字列:バフ・デバフ名 == ""
; 	アビ汎用文字列:バフ・デバフ名 '= アビテンプレート用_アビ名

;付与予定のバフ対象を保存
SIF アビ汎用文字列:付随先設定 != "パッシブ"
	アビテンプレート用_付与バフ保存 += @"%アビ汎用文字列:バフ・デバフ対象能力%_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	効果変動フラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF アビ汎用文字列:適用範囲 != ""
		適用範囲保存 '= アビ汎用文字列:適用範囲
	ELSE
		適用範囲保存 = {戦闘行動対象}
	ENDIF

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のバフと枠が被ってないかチェック
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == 適用範囲保存 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") == "バフ"
			;すでに同じ枠のバフがある場合、累積フラグで分岐
			IF 累積フラグ
				;表示文章用にスキップフラグ設定
				スキップフラグ = 10
				IF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合") < アビ汎用変数:累積上限_割合
					;上限に達してない場合は足してターン伸ばす
					累積量 = MIN(DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合") + アビ汎用変数:効果割合, アビ汎用変数:累積上限_割合)
					DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_割合", 累積量
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", アビ汎用変数:持続行動回数
					スキップフラグ = 11
					効果変動フラグ = 1
				ELSE
					;達してる場合はターン伸ばすだけ
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", アビ汎用変数:持続行動回数
				ENDIF
				;固定値の方も同様に処理
				IF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値") < アビ汎用変数:累積上限_固定値
					;上限に達してない場合は足してターン伸ばす
					累積量 = MIN(DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値") + アビ汎用変数:効果量, アビ汎用変数:累積上限_固定値)
					DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 累積量
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", アビ汎用変数:持続行動回数
					スキップフラグ = 11
					効果変動フラグ = 1
				ELSE
					;達してる場合はターン伸ばすだけ
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", アビ汎用変数:持続回数
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", アビ汎用変数:持続行動回数
				ENDIF
				BREAK
			ELSE
				IF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合") == 0 && アビ汎用変数:効果割合 == 0
					;大本バフも追加バフも割合が０の時、固定値を比較
					IF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値") <= アビ汎用変数:効果量
						;追加する方の固定値が高いか同じ場合は消して新しく追加
						DT_ROW_REMOVE "戦闘効果データベース", DT_CELL_GET("戦闘効果データベース", バフ番号, "id")
					ELSE
						;そうでない場合は終了
						スキップフラグ = 1
					ENDIF
				ELSEIF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合") <= アビ汎用変数:効果割合
					;追加する方の割合が高いか同じ場合は消して新しく追加
					DT_ROW_REMOVE "戦闘効果データベース", DT_CELL_GET("戦闘効果データベース", バフ番号, "id")
				ELSE
					;そうでない場合は終了
					スキップフラグ = 1
				ENDIF
				BREAK
			ENDIF
			;付随割合や付随値は見ないものとする。（頻繁に変動する値ではないため）
		ENDIF
	NEXT

	IF アビ汎用変数:特殊表示オプション == -1
		IF スキップフラグ == 0
			;累積時でないなら新しくデータベースにセット
			{
			DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ名, "バフ・デバフフラグ", "バフ", "適用範囲", 適用範囲保存,
				"効果枠", アビ汎用文字列:バフ・デバフ枠, "対象能力", アビ汎用文字列:バフ・デバフ対象能力,
				"効果量_割合", アビ汎用変数:効果割合, "効果量_固定値", アビ汎用変数:効果量,
				"持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数, "持続被ダメ回数", アビ汎用変数:持続被ダメ回数,
				"消去不可フラグ", アビ汎用変数:消去不可オプション, "特殊表示オプション", アビ汎用変数:特殊表示オプション,
				"付随効果枠", アビ汎用文字列:付随先設定, "付随効果枠_対象隊列", アビ汎用文字列:付随先_隊列設定, "付随効果量_割合", アビ汎用変数:付随割合, "付随効果量_固定値", アビ汎用変数:付随量,
				"パッシブ付随先NO", アビ汎用変数:パッシブ付随先NO, "参照効果枠", アビ汎用文字列:参照先設定, "参照効果枠_対象隊列", アビ汎用文字列:参照先_隊列設定,
				"アイコン", アビ汎用文字列:バフ・デバフアイコン
			}
			効果変動フラグ = 1
		ENDIF
	ELSEIF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果が無いようだ…
	ELSEIF スキップフラグ == 10
		IF アビ汎用文字列:バフ・デバフ名 == ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の累積%アビ汎用文字列:バフ・デバフ対象能力%バフ効果時間を更新した！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の累積%アビ汎用文字列:バフ・デバフ名%バフ効果時間を更新した！
		ENDIF
	ELSE
		IF アビ汎用変数:効果割合
			効果量一時格納 = {アビ汎用変数:効果割合}\%
			IF アビ汎用変数:効果量
				効果量一時格納 += @" + {アビ汎用変数:効果割合}"
			ENDIF
		ELSE
			効果量一時格納 = {アビ汎用変数:効果量}
		ENDIF
		IF アビ汎用文字列:バフ・デバフ名 == ""
			IF GETNUM(BATTLE_STATE, アビ汎用文字列:バフ・デバフ対象能力, 2) > 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%上がった！
			ELSEIF アビ汎用文字列:バフ・デバフ対象能力 == "連続攻撃率"
				効果量一時格納 = {アビ汎用変数:効果量}
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%\%上がった！
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%効果を得た！
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ名%効果を得た！
		ENDIF
		IF スキップフラグ < 10
			;累積時でないなら新しくデータベースにセット
			{
			DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ名, "バフ・デバフフラグ", "バフ", "適用範囲", 適用範囲保存,
				"効果枠", アビ汎用文字列:バフ・デバフ枠, "対象能力", アビ汎用文字列:バフ・デバフ対象能力,
				"効果量_割合", アビ汎用変数:効果割合, "効果量_固定値", アビ汎用変数:効果量,
				"持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数, "持続被ダメ回数", アビ汎用変数:持続被ダメ回数,
				"消去不可フラグ", アビ汎用変数:消去不可オプション, "特殊表示オプション", アビ汎用変数:特殊表示オプション,
				"付随効果枠", アビ汎用文字列:付随先設定, "付随効果枠_対象隊列", アビ汎用文字列:付随先_隊列設定, "付随効果量_割合", アビ汎用変数:付随割合, "付随効果量_固定値", アビ汎用変数:付随量,
				"パッシブ付随先NO", アビ汎用変数:パッシブ付随先NO, "参照効果枠", アビ汎用文字列:参照先設定, "参照効果枠_対象隊列", アビ汎用文字列:参照先_隊列設定,
				"アイコン", アビ汎用文字列:バフ・デバフアイコン
			}
			効果変動フラグ = 1
		ENDIF
	ENDIF

	;適用範囲が入ってるときは対象変わっても意味ないので一回でBREAKする
	SIF アビ汎用文字列:適用範囲 != ""
		BREAK
NEXT

SIF 効果変動フラグ
	CALL 戦闘中最大ＨＰＭＰ変動処理()

@アビテンプレート_有利効果_奥義ゲージUP
#DIM ゲージ上昇量
#DIM 対象番号

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += "奥義ゲージUP_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	;基本的にバフは死者にはかからない
	SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
		CONTINUE
	IF 戦闘行動対象 < 10
		;味方対象
		ゲージ上昇量 = BATTLE_STATE:戦闘行動対象:奥義ゲージ * アビ汎用変数:効果割合 / 100
		ゲージ上昇量 += アビ汎用変数:効果量
		CALL バフ・デバフ測定(戦闘行動対象, "ゲージ上昇効率")
		ゲージ上昇量 = ゲージ上昇量 * RESULT / 100

		ゲージ上昇量 = MIN(ゲージ上昇量, BATTLE_STATE:戦闘行動対象:ゲージ最大値 - BATTLE_STATE:戦闘行動対象:奥義ゲージ)
		IF ゲージ上昇量 > 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%の奥義ゲージが{ゲージ上昇量}％上昇！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%には効果がなかった…
		ENDIF
		BATTLE_STATE:戦闘行動対象:奥義ゲージ = MIN(BATTLE_STATE:戦闘行動対象:奥義ゲージ + ゲージ上昇量, BATTLE_STATE:戦闘行動対象:ゲージ最大値)
	ENDIF
	;奥義ゲージは味方にしか存在しないので、敵対象時の分岐は無し
NEXT

@アビテンプレート_有利効果_ウェポンバースト(表示文章フラグ = 0)
#DIM 対象番号
#DIM 表示文章フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += "ウェポンバースト_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	;基本的にバフは死者にはかからない
	SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
		CONTINUE
	IF 戦闘行動対象 < 10
		;味方対象
		IF BATTLE_STATE:戦闘行動対象:奥義ゲージ < 100
			IF 表示文章フラグ
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%の奥義ゲージが１００％に上昇した！
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%が奥義発動可能になった！
			ENDIF
			BATTLE_STATE:戦闘行動対象:奥義ゲージ = 100
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%には効果がなかった…
		ENDIF
	ENDIF
	;奥義ゲージは味方にしか存在しないので、敵対象時の分岐は無し
NEXT

@アビテンプレート_有利効果_デバフ消去(消去枠 = "")
#DIMS 消去枠
#DIM スキップフラグ
#DIM メッセージ非表示フラグ
#DIMS 名前一時格納
#DIM 対象番号
#DIM 消滅番号
#DIM デバフ番号

;特殊な適用範囲のデバフは消去されないので注意

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += "クリア_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	メッセージ非表示フラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF 消去枠 == ""
		;枠指定無しの場合、最新１個を消す
		消滅番号 = -1
		FOR デバフ番号, DT_ROW_LENGTH("戦闘効果データベース") - 1, -1, -1
			SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "バフ・デバフフラグ") != "デバフ"
				CONTINUE
			SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "適用範囲") != @"{戦闘行動対象}"
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", デバフ番号, "特殊表示オプション") == -1
				CONTINUE
			IF DT_CELL_GET("戦闘効果データベース", デバフ番号, "消去不可フラグ") == 0 || アビ汎用変数:消去不可バフデバフ削除オプション
				消滅番号 = デバフ番号
				BREAK
			ENDIF
		NEXT
		IF 消滅番号 < 0
			スキップフラグ = 1
		ENDIF
	ELSE
		;枠指定がある場合、その枠を消す。ないなら何もしない
		;枠指定は完全一致なので一個しかないはず
		消滅番号 = デバフ番号取得_枠(消去枠)
		IF DT_CELL_GET("戦闘効果データベース", 消滅番号, "消去不可フラグ") && アビ汎用変数:消去不可バフデバフ削除オプション == 0
			消滅番号 = -1
		ELSEIF DT_CELL_GET("戦闘効果データベース", 消滅番号, "特殊表示オプション") == -1
			メッセージ非表示フラグ = 1
		ENDIF
		IF 消滅番号 < 0
			スキップフラグ = 1
		ENDIF
	ENDIF

	IF メッセージ非表示フラグ
		IF スキップフラグ == 0
			CALL 戦闘効果削除(消滅番号)
		ENDIF
	ELSEIF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
	ELSE
		IF DT_CELL_GETS("戦闘効果データベース", 消滅番号, "効果名") == ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS("戦闘効果データベース", 消滅番号, "対象能力")%デバフが消え去った！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS("戦闘効果データベース", 消滅番号, "効果名")%デバフが消え去った！
		ENDIF
		CALL 戦闘効果削除(消滅番号)
	ENDIF
NEXT

CALL 戦闘中最大ＨＰＭＰ変動処理()


@アビテンプレート_有利効果_デバフ全て消去(消去能力 = "")
#DIM スキップフラグ
#DIM メッセージ非表示フラグ
#DIMS 名前一時格納
#DIMS 消去能力
#DIM 対象番号
#DIM 消滅番号
#DIM デバフ番号
#DIM 消滅配列番号
#DIM 消滅番号保存, 100

;特殊な適用範囲のバフは消去されないので注意

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += "クリア_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	VARSET 消滅番号保存
	スキップフラグ = 0
	消滅配列番号 = 0
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF スキップフラグ == 0
		;全部見る
		FOR LOCAL, 0, DT_ROW_LENGTH("戦闘効果データベース")
			SIF DT_CELL_GETS("戦闘効果データベース", LOCAL, "バフ・デバフフラグ") != "デバフ"
				CONTINUE
			SIF DT_CELL_GETS("戦闘効果データベース", LOCAL, "適用範囲") != @"{戦闘行動対象}"
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", LOCAL, "特殊表示オプション") == -1
				CONTINUE
			SIF DT_CELL_GET("戦闘効果データベース", LOCAL, "消去不可フラグ") != 0 && アビ汎用変数:消去不可バフデバフ削除オプション == 0
				CONTINUE
			IF 消去能力 != ""
				;消去能力が設定されている場合、特定の能力のみ消す
				SIF 消去能力 != DT_CELL_GETS("戦闘効果データベース", LOCAL, "対象能力")
					CONTINUE
			ENDIF
			;ここで削除しちゃうと行がズレるので一旦配列に退避
			消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", LOCAL, "id")
			消滅配列番号 += 1
		NEXT
		
		FOR LOCAL, 0, 消滅配列番号
			CALL 戦闘効果削除(消滅番号保存:LOCAL, 1)
		NEXT
	ENDIF

	IF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
	ELSE
		IF アビ汎用変数:消去不可バフデバフ削除オプション
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%消去能力%デバフが全て消え去った！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%消去能力%デバフが{消滅配列番号, 2}個消え去った！
		ENDIF
	ENDIF
NEXT

CALL 戦闘中最大ＨＰＭＰ変動処理()

@バフ・デバフ特殊表示_大技待機(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("黄")
PRINTFORML 大技待機中！！
PRINTFORML ※消去不可
RESETCOLOR


@アビテンプレート_有利効果_累積バフ効果セット
JUMP アビテンプレート_有利効果_バフ効果セット(1)


@アビテンプレート_有利効果_付随バフ効果セット(付随先枠)
#DIMS 付随先枠

アビ汎用文字列:付随先設定 '= 付随先枠
JUMP アビテンプレート_有利効果_バフ効果セット()


@アビテンプレート_有利効果_交代()
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:適用範囲
;------------------------------------------------------------------------
;・注意点
;対象は１人のみ。アビ対象を味方全体としても先頭の１人のみが交代する
;「任意の１人を交代させる」効果の場合は"アビ汎用文字列:適用範囲 = 味方全体"とすること
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIMS 適用範囲保存

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += "交代_"

;対象が複数いても1つのみ適用
戦闘行動対象 = アビテンプレート用_対象保存:0

IF アビ汎用文字列:適用範囲 != ""
	適用範囲保存 '= アビ汎用文字列:適用範囲
ELSE
	IF !INRANGE(戦闘行動対象, 0, 3) || BATTLE_STATE:戦闘行動対象:キャラ登録番号 < 1
		PRINTW 対象が不正です
		アビテンプレート用_キャンセルフラグ = 1
		RETURN -1
	ENDIF
	適用範囲保存 = {戦闘行動対象}
ENDIF

;HPチェックはスキップ（死者も交代させられる）

;既存の交代が既に存在しないかチェック
FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "対象能力") == "交代" && DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") == "バフ"
		;すでに交代バフがある場合、適用範囲によらず上書きはできない
		;（１行動で交代できるキャラは１人まで）
		RETURN 0
	ENDIF
NEXT

;それ以外は新しくデータベースにセット
{
DT_ROW_ADD "戦闘効果データベース", "効果名", "交代", "バフ・デバフフラグ", "バフ", "適用範囲", 適用範囲保存,
	"効果枠", "交代", "対象能力", "交代", "持続ターン", -1, "消去不可フラグ", 1, "特殊表示オプション", -1
}
RETURN 1


@アビテンプレート_有利効果_バフ効果延長(延長種別)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用変数:効果量
;------------------------------------------------------------------------
;効果量の分だけ効果種別の回数を増やす。０や－１は変動しない。
;適用範囲は対象のバフのみ、全体バフなどは変動しない
;アビ汎用文字列:バフ・デバフ枠にALLを入れると全部伸ばす
;------------------------------------------------------------------------
#DIM 対象番号
#DIMS 延長種別
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 効果量一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のバフと枠をチェック
	スキップフラグ = 1
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") != アビ汎用文字列:バフ・デバフ枠 && アビ汎用文字列:バフ・デバフ枠 != "ALL"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") != @"{戦闘行動対象}"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") != "バフ"
			CONTINUE

		;延長処理、0より上の場合だけ
		IF DT_CELL_GET("戦闘効果データベース", バフ番号, 延長種別) > 0
			効果量一時格納 = DT_CELL_GET("戦闘効果データベース", バフ番号, 延長種別) + アビ汎用変数:効果量
			DT_CELL_SET "戦闘効果データベース", バフ番号, 延長種別, 効果量一時格納
			SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "特殊表示オプション") != -1
				スキップフラグ = 0
		ENDIF
	NEXT

	IF スキップフラグ == 0
		IF アビ汎用文字列:バフ・デバフ枠 == "ALL"
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のバフが延長された！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ枠%が延長された！
		ENDIF
	ENDIF
NEXT


@アビテンプレート_有利効果_バフ効果延長_持続ターン

JUMP アビテンプレート_有利効果_バフ効果延長("持続ターン")

@アビテンプレート_有利効果_バフ効果延長_持続回数

JUMP アビテンプレート_有利効果_バフ効果延長("持続回数")

@アビテンプレート_有利効果_バフ効果延長_持続行動回数

JUMP アビテンプレート_有利効果_バフ効果延長("持続行動回数")

@アビテンプレート_有利効果_バフ効果延長_持続被ダメ回数

JUMP アビテンプレート_有利効果_バフ効果延長("持続被ダメ回数")


@アビテンプレート_有利効果_デバフ効果短縮(短縮種別)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用変数:効果量
;------------------------------------------------------------------------
;効果量の分だけ効果種別の回数を減らす。１,０や－１は変動しない。
;適用範囲は対象のデバフのみ、全体デバフなどは変動しない
;アビ汎用文字列:バフ・デバフ枠にALLを入れると全部縮める
;------------------------------------------------------------------------
#DIM 対象番号
#DIMS 短縮種別
#DIM デバフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 効果量一時格納

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にデバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のデバフと枠をチェック
	スキップフラグ = 1
	FOR デバフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "効果枠") != アビ汎用文字列:バフ・デバフ枠 && アビ汎用文字列:バフ・デバフ枠 != "ALL"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "適用範囲") != @"{戦闘行動対象}"
			CONTINUE
		SIF DT_CELL_GETS("戦闘効果データベース", デバフ番号, "バフ・デバフフラグ") != "デバフ"
			CONTINUE

		;短縮処理、1より上の場合だけ
		IF DT_CELL_GET("戦闘効果データベース", デバフ番号, 短縮種別) > 1
			効果量一時格納 = DT_CELL_GET("戦闘効果データベース", デバフ番号, 短縮種別) - アビ汎用変数:効果量
			DT_CELL_SET "戦闘効果データベース", デバフ番号, 短縮種別, 効果量一時格納
			SIF DT_CELL_GET("戦闘効果データベース", デバフ番号, "特殊表示オプション") != -1
				スキップフラグ = 0
		ENDIF
	NEXT

	IF スキップフラグ == 0
		IF アビ汎用文字列:バフ・デバフ枠 == "ALL"
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のデバフが短縮された！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ枠%が短縮された！
		ENDIF
	ENDIF
NEXT


@アビテンプレート_有利効果_デバフ効果短縮_持続ターン

JUMP アビテンプレート_有利効果_デバフ効果短縮("持続ターン")

@アビテンプレート_有利効果_デバフ効果短縮_持続回数

JUMP アビテンプレート_有利効果_デバフ効果短縮("持続回数")

@アビテンプレート_有利効果_デバフ効果短縮_持続行動回数

JUMP アビテンプレート_有利効果_デバフ効果短縮("持続行動回数")

@アビテンプレート_有利効果_デバフ効果短縮_持続被ダメ回数

JUMP アビテンプレート_有利効果_デバフ効果短縮("持続被ダメ回数")


@アビテンプレート_有利効果_チャージ加速
#DIM 対象番号
#DIM スキップフラグ
#DIM 増加チャージ
#DIM 成功率一時格納
#DIM バフ番号
#DIM 累積量

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		;味方はチャージ加速効果なし
		CONTINUE
	ELSE
		;基本的にチャージ加速は死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF アビ汎用変数:基礎成功確率
		;基礎成功率が設定されてる場合は成功率判定
		成功率一時格納 = アビ汎用変数:基礎成功確率
		IF RAND:100 > 成功率一時格納
			スキップフラグ = 2
		ENDIF
	ENDIF

	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン >= 敵BATTLE_STATE:(戦闘行動対象 - 10):最大チャージターン
		;そもそもチャージMAX
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のチャージは既にMAXだ！
	ELSEIF スキップフラグ == 2
		;スロウ外した
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%はチャージ加速効果を受けなかった…
	ELSE
		増加チャージ = MIN(アビ汎用変数:効果量, 敵BATTLE_STATE:(戦闘行動対象 - 10):最大チャージターン - 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン)
		敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン = MIN(敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン + アビ汎用変数:効果量, 敵BATTLE_STATE:(戦闘行動対象 - 10):最大チャージターン)
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のチャージが{増加チャージ}上がった！
	ENDIF
NEXT

@アビテンプレート_有利効果_パッシブバフ効果セット()

アビ汎用文字列:付随先設定 = パッシブ
アビ汎用変数:消去不可オプション = 1
JUMP アビテンプレート_有利効果_バフ効果セット()

@アビテンプレート_有利効果_テンション()
#DIM 対象番号
#DIM バフ番号
#DIM スキップフラグ
#DIM 現在テンション
#DIM テンション判定
#DIM 累積量
#DIMS 名前一時格納
#DIMS 適用範囲保存

アビテンプレート用_付与バフ保存 = テンション_

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF アビ汎用文字列:適用範囲 != ""
		適用範囲保存 '= アビ汎用文字列:適用範囲
	ELSE
		適用範囲保存 = {戦闘行動対象}
	ENDIF

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;既存のバフと枠が被ってないかチェック
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == "テンション" && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == 適用範囲保存 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") == "バフ"
		現在テンション = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
			IF 現在テンション < 4
				;上限に達してない場合は足してターン伸ばす
				累積量 = MIN(現在テンション + 1, 4)
				DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 累積量
				DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
				スキップフラグ = 1
			ELSE
				IF DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") <= アビ汎用変数:持続ターン
					;達してる場合はターン伸ばすだけ
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
					アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%が引き続きテンションＭＡＸ！！！
				ELSE
					アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%はもうテンションＭＡＸ！！！
				ENDIF
				スキップフラグ = 2
			ENDIF
			BREAK
			;付随割合や付随値は見ないものとする。（頻繁に変動する値ではないため）
		ENDIF
	NEXT

	IF スキップフラグ < 1
		;ランダム開始Lv
		テンション判定 = RAND:100
		IF アビ汎用変数:効果量 >= 4 && テンション判定 < 5
			累積量 = 4
		ELSEIF アビ汎用変数:効果量 >= 3 && テンション判定 < 15
			累積量 = 3
		ELSEIF アビ汎用変数:効果量 >= 2 && テンション判定 < 30
			累積量 = 2
		ELSEIF アビ汎用変数:効果量 >= 1 && テンション判定 < 50
			累積量 = 1
		ELSE
			累積量 = 0
		ENDIF
		SIF 累積量 > 0
			;累積時でないなら新しくデータベースにセット
			DT_ROW_ADD "戦闘効果データベース", "効果名", "テンション", "バフ・デバフフラグ", "バフ", "適用範囲", 適用範囲保存, "効果枠", "テンション", "対象能力", "テンション", "効果量_割合", -1, "効果量_固定値", 累積量, "持続ターン", アビ汎用変数:持続ターン, "消去不可フラグ", 0
	ENDIF

	IF スキップフラグ < 2
		SELECTCASE 累積量
			CASE 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のテンションは低い…
			CASE 1
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のテンションがあがる！
			CASE 2
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のテンションがとても上がる！
			CASE 3
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のテンションが凄く上がる！！
			CASE 4
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%のテンションＭＡＸ！！！
		ENDSELECT
	ENDIF



	;適用範囲が入ってるときは対象変わっても意味ないので一回でBREAKする
	SIF アビ汎用文字列:適用範囲 != ""
		BREAK
NEXT
