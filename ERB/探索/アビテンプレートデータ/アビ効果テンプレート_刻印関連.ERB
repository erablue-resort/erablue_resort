;刻印とはキャラに付与されるバフの一種、５個まで重ねられる
;しかし単体では効果がなく、アビや奥義の使用条件などに関わる
;ここでは名前自由な刻印を簡単にセット・除去出来るようにしている


@アビテンプレート_刻印追加(刻印数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（業火の刻印、のように入力を推奨）
;アビ汎用変数:累積上限_固定値（スタック上限）
;アビ汎用変数:持続ターン
;アビ汎用変数:特殊表示オプション
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 刻印数
#DIM 現在値
#DIM スタック上限
#DIM 文章スキップ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

IF アビ汎用変数:累積上限_固定値 > 0
	スタック上限 = アビ汎用変数:累積上限_固定値
ELSE
	スタック上限 = 5
ENDIF

SIF アビ汎用変数:持続ターン == 0
	アビ汎用変数:持続ターン = -1

SIF MATCH(属性刻印, アビ汎用文字列:バフ・デバフ対象能力) > 0 && アビ汎用変数:特殊表示オプション != -1
	アビ汎用変数:特殊表示オプション = 1

;付与予定のバフ対象を保存
アビテンプレート用_付与バフ保存 += @"%アビ汎用文字列:バフ・デバフ対象能力%_"

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;既存のバフと枠が被ってないかチェック
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{戦闘行動対象}"
			;すでに同じ枠のバフがある場合、数の分だけ固定値に追加
			;最大値未満の時のみ
			現在値 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
			IF 現在値 < スタック上限
				DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", MIN(スタック上限, 刻印数 + 現在値)
				SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") > 0 && DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") < アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
				スキップフラグ = MIN(スタック上限 - 現在値, 刻印数) + 10
			ELSE
				スキップフラグ = 10
			ENDIF
			BREAK
		ENDIF
	NEXT

	文章スキップ = 0
	IF アビ汎用文字列:テンプレ代替メッセージ１ != ""
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ１
		SIF アビ汎用文字列:テンプレ代替メッセージ２ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ２
		SIF アビ汎用文字列:テンプレ代替メッセージ３ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ３
		SIF アビ汎用文字列:テンプレ代替メッセージ４ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ４
		文章スキップ = 1
	ENDIF

	IF スキップフラグ == 10
		;スタック上限なので取得出来なかったパターン
		;文章は出さない
	ELSEIF スキップフラグ
		SIF 文章スキップ == 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{スキップフラグ - 10, 2}得た！
	ELSE
		SIF 文章スキップ == 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{MIN(スタック上限, 刻印数), 2}得た！
		;累積時でないなら新しくデータベースにセット
		{
		DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ対象能力, "バフ・デバフフラグ", "バフ", "適用範囲", @"{戦闘行動対象}",
			"効果枠", アビ汎用文字列:バフ・デバフ対象能力, "対象能力", "刻印_" + アビ汎用文字列:バフ・デバフ対象能力,
			"効果量_固定値", MIN(スタック上限, 刻印数), 
			"持続ターン", アビ汎用変数:持続ターン, "消去不可フラグ", 1, "特殊表示オプション", アビ汎用変数:特殊表示オプション
		}
	ENDIF
NEXT


@刻印現在数確認(刻印名, 対象隊列 = -1)
;これはアビテンプレートではなく、現在の刻印数を返す関数
;使用条件のチェックなどに使う
#FUNCTION
#DIMS 刻印名
#DIM 対象隊列
#DIM バフ番号

;戦闘中以外は常に０を返す（能力表示画面で呼ばれた時の対策）
SIF ダンジョン表示モード != "戦闘画面"
	RETURNF 0

SIF 対象隊列 == -1
	対象隊列 = 戦闘行動キャラ

;刻印は全体系はない
FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == 刻印名 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{対象隊列}"
		;バフ名一致で返す
		RETURNF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
	ENDIF
NEXT

;ないなら０
RETURNF 0



@アビテンプレート_刻印減少(削除数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（業火の刻印、のように入力を推奨）
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIMS 名前一時格納
#DIM 削除数
#DIM 現在値
#DIM 実削除フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

;デバフではないのだが、付与予定のデバフ対象として保存
アビテンプレート用_付与デバフ保存 += @"%アビ汎用文字列:バフ・デバフ対象能力%_"

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的に刻印は死者に存在しない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的に刻印は死者に存在しない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;刻印が存在するかチェック
	実削除フラグ = 0
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{戦闘行動対象}"
			;すでに同じ枠のバフがある場合、数の分だけ固定値を減少
			現在値 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
			IF 現在値 <= 削除数
				;全部削除
				CALL 戦闘効果削除(バフ番号)
				実削除フラグ = 現在値
			ELSE
				DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 現在値 - 削除数
				実削除フラグ = 削除数
			ENDIF
			BREAK
		ENDIF
	NEXT

	SIF 実削除フラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が{実削除フラグ, 2}減少した。
NEXT

;------------------------------------------------------------------------
;属性ごとの汎用刻印の名前を返す関数
;属性を指定せずキャラを指定した場合、そのキャラの属性の汎用刻印の名前を返す
;属性もキャラも指定しない場合、ターゲットか戦闘行動キャラの属性の汎用刻印の名前を返す
;------------------------------------------------------------------------
@属性刻印名(属性番号=-1, 対象キャラ=-1, 着色=0)
#FUNCTIONS
#DIM 属性番号
#DIM 対象キャラ
#DIM 着色

IF !INRANGE(属性番号, 0, 6)
	IF ダンジョン表示モード == "" && !INRANGE(対象キャラ, 0, CHARANUM-1) && INRANGE(TARGET, 0, CHARANUM-1)
		対象キャラ = TARGET
	ELSEIF ダンジョン表示モード != "" && !INRANGE(キャラ隊列検索(対象キャラ), 0, 14)
		対象キャラ = 戦闘行動キャラ
	ELSEIF 対象キャラ == -1
		属性番号 = 6
		GOTO 着色
	ENDIF

	IF ダンジョン表示モード == ""
		属性番号 = 基礎BATTLE_STATE:対象キャラ:属性
		IF 装備ステータス補正保存:対象キャラ:属性
			;0を火属性にしちゃったので数値そのままだと属性変更してるかどうか判別がつかないため、装備による属性変更は属性番号+10で保存している
			属性番号 = 装備ステータス補正保存:対象キャラ:属性 - 10
		ENDIF
	ELSE
		属性番号 = BATTLE_STATE:対象キャラ:属性
	ENDIF
ENDIF

$着色
IF 着色 == 1
	;HTML構文
	RETURNF @"<font color='#%属性数値文字色変換_HTML(属性番号)%'>%属性刻印:属性番号%</font>"
ELSEIF 着色 == 2
	;KSTR構文
	RETURNF @"_COLOR_{属性数値文字色変換(属性番号)}_%属性刻印:属性番号%_"
ELSEIF 着色 == 3
	;RESULTにカラーコードを代入して返す
	RESULT = 属性数値文字色変換(属性番号)
ENDIF
RETURNF 属性刻印:属性番号

;------------------------------------------------------------------------
;・属性刻印の特殊表示用関数
;------------------------------------------------------------------------

@バフ・デバフ特殊表示_業火の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("業火の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(0, , 1)%<br>"
LOCALS += @"火の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_玉水の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("玉水の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(1, , 1)%<br>"
LOCALS += @"水の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_狂風の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("狂風の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(2, , 1)%<br>"
LOCALS += @"風の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_荒土の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("荒土の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(3, , 1)%<br>"
LOCALS += @"土の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_極光の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("極光の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(4, , 1)%<br>"
LOCALS += @"光の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_幽闇の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("幽闇の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(5, , 1)%<br>"
LOCALS += @"闇の魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS

@バフ・デバフ特殊表示_虚無の刻印(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("虚無の刻印", 隊列番号)
LOCALS '= @"属性刻印：%属性刻印名(6, , 1)%<br>"
LOCALS += @"属性を持たぬ魔力が増幅している状態。<br>"
LOCALS += @"効果値［{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}］（最大5/消去不可）"
HTML_PRINT LOCALS
