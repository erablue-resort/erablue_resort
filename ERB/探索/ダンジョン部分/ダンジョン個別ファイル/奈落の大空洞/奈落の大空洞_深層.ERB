@奈落の大空洞_深層_正体(リセットフラグ = 0)
#FUNCTION
#DIM リセットフラグ
#DIM 正体階層

IF 正体階層 == 0 || リセットフラグ
	正体階層 = 1 + RAND(0, 奈落の大空洞踏破フロア数)
	CALLF 奈落の大空洞_深層_レベル補正(1)
	CALLF 奈落の大空洞_深層_特殊処理(1)
ENDIF
RETURNF 正体階層


@奈落の大空洞_深層_レベル補正(リセットフラグ = 0)
#FUNCTION
#DIM リセットフラグ
#DIM レベル補正

SIF レベル補正 == 0 || リセットフラグ
	レベル補正 = RAND(80, 101)
RETURNF レベル補正


@奈落の大空洞_深層_特殊処理(リセットフラグ = 0)
#FUNCTIONS
#DIMS CONST 特殊処理リスト = "", "", "奈落の瘴気展開", "酸の雨展開", "腐蝕の潮風展開", "怨念の呻き展開"
#DIM リセットフラグ
#DIMS 特殊処理

SIF リセットフラグ
	特殊処理 '= 特殊処理リスト:RAND(VARSIZE("特殊処理リスト"))
RETURNF 特殊処理


@奈落の大空洞_存在判定_0
;推奨レベルを返す
CALLFORM 奈落の大空洞_存在判定_{奈落の大空洞_深層_正体()}
RETURN RESULT * 奈落の大空洞_深層_レベル補正() / 100


@奈落の大空洞_エネミーセット_0_1(追加レベル補正)
#DIM CONST ウェーブ番号 = 1
#DIM 追加レベル補正
戦闘時_特殊処理フラグ '= 奈落の大空洞_深層_特殊処理()
JUMPFORM 奈落の大空洞_エネミーセット_{奈落の大空洞_深層_正体()}_{ウェーブ番号}(追加レベル補正)


@奈落の大空洞_エネミー勝利後_0_1
#DIM CONST ウェーブ番号 = 1
TRYCALLFORM 奈落の大空洞_エネミー勝利後_{奈落の大空洞_深層_正体()}_{ウェーブ番号}
CALLF 奈落の大空洞_深層_正体(1)
RETURN RESULT


@奈落の大空洞_エネミーセット_0_2(追加レベル補正)
#DIM CONST ウェーブ番号 = 2
#DIM 追加レベル補正
戦闘時_特殊処理フラグ '= 奈落の大空洞_深層_特殊処理()
JUMPFORM 奈落の大空洞_エネミーセット_{奈落の大空洞_深層_正体()}_{ウェーブ番号}(追加レベル補正)


@奈落の大空洞_エネミー勝利後_0_2
#DIM CONST ウェーブ番号 = 2
TRYCALLFORM 奈落の大空洞_エネミー勝利後_{奈落の大空洞_深層_正体()}_{ウェーブ番号}
CALLF 奈落の大空洞_深層_正体(1)
RETURN RESULT


@奈落の大空洞_エネミーセット_0_3(追加レベル補正)
#DIM CONST ウェーブ番号 = 3
#DIM 追加レベル補正
戦闘時_特殊処理フラグ '= 奈落の大空洞_深層_特殊処理()
JUMPFORM 奈落の大空洞_エネミーセット_{奈落の大空洞_深層_正体()}_{ウェーブ番号}(追加レベル補正)


@奈落の大空洞_エネミー勝利後_0_3
#DIM CONST ウェーブ番号 = 3
TRYCALLFORM 奈落の大空洞_エネミー勝利後_{奈落の大空洞_深層_正体()}_{ウェーブ番号}
CALLF 奈落の大空洞_深層_正体(1)
RETURN RESULT


@奈落の大空洞_エネミーセット_0_4(追加レベル補正)
#DIM CONST ウェーブ番号 = 4
#DIM 追加レベル補正
戦闘時_特殊処理フラグ '= 奈落の大空洞_深層_特殊処理()
JUMPFORM 奈落の大空洞_エネミーセット_{奈落の大空洞_深層_正体()}_{ウェーブ番号}(追加レベル補正)


@奈落の大空洞_エネミー勝利後_0_4
#DIM CONST ウェーブ番号 = 4
TRYCALLFORM 奈落の大空洞_エネミー勝利後_{奈落の大空洞_深層_正体()}_{ウェーブ番号}
CALLF 奈落の大空洞_深層_正体(1)
RETURN RESULT


@奈落の大空洞_エネミーセット_0_5(追加レベル補正)
#DIM CONST ウェーブ番号 = 5
#DIM 追加レベル補正
戦闘時_特殊処理フラグ '= 奈落の大空洞_深層_特殊処理()
JUMPFORM 奈落の大空洞_エネミーセット_{奈落の大空洞_深層_正体()}_{ウェーブ番号}(追加レベル補正)


@奈落の大空洞_エネミー勝利後_0_5
#DIM CONST ウェーブ番号 = 5
TRYCALLFORM 奈落の大空洞_エネミー勝利後_{奈落の大空洞_深層_正体()}_{ウェーブ番号}
CALLF 奈落の大空洞_深層_正体(1)
RETURN RESULT


@固有戦闘開始時処理_特殊_奈落の瘴気展開
WSTR:(K++) = 強敵の周囲には、紫色の瘴気が立ち込めている！
展開中フィールド名格納 = 奈落の瘴気


@固有呪い補正_フィールド_奈落の瘴気(回復量)
#DIM 回復量
RETURN 回復量 / 4


@フィールド説明文_奈落の瘴気
詳細文文字列受け渡し変数 '= "明らかに身体に良い効果を及ぼすとは思えない紫色の瘴気。<br>"
詳細文文字列受け渡し変数 += "敵・味方へのＨＰ回復が、その1/4のダメージに転換される。<br>"
詳細文文字列受け渡し変数 += "効果時間：永続<br>"


@固有戦闘開始時処理_特殊_酸の雨展開
WSTR:(K++) = 周囲一帯に、酸の雨が降り注いでいる！
展開中フィールド名格納 = 酸の雨


@固有ターン終了時処理_フィールド_酸の雨
#DIM 味方番号
#DIM メッセージ表示フラグ
#DIM デバフ番号

メッセージ表示フラグ = 0
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:ＨＰ < 1
		CONTINUE

	IF !メッセージ表示フラグ
		WSTR:(K++) = 酸の雨が%CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%たちに降り注ぐ！
		メッセージ表示フラグ = 1
	ENDIF
	デバフ番号 = デバフ番号取得_枠("酸の雨", 味方番号)
	IF デバフ番号 >= 0
		DT_CELL_SET "戦闘効果データベース", デバフ番号, "効果量_割合", (DT_CELL_GET("戦闘効果データベース", デバフ番号, "効果量_割合") + 5)
	ELSE
		{
		DT_ROW_ADD "戦闘効果データベース", "効果名", "酸の雨", "バフ・デバフフラグ", "デバフ", "適用範囲", @"{味方番号}",
			"効果枠", "酸の雨", "対象能力", "防御力", "効果量_割合", 5,
			"持続ターン", -1, "消去不可フラグ", 1
		}
	ENDIF
	WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%の防御力が5\%下がった！
NEXT

CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化


@フィールド説明文_酸の雨
詳細文文字列受け渡し変数 '= "防具を劣化させていく、強い酸の雨。<br>"
詳細文文字列受け渡し変数 += "ターン終了ごとに味方の防御力が、5％ずつ低下する。<br>"
詳細文文字列受け渡し変数 += "効果時間：永続<br>"


@固有戦闘開始時処理_特殊_腐蝕の潮風展開
WSTR:(K++) = どこからともなく、強い潮風があたりに吹き付ける！
展開中フィールド名格納 = 腐蝕の潮風


@固有バフ・デバフ処理_フィールド_腐蝕の潮風_ダメージ補正_通常攻撃ダメージ(隊列番号, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN 補正前数値 * 13 / 20
ELSE
	RETURN 補正前数値
ENDIF


@フィールド説明文_腐蝕の潮風
詳細文文字列受け渡し変数 '= "武器を劣化させていく、洞穴には場違いなはずの潮風。<br>"
詳細文文字列受け渡し変数 += "味方の通常攻撃によるダメージが0.65倍になる。<br>"
詳細文文字列受け渡し変数 += "効果時間：永続<br>"


@固有戦闘開始時処理_特殊_怨念の呻き展開
WSTR:(K++) = どこからともなく、怨念が呻いているかのような低い音がしている…
展開中フィールド名格納 = 怨念の呻き


@固有バフ・デバフ処理_フィールド_怨念の呻き_弱体耐性(隊列番号, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN MAX(0, 補正前数値 - 30)
ELSE
	RETURN 補正前数値
ENDIF


@固有ターン終了時処理_フィールド_怨念の呻き
#DIM 味方番号
#DIM メッセージ表示フラグ
#DIM 成功率一時格納

メッセージ表示フラグ = 0
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:ＨＰ < 1
		CONTINUE
	SIF デバフ番号取得("混乱", 味方番号) >= 0
		CONTINUE

	IF !メッセージ表示フラグ
		WSTR:(K++) = 呻きのような音があたりに激しく鳴り響く！
		メッセージ表示フラグ = 1
	ENDIF
	IF バフ番号取得("弱体無効", 味方番号) >= 0
		CALL バフ回数経過処理(味方番号, "弱体無効")
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%は弱体無効効果により混乱効果を弾いた！
		CONTINUE
	ENDIF

	成功率一時格納 = 5
	CALL バフ・デバフ測定(味方番号, "弱体耐性")
	成功率一時格納 -= RESULT
	CALL バフ・デバフ測定(味方番号, "状態異常耐性_混乱")
	成功率一時格納 -= RESULT
	IF RAND(100) > 成功率一時格納
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%は混乱効果を受けなかった…
		CONTINUE
	ENDIF
	{
	DT_ROW_ADD "戦闘効果データベース", "効果枠", "怨念の呻き", "バフ・デバフフラグ", "デバフ", "適用範囲", @"{味方番号}",
	"対象能力", "混乱", "効果名", "怨念の呻き", "持続ターン", -1
	}
	WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%は混乱効果を受けた！
NEXT

CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化


@フィールド説明文_怨念の呻き
詳細文文字列受け渡し変数 '= "奈落に巣くう怨念が呻いているかのような謎の低い音。<br>"
詳細文文字列受け渡し変数 += "味方の弱体耐性が-30される。<br>"
詳細文文字列受け渡し変数 += "ターン終了ごとに5％の確率で、味方に混乱効果。<br>"
詳細文文字列受け渡し変数 += "効果時間：永続<br>"
