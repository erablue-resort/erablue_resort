
@敵BATTLE_STATE_Lv19_ボールアニマル(対象エネミー)
#DIM 対象エネミー
;コンセプト：初手だけ速度だけが高い雑魚、エムダラビットより削り性能が高い
敵BATTLE_STATE:対象エネミー:Lv = 19
敵BATTLE_STATE:対象エネミー:最大ＨＰ = 1350
敵BATTLE_STATE:対象エネミー:ＨＰ = 1350
敵BATTLE_STATE:対象エネミー:攻撃力 = 690
敵BATTLE_STATE:対象エネミー:防御力 = 650
敵BATTLE_STATE:対象エネミー:回復力 = 0
敵BATTLE_STATE:対象エネミー:行動速度 = 520
敵BATTLE_STATE:対象エネミー:DA率 = 0
敵BATTLE_STATE:対象エネミー:TA率 = 0
敵BATTLE_STATE:対象エネミー:命中率 = 105
敵BATTLE_STATE:対象エネミー:回避率 = 10
敵BATTLE_STATE:対象エネミー:属性 = 2
敵BATTLE_STATE:対象エネミー:所持EXP = 493
敵BATTLE_STATE:対象エネミー:所持ルピ = 608
敵BATTLE_STATE:対象エネミー:最大チャージターン = 3

敵BATTLE_STATE_STR:対象エネミー:種族 = 動物_獣類

@敵討伐後処理_Lv19_ボールアニマル(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET(@"デバフデータベース_{隊列番号}", データベース行数, "デバフ効果量_固定値")

ドロップ確率 = 20 + トレハンLv * 5
ドロップアイテム = 獣の皮
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

IF !GETBIT(敵BATTLE_STATE:(隊列番号 - 10):トリガー行動フラグ, 0)
	;一度も行動を許さなかった場合
	ドロップ確率 = 100
	ドロップアイテム = 傷の無い獣皮
	CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)
ENDIF


@敵トリガー行動_Lv19_ボールアニマル
;通常攻撃はバウンドして複数体を殴る感じ
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は丸くなって周囲を跳ね回っている……
アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の攻撃！

CALL アビ対象選択テンプレート_味方ランダムＸ体(RAND:3 + 1, 1)
アビ汎用変数:効果割合 = 80
アビ汎用文字列:ダメージ種類 = 通常攻撃ダメージ
CALL アビテンプレート_ダメージ処理_自属性
IF デバフ番号取得("恐怖", 戦闘行動キャラ) < 0
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン += 1
ELSE
	;回数消費
	CALL デバフ回数経過処理(戦闘行動キャラ, "恐怖")
ENDIF

;フラグを立てる
SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
RETURN 1


@固有戦闘開始時処理_エネミー_Lv19_ボールアニマル()
;戦闘開始時だけ行動速度が高い

アビ汎用文字列:実行時メッセージ１ = ボールアニマルは勢いよく転がっている！

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 転がる
アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
アビ汎用変数:効果割合 = 100
アビ汎用変数:持続ターン = 1
アビ汎用変数:消去不可オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット


@敵チャージ行動_Lv19_ボールアニマル
アビテンプレート用_アビ名 = 落下体当たり
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は飛び上がり、勢いよく落下してきた！
アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の落下体当たり！

CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
アビ汎用変数:効果割合 = 250
アビ汎用変数:命中率補正値 = -30
CALL アビテンプレート_ダメージ処理_自属性

;フラグを立てる
SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
