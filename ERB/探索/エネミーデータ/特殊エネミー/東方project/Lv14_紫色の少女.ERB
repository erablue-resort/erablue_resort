
@敵BATTLE_STATE_Lv14_紫色の少女(ARG)
敵BATTLE_STATE:ARG:Lv = 14
敵BATTLE_STATE:ARG:最大ＨＰ = 15000
敵BATTLE_STATE:ARG:ＨＰ = 15000
敵BATTLE_STATE:ARG:攻撃力 = 500
敵BATTLE_STATE:ARG:防御力 = 530
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 200
敵BATTLE_STATE:ARG:DA率 = 50
敵BATTLE_STATE:ARG:TA率 = 0
敵BATTLE_STATE:ARG:命中率 = 100
敵BATTLE_STATE:ARG:回避率 = 0
敵BATTLE_STATE:ARG:属性 = 1
敵BATTLE_STATE:ARG:所持EXP = 1035
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 1

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_魔法使い

SIF EXIST画像FILE(@"%CSTR:(キャラ検索("[動かない大図書館]パチュリー・ノーレッジ")):画像フォルダ%/顔_デフォルト")
	敵BATTLE_STATE_STR:ARG:ボス画像 = %CSTR:(キャラ検索("[動かない大図書館]パチュリー・ノーレッジ")):画像フォルダ%/顔_デフォルト

@敵トリガー行動_Lv14_紫色の少女
#DIM 攻撃回数
#DIMS 立ち絵_パチュリー
#DIM 弱点記録, 6
#DIM 使用属性

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[動かない大図書館]パチュリー・ノーレッジ"),100,,,1,1)
立ち絵_パチュリー = %立ち絵リソース記録%


IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「属性の扱いには慣れてるの。
	WSTR:(K++) = 　あんたたちの弱点も、当然見えてるわ」
	CALL メッセージ欄表示用関数(立ち絵_パチュリー,"紫色の少女",,0)
	CALL 口上変数初期化

	;弱点取得
	VARSET 弱点記録
	FOR LOCAL, 0, 3
		弱点記録:(BATTLE_STATE:LOCAL:属性) ++
	NEXT
	使用属性 = MAXFIND(5, 弱点記録:0, 弱点記録:1, 弱点記録:2, 弱点記録:3, 弱点記録:4, 弱点記録:5)

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！

	SELECTCASE 使用属性
		CASE 0
			アビ汎用文字列:実行時メッセージ２ = ──火符「アグニシャイン」──
		CASE 1
			アビ汎用文字列:実行時メッセージ２ = ──水符「プリンセスウンディネ」──
		CASE 2
			アビ汎用文字列:実行時メッセージ２ = ──風符「シルフィホルン」──
		CASE 3
			アビ汎用文字列:実行時メッセージ２ = ──土符「レイジィトリリトン」──
		CASE 4
			アビ汎用文字列:実行時メッセージ２ = ──日符「ロイヤルフレア」──
		CASE 5
			アビ汎用文字列:実行時メッセージ２ = ──月符「サイレントセレナ」──
	ENDSELECT

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 180
	CALL アビテンプレート_ダメージ処理_属性付き(使用属性)

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「今日は喘息の調子がいいわ。とっておきの魔法も使えそう」
	CALL メッセージ欄表示用関数(立ち絵_パチュリー,"紫色の少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──火水木金土符 「賢者の石」──

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 80
	CALL アビテンプレート_ダメージ処理_火属性
	CALL アビテンプレート_ダメージ処理_水属性
	CALL アビテンプレート_ダメージ処理_風属性
	CALL アビテンプレート_ダメージ処理_土属性
	CALL アビテンプレート_ダメージ処理_光属性
	CALL アビテンプレート_ダメージ処理_闇属性

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	RETURN 0
ENDIF

RETURN 1

@敵チャージ行動_Lv14_紫色の少女
#DIM 使用属性

アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！

使用属性 = RAND:5

SELECTCASE 使用属性
	CASE 0
		アビ汎用文字列:実行時メッセージ２ = ──火符「アグニシャイン」──
	CASE 1
		アビ汎用文字列:実行時メッセージ２ = ──水符「プリンセスウンディネ」──
	CASE 2
		アビ汎用文字列:実行時メッセージ２ = ──風符「シルフィホルン」──
	CASE 3
		アビ汎用文字列:実行時メッセージ２ = ──土符「レイジィトリリトン」──
	CASE 4
		アビ汎用文字列:実行時メッセージ２ = ──日符「ロイヤルフレア」──
	CASE 5
		アビ汎用文字列:実行時メッセージ２ = ──月符「サイレントセレナ」──
ENDSELECT

CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用変数:効果割合 = 180
CALL アビテンプレート_ダメージ処理_属性付き(使用属性)

