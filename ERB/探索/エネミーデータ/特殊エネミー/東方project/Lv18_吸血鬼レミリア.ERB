
;コンセプト：吸血鬼パワー！！　ということでバフがガンガン付きます。全部ディスペルしましょう。
;ディスペル忘れると死にます。吸血鬼は強い。

@敵BATTLE_STATE_Lv18_吸血鬼レミリア(ARG)
敵BATTLE_STATE:ARG:Lv = 18
敵BATTLE_STATE:ARG:最大ＨＰ = 25000
敵BATTLE_STATE:ARG:ＨＰ = 25000
敵BATTLE_STATE:ARG:攻撃力 = 600
敵BATTLE_STATE:ARG:防御力 = 600
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 570
敵BATTLE_STATE:ARG:DA率 = 50
敵BATTLE_STATE:ARG:TA率 = 50
敵BATTLE_STATE:ARG:命中率 = 100
敵BATTLE_STATE:ARG:回避率 = 5
敵BATTLE_STATE:ARG:属性 = 5
敵BATTLE_STATE:ARG:所持EXP = 1750
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 3

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_妖怪_吸血鬼

@敵トリガー行動_Lv18_吸血鬼レミリア
#DIM 攻撃回数
#DIMS 立ち絵_レミリア

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[永遠に紅い幼き月]レミリア"),100,,,1,1)
立ち絵_レミリア = %立ち絵リソース記録%


IF GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「吸血鬼の力、存分に味わいなさい」
	CALL メッセージ欄表示用関数(立ち絵_レミリア,"レミリア",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──「ミレニアムの吸血鬼」──

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 2000
	アビ汎用文字列:バフ・デバフ枠 = 再生A
	アビ汎用文字列:バフ・デバフ対象能力 = 再生
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 100
	アビ汎用文字列:バフ・デバフ枠 = 防御力A
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 100
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2
ELSEIF バフ番号取得_枠("大技待機", 戦闘行動キャラ) > -1
	;バリアが残ってるか？
	IF バフ番号取得_枠("バリア", 戦闘行動キャラ) > -1
		;あるなら超大技
		;顔グラ込みのセリフを出す
		WSTR:(K++) = 「────吸血鬼の十字架を見たのなら」
		WSTR:(K++) = 「もう二度と朝を迎える事はできない！」
		CALL メッセージ欄表示用関数(立ち絵_レミリア,"レミリア",,0)
		CALL 口上変数初期化

		アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
		アビ汎用文字列:実行時メッセージ２ = ──紅符「不夜城レッド」──
	
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ()
		アビ汎用変数:効果割合 = 300
		FOR 攻撃回数, 0, 3
			CALL アビテンプレート_ダメージ処理_自属性()
		NEXT
		行動種別フラグ:戦闘行動キャラ:大技発動 = 1
	ELSE
		WSTR:(K++) = 「うあー！」
		CALL メッセージ欄表示用関数(立ち絵_レミリア,"レミリア",,0)
		CALL 口上変数初期化
		
		アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の大技阻止に成功した！
		アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は大きく体勢を崩している……。
		CALL アビテンプレート_何もしない
	ENDIF
	CALL 大技待機解除(戦闘行動キャラ)
	CALL デバフ削除_枠(戦闘行動キャラ, "行動速度A")
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「運命には抗えないわ！」
	CALL メッセージ欄表示用関数(立ち絵_レミリア,"レミリア",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──夜符「デーモンキングクレイドル」──

	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
	アビ汎用変数:効果割合 = 200
	CALL アビテンプレート_ダメージ処理_自属性()
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 70
	アビ汎用文字列:バフ・デバフ枠 = 回避率A
	アビ汎用文字列:バフ・デバフ対象能力 = 回避率
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「興が乗ったわ。吸血鬼の十字架を、見る勇気はあるかしら」
	CALL メッセージ欄表示用関数(立ち絵_レミリア,"レミリア",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード準備！
	アビ汎用文字列:実行時メッセージ２ = 次ターンの発動までにバリアを破壊し、宣言を止めろ！

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 大技待機
	アビ汎用文字列:バフ・デバフ対象能力 = 大技待機
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果量 = 2500
	アビ汎用文字列:バフ・デバフ枠 = バリア
	アビ汎用文字列:バフ・デバフ対象能力 = バリア
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果量 = 9999
	アビ汎用文字列:バフ・デバフ枠 = 行動速度A
	アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	RETURN 0
ENDIF

RETURN 1

@敵チャージ行動_Lv18_吸血鬼レミリア

アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
アビ汎用文字列:実行時メッセージ２ = ──天罰 「スターオブダビデ」──

CALL アビ対象選択テンプレート_味方全体_生者のみ()
アビ汎用変数:効果割合 = 150
CALL アビテンプレート_ダメージ処理_自属性()
	
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果割合 = 50
アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_バフ効果セット

