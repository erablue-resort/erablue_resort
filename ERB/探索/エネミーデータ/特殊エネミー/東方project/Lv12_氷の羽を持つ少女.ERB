
@敵BATTLE_STATE_Lv12_氷の羽を持つ少女(ARG)
敵BATTLE_STATE:ARG:Lv = 12
敵BATTLE_STATE:ARG:最大ＨＰ = 13000
敵BATTLE_STATE:ARG:ＨＰ = 13000
敵BATTLE_STATE:ARG:攻撃力 = 550
敵BATTLE_STATE:ARG:防御力 = 490
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 460
敵BATTLE_STATE:ARG:DA率 = 0
敵BATTLE_STATE:ARG:TA率 = 0
敵BATTLE_STATE:ARG:命中率 = 100
敵BATTLE_STATE:ARG:回避率 = 0
敵BATTLE_STATE:ARG:属性 = 1
敵BATTLE_STATE:ARG:所持EXP = 685
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_妖精

SIF EXIST画像FILE(@"%CSTR:(キャラ検索("[湖上の氷精]チルノ")):画像フォルダ%/顔_デフォルト")
	敵BATTLE_STATE_STR:ARG:ボス画像 = %CSTR:(キャラ検索("[湖上の氷精]チルノ")):画像フォルダ%/顔_デフォルト

@敵討伐後処理_Lv12_氷の羽を持つ少女(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")


ドロップ確率 = 10 + トレハンLv * 5
IF RAND:100 < ドロップ確率
	リザルト後特殊処理_処理キー = チルノ大ちゃん招待フラグ解放
ENDIF

@リザルト後特殊処理_チルノ大ちゃん招待フラグ解放()
#DIMS 立ち絵_チルノ
#DIMS 立ち絵_大ちゃん
#DIM 対象キャラ
#DIM 対象キャラ２

対象キャラ = キャラ検索("[湖上の氷精]チルノ")
対象キャラ２ = キャラ検索("[名もなき大きな妖精]大妖精")
;既に解放済みなら戻す
SIF CFLAG:対象キャラ:招待不可フラグ == 0 && CFLAG:対象キャラ２:招待不可フラグ == 0
	RETURN 0

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(対象キャラ,100,,,1,1)
立ち絵_チルノ = %立ち絵リソース記録%
CALLF 顔グラ表示用文字列関数(対象キャラ２,101,,,1,1)
立ち絵_大ちゃん = %立ち絵リソース記録%

;会話部分

CALL 口上変数初期化
WSTR:(K++) = ……なんと！　青髪の童女が起き上がり、敵意のない目でこちらを見ている！
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「あたいと互角だなんて、見どころのある人間ね。
WSTR:(K++) = 　強敵たるあたいに果敢に挑んだ証として、これをあげる」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「チルノちゃん、チルノちゃん。
WSTR:(K++) = 　ふつう、凍ったカエルを渡されても、人間さんは困っちゃうんだよ」
CALL メッセージ欄表示用関数(立ち絵_大ちゃん,"大きな妖精のような少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「ええー？　そんなはずはない。
WSTR:(K++) = 　あたいの普通は『ものを貰ったら嬉しい』だよ」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 緑髪の少女（いつの間にか一緒にいた）が言う通り、凍ったカエル……カエルの閉じ込められた氷の塊……を貰っても、少々困る。
WSTR:(K++) = これから帰るならともかく、まだ用事が残っていることだし。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「用事？　……えっと？
WSTR:(K++) = 　この紅い霧を止めに、異世界から来た、ってぇ？
WSTR:(K++) = 　なに言ってるのこいつ」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = うーん、本当なんだけどなー。
WSTR:(K++) = 青髪の童女（チルノという名前らしい）は、疑いの表情で%CALLNAME:MASTER%を睨んだ。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「ほんとに異世界から来たって言うなら、証拠見せてよ。
WSTR:(K++) = 　出せる？　出せないだろ！　異世界なんて持って来れるわけないんだから！」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 持ってくることは確かにできないが、連れていくことは可能だ。
WSTR:(K++) = 百聞は一見に如かずと言うし、証拠と言うなら、これ以上ないものになるだろう。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「…………？？？」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 分かっていないっぽい顔のチルノに、つまり、と説明する。
WSTR:(K++) = ウチに遊びに来てみる？
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「なんだ、分かりにくい説明をするやつ！
WSTR:(K++) = 　でも招待は受けて行ってやる！　大ちゃんも一緒に行こう！」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 「あ、私もお邪魔しても……いいですか？
WSTR:(K++) = 　行ってみたいなぁ、異世界」
CALL メッセージ欄表示用関数(立ち絵_大ちゃん,"大ちゃん",,0)
CALL 口上変数初期化
WSTR:(K++) = %CALLNAME:MASTER%は頷く。
WSTR:(K++) = でも色々終わったあとでね。凍ったカエルもその時によろしく。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「凍った鯉も作れるよ」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = それは要らないかなー。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = [湖上の氷精]チルノがリゾートを訪れるようになった！
WSTR:(K++) = [名もなき大きな妖精]大妖精がリゾートを訪れるようになった！
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化

CFLAG:対象キャラ:招待不可フラグ = 0
CFLAG:対象キャラ２:招待不可フラグ = 0


@敵トリガー行動_Lv12_氷の羽を持つ少女
#DIM 攻撃回数
#DIM 大ちゃん生存３回死亡５回
#DIMS 立ち絵_チルノ

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[湖上の氷精]チルノ"),100,,,1,1)
立ち絵_チルノ = %立ち絵リソース記録%


IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("もう二度と陸には上がらせないよ！/冷凍保存してやるわ！/誰相手でもコチンコチンだよ！")%」
	CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──氷符「アイシクルフォール」──

	FOR 攻撃回数, 0, 3
		CALL アビ対象選択テンプレート_味方ランダムＸ体(3, 1)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 氷結
	アビ汎用文字列:バフ・デバフ対象能力 = 氷結
	アビ汎用変数:効果量 = 50
	アビ汎用変数:累積上限_固定値 = 500
	アビ汎用変数:持続ターン = 5
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_累積デバフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("もう二度と陸には上がらせないよ！/冷凍保存してやるわ！/誰相手でもコチンコチンだよ！")%」
	CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──凍符「コールドディヴィニティー」──

	IF 敵BATTLE_STATE:4:ＨＰ < 1
		大ちゃん生存３回死亡５回 = 5
	ELSE
		大ちゃん生存３回死亡５回 = 3
	ENDIF


	FOR 攻撃回数, 0, 大ちゃん生存３回死亡５回
		SIF 攻撃回数 > 0
			CALL アビ汎用変数文字列リセット

		アビ汎用変数:敵対心無視オプション = 1
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 凍符_行動速度
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_累積デバフ効果セット
	NEXT

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	;通常攻撃は複数回攻撃
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の通常弾幕！

	FOR 攻撃回数, 0, RAND:2 + 2
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 80
		アビ汎用文字列:ダメージ種類 = 通常攻撃ダメージ
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT
	IF デバフ番号取得("恐怖", 戦闘行動キャラ) < 0
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン += 1
	ELSE
		;回数消費
		CALL デバフ回数経過処理(戦闘行動キャラ, "恐怖")
	ENDIF
ENDIF

RETURN 1

@敵チャージ行動_Lv12_氷の羽を持つ少女
#DIM 攻撃回数
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
アビ汎用文字列:実行時メッセージ２ = ──氷符「アイシクルフォール」──

FOR 攻撃回数, 0, 3
	CALL アビ対象選択テンプレート_味方ランダムＸ体(3, 1)
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
NEXT

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 氷結
アビ汎用文字列:バフ・デバフ対象能力 = 氷結
アビ汎用変数:効果量 = 50
アビ汎用変数:累積上限_固定値 = 500
アビ汎用変数:持続ターン = 5
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_不利効果_累積デバフ効果セット

