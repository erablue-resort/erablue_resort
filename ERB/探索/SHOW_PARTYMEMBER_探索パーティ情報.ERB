;
@探索パーティ情報
#DIM ループ用
#DIM 改行用
#DIM 最大レベル
#DIM 最小レベル
#DIM レベル合計
#DIM 人数合計
#DIM 素質値保存
#DIMS カラー指定

LOCALS '= "<div rect='5962, 0, 3700, 1250' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='6024, 62, 3575, 1125'>"
LOCALS += "■探索パーティ情報<br>"

LOCALS += "・基礎情報<br>　"
;レベル情報
レベル合計 = 0
最大レベル = -99
最小レベル = 999
人数合計 = 0
FOR ループ用, 0, 14
	SIF INRANGE(ループ用, 4, 9) != 0
		CONTINUE
	SIF BATTLE_STATE:ループ用:キャラ登録番号 < 1
		CONTINUE
	
	SIF BATTLE_STATE:ループ用:Lv > 最大レベル
		最大レベル = BATTLE_STATE:ループ用:Lv
	SIF BATTLE_STATE:ループ用:Lv < 最小レベル
		最小レベル = BATTLE_STATE:ループ用:Lv
	レベル合計 += BATTLE_STATE:ループ用:Lv
	人数合計 ++
NEXT

SIF 人数合計 > 0
	LOCALS += @"平均Lv:{レベル合計 / 人数合計, 2}　最大Lv:{最大レベル, 2}　最小Lv:{最小レベル, 2}　<br>"
SIF 人数合計 < 1
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>平均Lv:--　最大Lv:--　最小Lv:--　</font><br>"
LOCALS += "<br>"

;知識情報
LOCALS += "・保有知識<br>　"
改行用 = 0
FOR ループ用, 1, 100
	SIF ERDNAME(知識素質, ループ用) == ""
		CONTINUE
	
	素質値保存 = 知識素質_最大値取得(ループ用)
	カラー指定 '= "C0C0C0"
	SELECTCASE 1
		CASE 素質値保存 == 0
			カラー指定 '= カラーパレット_HTML("グレーアウト")
		CASE 素質値保存 < 0
			カラー指定 '= カラーパレット_HTML("赤")
	ENDSELECT
	
	SELECTCASE 1
		CASE OPTION変数:ツールチップ切り替え == 0
			LOCALS += @"<font color='#%カラー指定%'><nonbutton title='%GET_知識素質TOOLTIP(ループ用)%'>%ERDNAME(知識素質,ループ用), 8, RIGHT%：Lv{素質値保存}</nonbutton></font>"
		CASEELSE
			LOCALS += @"<font color='#%カラー指定%'><nonbutton>%ERDNAME(知識素質,ループ用), 8, RIGHT%：Lv{素質値保存}</nonbutton></font>"
	ENDSELECT
	LOCALS += "　"
	改行用 ++
	SIF 改行用 < 4
		CONTINUE
	LOCALS += "<br>　"
	改行用 = 0
NEXT

LOCALS += "</div>"
HTML_PRINT LOCALS, 1

@知識素質_最大値取得(素質番号)
#FUNCTION
#DIM 素質番号
#DIM ループ用
#DIM 素質値保存
#DIM キャラ番号
#DIM キャラ素質値

SIF ERDNAME(知識素質, 素質番号) == ""
	RETURNF -99

素質値保存 = -99

FOR ループ用, 0, 14
	SIF INRANGE(ループ用, 4, 9) != 0
		CONTINUE
	SIF BATTLE_STATE:ループ用:キャラ登録番号 < 1
		CONTINUE
	
	キャラ番号 = BATTLE_STATE:ループ用:キャラ登録番号
	キャラ素質値 = 知識素質:(BATTLE_STATE:ループ用:キャラ登録番号):素質番号
	;例外処理
	SIF ERDNAME(知識素質, 素質番号) == "機械知識" && DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:キャラ番号:装備証番号, "ジョブ証名", 1) == "メカニックの証"
		キャラ素質値 = MAX(キャラ素質値, 1)
	
	SELECTCASE 1
		;CASE キャラ素質値 == 0
			
		CASE キャラ素質値 > 素質値保存
			素質値保存 = キャラ素質値
			;DEBUGPRINTFORML %ERDNAME(知識素質,素質番号)%最大値更新:%CALLNAME:キャラ番号% Lv == {素質値保存}
	ENDSELECT
NEXT

;
IF 知識素質:MASTER:素質番号 > 素質値保存
	素質値保存 = 知識素質:MASTER:素質番号
	;DEBUGPRINTFORML %ERDNAME(知識素質,素質番号)%最大値更新:%CALLNAME:MASTER% Lv == {素質値保存}
ENDIF

SIF 素質値保存 == -99
	素質値保存 = 0

RETURNF 素質値保存

