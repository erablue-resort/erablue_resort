@経験値増加アイテム処理画面(キャラ番号)
#DIM キャラ番号
#DIM 配列番号
#DIM 星晶系所持数, 3
#DIM 星晶系使用数, 3
#DIM 増加経験値量
#DIM 予測レベル


CALL 戦闘能力表示画面(キャラ番号)
DRAWLINE
PRINTL アイテムを使用し、キャラクターの経験値を増加させることが出来ます。
PRINTFORML ※存在するキャラクターのうち、最も高いレベルが上限。
PRINTFORML 現在限界：Lv{最大レベル保存, 2}
DRAWLINE
星晶系所持数:0 = 素材アイテム数量算出("星晶の破片")
星晶系所持数:1 = 素材アイテム数量算出("星晶の欠片")
星晶系所持数:2 = 素材アイテム数量算出("星晶塊")

増加経験値量 = 星晶系使用数:0 * 500 + 星晶系使用数:1 * 3000 + 星晶系使用数:2 * 15000
FOR LOCAL, 0, 80
	IF 戦闘経験値テーブル:LOCAL >= 基礎BATTLE_STATE:キャラ番号:蓄積EXP + 増加経験値量
		予測レベル = LOCAL + 1
		BREAK
	ENDIF
NEXT

IF SUMARRAY(星晶系所持数) > 0
	IF 星晶系所持数:0 > 0
		PRINTFORM ■星晶の破片　　所持数：{星晶系所持数:0, 3}　　使用数：{星晶系使用数:0, 3}　　
		PRINTBUTTON "[-10]", 1
		PRINTBUTTON "[-1]", 2
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 3
			PRINTBUTTON "[+10]", 4
		ENDIF
		PRINTL
	ENDIF
	IF 星晶系所持数:1 > 0
		PRINTFORM ■星晶の欠片　　所持数：{星晶系所持数:1, 3}　　使用数：{星晶系使用数:1, 3}　　
		PRINTBUTTON "[-10]", 101
		PRINTBUTTON "[-1]", 102
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 103
			PRINTBUTTON "[+10]", 104
		ENDIF
		PRINTL
	ENDIF
	IF 星晶系所持数:2 > 0
		PRINTFORM ■星晶塊　　　所持数：{星晶系所持数:2, 3}　　使用数：{星晶系使用数:2, 3}　　
		PRINTBUTTON "[-10]", 201
		PRINTBUTTON "[-1]", 202
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 203
			PRINTBUTTON "[+10]", 204
		ENDIF
		PRINTL
	ENDIF
	PRINTFORML 予測成長：Lv{基礎BATTLE_STATE:キャラ番号:Lv, 2} > Lv{予測レベル, 2}
	PRINTL
	IF SUMARRAY(星晶系使用数) > 0
		PRINTBUTTON "[998] 以上の数を使用する", 998
		PRINTL
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAIN [998] 以上の数を使用する
		RESETCOLOR
		PRINTL
	ENDIF
ELSE
	PRINTL ※対象のアイテムを所持していません。
ENDIF
[IF_DEBUG]
	PRINTBUTTON "[997] 指定レベルまで上げる", 997
	PRINTL
[ENDIF]
DRAWLINE
PRINTBUTTON "[999] 戻る", 999
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 998
		IF 星晶系使用数:0 > 0
			PRINTFORML 星晶の破片を{星晶系使用数:0, 3}個使用した！
			CALL 素材消費処理("星晶の破片", 星晶系使用数:0)
		ENDIF
		IF 星晶系使用数:1 > 0
			PRINTFORML 星晶の欠片を{星晶系使用数:1, 3}個使用した！
			CALL 素材消費処理("星晶の欠片", 星晶系使用数:1)
		ENDIF
		IF 星晶系使用数:2 > 0
			PRINTFORML 星晶塊を{星晶系使用数:2, 3}個使用した！
			CALL 素材消費処理("星晶塊", 星晶系使用数:2)
		ENDIF
		PRINTFORMW 経験値を{増加経験値量, 6}取得！
		基礎BATTLE_STATE:キャラ番号:蓄積EXP += 増加経験値量
		CALL 口上変数初期化
		CALL レベルアップ処理(キャラ番号)
		SIF RESULT
			CALL 口上変数表示(キャラ番号, 0)
		VARSET 星晶系使用数
		RESTART
	CASE 999
		VARSET 星晶系使用数
		RETURN 0
	CASE 997
		PRINTL 設定したいレベルを入力してください（最大80、レベルダウンはできません）
		INPUT
		IF INRANGE(RESULT, 基礎BATTLE_STATE:キャラ番号:Lv+2, 80)
			基礎BATTLE_STATE:キャラ番号:蓄積EXP = 戦闘経験値テーブル:(RESULT - 1)
			CALL レベルアップ処理(キャラ番号)
		ENDIF
		RESTART
	CASEELSE
		SELECTCASE LOCAL % 100
			CASE 1
				星晶系使用数:(LOCAL / 100) = MAX(0, 星晶系使用数:(LOCAL / 100) - 10)
			CASE 2
				星晶系使用数:(LOCAL / 100) = MAX(0, 星晶系使用数:(LOCAL / 100) - 1)
			CASE 3
				星晶系使用数:(LOCAL / 100) = MIN(星晶系所持数:(LOCAL / 100), 星晶系使用数:(LOCAL / 100) + 1)
			CASE 4
				FOR 配列番号, 0, 10
					星晶系使用数:(LOCAL / 100) = MIN(星晶系所持数:(LOCAL / 100), 星晶系使用数:(LOCAL / 100) + 1)
					増加経験値量 = 星晶系使用数:0 * 500 + 星晶系使用数:1 * 3000 + 星晶系使用数:2 * 15000

					FOR LOCAL, 0, 80
						IF 戦闘経験値テーブル:LOCAL >= 基礎BATTLE_STATE:キャラ番号:蓄積EXP + 増加経験値量
							予測レベル = LOCAL + 1
							BREAK
						ENDIF
					NEXT
					IF 予測レベル >= 最大レベル保存
						BREAK
					ENDIF
				NEXT
		ENDSELECT
		RESTART
ENDSELECT

