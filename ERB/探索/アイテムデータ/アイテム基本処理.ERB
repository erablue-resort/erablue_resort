@DUNGEON_ITEM描画
#DIM レイヤー番号
レイヤー番号 = 1

GDISPOSE レイヤー番号
SPRITEDISPOSE ("ダンジョン_1")
GCREATE レイヤー番号, 960, 500
GSETFONT レイヤー番号, GETFONT(), 18
GSETPEN レイヤー番号, 0x00FFFFFF, -1

;周囲の枠
GSETBRUSH レイヤー番号, 0xFFC0C0C0
GFILLRECTANGLE レイヤー番号, 32 , 30, 480, 5
GFILLRECTANGLE レイヤー番号, 32 , 30, 5, 480
GFILLRECTANGLE レイヤー番号, 32 , 495, 480, 5
GFILLRECTANGLE レイヤー番号, 507 , 30, 5, 480

;基礎情報
LOCALS = %月算出()%
GDRAWTEXT レイヤー番号, @"{RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)：■%ダンジョン名%", 32, 7
CALL リソース登録(@"ダンジョン_1", レイヤー番号)


@ダンジョンアイテム画面表示
IF 選択用変数 > 0
	詳細文文字列受け渡し変数 = 
	TRYCALLFORM アイテム個別文章表示_%表示アイテム名:選択用変数%
	TRYCALLFORM アイテム使用先表示_%表示アイテム名:選択用変数%
	IF ダンジョン表示モード == "戦闘画面"
		CALL メッセージ欄表示用関数(,,,0)
		RETURN -1
	ENDIF
	LOCALS = %詳細文文字列受け渡し変数%
ELSE
	VARSET 表示アイテム名
	LOCALS = 
	DT_COLUMN_NAMES "所持アイテムデータベース", 表示アイテム名
	FOR LOCAL, 1, 25
		IF 表示アイテム名:LOCAL != ""
			LOCALS += @"<button value = '{LOCAL}'>[{LOCAL, 2}]%表示アイテム名:LOCAL, 40, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL))), 4%</button><br>"
		ELSE
			LOCALS += "<br>"
		ENDIF
	NEXT
ENDIF

HTML_PRINT @"<div rect = '250, 206, 2937, 2937'>%LOCALS%</div>", 1
HTML_PRINT "<div rect = '250, 2800, 2937, 187'>　　　　　　　　　　　　　　　　　　　　　　　<button value = '999'>[999]戻る</button></div>", 1


@DUNGEON_操作処理_アイテム画面
$INPUT_LOOP
INPUT

LOCAL = RESULT
IF 選択用変数 > -1
	IF LOCAL == 999
		キャラ画像ボタン化フラグ = 0
		CLEARLINE 1
		選択用変数 = -1
		RETURN -1
	ENDIF
	TRYCCALLFORM アイテム効果処理_%表示アイテム名:選択用変数%(LOCAL)
		IF RESULT == 0
			RETURN -1
		ENDIF
	CATCH
		REUSELASTLINE 
		GOTO INPUT_LOOP
	ENDCATCH
ELSE
	SELECTCASE LOCAL
		CASE 1 TO 24
			IF DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL)
				選択用変数 = LOCAL
			ELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		CASE 999
			ダンジョン表示モード = マップ画面
			キャラ画像ボタン化フラグ = 1
			選択用変数 = -1
		CASEELSE
			REUSELASTLINE 
			GOTO INPUT_LOOP
	ENDSELECT
ENDIF
RETURN -1



@アイテム増減汎用処理(アイテム名, 個数)
#DIMS アイテム名
#DIM 個数
;何らかの理由で失敗時はRETURN 0
;ちゃんと増減した場合はRETURN 1

;まず増減するアイテム名が誤字ってないかチェック
ENUMFUNCBEGINSWITH @"アイテム個別文章表示_%アイテム名%"
IF RESULT < 1
	;個別文章がない場合はアイテムがないと判断
	PRINTL 指定したアイテムがみつかりません
	RETURN 0
ENDIF

;同じアイテムを持ってるかどうかチェック
IF DT_COLUMN_EXIST("所持アイテムデータベース", アイテム名)
	;持ってる
	LOCAL:1 = DT_CELL_GET("所持アイテムデータベース", 0, アイテム名) + 個数
	IF LOCAL:1 > 0
		DT_CELL_SET "所持アイテムデータベース", 0, アイテム名, LOCAL:1
	ELSEIF LOCAL:1 == 0
		DT_COLUMN_REMOVE "所持アイテムデータベース", アイテム名
	ELSE
		PRINTFORML %アイテム名%が足りません。
		RETURN 0
	ENDIF
ELSE
	;未所持
	IF 個数 > 0
		DT_COLUMN_ADD "所持アイテムデータベース", アイテム名, 1
		DT_CELL_SET "所持アイテムデータベース", 0, アイテム名, 個数
	ELSE
		PRINTFORML %アイテム名%を所持していません。
		RETURN 0
	ENDIF
ENDIF

RETURN 1

@アイテム削除処理(アイテム名)
#DIMS アイテム名
;何らかの理由で失敗時はRETURN 0
;ちゃんと増減した場合はRETURN 1

;まず増減するアイテム名が誤字ってないかチェック
ENUMFUNCBEGINSWITH @"アイテム個別文章表示_%アイテム名%"
IF RESULT < 1
	;個別文章がない場合はアイテムがないと判断
	PRINTL 指定したアイテムがみつかりません
	RETURN 0
ENDIF

;削除処理
DT_COLUMN_REMOVE "所持アイテムデータベース", アイテム名

RETURN 1



@ダンジョンアイテム画面表示_USERCOM(y軸位置 = 0)
#DIM ページ番号
#DIM y軸位置

LOCALS = 
LOCALS += @"<div rect='200,{y軸位置},5662,3625' border='31' bcolor='#C0C0C0'></div>"
LOCALS += @"<div rect='262,{y軸位置 + 100},5537,3625'>"
VARSET 表示アイテム名
DT_COLUMN_NAMES "所持アイテムデータベース", 表示アイテム名
FOR LOCAL, (ページ番号 * 25) + 1, (ページ番号 * 25) + 25
	IF 表示アイテム名:LOCAL != ""
		LOCALS += @"<button value = '{LOCAL}'>[{LOCAL, 2}]%表示アイテム名:LOCAL, 40, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL))), 4%</button><br>"
	ELSE
		LOCALS += "<br>"
	ENDIF
NEXT
LOCALS += "</div>"

LOCALS += @"<div rect='262,{y軸位置 + 3462},5537,300'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF (ページ番号 * 25) + 25 < 200 && 表示アイテム名:((ページ番号 * 25) + 25) != ""
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='999'>[999]戻る</button></div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 32 + (y軸位置 / 100)
	PRINTL
NEXT

BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	ページ番号 = 0
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 999
		ページ番号 = 0
		RETURN -1
	CASE 1000
		ページ番号 -= 1
	CASE 1001
		ページ番号 += 1
	CASEELSE
		詳細文文字列受け渡し変数 = 
		TRYCALLFORM アイテム個別文章表示_%表示アイテム名:LOCAL%
		LOCALS = %詳細文文字列受け渡し変数%
		HTML_PRINT LOCALS
		WAIT
ENDSELECT
