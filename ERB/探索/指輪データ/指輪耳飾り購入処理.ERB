;-------------------------------------------------
;配列にアイテム名と値段放り込んでこれを呼ぶとお店になる処理
;完全に独立しているのでリゾートだろうがダンジョンだろうが呼べる
;基本的に汎用アイテム購入と同じ処理だが、レイアウトが違うため別関数化
;
;指輪購入用_品揃え登録:0 = 栄冠の指輪・力,2000,ルピ
;のように記述する
;-------------------------------------------------
@指輪耳飾り購入画面処理
#DIMS 商売文字列保存, 3
#DIMS 商品名保存, 100
#DIMS 商品簡易文保存, 100
#DIM ページ番号
VARSET LOCALS
VARSET 商売文字列保存
VARSET 表示用ステータス格納配列:0:0

IF 汎用アイテム購入用_序文 != ""
	DRAWLINE
	HTML_PRINT 汎用アイテム購入用_序文
	DRAWLINE
ENDIF

;中身描写
;まず指輪のステータス補正を全部変数になおして放り込む
IF 表示用ステータス格納配列:0:0 == 0
	FOR LOCAL, 0, 500
		VARSET LOCALS
		IF 指輪購入用_品揃え登録:LOCAL == ""
			BREAK
		ENDIF
		SPLIT 指輪購入用_品揃え登録:LOCAL, ",", 商売文字列保存
		商品名保存:LOCAL = %商売文字列保存:0%
		CALLFORM 指輪ステータス決定_%商売文字列保存:0%(0, 1)
		SPLIT 詳細文文字列受け渡し変数, "_", LOCALS
		FOR LOCAL:1, 0, 8
			IF LOCALS:(LOCAL:1 * 2) != ""
				SELECTCASE LOCALS:(LOCAL:1 * 2)
					CASE "最大ＨＰ"
						表示用ステータス格納配列:LOCAL:1 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "最大ＭＰ"
						表示用ステータス格納配列:LOCAL:2 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "攻撃力"
						表示用ステータス格納配列:LOCAL:3 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "防御力"
						表示用ステータス格納配列:LOCAL:4 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "回復力"
						表示用ステータス格納配列:LOCAL:5 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "行動速度"
						表示用ステータス格納配列:LOCAL:6 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "DA率"
						表示用ステータス格納配列:LOCAL:7 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "TA率"
						表示用ステータス格納配列:LOCAL:8 = TOINT(LOCALS:(LOCAL:1 * 2 + 1))
				ENDSELECT
			ENDIF
		NEXT
		表示用ステータス格納配列:LOCAL:0 = TOINT(商売文字列保存:1)
		IF 商売文字列保存:2 == "ルピ"
			表示用ステータス格納配列:LOCAL:9 = 1
		ELSEIF 商売文字列保存:2 == "ZP"
			表示用ステータス格納配列:LOCAL:9 = 2
		ENDIF
	NEXT
	;次に耳飾りを配列に突っ込む
	LOCAL:1 = 0
	FOR LOCAL, LOCAL - 1, 500
		VARSET LOCALS
		DEBUGPRINTFORML {LOCAL}:{LOCAL:1}:%耳飾り購入用_品揃え登録:(LOCAL:1)%
		IF 耳飾り購入用_品揃え登録:(LOCAL:1) == ""
			BREAK
		ENDIF
		SPLIT 耳飾り購入用_品揃え登録:(LOCAL:1), ",", 商売文字列保存
		商品名保存:LOCAL = %商売文字列保存:0%
		詳細文文字列受け渡し変数 = 
		CALLFORM 耳飾りショップ簡易文章表示_%商売文字列保存:0%
		商品簡易文保存:LOCAL = %詳細文文字列受け渡し変数%
		表示用ステータス格納配列:LOCAL:0 = TOINT(商売文字列保存:1)
		IF 商売文字列保存:2 == "ルピ"
			表示用ステータス格納配列:LOCAL:9 = 1
		ELSEIF 商売文字列保存:2 == "ZP"
			表示用ステータス格納配列:LOCAL:9 = 2
		ENDIF
		LOCAL:1 += 1
	NEXT
ENDIF



LOCALS = <div rect='32px,0px,306px,55px' border='5px' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='42px,10px,306px,45px'>所持ルピ：{MONEY}<br>所持ZP　：%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='343px,0px,595px,55px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += @"<div rect='353px,10px,550px,45px'><button value='1010'>[所持指輪一覧]</button>　　　<button value='1011'>[所持耳飾り一覧]</button></div>"
LOCALS += "<div rect='32px,60px,906px,600px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='42px,70px,906px,580px'><nobr>"
LOCALS += "　　　 指輪名　　　　　　　　　　値段　　　　　　ＨＰ　　ＭＰ　　攻撃力　防御力　回復力　行動速度　DA率　TA率<br>"
LOCALS += "--------------------------------------------------------------------------------------------------------------<br>"
LOCAL:1 = 0
FOR LOCAL, 0, 500
	IF 表示用ステータス格納配列:LOCAL:0 == 0
		BREAK
	ENDIF
	LOCALS += @"<button value='{LOCAL + 100}'>[詳細]</button> "
	LOCALS += @"<button value='{LOCAL}'>"
	LOCALS += @"%商品名保存:LOCAL, 26, LEFT%"
	IF 表示用ステータス格納配列:LOCAL:9 == 1
		LOCALS += @"%TOSTR(表示用ステータス格納配列:LOCAL:0) + "ルピ", 16, LEFT%"
	ELSEIF 表示用ステータス格納配列:LOCAL:9 == 2
		LOCALS += @"{表示用ステータス格納配列:LOCAL:0}ZP"
	ENDIF
	IF 商品簡易文保存:LOCAL == ""
		FOR LOCAL:2, 1, 9
			SELECTCASE LOCAL:2
				CASE 1 TO 2
					LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　　"
				CASE 3 TO 5
					LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 6}　"
				CASE 6
					LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 8}　"
				CASE 7 TO 8
					LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　"
			ENDSELECT
		NEXT
	ELSE
		LOCALS += @"%商品簡易文保存:LOCAL%"
	ENDIF
	LOCALS += "</button><br>"
NEXT

LOCALS += "</div>"

;戻るボタンと前次ページボタン
LOCALS += "<div rect='42px,630px,886px,580px'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:(ページ番号 * 30 + 30):0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='999'>[999]戻る</button></div>"
HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 37
	PRINTL
NEXT

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0 TO 99
		IF 表示用ステータス格納配列:LOCAL:9 == 1
			IF MONEY < 表示用ステータス格納配列:LOCAL:0
				PRINTW ルピが足りません
				RESTART
			ENDIF
			MONEY -= 表示用ステータス格納配列:LOCAL:0
		ELSEIF 表示用ステータス格納配列:LOCAL:9 == 2
			IF FLAG:ZP所持量 < 表示用ステータス格納配列:LOCAL:0
				PRINTW ZPが足りません
				RESTART
			ENDIF
			FLAG:ZP所持量 -= 表示用ステータス格納配列:LOCAL:0
		ENDIF
		IF 商品簡易文保存:LOCAL == ""
			CALL 汎用指輪取得処理(商品名保存:LOCAL, 0)
		ELSE
			CALL 汎用耳飾り取得処理(商品名保存:LOCAL)
		ENDIF
		IF RESULT != -1
			PRINTFORMW %商品名保存:LOCAL%を購入しました。
		ENDIF
		RESTART
	CASE 100 TO 199
		IF 商品簡易文保存:(LOCAL - 100) == ""
			TRYCALLFORM 指輪個別文章表示_%商品名保存:(LOCAL - 100)%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 指輪基礎効果文章表示_%商品名保存:(LOCAL - 100)%(0)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		ELSE
			TRYCALLFORM 耳飾り個別文章表示_%商品名保存:(LOCAL - 100)%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 耳飾り基礎効果文章表示_%商品名保存:(LOCAL - 100)%(0)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		ENDIF
		HTML_PRINT LOCALS
		WAIT
		RESTART
	CASE 999
		汎用アイテム購入用_序文 = 
		VARSET 商品名保存
		VARSET 指輪購入用_品揃え登録
		VARSET 商品簡易文保存
		VARSET 表示用ステータス格納配列:0:0
		RETURN 0
	CASE 1000
		ページ番号 -= 1
		RESTART
	CASE 1001
		ページ番号 += 1
		RESTART
	CASE 1010
		CALL 所持指輪リスト表示_USERCOM
		RESTART
	CASE 1011
		CALL 所持耳飾りリスト表示_USERCOM
		RESTART
ENDSELECT
LOOP 1


