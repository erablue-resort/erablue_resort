@奥義後処理関数
#DIM バフ番号
#DIM デバフ番号
#DIM 生存エネミー数
#DIM 全滅フラグ

;奥義が全て終わった後に読みに来る関数
;主に奥義で効果が発動するバフなどの処理を想定

;アビテンプレート用_奥義使用先保存の配列に奥義の対象が入っている

全滅フラグ = 0
;敵が全滅してるかチェック
生存エネミー数 = 0
FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	生存エネミー数 += 1
NEXT
SIF 生存エネミー数 < 1
	全滅フラグ = 1

;フィールドを見る
IF 展開中フィールド名格納 != ""
	TRYCALLFORM 固有奥義後時処理_フィールド_%展開中フィールド名格納%(全滅フラグ)
ENDIF

IF 戦闘行動キャラ < 10
	;奥義後処理は味方専用

	;指輪を見る
	TRYCALLFORM 固有奥義後処理_指輪_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):装備指輪番号, "指輪名", 1)%(全滅フラグ)

	;ジョブ証を見る
	TRYCALLFORM 固有奥義後処理_ジョブ証_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):装備証番号, "ジョブ証名", 1)%(全滅フラグ)

	;キャラを見る
	TRYCALLFORM 固有奥義後処理_キャラ_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)}(全滅フラグ)

	;バフを見る
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF バフ・デバフ効果適用判定_行指定(バフ番号, "バフ", "", 戦闘行動キャラ)
			TRYCALLFORM 固有奥義後処理_バフ_%DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠")%(バフ番号, 全滅フラグ)
		ENDIF
	NEXT
ENDIF

;個別処理追加エリア
;上記の汎用的な処理では呼べない時の固有関数用

