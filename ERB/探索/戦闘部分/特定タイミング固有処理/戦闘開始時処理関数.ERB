@戦闘開始時処理関数()
#DIM 生存チェック_味方
#DIM 生存チェック_敵
生存チェック_味方 = 0
生存チェック_敵 = 0

;戦闘が始まった直後に呼ぶ関数
;主に特殊なバフなどを取得する処理を想定

;戦闘開始時にフィールドはないので省略

CALL 口上変数初期化
CALL アビテンプレ変数リセット

;まず戦闘時_特殊処理フラグから
IF 戦闘時_特殊処理フラグ != ""
	TRYCCALLFORM 固有戦闘開始時処理_特殊_%戦闘時_特殊処理フラグ%()
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH
ENDIF

FOR 戦闘行動キャラ, 0, 4
	;味方の場合
	SIF BATTLE_STATE:戦闘行動キャラ:キャラ番号 < 1
		CONTINUE

	;指輪を見る
	TRYCCALLFORM 固有戦闘開始時処理_指輪_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ番号):装備指輪番号, "指輪名", 1)%()
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH

	;ジョブ証を見る
	TRYCCALLFORM 固有戦闘開始時処理_ジョブ証_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ番号):装備証番号, "ジョブ証名", 1)%()
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH

	;キャラを見る
	TRYCCALLFORM 固有戦闘開始時処理_キャラ_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)}()
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH
NEXT

FOR 戦闘行動キャラ, 10, 20
	;敵の場合
	SIF 敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名 == ""
		CONTINUE

	;キャラを見る
	TRYCCALLFORM 固有戦闘開始時処理_エネミー_%敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー関数キー%()
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH
NEXT


;全滅チェック
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	生存チェック_味方 = 1
NEXT

IF 生存チェック_味方 == 0
	RETURN 1
ENDIF

FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	生存チェック_敵 = 1
NEXT

IF 生存チェック_敵 == 0
	RETURN 1
ENDIF
