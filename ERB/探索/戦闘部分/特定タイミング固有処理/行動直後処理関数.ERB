@行動直後発動効果処理
;戦闘行動キャラで呼ぶため、引数なし
#DIM 効果番号

;キャラの行動、エネミーの行動の直後に読みに来る関数
;「味方（敵）が特定の行動をした時に反応する」タイプの処理を想定

;行動の処理に追加して組む形で実装されているため、アビテンプレ変数リセットを呼ぶといろいろバグる
;アビ汎用変数文字列リセットを使用すること

CALL アビ汎用変数文字列リセット
戦闘行動内容 = -1
アビテンプレート用_アビ名表示済フラグ = 0
行動口上呼び出し用文字列 = 

;固有処理

;フィールドを見る
IF 展開中フィールド名格納 != ""
	TRYCALLFORM 固有行動直後時処理_フィールド_%展開中フィールド名格納%()

	CALL アビ汎用変数文字列リセット
	戦闘行動内容 = -1
	アビテンプレート用_アビ名表示済フラグ = 0
ENDIF

IF 戦闘行動キャラ < 10
	;指輪を見る
	TRYCALLFORM 固有行動直後処理_指輪_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):装備指輪番号, "指輪名", 1)%()
	CALL アビ汎用変数文字列リセット
	戦闘行動内容 = -1
	アビテンプレート用_アビ名表示済フラグ = 0

	;ジョブ証を見る
	TRYCALLFORM 固有行動直後処理_ジョブ証_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):装備証番号, "ジョブ証名", 1)%()
	CALL アビ汎用変数文字列リセット
	戦闘行動内容 = -1
	アビテンプレート用_アビ名表示済フラグ = 0

	;キャラを見る
	TRYCALLFORM 固有行動直後処理_キャラ_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)}()
	CALL アビ汎用変数文字列リセット
	戦闘行動内容 = -1
	アビテンプレート用_アビ名表示済フラグ = 0

	;バフ・デバフを見る
	FOR 効果番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF バフ・デバフ効果適用判定_行指定(効果番号, "ALL", "ALL", 戦闘行動キャラ)
			TRYCALLFORM 固有行動直後処理_バフ・デバフ_%DT_CELL_GETS("戦闘効果データベース", 効果番号, "効果枠")%(効果番号)
			CALL アビ汎用変数文字列リセット
			戦闘行動内容 = -1
			アビテンプレート用_アビ名表示済フラグ = 0
		ENDIF
	NEXT
ELSE
	;敵の場合はバフ・デバフだけ
	FOR 効果番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF バフ・デバフ効果適用判定_行指定(効果番号, "ALL", "ALL", 戦闘行動キャラ)
			TRYCALLFORM 固有行動直後処理_バフ・デバフ_%DT_CELL_GETS("戦闘効果データベース", 効果番号, "効果枠")%(効果番号)
			CALL アビ汎用変数文字列リセット
			戦闘行動内容 = -1
			アビテンプレート用_アビ名表示済フラグ = 0
		ENDIF
	NEXT
ENDIF

CALL アビテンプレート用_表示メッセージ変換処理
CALL メッセージ欄表示用関数(,,,0)