
@BATTLE_MAIN_LOOP
#DIM 行動対象
#DIM 探索口上キャラ番号
#DIM 行動キャラ一時保存

;戦闘メイン処理
ダンジョン表示モード = 戦闘画面
エネミー画像ボタン化フラグ = 0
キャラ画像ボタン化フラグ = 0
アイテム使用済みフラグ = 0
ターン中奥義回数保存 = 0
兵装使用済みフラグ = 0
VARSET チェンバ基準キャラ保存, -1
行動口上呼び出し用文字列 = 
サイド描画_戦闘ログ保存 = 
サイド描画縦スクロール保存 = 0
アビテンプレート用_味方全滅フラグ = 0
アビテンプレート用_敵全滅フラグ = 0
特殊反応フラグ = 0
VARSET 回避反応者, -1
VARSET 被タゲ反応者, -1
VARSET 被ダメ反応者, -1
VARSET 回避反応回数, -1
VARSET 被タゲ反応回数, -1
VARSET 被ダメ反応回数, -1
現在ターン数 = 1
SIF 戦闘背景画像 == ""
	戦闘背景画像 = %ダンジョン背景画像%
;画像リソースを全解放して顔グラ再生成
CALL 探索用顔グラフィック生成
;敵の登録
CALL 戦闘初期処理_敵

CALL 口上変数初期化()
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　　　　　　　　　　　　　　　　　　　　　　　　　READY
CALL メッセージ欄表示用関数()

VARSET 戦闘中カウント:0:0
VARSET 行動種別フラグ:0:0
CALL 戦闘開始時処理関数()
SIF RESULT
	GOTO 戦闘終了処理


$戦闘_LOOP
CALL 口上変数初期化()
戦闘行動キャラ = -1
戦闘行動対象 = -1
戦闘行動内容 = -1
VARSET LOCAL
CALL アビテンプレ変数リセット

;行動順の判定
CALL 行動順決定

;行動前発動処理
;魅了など
CALL 行動前発動効果処理()

IF 行動不能フラグ
	;行動不能なのでスキップして行動後処理へ
	IF 戦闘行動キャラ < 10
		BATTLE_STATE:戦闘行動キャラ:行動済みフラグ = 1
	ELSE
		敵BATTLE_STATE:(戦闘行動キャラ - 10):行動済みフラグ = 1
	ENDIF
ELSEIF 戦闘行動キャラ < 10
	$行動入力_LOOP
	行動口上呼び出し用文字列 = 
	CALL 行動内容入力
	IF RESULT == -2
		;逃走時
		FOR LOCAL, 0, 4
			IF BATTLE_STATE:LOCAL:キャラ番号 < 1
				BREAK
			ENDIF
		NEXT
		探索口上キャラ番号 = (BATTLE_STATE:(RAND:LOCAL):キャラ番号)
		TRYCALLFORM KOJO_DUNGEON_逃走時_{NO:探索口上キャラ番号}(探索口上キャラ番号)
		KSTR:(K++) = 君たちは逃走した……
		CALL メッセージ欄表示用関数()
		探索中全体カウント:累積逃走数 += 1
		CALL 戦闘終了時処理
		RETURN -1
	ENDIF
	;行動対象に入ってる数値は10+した状態なので注意
	CALL 戦闘行動実処理関数
	IF RESULT == -1
		CALL アビテンプレ変数リセット
		GOTO 行動入力_LOOP
	ENDIF
	IF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
		GOTO 戦闘終了処理
	ENDIF
	IF 戦闘行動キャラ == 9
		戦闘行動キャラ = 戦闘行動キャラ_一時保存
		CALL アビテンプレ変数リセット
		GOTO 行動入力_LOOP
	ENDIF
ELSE
	;10以上の数値が入ってるとエネミーの行動
	;CALL 敵行動決定
	上メニュー操作許可フラグ = 0
	CALL 画面再描画
	CALL 敵戦闘行動実処理関数
ENDIF

;行動後に特殊反応をまとめて動かす
行動キャラ一時保存 = 戦闘行動キャラ

特殊反応フラグ = 1
CALL 被タゲ時効果処理(行動キャラ一時保存)
CALL 回避時効果処理(行動キャラ一時保存)
CALL 被ダメージ時効果処理(行動キャラ一時保存)

VARSET 被タゲ反応者, -1
VARSET 回避反応者, -1
VARSET 被ダメ反応者, -1
VARSET 回避反応回数, -1
VARSET 被タゲ反応回数, -1
VARSET 被ダメ反応回数, -1
特殊反応フラグ = 0

戦闘行動キャラ = 行動キャラ一時保存

;行動後発動処理
;毒や再生など
CALL 行動後発動効果処理()

$戦闘終了処理
VARSET LOCAL
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	SIF !BATTLE_STATE:LOCAL:行動済みフラグ
		LOCAL:3 = 1
	LOCAL:1 = 1
NEXT

FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	SIF !敵BATTLE_STATE:LOCAL:行動済みフラグ
		LOCAL:3 = 1
	LOCAL:2 = 1
NEXT

IF !LOCAL:2
	CALL 戦闘勝利時処理
	RETURN 0
ELSEIF !LOCAL:1
	CALL 全滅時処理
	RETURN 1
ELSEIF LOCAL:3 == 0
	;ターン数を増やして最初から行動
	戦闘行動内容 = -1
	CALL ターン切り替え処理
	IF RESULT
		GOTO 戦闘終了処理
	ENDIF
ENDIF

GOTO 戦闘_LOOP


@画面再描画
REDRAW 0
FOR LOCAL, 0, 10
	PRINTL
NEXT
DRAWLINE
;CALL 探索用顔グラフィック生成
SELECTCASE ダンジョン表示モード
	CASE "戦闘画面"
		IF サイド領域非表示オプション == 0
			CALL サイド領域描画_戦闘中
		ENDIF
		CALL 汎用戦闘情報表示_上側
		CALL BATTLE_エネミー描画
		CALL DUNGEON_キャラ描画
		;メッセージ欄の顔グラもここで登録
		CALL ダンジョンメッセージ欄表示("下", 表示顔グラ対象番号_下)
		CALL ダンジョンメッセージ欄表示("上", 表示顔グラ対象番号_上)
		CALL 戦闘画像表示部分
	CASE "マップ画面"
		IF サイド領域非表示オプション == 0
			CALL サイド領域描画_ダンジョン
		ENDIF
		CALL DUNGEON_MAP描画
		CALL DUNGEON_キャラ描画
		;メッセージ欄の顔グラもここで登録
		CALL ダンジョンメッセージ欄表示("下", 表示顔グラ対象番号_下)
		CALL ダンジョン画像表示部分
	CASE "アイテム画面"
		PRINTL
		CALL DUNGEON_ITEM描画
		CALL DUNGEON_キャラ描画
		;メッセージ欄の顔グラもここで登録
		CALL ダンジョンメッセージ欄表示("下", 表示顔グラ対象番号_下)
		CALL ダンジョン画像表示部分
ENDSELECT

FOR LOCAL, 0, RESULT
	PRINTL
NEXT

REDRAW 1

@汎用戦闘情報表示_上側
VARSET LOCALS
表示顔グラ対象番号_上 = 0

LOCALS:0 = {現在ターン数}ターン目
LOCALS:1 = ------------------------------------------------------------------------------------------
IF 上メニュー操作許可フラグ
	IF アイテム使用済みフラグ == 0
		LOCALS:2 = <button value='1000'>[1000]アイテムを使う</button>　
	ELSE
		LOCALS:2 = <nonbutton><font color='#666666'>[1000]アイテムを使う</font></nonbutton>　
	ENDIF
	IF 兵装使用済みフラグ == 0 && 装備乗機ID
		LOCALS:2 += "<button value='2000'>[2000]兵装を使う</button>　"
	ELSE
		LOCALS:2 += "<nonbutton><font color='#666666'>[2000]兵装を使う</font></nonbutton>　"
	ENDIF
	LOCALS:2 +="<button value='1001'>[1001]逃げる</button>　"
	IF アビ使用確認スキップフラグ == 0
		LOCALS:3 +="<button value='1002'>[1002]アビ使用確認:ON</button>　"
	ELSE
		LOCALS:3 +="<button value='1002'>[1002]アビ使用確認:SKIP</button>　"
	ENDIF
	IF 奥義使用確認スキップフラグ == 0
		LOCALS:3 +="<button value='1003'>[1003]奥義使用確認:ON</button>　"
	ELSE
		LOCALS:3 +="<button value='1003'>[1003]奥義使用確認:SKIP</button>　"
	ENDIF
	IF クイック行動フラグ == 0
		LOCALS:3 +="<button value='1004'>[1004]クイック通常攻撃:OFF</button>　"
	ELSE
		LOCALS:3 +="<button value='1004'>[1004]クイック通常攻撃:ON</button>　"
	ENDIF
	SELECTCASE カットイン演出一括表示フラグ
		CASE 1
			LOCALS:3 +="<button value='1005'>[1005]全体カットイン設定:非表示</button>　"
		CASE 2
			LOCALS:3 +="<button value='1005'>[1005]全体カットイン設定:一日一回</button>　"
		CASEELSE
			LOCALS:3 +="<button value='1005'>[1005]全体カットイン設定:表示</button>　"
	ENDSELECT
ENDIF
IF 展開中フィールド名格納 != ""
	LOCALS:4 = <button value='1010'>フィールド：「%展開中フィールド名格納%」展開中</button>
ENDIF

FOR LOCAL:1, 0, 6
	LOCALS:10 += LOCALS:(LOCAL:1)
	LOCALS:10 += "<br>"
NEXT
TSTR:メッセージ欄文字列受け渡し_上側 = <div rect='281,62,5662,1687'>%LOCALS:10%</div>




@行動順決定
#DIM ループ用
#DIM データベース行数
;全員の比較
LOCAL:1 = 0

FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:行動済みフラグ > 0
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	CALL バフ・デバフ測定(LOCAL, "行動速度")
	LOCAL:2 = RESULT
	IF LOCAL:2 > LOCAL:1
		LOCAL:1 = LOCAL:2
		戦闘行動キャラ = LOCAL
	ENDIF
NEXT

FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:行動済みフラグ
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	CALL バフ・デバフ測定(LOCAL + 10, "行動速度")
	LOCAL:2 = RESULT
	IF LOCAL:2 > LOCAL:1
		LOCAL:1 = 敵BATTLE_STATE:LOCAL:行動速度
		戦闘行動キャラ = LOCAL + 10
	ENDIF
NEXT



@奥義ゲージリセット

FOR LOCAL, 0, 4
	SIF BATTLE_STATE:(LOCAL):キャラ番号 < 1
		CONTINUE
	BATTLE_STATE:LOCAL:奥義ゲージ = 0
NEXT

@戦闘初期処理_味方(初期設定フラグ = 0)
#DIM 初期設定フラグ
#DIM 表示兵装数
#DIM 兵装番号


;キャラ追加
FOR LOCAL, 0, 14
	IF LOCAL > 3 && LOCAL < 10
		CONTINUE
	ENDIF

	SIF BATTLE_STATE:(LOCAL):キャラ番号 < 1
		CONTINUE

	;キャラ番号だけは変動しないので入れない
	BATTLE_STATE:LOCAL:Lv = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):Lv
	BATTLE_STATE:LOCAL:最大ＨＰ = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):最大ＨＰ + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):最大ＨＰ
	BATTLE_STATE:LOCAL:最大ＭＰ = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):最大ＭＰ + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):最大ＭＰ
	IF 初期設定フラグ
		BATTLE_STATE:LOCAL:ＨＰ = BATTLE_STATE:LOCAL:最大ＨＰ
		BATTLE_STATE:LOCAL:ＭＰ = BATTLE_STATE:LOCAL:最大ＭＰ
		BATTLE_STATE:LOCAL:奥義ゲージ = 0
	ENDIF
	BATTLE_STATE:LOCAL:攻撃力 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):攻撃力 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):攻撃力
	BATTLE_STATE:LOCAL:防御力 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):防御力 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):防御力
	BATTLE_STATE:LOCAL:回復力 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):回復力 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):回復力
	BATTLE_STATE:LOCAL:行動速度 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):行動速度 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):行動速度
	BATTLE_STATE:LOCAL:DA率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):DA率 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):DA率
	BATTLE_STATE:LOCAL:TA率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):TA率 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):TA率
	BATTLE_STATE:LOCAL:ゲージ上昇効率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ上昇効率 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ上昇効率
	BATTLE_STATE:LOCAL:ゲージ上昇効率_被ダメ時 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ上昇効率_被ダメ時 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ上昇効率_被ダメ時
	BATTLE_STATE:LOCAL:ゲージ最大値 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ最大値 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):ゲージ最大値
	BATTLE_STATE:LOCAL:命中率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):命中率 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):命中率
	BATTLE_STATE:LOCAL:回避率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):回避率 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):回避率
	BATTLE_STATE:LOCAL:属性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):属性
	IF 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):属性
		;0を火属性にしちゃったので数値そのままだと属性変更してるかどうか判別がつかないため、装備による属性変更は属性番号+10で保存している
		BATTLE_STATE:LOCAL:属性 = 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):属性 - 10
	ENDIF
	BATTLE_STATE:LOCAL:得意武器 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):得意武器
	BATTLE_STATE:LOCAL:クリティカル率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):クリティカル率
	BATTLE_STATE:LOCAL:クリティカルダメージ率 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):クリティカルダメージ率
	BATTLE_STATE:LOCAL:敵対心 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):敵対心
	BATTLE_STATE:LOCAL:行動済みフラグ = 0

	BATTLE_STATE:LOCAL:火耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):火耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):火耐性
	BATTLE_STATE:LOCAL:水耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):水耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):水耐性
	BATTLE_STATE:LOCAL:風耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):風耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):風耐性
	BATTLE_STATE:LOCAL:土耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):土耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):土耐性
	BATTLE_STATE:LOCAL:光耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):光耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):光耐性
	BATTLE_STATE:LOCAL:闇耐性 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):闇耐性 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):闇耐性
	BATTLE_STATE:LOCAL:火強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):火強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):火強化
	BATTLE_STATE:LOCAL:水強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):水強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):水強化
	BATTLE_STATE:LOCAL:風強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):風強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):風強化
	BATTLE_STATE:LOCAL:土強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):土強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):土強化
	BATTLE_STATE:LOCAL:光強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):光強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):光強化
	BATTLE_STATE:LOCAL:闇強化 = 基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ番号):闇強化 + 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):闇強化

	;初期設定時は衣装を探索時衣装に変更
	SIF 初期設定フラグ
		CSTR:(BATTLE_STATE:LOCAL:キャラ番号):ダンジョン突入前の服 = %CSTR:(BATTLE_STATE:LOCAL:キャラ番号):着せ替え服%

	IF 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):装備衣装番号
		TRYCCALLFORM CLOTHES_CHANGE_%DT_CELL_GETS("所持衣装データベース", 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ番号):装備衣装番号, "衣装名", 1)%
		CATCH
			CALL CLOTHES_RESET(BATTLE_STATE:LOCAL:キャラ番号)
		ENDCATCH
	ELSE
		CALL CLOTHES_RESET(BATTLE_STATE:LOCAL:キャラ番号)
	ENDIF
NEXT

;乗機装備時、９にPLAYERのステータスを入れる
BATTLE_STATE:9:キャラ番号 = PLAYER
BATTLE_STATE:9:攻撃力 = DT_CELL_GET("所持乗機データベース", 装備乗機ID, "乗機攻撃力", 1)
BATTLE_STATE:9:命中率 = DT_CELL_GET("所持乗機データベース", 装備乗機ID, "乗機命中率", 1)
BATTLE_STATE:9:クリティカルダメージ率 = 150

;初期設定時に残弾のリセット
IF 初期設定フラグ
	VARSET 乗機残弾種別
	VARSET 乗機残弾数
	表示兵装数 = ENUMFUNCBEGINSWITH(@"乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%_")
	FOR LOCAL, 0, 表示兵装数
		兵装番号 = TOINT(REPLACE(RESULTS:LOCAL, @"乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%_", ""))
		IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
			CALLFORM %RESULTS:LOCAL%("残弾セット", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
		ELSE
			CALLFORM %RESULTS:LOCAL%("残弾セット")
		ENDIF
		IF 乗機残弾種別:兵装番号 == 2
			;CT制の時は残CTを0にセット
			乗機残弾数:兵装番号 = 0
		ENDIF
	NEXT
ENDIF

@戦闘初期処理_敵
FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	IF 敵BATTLE_STATE:LOCAL:ランダムエネミーフラグ
		CALL ランダムエネミーステータス設定(LOCAL)
	ELSE
		TRYCALLFORM 敵BATTLE_STATE_%敵BATTLE_STATE_STR:LOCAL:エネミー名%(LOCAL)
	ENDIF
	SIF 敵BATTLE_STATE:LOCAL:クリティカルダメージ率 == 0
		敵BATTLE_STATE:LOCAL:クリティカルダメージ率 = 150
	SIF 敵BATTLE_STATE:LOCAL:敵対心 == 0
		敵BATTLE_STATE:LOCAL:敵対心 = 100
	敵BATTLE_STATE:LOCAL:行動済みフラグ = 0
	敵BATTLE_STATE:LOCAL:チャージターン = 0
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ && LOCAL % 2 == 0 && LOCAL < 8
		敵BATTLE_STATE_STR:(LOCAL + 1):エネミー名 = 
		敵BATTLE_STATE_STR:(LOCAL + 2):エネミー名 = 
		敵BATTLE_STATE_STR:(LOCAL + 3):エネミー名 = 
		LOCAL += 3
	ENDIF
NEXT

@ターン切り替え処理

;チェインバースト処理
IF ターン中奥義回数保存 > 1
	口上用技名保存 = チェインバースト
	TRYCALLFORM KOJO_DUNGEON_チェンバ発動時_{NO:(BATTLE_STATE:(チェンバ基準キャラ保存:0):0)}()
	CALL アビテンプレ変数リセット
	
	;2回以上奥義を撃っていた場合、敵全体にダメージ
	戦闘行動キャラ = チェンバ基準キャラ保存:0
	CALL バフ込み数値算出("攻撃力")
	LOCAL = RESULT
	LOCALS = 攻撃力
	CALL バフ込み数値算出("防御力")
	IF LOCAL < RESULT
		LOCAL = RESULT
		LOCALS = 防御力
	ENDIF
	CALL バフ込み数値算出("回復力")
	IF LOCAL < RESULT
		LOCAL = RESULT
		LOCALS = 回復力
	ENDIF
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果割合 = ターン中奥義回数保存 * 50
	アビ汎用文字列:攻撃側使用能力値 = %LOCALS%
	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:(チェンバ基準キャラ保存:0):0)%たちが放った奥義の魔力が形を成し、敵に襲いかかる！
	SELECTCASE ターン中奥義回数保存
		CASE 2
			アビ汎用文字列:実行時メッセージ２ = チェインバースト！　
			SELECTCASE BATTLE_STATE:戦闘行動キャラ:属性
				CASE 0
					アビ汎用文字列:実行時メッセージ２ += "レッドデトネーション！！"
				CASE 1
					アビ汎用文字列:実行時メッセージ２ += "ブルーデトネーション！！"
				CASE 2
					アビ汎用文字列:実行時メッセージ２ += "アースデトネーション！！"
				CASE 3
					アビ汎用文字列:実行時メッセージ２ += "エアロデトネーション！！"
				CASE 4
					アビ汎用文字列:実行時メッセージ２ += "アークデトネーション！！"
				CASE 5
					アビ汎用文字列:実行時メッセージ２ += "カオスデトネーション！！"
			ENDSELECT
		CASE 3
			アビ汎用文字列:実行時メッセージ２ = チェインバースト！　
			SELECTCASE BATTLE_STATE:戦闘行動キャラ:属性
				CASE 0
					アビ汎用文字列:実行時メッセージ２ += "紅蓮のコラプション！！"
				CASE 1
					アビ汎用文字列:実行時メッセージ２ += "霧氷のイルミネート！！"
				CASE 2
					アビ汎用文字列:実行時メッセージ２ += "地絶のパルヴァライズ！！"
				CASE 3
					アビ汎用文字列:実行時メッセージ２ += "乱壊のテンペスト！！"
				CASE 4
					アビ汎用文字列:実行時メッセージ２ += "雷電のエラディケート！！"
				CASE 5
					アビ汎用文字列:実行時メッセージ２ += "奈落のフォールダウン！！"
			ENDSELECT
		CASEELSE
			アビ汎用文字列:実行時メッセージ２ = フルチェイン！　
			SELECTCASE BATTLE_STATE:戦闘行動キャラ:属性
				CASE 0
					アビ汎用文字列:実行時メッセージ２ += "プロミネンスフレア！！"
				CASE 1
					アビ汎用文字列:実行時メッセージ２ += "アブソリュート・ゼロ！！"
				CASE 2
					アビ汎用文字列:実行時メッセージ２ += "ディアストロフィズム！！"
				CASE 3
					アビ汎用文字列:実行時メッセージ２ += "バーストゲイル！！"
				CASE 4
					アビ汎用文字列:実行時メッセージ２ += "アセンション！！"
				CASE 5
					アビ汎用文字列:実行時メッセージ２ += "ケイオスタイド！！"
			ENDSELECT
	ENDSELECT
	SETBIT アビ汎用変数:かばう無視オプション, 0
	SETBIT アビ汎用変数:かばう無視オプション, 1
	アビ汎用変数:命中率補正値 = 200
	CALLFORM アビテンプレート_ダメージ処理_自属性

	CALL カットイン呼出("チェインバースト")
	WAIT
	CALL カットイン終了
	CALL アビテンプレート用_表示メッセージ変換処理
	CALL メッセージ欄表示用関数(,,,0)

	;トドメ口上
	IF 口上用トドメフラグ
		TRYCALLFORM KOJO_DUNGEON_トドメ時_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)}
	ENDIF
	口上用技名保存 = 
	口上用トドメフラグ = 0
	
	;敵全滅チェック
	VARSET LOCAL
	FOR LOCAL, 0, 10
		SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
			CONTINUE
		LOCAL:2 = 1
	NEXT
	;全滅シてる時、１を返す
	SIF !LOCAL:2
		RETURN 1
ENDIF

CALL ターン終了時処理関数
;敵味方のどちらかが全滅した場合、１が帰ってくる
SIF RESULT
	RETURN 1

;行動済みフラグの解消
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	BATTLE_STATE:LOCAL:行動済みフラグ = 0
NEXT

FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	敵BATTLE_STATE:LOCAL:行動済みフラグ = 0
NEXT

;バフ、デバフのターン経過処理
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:(LOCAL):キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:(LOCAL):ＨＰ < 1
		CONTINUE
	CALL バフターン経過処理(LOCAL)
	CALL デバフターン経過処理(LOCAL)
NEXT

FOR LOCAL, 10, 20
	SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ < 1
		CONTINUE
	CALL バフターン経過処理(LOCAL)
	CALL デバフターン経過処理(LOCAL)
NEXT

;フィールドのターン経過処理
IF 展開中フィールド残りターン数 == 1
	CALL 口上変数初期化
	WSTR:(K++) = %展開中フィールド名格納%が消失した！
	CALL メッセージ欄表示用関数(,,,0)
	展開中フィールド残りターン数 = 0
	展開中フィールド名格納 = 
ELSEIF 展開中フィールド残りターン数 > 1
	展開中フィールド残りターン数 -= 1
ENDIF

;各種変数の更新
現在ターン数 += 1
アイテム使用済みフラグ = 0
兵装使用済みフラグ = 0
ターン中奥義回数保存 = 0
VARSET チェンバ基準キャラ保存, -1
VARSET 行動種別フラグ:0:0

;ターン切り替わり時に顔グラ再生成
CALL 探索用顔グラフィック生成

CALL ターン開始時処理関数
;敵味方のどちらかが全滅した場合、１が帰ってくる
SIF RESULT
	RETURN 1


@行動後発動効果処理
#DIMS 効果種類
#DIM 効果量
#DIM ループ用
#DIM データベース行数

CALL 口上変数初期化
行動不能フラグ = 0

;毒処理
IF 戦闘行動キャラ < 10
	データベース行数 = DT_ROW_LENGTH(@"デバフデータベース_{戦闘行動キャラ}")
	FOR LOCAL:1, 0, データベース行数
		効果種類 = %DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ対象能力")%
		IF 効果種類 == "毒" || 効果種類 == "火傷" || 効果種類 == "自傷"
			;ダメージ量決定
			効果量 = DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_固定値") + (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ * DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, BATTLE_STATE:戦闘行動キャラ:ＨＰ)
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%は%DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ名")%の効果で{効果量}のダメージ！
			BATTLE_STATE:戦闘行動キャラ:ＨＰ = MAX(BATTLE_STATE:戦闘行動キャラ:ＨＰ - 効果量, 0)
			IF BATTLE_STATE:戦闘行動キャラ:ＨＰ < 1
				WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%は倒れた！
				CALL 戦闘不能時処理(戦闘行動キャラ, -1)
			ENDIF
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		IF 効果種類 == "ＭＰ自傷"
			;ダメージ量決定
			効果量 = DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_固定値") + (BATTLE_STATE:戦闘行動キャラ:最大ＭＰ * DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, BATTLE_STATE:戦闘行動キャラ:ＭＰ)
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%は%DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ名")%の効果でＭＰが{効果量}減少！
			BATTLE_STATE:戦闘行動キャラ:ＭＰ = MAX(BATTLE_STATE:戦闘行動キャラ:ＭＰ - 効果量, 0)
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
	NEXT
	;回数消費
	CALL デバフ回数経過処理(戦闘行動キャラ, "毒")
	CALL デバフ回数経過処理(戦闘行動キャラ, "火傷")
	CALL デバフ回数経過処理(戦闘行動キャラ, "自傷")
	CALL デバフ回数経過処理(戦闘行動キャラ, "ＭＰ自傷")
ELSE
	データベース行数 = DT_ROW_LENGTH(@"デバフデータベース_{戦闘行動キャラ}")
	FOR LOCAL:1, 0, データベース行数
		効果種類 = %DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ対象能力")%
		IF 効果種類 == "毒" || 効果種類 == "火傷" || 効果種類 == "自傷"
			;ダメージ量決定
			効果量 = DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_固定値") + (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ)
			WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は%DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ名")%の効果で{効果量}のダメージ！
			敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ = MAX(敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ - 効果量, 0)
			IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 1
				WSTR:(K++) = %(敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名)%は倒れた！
				CALL 戦闘不能時処理(戦闘行動キャラ, -1)
			ENDIF
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		IF 効果種類 == "ＭＰ自傷"
			;ダメージ量決定
			効果量 = DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_固定値") + (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＭＰ * DT_CELL_GET(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＭＰ)
			WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は%DT_CELL_GETS(@"デバフデータベース_{戦闘行動キャラ}", LOCAL:1, "デバフ名")%の効果でＭＰが{効果量}減少！
			敵BATTLE_STATE:(戦闘行動キャラ - 10):ＭＰ = MAX(敵BATTLE_STATE:(戦闘行動キャラ - 10):ＭＰ - 効果量, 0)
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
	NEXT
	;回数消費
	CALL デバフ回数経過処理(戦闘行動キャラ, "毒")
	CALL デバフ回数経過処理(戦闘行動キャラ, "火傷")
	CALL デバフ回数経過処理(戦闘行動キャラ, "自傷")
	CALL デバフ回数経過処理(戦闘行動キャラ, "ＭＰ自傷")
ENDIF

;再生処理
IF 戦闘行動キャラ < 10
	データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{戦闘行動キャラ}")
	FOR LOCAL:1, 0, データベース行数
		効果種類 = %DT_CELL_GETS(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ対象能力")%
		IF 効果種類 == "再生"
			;回復量決定
			効果量 = DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ効果量_固定値") + (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ * DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, BATTLE_STATE:戦闘行動キャラ:最大ＨＰ - BATTLE_STATE:戦闘行動キャラ:ＨＰ)
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%は%DT_CELL_GETS(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ名")%の効果で{効果量}のＨＰを回復！
			BATTLE_STATE:戦闘行動キャラ:ＨＰ = MIN(BATTLE_STATE:戦闘行動キャラ:ＨＰ + 効果量, BATTLE_STATE:戦闘行動キャラ:最大ＨＰ)
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
	NEXT
	;回数消費
	CALL バフ回数経過処理(戦闘行動キャラ, "再生")
ELSE
	データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{戦闘行動キャラ}")
	FOR LOCAL:1, 0, データベース行数
		効果種類 = %DT_CELL_GETS(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ対象能力")%
		IF 効果種類 == "再生"
			;回復量決定
			効果量 = DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ効果量_固定値") + (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ効果量_割合") / 100)
			効果量 = 効果量 * RAND(95, 105) / 100
			効果量 = MIN(効果量, 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ - 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ)
			WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は%DT_CELL_GETS(@"バフデータベース_{戦闘行動キャラ}", LOCAL:1, "バフ名")%の効果で{効果量}のＨＰを回復！
			敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ = MIN(敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ + 効果量, 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ)
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
	NEXT
	;回数消費
	CALL バフ回数経過処理(戦闘行動キャラ, "再生")
ENDIF

;行動回数持続の効果の回数を減少
CALL バフ行動回数経過処理(戦闘行動キャラ)
CALL デバフ行動回数経過処理(戦闘行動キャラ)
