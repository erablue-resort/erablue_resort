

@戦闘効果経過処理(隊列番号, 経過能力 = "", 経過枠 = "", バフ・デバフ = "", 経過種類 = "ターン")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力
#DIMS バフ・デバフ
#DIM データベース行数
#DIM 効果番号
#DIM 持続保存
#DIM 消滅配列番号
#DIM 消滅番号保存, 100
#DIMS 経過種類

IF 経過能力 != ""
	SIF バフ番号取得(経過能力, 隊列番号) == -1
		RETURN 0
ENDIF
IF 経過枠 != ""
	SIF バフ番号取得_枠(経過枠, 隊列番号) == -1
		RETURN 0
ENDIF

VARSET 消滅番号保存
消滅配列番号 = 0
データベース行数 = DT_ROW_LENGTH("戦闘効果データベース")
FOR 効果番号, 0, データベース行数
	IF 経過能力 != ""
		SIF DT_CELL_GETS("戦闘効果データベース", 効果番号, "対象能力") != 経過能力
			CONTINUE
	ENDIF
	IF 経過枠 != ""
		SIF DT_CELL_GETS("戦闘効果データベース", 効果番号, "効果枠") != 経過枠
			CONTINUE
	ENDIF
	IF 隊列番号 != -1
		SIF DT_CELL_GETS("戦闘効果データベース", 効果番号, "適用範囲") != @"{隊列番号}"
			CONTINUE
	ENDIF
	IF バフ・デバフ != ""
		SIF DT_CELL_GETS("戦闘効果データベース", 効果番号, "バフ・デバフフラグ") != バフ・デバフ
			CONTINUE
	ENDIF
	SELECTCASE 経過種類
		CASE "ターン"
			持続保存 = DT_CELL_GET("戦闘効果データベース", 効果番号, "持続ターン")
			IF 持続保存 == 1
				;ここで削除しちゃうと行がズレるので一旦配列に退避
				消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", 効果番号, "id")
				消滅配列番号 += 1
				IF DT_CELL_GETS("戦闘効果データベース", 効果番号, "バフ・デバフフラグ") == "フィールド"
					;フィールド削除時はメッセージを出す
					CALL 口上変数初期化
					WSTR:(K++) = %展開中フィールド名格納%が消失した！
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
					展開中フィールド名格納 = 
				ENDIF
			ELSEIF 持続保存 > 1
				DT_CELL_SET "戦闘効果データベース", 効果番号, "持続ターン", 持続保存 - 1
			ENDIF
		CASE "回数"
			持続保存 = DT_CELL_GET("戦闘効果データベース", 効果番号, "持続回数")
			IF 持続保存 == 1
				;ここで削除しちゃうと行がズレるので一旦配列に退避
				消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", 効果番号, "id")
				消滅配列番号 += 1
			ELSEIF 持続保存 > 1
				DT_CELL_SET "戦闘効果データベース", 効果番号, "持続回数", 持続保存 - 1
			ENDIF
		CASE "行動回数"
			持続保存 = DT_CELL_GET("戦闘効果データベース", 効果番号, "持続行動回数")
			IF 持続保存 == 1
				;ここで削除しちゃうと行がズレるので一旦配列に退避
				消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", 効果番号, "id")
				消滅配列番号 += 1
			ELSEIF 持続保存 > 1
				DT_CELL_SET "戦闘効果データベース", 効果番号, "持続行動回数", 持続保存 - 1
			ENDIF
		CASE "被ダメ回数"
			持続保存 = DT_CELL_GET("戦闘効果データベース", 効果番号, "持続被ダメ回数")
			IF 持続保存 == 1
				;ここで削除しちゃうと行がズレるので一旦配列に退避
				消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", 効果番号, "id")
				消滅配列番号 += 1
			ELSEIF 持続保存 > 1
				DT_CELL_SET "戦闘効果データベース", 効果番号, "持続被ダメ回数", 持続保存 - 1
			ENDIF
	ENDSELECT
		
NEXT

FOR 効果番号, 0, 消滅配列番号
	CALL 戦闘効果削除(消滅番号保存:効果番号, 1)
NEXT


@戦闘効果ターン経過処理(隊列番号 = -1, 経過能力 = "", 経過枠 = "", バフ・デバフ = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力
#DIMS バフ・デバフ

CALL 戦闘効果経過処理(隊列番号, 経過能力, 経過枠, バフ・デバフ, "ターン")

@戦闘効果回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "", バフ・デバフ = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力
#DIMS バフ・デバフ

CALL 戦闘効果経過処理(隊列番号, 経過能力, 経過枠, バフ・デバフ, "回数")

@バフ回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果回数経過処理(隊列番号, 経過能力, 経過枠, "バフ")

@デバフ回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果回数経過処理(隊列番号, 経過能力, 経過枠, "デバフ")

@戦闘効果行動回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "", バフ・デバフ = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力
#DIMS バフ・デバフ

CALL 戦闘効果経過処理(隊列番号, 経過能力, 経過枠, バフ・デバフ, "行動回数")

@バフ行動回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果行動回数経過処理(隊列番号, 経過能力, 経過枠, "バフ")

@デバフ行動回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果行動回数経過処理(隊列番号, 経過能力, 経過枠, "デバフ")

@戦闘効果被ダメ回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "", バフ・デバフ = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力
#DIMS バフ・デバフ

CALL 戦闘効果経過処理(隊列番号, 経過能力, 経過枠, バフ・デバフ, "被ダメ回数")

@バフ被ダメ回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果被ダメ回数経過処理(隊列番号, 経過能力, 経過枠, "バフ")

@デバフ被ダメ回数経過処理(隊列番号, 経過能力 = "", 経過枠 = "")
#DIM 隊列番号
#DIMS 経過枠
#DIMS 経過能力

CALL 戦闘効果被ダメ回数経過処理(隊列番号, 経過能力, 経過枠, "デバフ")

@戦闘効果削除(バフ番号, ID削除フラグ = 0)
#DIM バフ番号
#DIM 消滅ID保存
#DIM ID削除フラグ

IF ID削除フラグ
	SIF DT_CELL_ISNULL("戦闘効果データベース", バフ番号, "id", 1)
		RETURN 0
	消滅ID保存 = バフ番号
ELSE
	SIF バフ番号 < 0 || バフ番号 >= DT_ROW_LENGTH("戦闘効果データベース")
		RETURN 0
	消滅ID保存 = DT_CELL_GET("戦闘効果データベース", バフ番号, "id")
ENDIF

IF DT_CELL_GETS("戦闘効果データベース", 消滅ID保存, "バフ・デバフフラグ", 1) == "バフ"
	TRYCALLFORM バフ消滅時処理_%DT_CELL_GETS("戦闘効果データベース", 消滅ID保存, "効果枠", 1)%
ELSEIF DT_CELL_GETS("戦闘効果データベース", 消滅ID保存, "バフ・デバフフラグ", 1) == "デバフ"
	TRYCALLFORM デバフ消滅時処理_%DT_CELL_GETS("戦闘効果データベース", 消滅ID保存, "効果枠", 1)%
ENDIF
DT_ROW_REMOVE "戦闘効果データベース", 消滅ID保存


@戦闘効果直接削除(隊列番号, バフ・デバフ, 枠・能力, 消去対象能力)
#DIM 消滅配列番号
#DIM 消滅番号保存, 100
#DIM 隊列番号
#DIMS バフ・デバフ
#DIMS 枠・能力
#DIMS 消去対象能力
#DIM 対象番号
#DIM デバフ番号

VARSET 消滅番号保存
消滅配列番号 = 0

FOR 対象番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	SIF DT_CELL_GETS("戦闘効果データベース", 対象番号, "適用範囲") != @"{隊列番号}"
		CONTINUE
	IF 消去対象能力 != "ALL"
		IF 枠・能力 == "枠"
			SIF DT_CELL_GETS("戦闘効果データベース", 対象番号, "効果枠") != 消去対象能力
				CONTINUE
		ELSEIF 枠・能力 == "能力"
			SIF DT_CELL_GETS("戦闘効果データベース", 対象番号, "対象能力") != 消去対象能力
				CONTINUE
		ENDIF
	ENDIF
	IF バフ・デバフ == "バフ"
		SIF DT_CELL_GETS("戦闘効果データベース", 対象番号, "バフ・デバフフラグ") != "バフ"
			CONTINUE
	ELSEIF バフ・デバフ == "デバフ"
		SIF DT_CELL_GETS("戦闘効果データベース", 対象番号, "バフ・デバフフラグ") != "デバフ"
			CONTINUE
	ENDIF

	;ここで削除しちゃうと行がズレるので一旦配列に退避
	消滅番号保存:消滅配列番号 = DT_CELL_GET("戦闘効果データベース", 対象番号, "id")
	消滅配列番号 += 1
NEXT

FOR デバフ番号, 0, 消滅配列番号
	CALL 戦闘効果削除(消滅番号保存:デバフ番号, 1)
NEXT

@バフ削除_能力(隊列番号, 消去対象能力)
#DIM 隊列番号
#DIMS 消去対象能力

CALL 戦闘効果直接削除(隊列番号, "バフ", "能力", 消去対象能力)

@バフ削除_枠(隊列番号, 消去枠)
#DIM 隊列番号
#DIMS 消去枠

CALL 戦闘効果直接削除(隊列番号, "バフ", "枠", 消去枠)

@デバフ削除_能力(隊列番号, 消去対象能力)
#DIM 隊列番号
#DIMS 消去対象能力

CALL 戦闘効果直接削除(隊列番号, "デバフ", "能力", 消去対象能力)

@デバフ削除_枠(隊列番号, 消去枠)
#DIM 隊列番号
#DIMS 消去枠

CALL 戦闘効果直接削除(隊列番号, "デバフ", "枠", 消去枠)

@大技待機解除(隊列番号)
#DIM 隊列番号
JUMP バフ削除_枠(隊列番号, "大技待機")
