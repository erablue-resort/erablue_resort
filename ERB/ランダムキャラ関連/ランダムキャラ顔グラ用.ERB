
;CALLNAMEを使ってHASH値をつくる
@NAMEHASH(C_NAME, DIVISOR, INIT = 0)
#FUNCTION
#DIMS C_NAME
#DIM DIVISOR
#DIM DYNAMIC INIT
#DIM 文字位置カウンタ
#DIM HASH

HASH = INIT
FOR 文字位置カウンタ, 0, STRLENSU(C_NAME)
	HASH ^= ENCODETOUNI(C_NAME, 文字位置カウンタ)
NEXT
RETURNF HASH % DIVISOR + 1


@汎用立ち絵取得処理(名前, 種族, 性別, 体格, バスト, 特殊種族="")
#FUNCTIONS
#DIMS 名前
#DIM 種族
#DIM 性別
#DIM 体格
#DIM バスト
#DIM バスト種別
#DIMS 特殊種族

IF 性別 == 2
	;ヒューマン以外の種族は未実装
	SIF 種族 == 4
		RETURNF @"ヒューマン小男{NAMEHASH(名前, 3)}_0"
	RETURNF @"ヒューマン\@ (体格 <= -2) ? 小 # \@男{NAMEHASH(名前, 3)}_0"
ELSE
	;しょっちゅう使うので先に出しちゃう
	SELECTCASE バスト
		CASE -2
			バスト種別 = 0
		CASE -1, 0
			バスト種別 = 1
		CASE 1
			バスト種別 = 2
		CASE 2, 3
			バスト種別 = 3
	ENDSELECT

	SELECTCASE 種族
		CASE 2
			RETURNF @"エルーン\@ (体格 <= -2) ? 小 # \@{NAMEHASH(名前, 4)}_{バスト種別}"
		CASE 3
			バスト種別 = (バスト <= 2) ? 3 # 4
			RETURNF @"ドラフ\@ (体格 <= -2) ? 小 # \@{NAMEHASH(名前, 4)}_{バスト種別}"
		CASE 4
			バスト種別 = 0
			RETURNF @"ハーヴィン{NAMEHASH(名前, 4)}_{バスト種別}"
		CASE 6
			IF 特殊種族 == "ワーウルフ"
				RETURNF @"ワーウルフ\@ (体格 <= -2) ? 小 # \@{OPTION変数:ワーウルフ画像ケモ度}{NAMEHASH(名前, 4)}_{バスト種別}"
			ELSEIF 特殊種族 == "ヴァンパイア"
				RETURNF @"ヴァンパイア\@ (体格 <= -2) ? 小 # \@{NAMEHASH(名前, 4)}_{バスト種別}"
			ENDIF
	ENDSELECT
	;ここまで来るのはヒューマンor固有グラフィックがない種族
	RETURNF @"ヒューマン\@ (体格 <= -2) ? 小 # \@{NAMEHASH(名前, 4)}_{バスト種別}"
ENDIF
