@ランダムキャラ生成準備開始(ＤＢ名 = "", リセットフラグ = 0)
#DIMS ＤＢ名
#DIM リセットフラグ

SIF ＤＢ名 == ""
	THROW ＤＢ名が指定されていません。

IF DT_EXIST(ＤＢ名)
	SIF リセットフラグ
		DT_CLEAR ＤＢ名
ELSE
	CALL ランダムキャラデータベースセット(ＤＢ名)
ENDIF

CALL ランダムキャラデータ_NEW(ＤＢ名, 0)
;管理番号を返す
RETURN RESULT


@ランダムキャラ生成(ＤＢ名, 管理番号, 肩書き = "一般人")
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CONST 上書きしない = 0
#DIMS ＤＢ名
#DIM 管理番号
#DIMS 肩書き
#DIM Ｒキャラ属性
#DIM Ｒキャラ種族
#DIMS Ｒキャラ特殊種族
#DIM Ｒキャラ性別
#DIMS Ｒキャラ名前
#DIM Ｒキャラ経験済フラグ ;bit0=非童貞, bit1=V経験済, bit2=A経験済
#DIM Ｒキャラ年齢層
#DIM Ｒキャラ外見年齢
#DIM Ｒキャラバストサイズ
#DIM Ｒキャラ性知識
#DIM Ｒキャラ体格
#DIM 素質選択用, 8
#DIM 部位感覚Lv, 5
#DIM 部位絶頂回数, 5
#DIM 経験強度

;属性：完全ランダム
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "基礎BATTLE_STATE", "属性") == 0
	Ｒキャラ属性 = RAND:6
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "基礎BATTLE_STATE", "属性", Ｒキャラ属性)
ELSE
	Ｒキャラ属性 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "基礎BATTLE_STATE", "属性")
ENDIF

;得意武器：10％で2種得意持ちが現れる
LOCAL = 0
SETBIT LOCAL, RAND:10
SIF RAND:10 == 0
	SETBIT LOCAL, RAND:10
CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "基礎BATTLE_STATE", "得意武器", LOCAL, 上書きしない)

;ステータスタイプ：完全ランダム
CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "基礎BATTLE_STATE", "ステータスタイプ保存", RAND:8, 上書きしない)

;種族：ヒューマンが若干多く (36％～)、エルーン・ドラフ・ハーヴィンは20％ずつ。まれに４種族以外が出る
;ランダムのあたり枠といえば3％だよな
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "種族") == 0
	Ｒキャラ特殊種族 '= ""
	SELECTCASE RAND:100
		CASE 0 TO 19
			Ｒキャラ種族 = 種族_エルーン
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "エルーン耳", 1, 上書きしない)
			SIF RAND:5 == 0
				CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "尻尾あり", 1, 上書きしない)
		CASE 20 TO 39
			Ｒキャラ種族 = 種族_ドラフ
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "角あり", 1, 上書きしない)
		CASE 40 TO 59
			Ｒキャラ種族 = 種族_ハーヴィン
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "ハーヴィン耳", 1, 上書きしない)
		CASE 94 TO 96
			;星晶獣は属性に応じた施設開放条件を満たしている必要がある
			;満たしてない場合はヒューマンに置き換えられる
			SELECTCASE Ｒキャラ属性
				CASE 0
					;火
					Ｒキャラ種族 = 鍛冶場作成許可フラグ ? 種族_星晶獣 # 種族_ヒューマン
				CASE 1
					;水
					Ｒキャラ種族 = 海作成許可フラグ ? 種族_星晶獣 # 種族_ヒューマン
				CASE 2
					;風
					Ｒキャラ種族 = 高速船作成済フラグ ? 種族_星晶獣 # 種族_ヒューマン
				CASE 3
					;土
					Ｒキャラ種族 = 温泉作成許可フラグ ? 種族_星晶獣 # 種族_ヒューマン
				CASE 4
					;光
					Ｒキャラ種族 = 0 ? 種族_星晶獣 # 種族_ヒューマン
				CASE 5
					;闇
					Ｒキャラ種族 = 0 ? 種族_星晶獣 # 種族_ヒューマン
				CASEELSE
					Ｒキャラ種族 = 種族_ヒューマン
			ENDSELECT
		CASE 97 TO 99
			;亜人
			Ｒキャラ種族 = 種族_その他
			STRDATA Ｒキャラ特殊種族
				DATA クリスタリア
				DATA スヴァル
				DATA ワーウルフ
				DATA ヴァンパイア
				DATA 烈人
				DATA 魔ヒ人
			ENDDATA

			SELECTCASE Ｒキャラ特殊種族
				CASE "クリスタリア"
					;女性のみの種族なので、許可されていないときはヒューマンに置き換え
					IF GETBIT(OPTION変数:性別招待制御, 0)
						Ｒキャラ種族 = 種族_ヒューマン
						Ｒキャラ特殊種族 '= ""
					ELSE
						CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "性別", 1)
						CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "ハーヴィン耳", 1, 上書きしない)
					ENDIF
				CASE "ヴァンパイア"
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "ハーヴィン耳", 1, 上書きしない)
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "吸血鬼", 1, 上書きしない)
				CASE "ワーウルフ"
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "エルーン耳", 1, 上書きしない)
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "尻尾あり", 1, 上書きしない)
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "発情期あり", 1, 上書きしない)
					CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "汚臭耐性", (RAND:5 ? -1 # -2), 上書きしない)
			ENDSELECT
		CASEELSE
			Ｒキャラ種族 = 種族_ヒューマン
	ENDSELECT

	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "種族", Ｒキャラ種族)
	CALL ランダムキャラデータ_SETS_B(ＤＢ名, 管理番号, "CSTR", "特殊種族", Ｒキャラ特殊種族)
ELSE
	Ｒキャラ種族 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "種族")
	Ｒキャラ特殊種族 '= ランダムキャラデータ_GETS_B(ＤＢ名, 管理番号, "CSTR", "特殊種族")
ENDIF

;性別
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "性別") == 0
	Ｒキャラ性別 = ランダム性別()
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "性別", Ｒキャラ性別)
ELSE
	Ｒキャラ性別 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "性別")
ENDIF

;名前
IF ランダムキャラデータ_FIND(ＤＢ名, 管理番号, "呼び名") == 0
	Ｒキャラ名前 '= ランダムキャラ名前生成(Ｒキャラ種族, Ｒキャラ性別)
	CALL ランダムキャラデータ_SETS(ＤＢ名, 管理番号, "名前", , @"[%肩書き%]%Ｒキャラ名前%")
	CALL ランダムキャラデータ_SETS(ＤＢ名, 管理番号, "呼び名", , Ｒキャラ名前)
ELSE
	Ｒキャラ名前 '= ランダムキャラデータ_GETS(ＤＢ名, 管理番号, "呼び名")
ENDIF

;未経験
Ｒキャラ経験済フラグ = 0
IF GETBIT(Ｒキャラ性別, 0)
	;処女
	IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "処女") == 0
		IF RAND:10 == 0
			;10％で非処女
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "処女", 0)
			SETBIT Ｒキャラ経験済フラグ, 1
		ELSE
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "処女", 1)
		ENDIF
	ELSEIF ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "処女") != 1
		SETBIT Ｒキャラ経験済フラグ, 1
	ENDIF
ENDIF
IF GETBIT(Ｒキャラ性別, 1)
	;童貞
	IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "非童貞") == 0
		SELECTCASE RAND:100
			CASE 0
				;1％の確率でA性交も経験済の非童貞
				CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "非童貞", 3)
				SETBIT Ｒキャラ経験済フラグ, 0
			CASE 1 TO 9
				;10％で非童貞
				CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "非童貞", 1)
				SETBIT Ｒキャラ経験済フラグ, 0
			CASEELSE
				;残りは童貞
				CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "非童貞", 0)
		ENDSELECT
	ELSEIF ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "非童貞") != 0
		SETBIT Ｒキャラ経験済フラグ, 0
	ENDIF
ENDIF

;Ａ処女 (5％で非Ａ処女)
IF RAND:20 == 0
	SETBIT Ｒキャラ経験済フラグ, 2
ELSE
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "Ａ処女", 1, 上書きしない)
ENDIF

;年齢層/外見年齢
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "年齢層") == 0
	LOCAL = ランダム年齢(Ｒキャラ種族)
	Ｒキャラ年齢層 = ランダムキャラ年齢層(LOCAL)
	Ｒキャラ外見年齢 = ランダムキャラ外見年齢(LOCAL)
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "年齢層", Ｒキャラ年齢層)
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "外見年齢", Ｒキャラ外見年齢)
ELSE
	Ｒキャラ年齢層 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "年齢層")
	Ｒキャラ外見年齢 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "外見年齢")
ENDIF

;バストサイズ：種族、年齢層で分布が異なる
IF GETBIT(Ｒキャラ性別, 0)
	IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "バストサイズ") == 0
		Ｒキャラバストサイズ = ランダムバストサイズ(Ｒキャラ種族, Ｒキャラ年齢層)
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "バストサイズ", Ｒキャラバストサイズ)
	ELSE
		Ｒキャラバストサイズ = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "バストサイズ")
	ENDIF
ENDIF

;性知識：処女/童貞、年齢層、エルーンかどうかで分布が異なる
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "知識素質", "性知識") == 0
	Ｒキャラ性知識 = ランダム性知識(!(Ｒキャラ経験済フラグ & 3), Ｒキャラ年齢層, Ｒキャラ種族 == 種族_エルーン)
	SELECTCASE Ｒキャラ性知識
		CASE -2
			;無知
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", -2)
		CASE -1
			;獣並み
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", -1)
		CASE 0
			;疎い
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", 0)
		CASE 1
			;耳年増
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "性的興味", 1)
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", 0)
		CASE 2
			;必要十分
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", 1)
		CASE 3
			;有識者
			CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", "性知識", 2)
	ENDSELECT
ELSE
	Ｒキャラ性知識 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "知識素質", "性知識")
	SELECTCASE Ｒキャラ性知識
		CASE IS > 0
			Ｒキャラ性知識 ++
		CASE 0
			SIF ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "性的興味") > 0
				Ｒキャラ性知識 = 1
	ENDSELECT
ENDIF

;容姿
CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "容姿", ランダム容姿(), 上書きしない)

;体格：種族、性別、見た目年齢、ドラフ♀のみバストサイズ、長命者かどうかで分布が異なる
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "体格") == 0
	Ｒキャラ体格 = ランダム体格(Ｒキャラ種族, Ｒキャラ性別, GET_見た目年齢(Ｒキャラ年齢層, Ｒキャラ外見年齢), Ｒキャラバストサイズ, (Ｒキャラ年齢層 >= 年齢層_老年))
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "体格", Ｒキャラ体格)
ELSE
	Ｒキャラ体格 = ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "体格")
ENDIF

;体型：種族で分布が異なる
CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "体型", ランダム体型(Ｒキャラ種族), 上書きしない)

;男性器サイズ：種族、性別、年齢層で分布が異なる
SIF GETBIT(Ｒキャラ性別, 1)
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "男性器サイズ", ランダム男性器サイズ(Ｒキャラ種族, Ｒキャラ性別, Ｒキャラ年齢層), 上書きしない)

;通常素質：6個～8個（うち2つは性格系の素質確定）
CALL ランダム素質選択(素質選択用)
FOR LOCAL, 0, 8
	SIF 素質選択用:LOCAL == 0
		BREAK
	CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "素質", ランダムキャラ素質番号(素質選択用:LOCAL), ランダムキャラ素質値(素質選択用:LOCAL), 上書きしない)
NEXT

;敏感系素質
FOR LOCAL, GETNUM(TALENT, "Ｃ感度"), (GETNUM(TALENT, "Ｓ感度") + 1)
	SELECTCASE RAND:10
		CASE IS < 2
			;鈍感
			CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "素質", LOCAL, -1, 上書きしない)
		CASE IS < 7
			;通常
		CASEELSE
			;敏感
			CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "素質", LOCAL, 1, 上書きしない)
	ENDSELECT
NEXT

;元男/元女
IF RAND:100 == 0
	IF Ｒキャラ性別 == 2
		;元女
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "性転換", 1, 上書きしない)
	ELSE
		;元男
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "性転換", 2, 上書きしない)
	ENDIF
ENDIF

;陥没乳首：爆乳以上のみ
SIF Ｒキャラバストサイズ >= 2 && 素質切り替えOPTION記憶:陥没乳首_なし == 0 && RAND:10 == 0
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "陥没乳首", 1, 上書きしない)

;母乳体質：爆乳以上のみ
SIF Ｒキャラバストサイズ >= 2 && 素質切り替えOPTION記憶:母乳体質_なし == 0 && RAND:10 == 0
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "母乳体質", 1, 上書きしない)

;絶倫：ふたなりのみ
SIF Ｒキャラ性別 == 3 && RAND:3 == 0
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "絶倫", 1, 上書きしない)

;交際経験(処女/童貞、見た目年齢、長命者かどうかで分布が異なる)
IF ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "恋人持ち") == 0 && ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "セフレあり") == 0
	LOCAL = ランダム交際経験(!(Ｒキャラ経験済フラグ & 3), GET_見た目年齢(Ｒキャラ年齢層, Ｒキャラ外見年齢), (Ｒキャラ年齢層 >= 年齢層_老年))
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "恋人持ち", ランダムキャラ恋人持ち(LOCAL))
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "セフレあり", ランダムキャラセフレあり(LOCAL))

	;出産経験
	LOCAL = ランダムキャラ出産経験(LOCAL)
	IF LOCAL >= 1 && ランダムキャラデータ_FIND_B(ＤＢ名, 管理番号, "素質", "子持ち") == 0
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "素質", "子持ち", 1)
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "経験", "妊娠経験", LOCAL)
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "経験", "出産経験", LOCAL)
	ENDIF
ENDIF

;知識素質
IF RAND:10 == 0
	STRDATA LOCALS
		DATA 調合知識
		DATA 魔法知識
		DATA 機械知識
		DATA 錬金知識
		DATA 魔物知識
		DATA 絵師
		DATA 高貴
		DATA 釣り好き
		DATA 踊り子
		DATA 歌唱技能
		DATA 料理人
		DATA 歴史知識
		DATA 経営知識
		DATA 水泳技能
		DATA 農業知識
	ENDDATA
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "知識素質", LOCALS, 1, 上書きしない)
ENDIF

IF !Ｒキャラ経験済フラグ && Ｒキャラ性知識 < 0
	;処女・童貞かつ無知キャラの場合、全ての能力・経験追加をスキップ
ELSE
	;感覚系能力と部位経験・部位絶頂経験
	VARSET 部位感覚Lv, -1
	VARSET 部位絶頂回数, 0
	FOR LOCAL, 0, 5
		;経験強度が高いほど部位感覚Lvが高く、部位経験が多くなりやすい
		;経験強度のベースは部位感度系素質により決定される
		経験強度 = ランダムキャラデータ_GET(ＤＢ名, 管理番号, "素質", GETNUM(TALENT, "Ｃ感度") + LOCAL)
		;部位が無い場合はスキップ
		SIF 経験強度 <= -2
			CONTINUE
		;部位ごとに部位感覚レベルの決定
		SELECTCASE LOCAL
			CASE 0
				;Ｃ　ちんこ持ち・処女の耳年増以上はレベル+1
				IF GETBIT(Ｒキャラ性別, 1)
					経験強度 ++
				ELSEIF GETBIT(Ｒキャラ性別, 0) && !GETBIT(Ｒキャラ経験済フラグ, 1) && Ｒキャラ性知識 >= 1
					経験強度 ++
				ENDIF
			CASE 1
				;Ｖ　男・処女はスキップ
				SIF !GETBIT(Ｒキャラ性別, 0)
					CONTINUE
				SIF !GETBIT(Ｒキャラ経験済フラグ, 1)
					CONTINUE
				;性知識が必要十分以上ならレベル+1
				SIF Ｒキャラ性知識 >= 2
					経験強度 ++
			CASE 2
				;Ａ　Ａ処女はスキップ
				SIF !GETBIT(Ｒキャラ経験済フラグ, 2)
					CONTINUE
				;ちんこを持たないならレベル-1
				SIF !GETBIT(Ｒキャラ性別, 1)
					経験強度 --
			CASE 3
				;Ｂ　男はレベル-1
				SIF !GETBIT(Ｒキャラ性別, 0)
					経験強度 --
			CASEELSE
				;Ｓ　常にレベル-2
				経験強度 -= 2
		ENDSELECT
		IF Ｒキャラ性知識 >= 4
			;百戦錬磨は全レベル+1
			経験強度 ++
		ELSEIF Ｒキャラ性知識 >= 3 && !(Ｒキャラ経験済フラグ & 3)
			;処女・童貞かつ有識者の場合、全レベル+1 (こじらせていろいろ試したイメージ)
			経験強度 ++
		ENDIF
		部位感覚Lv:LOCAL = ランダム能力レベル(経験強度)
		SELECTCASE 部位感覚Lv:LOCAL
			CASE 2
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "能力", GETNUM(ABL, "Ｃ感覚") + LOCAL, 2, 上書きしない)
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "経験", GETNUM(EXP, "Ｃ経験") + LOCAL, 100, 上書きしない)
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "経験", GETNUM(EXP, "Ｃ絶頂経験") + LOCAL, 20, 上書きしない)
				部位絶頂回数:LOCAL = 20
			CASE 1
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "能力", GETNUM(ABL, "Ｃ感覚") + LOCAL, 1, 上書きしない)
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "経験", GETNUM(EXP, "Ｃ経験") + LOCAL, 30, 上書きしない)
				CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "経験", GETNUM(EXP, "Ｃ絶頂経験") + LOCAL, 5, 上書きしない)
				部位絶頂回数:LOCAL = 5
			CASE 0
				;ＶとＡは非処女のみ通ってくるはずなので、経験を多少与える
				SIF GROUPMATCH(LOCAL, 1, 2)
					CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "経験", GETNUM(EXP, "Ｃ経験") + LOCAL, 5, 上書きしない)
		ENDSELECT
	NEXT
	;欲望：Ｃ～Ｓ感覚の最大値と同じ
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "能力", "欲望", MAX(0, MAXARRAY(部位感覚Lv)), 上書きしない)
	;絶頂経験：部位絶頂経験の合計と同じ
	CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "経験", "絶頂経験", SUMARRAY(部位絶頂回数), 上書きしない)
	;射精経験 (男性のみ)：Ｃ絶頂経験＋(0 ～ 残りの部位での絶頂経験の1/4)
	SIF GETBIT(Ｒキャラ性別, 1)
		CALL ランダムキャラデータ_SET_B(ＤＢ名, 管理番号, "経験", "射精経験", 部位絶頂回数:0 + RAND(SUMARRAY(部位絶頂回数, 1, 5) + 1) / 4, 上書きしない)

	;露出癖・奉仕精神・マゾ性感・サド性感
	FOR LOCAL, GETNUM(ABL, "露出癖"), (GETNUM(ABL, "サド性感") + 1)
		経験強度 = (Ｒキャラ性知識 + 3) / 2 - 2
		CALL ランダムキャラデータ_SET(ＤＢ名, 管理番号, "能力", LOCAL, ランダム能力レベル(経験強度), 上書きしない)
	NEXT
ENDIF

;プレゼント好み
STRDATA LOCALS
	DATA 飲食系
	DATA 肉
	DATA 魚
	DATA 野菜
	DATA お菓子
	DATA 珍味
	DATA 酒
	DATA 茶類
	DATA 芸術品
	DATA 宝石
	DATA 絵画
	DATA 彫刻
	DATA 詩
	DATA 美容系
	DATA アクセサリ
	DATA 化粧品
	DATA 実用品
	DATA 文房具
	DATA 健康器具
	DATA 日用品
	DATA 調理器具
	DATA 工具
	DATA 娯楽
	DATA 花
	DATA 本
	DATA おもちゃ
	DATA 機械
ENDDATA
SIF ランダムキャラデータ_GET_B(ＤＢ名, 管理番号, "素質", "飲兵衛") > 0 && STRFIND(LOCALS, "酒") < 0
	LOCALS += "_酒"
CALL ランダムキャラデータ_SETS_B(ＤＢ名, 管理番号, "CSTR", "プレゼント好み", LOCALS, 上書きしない)

;画像キー
LOCALS '= 汎用立ち絵取得処理(Ｒキャラ名前, Ｒキャラ種族, Ｒキャラ性別, Ｒキャラ体格, Ｒキャラバストサイズ, Ｒキャラ特殊種族)
CALL ランダムキャラデータ_SETS_B(ＤＢ名, 管理番号, "CSTR", "汎用立ち絵登録文字列", LOCALS, 上書きしない)

