
;データベースの情報をキャラに対して反映する共通処理
;どの情報を対象とするかは条件で与えるが、扱いを間違えるとキャラが壊れるので基本的にラッパ関数から使用する
@ランダムキャラデータ復元_実処理(ＤＢ名, キャラ番号, 条件, 順序 = "")
#DIMS ＤＢ名
#DIM キャラ番号
#DIMS 条件
#DIMS 順序
#DIM DYNAMIC ランダムキャラレコード, ランダムキャラレコード最大数
#DIM ランダムキャラレコード行数
#DIM ループ用

ランダムキャラレコード行数 = DT_SELECT(ＤＢ名, 条件, 順序, ランダムキャラレコード)
SIF ランダムキャラレコード行数 > ランダムキャラレコード最大数
	THROW ランダムキャラレコードの行数が多すぎます。開発者に連絡し、要素数の拡張を依頼してください。

FOR ループ用, 0, ランダムキャラレコード行数
	SELECTCASE DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "カテゴリ", 1)
		;管理用ヘッダ
		CASE "人物番号"
			;NOOP

		;CSVに記述できる内容
		CASE "番号"
			;違う番号のキャラの内容を反映するのは危険なので無視
		; CASE "助手"
		; 	;CSVには記述できるがキャラ追加せずに取り出す方法がないためいろいろ不都合があり無視 それにもともと未使用だし
		CASE "名前"
			NAME:キャラ番号 '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		CASE "呼び名"
			CALLNAME:キャラ番号 '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		CASE "あだ名"
			;未使用
			;N/I/C/K/N/A/M/E:キャラ番号 '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		CASE "主人の呼び方"
			;未使用
			;M/A/S/T/E/R/N/A/M/E:キャラ番号 '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		CASE "基礎"
			MAXBASE:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
			BASE:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "素質"
			TALENT:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "能力"
			ABL:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "経験"
			EXP:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "刻印"
			MARK:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		; CASE "相性"
		; 	;使用禁止に設定されている
		CASE "珠"
			JUEL:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "装着物"
			EQUIP:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "フラグ", "CFLAG"
			CFLAG:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "CSTR"
			CSTR:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)

		;追加の変数
		CASE "知識素質"
			知識素質:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "性癖素質"
			性癖素質:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "フレーバー素質"
			フレーバー素質:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "基礎BATTLE_STATE"
			基礎BATTLE_STATE:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) = DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "値_数値", 1)
		CASE "表示奥義"
			表示奥義:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
		CASE "陥落奥義"
			陥落奥義:キャラ番号:DT_CELL_GET(ＤＢ名, ランダムキャラレコード:ループ用, "項目", 1) '= DT_CELL_GETS(ＤＢ名, ランダムキャラレコード:ループ用, "値_文字列", 1)
	ENDSELECT
NEXT


;データベースの管理番号で指定した情報を全てキャラに対して反映する処理
;他のキャラ初期化処理は関数の外側で行う
@ランダムキャラデータ復元(ＤＢ名 = Ｒキャラ初期値記録, キャラ番号, 管理番号)
#DIMS ＤＢ名
#DIM キャラ番号
#DIM 管理番号

JUMP ランダムキャラデータ復元_実処理(ＤＢ名, キャラ番号, @"管理番号 = {管理番号}")
