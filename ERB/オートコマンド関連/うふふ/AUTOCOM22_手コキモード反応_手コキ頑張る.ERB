;------------------------------------------------------------------------------
;手コキ時、頑張って手コキをして通常より大きな快楽を与える
;------------------------------------------------------------------------------
@AUTOCOM22
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:AUTOCOM_相手番号保存:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:AUTOCOM_相手番号保存:体力 += 0
DOWNBASE:AUTOCOM_相手番号保存:気力 += 20

CALL SOURCE_CALC_露出_反感無し(AUTOCOM_相手番号保存, PLAYER, 50)
CALL SOURCE_CALC_接触_不快無し(AUTOCOM_相手番号保存, PLAYER, 150)
CALL SOURCE_CALC_奉仕(AUTOCOM_相手番号保存, PLAYER, 200)
LOCAL = ABL:AUTOCOM_相手番号保存:技巧 * 100
CALL SOURCE_CALC_快Ｃ(PLAYER, AUTOCOM_相手番号保存, LOCAL)

TIME_手コキ頑張る:AUTOCOM_相手番号保存:0 = TIME

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:AUTOCOM_相手番号保存:手淫経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(AUTOCOM_相手番号保存,PLAYER)
	EXP:AUTOCOM_相手番号保存:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:AUTOCOM_相手番号保存:恋慕 == 1
	EXP:AUTOCOM_相手番号保存:愛情経験 ++

RETURN 1

@AUTOCOM_ABLE22(キャラ番号)
#DIM キャラ番号
;手コキモードじゃない時はアウト
SIF MODE_確認("手コキ系", キャラ番号) == 0
	RETURN 0
;30分に１回のみ発動
SIF TIME - TIME_手コキ頑張る:キャラ番号:0 < 31
	RETURN 0
;無関心はアウト、恥じらいは性欲必要
SIF TALENT:キャラ番号:無関心
	RETURN 0
SIF TALENT:キャラ番号:羞恥心 > 0 && BASE:キャラ番号:性欲 < 8000
	RETURN 0
;性知識2必要
SIF 知識素質:キャラ番号:性知識 < 2
	RETURN 0
;技巧2必要
SIF ABL:キャラ番号:技巧 < 2
	RETURN 0
;基礎確率1/4、素質により変動
LOCAL = MAX(4 - (ABL:キャラ番号:奉仕精神 / 3) - TALENT:キャラ番号:恋慕 - TALENT:キャラ番号:性的興味, 1)
SIF RAND:LOCAL
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM22
PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は

SELECTCASE ABL:技巧
	CASE IS <= 2
		PRINTFORML ぎこちないながらも、%CALLNAME:PLAYER%の陰茎を手で擦り上げ、
		PRINTL 少しでも気持ちよくなってもらおうと頑張っている…
	CASE 3 TO 4
		PRINTFORML こなれた様子で%CALLNAME:PLAYER%の反応を見ながら指の動きを変え、
		PRINTFORML %CALLNAME:PLAYER%が感じる動きを探っている…
	CASE IS >= 5
		PRINTFORML 巧みな指の動きと的確なリズムで手を上下させ、
		PRINTFORML %CALLNAME:PLAYER%の弱点を的確に責め立てている…
ENDSELECT



