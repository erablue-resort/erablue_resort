;------------------------------------------------------------------------------
;合意済みの相手
;ムード・性欲一定以上で二人きりだとうふふのお誘いが来る
;------------------------------------------------------------------------------
@AUTOCOM8
;チラ見せと時間共通
DRAWLINE
PRINTFORML %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%に向けて、肌を重ねる誘いをしてきた……
PRINTL 誘いを受けますか？
PRINTBUTTON "[0] はい", 0
PRINTL
PRINTBUTTON "[1] いいえ", 1
PRINTL
TIME_うふふお誘い:AUTOCOM_相手番号保存:0 = TIME
BINPUT
IF RESULT == 0
	;許可
	RFLAG:コマンド結果受渡し変数 = 1
	IF TFLAG:調教モード == 3
		CALL 飲み会終了処理
	ENDIF
	CFLAG:AUTOCOM_相手番号保存:うふふ = 1
	CFLAG:MASTER:うふふ = 1

	;管理フラグを初期化
	FLAG:モード管理 = 0
ELSE
	;拒否
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF

RETURN 1

@AUTOCOM_ABLE8(対象キャラ)
#DIM 対象キャラ
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(対象キャラ), 0) == 0
	RETURN 0
;ターゲットが陥落済み
SIF 陥落チェック(対象キャラ) == 0
	RETURN 0
;初うふふ済
SIF 初体験日:対象キャラ:初うふふ == 0
	RETURN 0
;二人っきりのみ
SIF GET_TARGETNUM() > 1
	RETURN 0
;隠密一致
SIF CFLAG:MASTER:隠密 != CFLAG:対象キャラ:隠密
	RETURN 0
;馴れ合い強度2
SIF TCVAR:MASTER:馴れ合い強度 < 2
	RETURN 0
;うふふ中ではない
SIF CFLAG:対象キャラ:うふふ
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_うふふお誘い:対象キャラ:0 < 120
	RETURN 0
;性欲一定以上
SIF BASE:対象キャラ:性欲 < 700
	RETURN 0
;無関心があるとアウト
SIF TALENT:対象キャラ:無関心 > 0
	RETURN 0
;性知識-1必要
SIF 知識素質:対象キャラ:性知識 < -1
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM8

IF RFLAG:コマンド結果受渡し変数
	;許可時
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_相手番号保存%の誘いに快く応じた
	PRINTFORML %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%の肩に手を回し、抱き寄せた……
ELSE
	;拒否時
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_相手番号保存%の誘いを断った
	PRINTFORML %CALLNAME:AUTOCOM_相手番号保存%は残念そうにしている……
ENDIF



