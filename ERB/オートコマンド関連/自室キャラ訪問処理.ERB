@自室キャラ訪問処理
#DIM 訪問候補者, 1000
#DIM キャラ番号
#DIM 候補番号
#DIMS 場所候補, 10
#DIM 目的候補, 10
#DIM 候補数
#DIM 飲み会人数
;休憩時、キャラが自室に遊びに来る処理

;OPTION制御
SIF OPTION変数:休憩制御
	RETURN -1

;休憩時に誰か来るのは1/2の確率
SIF RAND:2
	RETURN -1

;候補者リスト作成
候補番号 = 0
FOR キャラ番号, 1, CHARANUM
	;もう今日は遊びに来た
	SIF TCVAR:キャラ番号:自室訪問フラグ
		CONTINUE
	;島にいる
	SIF CFLAG:キャラ番号:滞在期間 < 1
		CONTINUE
	;寝てる
	SIF CFLAG:キャラ番号:睡眠
		CONTINUE
	;何らかの原因で隠密
	SIF CFLAG:キャラ番号:隠密
		CONTINUE
	;デート中
	SIF CFLAG:キャラ番号:デート
		CONTINUE
	;体力限界
	SIF CFLAG:キャラ番号:体力限界
		CONTINUE
	;好感度が一定以上か、従業員のどちらか
	SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2 && !TALENT:キャラ番号:従業員
		CONTINUE
	訪問候補者:候補番号 = キャラ番号
	候補番号 += 1
	IF TALENT:キャラ番号:恋慕
		;恋慕は確率３倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ELSEIF TALENT:キャラ番号:セフレ
		;セフレは確率２倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ENDIF
NEXT

;一人も候補がいないなら戻る
IF 候補番号 == 0
	;-1を返すことで口上などの処理をスキップ
	RETURN -1
ENDIF

;訪問キャラ決定
AUTOCOM_実行番号保存 = 訪問候補者:(RAND:候補番号)
TCVAR:AUTOCOM_実行番号保存:自室訪問フラグ = 1
AUTOCOM_対象番号保存 = MASTER

;訪問目的決定
VARSET 目的候補
候補番号 = 0
;0:遊びに誘いに来る、1:自室で遊ぶ、2:仕事の用事で来る 3:うふふお誘い
;遊びは無条件
目的候補:(候補番号++) = 0
;自室は仲良いor同性
SIF CFLAG:AUTOCOM_実行番号保存:好感度レベル >= 関係閾値:3 || 同性愛性癖判定(AUTOCOM_実行番号保存, PLAYER)
	目的候補:(候補番号++) = 1
;仕事は従業員のみ
SIF TALENT:AUTOCOM_実行番号保存:従業員
	目的候補:(候補番号++) = 2
;飲み会は酒好きor飲酒経験一定以上（対応場所がない場合はなし）
IF 施設改造度:5:0 || 施設改造度:3:0 || 施設改造度:20:0
	IF TALENT:AUTOCOM_実行番号保存:飲酒拒否 < 1
		SIF TALENT:AUTOCOM_実行番号保存:飲兵衛 > 0 || (TALENT:AUTOCOM_実行番号保存:飲兵衛 == 0 && EXP:AUTOCOM_実行番号保存:飲酒経験 >= 50)
			目的候補:(候補番号++) = 4
	ENDIF
ENDIF

訪問目的 = 目的候補:(RAND:候補番号)
;陥落済み、かつ性欲が溜まっているとうふふお誘い。他すべてに優先する
IF 陥落チェック_性的(AUTOCOM_実行番号保存) && BASE:AUTOCOM_実行番号保存:性欲 > 1500 && 初体験日取得("初うふふ", AUTOCOM_実行番号保存, MASTER)
	訪問目的 = 3
ENDIF
;口上用に目的を保存
RFLAG:コマンド結果受渡し変数 = 訪問目的

;遊ぶ場所の決定
IF 訪問目的 == 0
	VARSET 場所候補
	候補番号 = 0
	場所候補:(候補番号++) = 遊戯室
	場所候補:(候補番号++) = プール
	SIF 施設改造度:3:0
		場所候補:(候補番号++) = キャンプ場
	SIF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0
		場所候補:(候補番号++) = 海
	RSTR:コマンド結果受渡し文字列 = %場所候補:(RAND:候補番号)%
ELSEIF 訪問目的 == 4
	VARSET 場所候補
	候補番号 = 0
	SIF 施設改造度:5:0
		場所候補:(候補番号++) = バーエリア
	SIF 施設改造度:3:0
		場所候補:(候補番号++) = バーベキューエリア
	SIF 施設改造度:20:0
		場所候補:(候補番号++) = %CALLNAME:MASTER%の自宅

	RSTR:コマンド結果受渡し文字列 = %場所候補:(RAND:候補番号)%
ENDIF

;訪問処理
$訪問ラベル
DRAWLINE
PRINTFORML %CALLNAME:PLAYER%が自室で休んでいると、%CALLNAME:AUTOCOM_実行番号保存%が訪ねて来た。
TCVAR:AUTOCOM_実行番号保存:挨拶フラグ = 1
SELECTCASE 訪問目的
	CASE 0
		PRINTFORML %RSTR:コマンド結果受渡し文字列%で遊ばないかという誘いのようだが……どうしようか？
		DRAWLINE
		PRINTBUTTON "[0] 遊びに行く", 0
		PRINTL
		PRINTBUTTON "[1] 遊びに行く（簡易版）", 1
		PRINTL
		PRINTBUTTON "[2] 断る", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 選択肢について説明", 100
	CASE 1
		PRINTFORML 部屋に遊びに来たとのことだが……どうしようか？
		DRAWLINE
		PRINTBUTTON "[0] 部屋に招く", 0
		PRINTL
		PRINTBUTTON "[1] 部屋に招く（簡易版）", 1
		PRINTL
		PRINTBUTTON "[2] 断る", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 選択肢について説明", 100
	CASE 2
		PRINTFORML 仕事で聞きたいことがあるらしい。
		WAIT
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の質問に答え、的確な指示を出した。
		PRINTFORMW %CALLNAME:AUTOCOM_実行番号保存%は納得した様子で仕事に戻っていった。

		CALL SOURCE_CALC_好感度要素_信頼度UP(AUTOCOM_実行番号保存, PLAYER, 250)
		TARGET = AUTOCOM_実行番号保存
		RETURN 0
	CASE 3
		IF 知識素質:AUTOCOM_実行番号保存:性知識 > 1
			IF 月算出() == "冬の月" && EXIST画像FILE("resources/" + CSTR:AUTOCOM_実行番号保存:画像フォルダ + "/短冊_裸リボン")
				PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%はあなたを喜ばせようと、自らの肉体を贈り物に見立ててリボンで装飾している…
				CALL PRINT_STRL(@"IMAGEPATH_%CSTR:AUTOCOM_実行番号保存:画像フォルダ%/短冊_裸リボン")
				RSTR:コマンド結果受渡し文字列 = 裸リボン
				;初視聴で衣装ゲット
				IF STRCOUNT(所持エロ衣装文字列, @"エロ/裸リボン_") == 0
					CALL エロ衣装登録処理("裸リボン")
				ENDIF
			ELSE
				PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%はあなたを喜ばせようと、情欲を煽る下着を着けて来ている…
				IF EXIST画像FILE("resources/" + CSTR:AUTOCOM_実行番号保存:画像フォルダ + "/短冊_セクシーランジェリー")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:AUTOCOM_実行番号保存:画像フォルダ%/短冊_セクシーランジェリー")
				ENDIF
				RSTR:コマンド結果受渡し文字列 = セクシーランジェリー
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%は艶っぽい目で%CALLNAME:PLAYER%を見つめている……
			RSTR:コマンド結果受渡し文字列 = 通常
		ENDIF
		PRINTL どうやら肌を重ねるお誘いのようだが、どうしようか？
		DRAWLINE
		PRINTBUTTON "[0] 部屋に招く", 0
		PRINTL
		PRINTBUTTON "[2] 断る", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 選択肢について説明", 100
	CASE 4
		PRINTFORML %RSTR:コマンド結果受渡し文字列%で飲み会しないかという誘いのようだが……どうしようか？
		DRAWLINE
		PRINTBUTTON "[0] 飲み会をする", 0
		PRINTL
		PRINTBUTTON "[1] 飲み会をする（簡易版）", 1
		PRINTL
		PRINTBUTTON "[2] 断る", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 選択肢について説明", 100
ENDSELECT

BINPUT
;口上用に選択肢を記録
RFLAG:コマンド結果受渡し変数２ = RESULT

SELECTCASE 訪問目的
	CASE 0
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に%RSTR:コマンド結果受渡し文字列%で遊ぶことにした。
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				SELECTCASE RSTR:コマンド結果受渡し文字列
					CASE "遊戯室"
						IF 施設改造度:5:0 == 1
							LOCAL = RAND(503, 505)
							訪問先場所番号 = LOCAL
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ELSE
							訪問先場所番号 = 5
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ENDIF
					CASE "キャンプ場"
						LOCAL = RAND(303, 307)
						訪問先場所番号 = LOCAL
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE "プール"
						IF 施設改造度:15:0 == 1
							LOCAL = RAND(1503, 1506)
							訪問先場所番号 = LOCAL
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ELSE
							訪問先場所番号 = 15
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ENDIF
					CASE "海"
						LOCAL = RAND(2, 3)
						CFLAG:PLAYER:現在マップ種別 = 3
						CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 3
						訪問先場所番号 = LOCAL
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
				ENDSELECT
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に%RSTR:コマンド結果受渡し文字列%で楽しく遊んだ。
				PRINTFORMW 満足した%CALLNAME:AUTOCOM_実行番号保存%と別れ、%CALLNAME:PLAYER%は自室に戻ってきた。

				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 予定があると言って、%CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の誘いを断った……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 遊びに行く：誘いに来たキャラクターと一緒に場所を移動します。
				PRINTL 　　　　　　その後の行動について制限はありません。
				PRINTL
				PRINT 遊びに行く（簡易版）：
				PRINTL 簡単な描写と一定の好感度変化が実行されます。
				PRINTL 　　　　　　　　　　　場所移動などは起こらず、キャラクターは訪問前にいた場所に戻ります。
				PRINTL 
				PRINTL 断る：断ります。キャラクターは訪問前にいた場所に戻り、好感度変化は起こりません。
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 1
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に自室で遊ぶことにした。
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に自室で楽しく遊んだ。
				PRINTFORMW 満足した%CALLNAME:AUTOCOM_実行番号保存%はリゾートへと戻っていった。

				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 予定があると言って、%CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の誘いを断った……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 部屋に招く：訪れたキャラクターを自室に招き入れます。
				PRINTL 　　　　　　この時、雰囲気に補正が掛かり一部のコマンドの発生条件が緩くなります。
				PRINTL 　　　　　　その後の行動について制限はありません。
				PRINTL
				PRINT 部屋に招く（簡易版）：
				PRINTL 簡単な描写と一定の好感度変化が実行されます。
				PRINTL 　　　　　　　　　　　場所移動などは起こらず、キャラクターは訪問前にいた場所に戻ります。
				PRINTL 
				PRINTL 断る：断ります。キャラクターは訪問前にいた場所に戻り、好感度変化は起こりません。
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 2
		;選択肢なし
	CASE 3
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%を部屋に招き入れ、優しく抱きしめた。
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				TARGET:(GET_TARGETNUM() + 1) = AUTOCOM_実行番号保存
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
				IF RSTR:コマンド結果受渡し文字列 != "通常"
					SIF CSTR:AUTOCOM_実行番号保存:コスプレ前の服 == "" && CSTR:AUTOCOM_実行番号保存:表示グラフィックカテゴリ != ""
						CSTR:AUTOCOM_実行番号保存:コスプレ前の服 '= CSTR:AUTOCOM_実行番号保存:表示グラフィックカテゴリ
					LOCALS '= CSTR:AUTOCOM_実行番号保存:脱ぐ前の服
					CALL CLOTHES_CHANGE(AUTOCOM_実行番号保存, RSTR:コマンド結果受渡し文字列)
					CSTR:AUTOCOM_実行番号保存:脱ぐ前の服 '= LOCALS
				ENDIF
				;3Pモード管理フラグを初期化
				FLAG:モード管理 = 0
				CALL うふふ開始キャラ処理(AUTOCOM_実行番号保存, "キャラからお誘い")
				CFLAG:MASTER:うふふ = 1
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
			CASE 2
				PRINTFORMW 予定があると言って、%CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の誘いを断った……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 部屋に招く：訪れたキャラクターを自室に招き入れ、押し倒します。
				PRINTL
				PRINTL 断る：断ります。キャラクターは訪問前にいた場所に戻り、好感度変化は起こりません。
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 4
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に%RSTR:コマンド結果受渡し文字列%で飲み会をすることにした。
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				SELECTCASE RSTR:コマンド結果受渡し文字列
					CASE "バーエリア"
						訪問先場所番号 = 503
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE "バーベキューエリア"
						訪問先場所番号 = 304
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE @"%CALLNAME:MASTER%の自宅"
						訪問先場所番号 = 2005
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
				ENDSELECT
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
				
				;先にここでモードを変えておかないとTARGETが上手く格納されない
				TFLAG:調教モード = 3
				CFLAG:MASTER:飲み会 = 1
				あなた追跡キャラ = 0
				CFLAG:AUTOCOM_実行番号保存:飲み会 = 1
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				オートコマンド_飲み会開始フラグ = 1

			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%と一緒に%RSTR:コマンド結果受渡し文字列%で楽しく飲み会をした。
				PRINTFORMW 満足した%CALLNAME:AUTOCOM_実行番号保存%と別れ、%CALLNAME:PLAYER%は自室に戻ってきた。

				
				IF TALENT:AUTOCOM_実行番号保存:酒に強い == 2
					BASE:AUTOCOM_実行番号保存:酩酊 = 100
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == 1
					BASE:AUTOCOM_実行番号保存:酩酊 = 300
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == 0
					BASE:AUTOCOM_実行番号保存:酩酊 = 600
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == -1
					BASE:AUTOCOM_実行番号保存:酩酊 = 900
				ENDIF
				IF TALENT:PLAYER:酒に強い == 2
					BASE:PLAYER:酩酊 = 100
				ELSEIF TALENT:PLAYER:酒に強い == 1
					BASE:PLAYER:酩酊 = 300
				ELSEIF TALENT:PLAYER:酒に強い == 0
					BASE:PLAYER:酩酊 = 600
				ELSEIF TALENT:PLAYER:酒に強い == -1
					BASE:PLAYER:酩酊 = 900
				ENDIF
				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 予定があると言って、%CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の誘いを断った……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 飲み会をする：誘いに来たキャラクターと一緒に場所を移動し、飲み会モードに移行します。
				PRINTL 　　　　　　　その後の行動について制限はありません。
				PRINTL
				PRINT 飲み会をする（簡易版）：
				PRINTL 簡単な描写と一定の好感度変化が実行されます。
				PRINTL 　　　　　　　　　　　　場所移動などは起こらず、キャラクターは訪問前にいた場所に戻ります。
				PRINTL 
				PRINTL 断る：断ります。キャラクターは訪問前にいた場所に戻り、好感度変化は起こりません。
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
ENDSELECT

