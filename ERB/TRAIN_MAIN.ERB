;TRAINの骨格

@TRAIN_MAIN_LOOP
FLAG:一日終了 = 0

IF TFLAG:オートコマンドフラグ
ELSE
	CALL 画像描画終了
	CALL SHOW_STATUS
	CALL SHOW_USERCOM
	RESULTS = 
	CALL 追跡処理
	IF RESULT
		CALL R系列フラグリセット
		RESTART
	ENDIF
	RESULT = -1
	SIF NEXTCOM == -1 && RFLAG:追跡フラグ == 0
		INPUTANY
	
	IF STRCOUNT(RESULTS, "サイド領域") > 0
		RFLAG:サイドボタン操作 = 1
		CALL サイド領域_実行処理
	ELSEIF RESULTS != ""
		RSTR:SELECTCOM_S受け渡し = %RESULTS%
		CALL USERCOM
	ELSE
		CALL USERCOM
	ENDIF
	
	;コマンドが呼ばれていない、実行不能
	IF RESULT
		CALL R系列フラグリセット
		RESTART
	ENDIF
ENDIF

CALL SOURCE_CHECK
;コマンド中に戻る場合-1が帰ってくるのでリスタート
IF RESULT == -1
	CALL R系列フラグリセット
	RESTART
ENDIF

IF RFLAG:入替えコマンドフラグ == 1
	SWAP TARGET, PLAYER
	RFLAG:入替えコマンドフラグ = 0
ENDIF

CALLFORM EVENTCOMEND0_%SAVESTR:ゲームフェイズ管理%
;TRAIN終了フラグ
IF FLAG:一日終了
	RETURN 0
ELSE
	RESTART
ENDIF


@TRAIN_MAIN
;変数初期化
CALL TRAIN_VAR_RESET
;TRAIN開始前処理
CALL EVENTTRAIN0

;メインループ
CALL TRAIN_MAIN_LOOP

;TRAIN終了処理
CALL EVENTEND0

;これが終わるとABLUPに移行し、EVENTTURNENDに続く

@TRAIN_VAR_RESET
#LOCALSIZE 2
VARSET PREVCOM, 0
VARSET コマンド履歴, -2
VARSET 派生コマンド履歴, 
VARSET NEXTCOM, -1
VARSET SELECTCOM, 0
VARSET TFLAG, 0
VARSET TSTR, ""
VARSET UP, 0
VARSET DOWN, 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	VARSET GOTJUEL:LOCAL:0, 0
	VARSET PALAM:LOCAL:0, 0
	VARSET TEQUIP:LOCAL:0, 0
	VARSET EX:LOCAL:0, 0
	VARSET SOURCE:LOCAL:0, 0

	VARSET CUP:LOCAL:0, 0
	VARSET CDOWN:LOCAL:0, 0
	VARSET DOWNBASE:LOCAL:0, 0
	VARSET TCVAR:LOCAL:0, 0

	CFLAG:LOCAL:うふふ = 0
	CFLAG:LOCAL:飲み会 = 0
	CFLAG:LOCAL:睡眠 = 1
	CFLAG:LOCAL:同行 = 0
	CFLAG:LOCAL:一緒に遊ぶ = 0

	FOR LOCAL:1,0,200
		TCVAR:LOCAL:(400 + LOCAL:1) = EXP:LOCAL:(LOCAL:1)
	NEXT

	;服装再セット
	CALL CLOTHES_RESET(LOCAL)
	CALL CLOTHES_RESET_TRAIN(LOCAL)
NEXT


@TRAIN_イベントうふふ_LOOP
FLAG:イベントうふふ終了 = 0
FLAG:イベントうふふ中フラグ = 1
TIME = FLAG:イベントTIME記録

IF TFLAG:オートコマンドフラグ
ELSE
	CALL 画像描画終了
	CALL SHOW_STATUS
	CALL SHOW_USERCOM
	RESULT = -1
	RESULTS = 
	SIF NEXTCOM == -1
		INPUTANY
	
	IF STRCOUNT(RESULTS, "サイド領域") > 0
		RFLAG:サイドボタン操作 = 1
		CALL サイド領域_実行処理
	ELSEIF RESULTS != ""
		RSTR:SELECTCOM_S受け渡し = %RESULTS%
		CALL USERCOM
	ELSE
		CALL USERCOM
	ENDIF
	TIME = FLAG:イベントTIME記録
	SIF FLAG:イベントうふふ終了
		GOTO イベントうふふ終了
	
	;コマンドが呼ばれていない、実行不能
	IF RESULT
		CALL R系列フラグリセット
		RESTART
	ENDIF
ENDIF

CALL SOURCE_CHECK
TIME = FLAG:イベントTIME記録
;コマンド中に戻る場合-1が帰ってくるのでリスタート
IF RESULT == -1
	CALL R系列フラグリセット
	RESTART
ENDIF
CALLFORM EVENTCOMEND0_%SAVESTR:ゲームフェイズ管理%

$イベントうふふ終了
;TRAIN終了フラグ
IF FLAG:イベントうふふ終了
	FLAG:イベントTIME記録 = 0
	FLAG:イベントうふふ中フラグ = 0
	RETURN 0
ELSE
	RESTART
ENDIF
