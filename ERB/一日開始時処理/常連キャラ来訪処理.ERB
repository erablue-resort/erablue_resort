@常連キャラ来訪処理
#DIM 常連来訪可能枠
#DIM 常連来訪人数
#DIM 候補人数
#DIM 常連リスト, 500
#DIM 来訪候補, 10
#DIM 対象キャラ
#DIM 同行キャラ
#DIM 同行確率
#DIM 同行判定値
#DIM リストカウント
#DIM 入替先
#DIM L_CNT
#DIM 同行L_CNT
#DIM 滞在期間基本日数
#DIM 滞在期間補正日数
#DIM CONST 滞在期間下限値 = 1 + 1 ;[日] 来訪処理→滞在期間減算の順で処理するので+1日する
VARSET 常連リスト
VARSET 来訪候補

;広告を打ってなくても、滞在枠に空きがあるなら常連が入れる
;まず常連キャラが入れる枠を計算
CALL ユニーク客人数チェック()
常連来訪可能枠 = 滞在キャラ上限 - 滞在キャラ数

;入れないなら帰る
SIF 常連来訪可能枠 < 1
	RETURN 0

;常連来訪数を算出
SELECTCASE 常連来訪可能枠
	CASE IS > 10
		;8~10人
		常連来訪人数 = RAND:2 + 8
	CASE IS > 5
		;空きの半分プラマイ1
		常連来訪人数 = (常連来訪可能枠 / 2) - 1 + RAND:2
	CASEELSE
		;1人だけ
		常連来訪人数 = 1
ENDSELECT

;来訪候補を登録
リストカウント = 0
FOR 対象キャラ, 1, CHARANUM
	;滞在状態ではない、かつ常連キャラをカウント
	SIF TALENT:対象キャラ:常連 == 0
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(対象キャラ)
		CONTINUE
	IF CFLAG:対象キャラ:滞在期間 == -1
		常連リスト:リストカウント = 対象キャラ
		リストカウント += 1
	ENDIF
NEXT
候補人数 = リストカウント

;候補人数が０の場合はそのまま終わる
SIF 候補人数 == 0
	RETURN 0

;候補人数が常連来訪人数より少ない場合は全員登録
IF 候補人数 <= 常連来訪人数
	FOR リストカウント, 0, 候補人数
		来訪候補:リストカウント = 常連リスト:リストカウント
	NEXT
ELSE
	リストカウント = 0
	L_CNT = 候補人数 - 1
	;常連リスト配列をシャッフル
	WHILE L_CNT > 0
		入替先 = RAND:L_CNT
		SWAP 常連リスト:L_CNT, 常連リスト:入替先
		L_CNT -= 1
		リストカウント += 1
		;必要分集まったらおしまい
		SIF リストカウント >= 常連来訪人数
			BREAK
	WEND
	;配列の後ろの方にランダムシャッフルされた候補が集まってるはずなので常連来訪人数分取得
	FOR リストカウント, 0, 常連来訪人数
		来訪候補:リストカウント = 常連リスト:(候補人数 - 1 - リストカウント)
	NEXT
ENDIF


FOR L_CNT, 0, 10
	対象キャラ = 来訪候補:L_CNT
	SIF 対象キャラ == 0
		BREAK
	;同行キャラ処理の都合で招待キャラが被る可能性があるため、既に滞在期間があるキャラはスルー
	SIF CFLAG:対象キャラ:滞在期間 > 0
		CONTINUE
	;滞在期間の算出
	CALL 滞在期間基本日数算出(対象キャラ)
	滞在期間基本日数 = RESULT
	;常連キャラの来訪時は滞在期間の補正なし
	;CALL 滞在期間補正日数算出(対象キャラ)
	滞在期間補正日数 = 0
	CFLAG:対象キャラ:滞在期間 = MAX(滞在期間基本日数 + 滞在期間補正日数, 滞在期間下限値)
	CFLAG:対象キャラ:リゾート来訪回数 += 1
	PRINTFORML 常連の%CALLNAME:対象キャラ%がリゾート地を訪れたようだ。
	BASE:対象キャラ:体力 = MAXBASE:対象キャラ:体力
	常連来訪可能枠 -= 1
	CALL 滞在者部屋割り配列挿入処理(対象キャラ)
	CALL 招待時妊娠メッセージ(対象キャラ)
	IF 初体験日:対象キャラ:リゾート初来訪 == 0
		初体験日:対象キャラ:リゾート初来訪 = DAY
		CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"初めてリゾートを訪れた","日常")
	ENDIF
	;こっから同行キャラ判定
	IF 常連来訪可能枠 > 0
		FOR 同行L_CNT, 0, 100
			同行キャラ = 同行候補キャラ番号:対象キャラ:同行L_CNT
			同行確率   = 同行候補キャラ確率:対象キャラ:同行L_CNT
			SIF 常連来訪可能枠 <= 0
				BREAK
			SIF 同行キャラ < 1
				BREAK
			SIF CFLAG:同行キャラ:招待不可フラグ > 0
				CONTINUE
			;オプションによる制御
			SIF 性別招待制御_実処理(同行キャラ)
				CONTINUE
			同行判定値 = RAND:100
			IF 同行確率 > 同行判定値
				;同行キャラ処理の都合で招待キャラが被る可能性があるため、既に滞在期間があるキャラはスルー
				SIF CFLAG:同行キャラ:滞在期間 > 0
					CONTINUE
				;常連キャラの来訪時は滞在期間の補正なし
				;CALL 滞在期間補正日数算出(対象キャラ)
				滞在期間補正日数 = 0
				CFLAG:同行キャラ:滞在期間 = MAX(滞在期間基本日数 + 滞在期間補正日数, 滞在期間下限値)
				CFLAG:同行キャラ:リゾート来訪回数 += 1
				PRINTFORML 　%CALLNAME:対象キャラ%と一緒に%CALLNAME:同行キャラ%がリゾート地を訪れたようだ。
				BASE:同行キャラ:体力 = MAXBASE:同行キャラ:体力
				IF 初体験日:同行キャラ:リゾート初来訪 == 0
					初体験日:同行キャラ:リゾート初来訪 = DAY
					CALL 履歴データベース登録(CFLAG:同行キャラ:人物番号, @"初めてリゾートを訪れた","日常")
				ENDIF
				CALL 滞在者部屋割り配列挿入処理(同行キャラ)
				CALL 招待時妊娠メッセージ(同行キャラ)
				常連来訪可能枠 -= 1
			ENDIF
		NEXT
	ENDIF
NEXT
CALL 招待時妊娠メッセージ(0,1)

CALL ユニーク客人数チェック()
