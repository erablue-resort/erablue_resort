@短冊グラ表示用文字列関数(ARG, g_loop = 0, size = 1125)
#FUNCTIONS
#DIM g_loop
#DIM size
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔

;リソース解放
SPRITEDISPOSE @"短冊グラ{g_loop}"
GDISPOSE 3000 + g_loop

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

グラ表示用一時文字列_顔 = %グラ表示用文字列算出(ARG, "顔_")%
グラ表示用一時文字列_短冊 = %グラ表示用文字列算出(ARG, "短冊_")%

IF グラ表示用一時文字列_短冊 == ""
	RESULTS:(2 + g_loop) = 短冊_デフォルト.png
	GCREATEFROMFILE 3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/短冊_デフォルト.png"
ELSE
	RESULTS:(2 + g_loop) = %グラ表示用一時文字列_短冊%.png
	GCREATEFROMFILE 3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%.png"
ENDIF
IF RESULT == 0
	;該当画像が無い場合
	IF グラ表示用一時文字列_短冊 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す
		RESULTS:(2 + g_loop) = Noimage_短冊.png
		RETURNF @"<img src='Noimage_短冊' width='{size}' height='{size * 2}'>"
	ELSEIF GCREATEFROMFILE(3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%.png")
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
			LOCALS '= @"<img src='短冊グラ{g_loop}' width='{size}' height='{size * 2}'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
		RESULTS:(2 + g_loop) = %グラ表示用一時文字列_顔%.png
	ELSEIF GCREATEFROMFILE(3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/短冊_デフォルト.png")
		;変更後画像も設定中の顔グラも無い場合はデフォルト短冊を表示させる
		SIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
			LOCALS '= @"<img src='短冊グラ{g_loop}' width='{size}' height='{size}'>"
		RESULTS:(2 + g_loop) = 短冊_デフォルト.png
	ELSE
		IF CSTR:ARG:汎用立ち絵登録文字列 == ""
			;どれもない場合はNoimage画像で返す
			RESULTS:(2 + g_loop) = Noimage_短冊.png
			RETURNF @"<img src='Noimage_短冊' width='{size}' height='{size * 2}'>"
		ELSEIF GCREATEFROMFILE(3000 + g_loop, @"汎用グラフィック/短冊_%CSTR:ARG:汎用立ち絵登録文字列%.png")
			;汎用立ち絵登録されてる場合はそれを表示させる
			SIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
				LOCALS '= @"<img src='短冊グラ{g_loop}' width='{size}' height='{size * 2}'>"
			RESULTS:(2 + g_loop) = 短冊_%CSTR:ARG:汎用立ち絵登録文字列%.png
		ENDIF
	ENDIF
ELSEIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
	;該当画像がある場合、そのまま作成
	LOCALS '= @"<img src='短冊グラ{g_loop}' width='{size}' height='{size * 2}'>"
ELSE
ENDIF

SIF 差分エフェクト != ""
	LOCAL = スプライト合成(@"短冊グラ{g_loop}", 3000 + g_loop, 差分エフェクト)
;	GDRAWSPRITE 3000 + g_loop, 差分エフェクト, 0, 0, SPRITEWIDTH(@"短冊グラ{g_loop}"), SPRITEHEIGHT(@"短冊グラ{g_loop}")
SIF  差分エフェクト:1 != ""
	LOCAL = スプライト合成(@"短冊グラ{g_loop}", 3000 + g_loop, 差分エフェクト:1)
;	GDRAWSPRITE 3000 + g_loop, 差分エフェクト:1, 0, 0, SPRITEWIDTH(@"短冊グラ{g_loop}"), SPRITEHEIGHT(@"短冊グラ{g_loop}")

立ち絵リソース記録 = 短冊グラ{g_loop}
RETURNF LOCALS
