;-------------------------------------------------
;地の文で表示するオノマトペ関連
;-------------------------------------------------
@TEXT_ONOMATOPE(オノマトペタイプ)
#DIMS オノマトペタイプ

;基本的にモードの名前で分岐する
SELECTCASE オノマトペタイプ
	CASE ""
		IF RAND:3
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("ちゅぱ音短", 0, 0)%</font>", 1
		ENDIF
		;指入れ系
	CASE ""
		IF RAND:3
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("くちゅ音短", 0, 0)%</font>", 1
		ENDIF
	CASE "Ｖ挿入系"
		IF RAND:3
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("挿入音短", 0, 0)%</font>", 1
		ELSE
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("くちゅ音長", 0, 0)%</font>", 1
		ENDIF
		;挿入系(触手)
	CASE ""
		IF RAND:3
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("フェラ音長", 0, 0)%</font>", 1
		ELSE
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("くちゅ音長", 0, 0)%</font>", 1
		ENDIF
	CASE "フェラチオ系"
		IF RAND:3
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("フェラ音長", 0, 0)%</font>", 1
		ELSE
			HTML_PRINT @"<font color='#%カラーパレット_HTML("えっちな色")%'>%ONOMATOPE_PRESET("くちゅ音長", 0, 0)%</font>", 1
		ENDIF
	CASEELSE

ENDSELECT

;─────────────────────────────────────── 
;オノマトペ生成用のプリセット
; 第1引数は呼出す際のシチュエーション
; 第2引数 平仮名 = 0, カナ = 1
; 第3引数 ハート無し = 1　ハートマークをつけたくない時に
; @GEN_ONOMATOPE()のメモ
; 第2 ~ 6引数は0~10で指定
; 濁音、促音、付帯音、端音、テンポ
;─────────────────────────────────────── 
@ONOMATOPE_PRESET(種類,カナ,ハート無し)
#FUNCTIONS
#DIMS 種類
#DIM カナ
#DIM ハート無し


#DIMS 表示テキスト
表示テキスト '= ""

SELECTCASE 種類
	CASE "ちゅぱ音短"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("ちゅ/ちゅっ/ちゅる")%/%TEXTR("ちゅぷ/ちゅ/ちゅぷ")%/%TEXTR("ちゅ/ちゅる/ちゅぷ")%",2,5,1,1,0,カナ,ハート無し)
	CASE "くちゅ音短"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("くちゅ/ちゅっ/ぬちゅ")%/%TEXTR("くちゅ/ぬちゃ/ちゅぷ")%/%TEXTR("ちゅっ/じゅっ/ぬちゅ")%",5,8,1,1,0,カナ,ハート無し)
	CASE "挿入音短"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("ぱんっ/ぱん/ぱちゅ")%/%TEXTR("くちゅ/ぬちゃ/ちゅぷ/ぱん/ぱちゅ/じゅぶ！")%/%TEXTR("ぱちゅ/ぱんっ/ぱん")%",5,5,0,0,0,カナ, ハート無し)
		IF RAND:2
			表示テキスト '= ARRANGE_ONOMATOPE(表示テキスト,"文中") ;音節の末尾を整形
			表示テキスト += GEN_ONOMATOPE(@"%TEXTR("ぱんっ/ぱん/ぱちゅ")%/%TEXTR("くちゅ/ぬちゃ/ちゅぷ/ぱん/ぱちゅ/じゅぶ！")%/%TEXTR("ぱちゅ/ぱんっ/ぱん")%",5,5,0,0,0,カナ, ハート無し)
		ENDIF
	CASE "くちゅ音長"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("くちゅ/ちゅっ/ぬちゅ")%/%TEXTR("くちゅ/ぬちゃ/ちゅぷ")%/%TEXTR("ちゅっ/じゅっ/ぬちゅ")%",5,8,1,1,0,カナ,ハート無し)
		表示テキスト '= ARRANGE_ONOMATOPE(表示テキスト,"文中") ;音節の末尾を整形
		表示テキスト += GEN_ONOMATOPE(@"%TEXTR("くちゅ/ちゅっ/ぬちゅ")%/%TEXTR("くちゅ/ぬちゃ/ちゅぷ")%/%TEXTR("ちゅく/くちゅ/ぬちゅ")%",5,3,1,5,0,カナ,ハート無し)
	CASE "フェラ音長"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("くちゅ/ちゅっ/ぬちゅ")%/%TEXTR("じゅる/じゅぽ/ちゅる")%/%TEXTR("じゅぷ/ぢゅる/ぐちゅ")%",5,8,1,1,0,カナ,ハート無し)
		表示テキスト '= ARRANGE_ONOMATOPE(表示テキスト,"文中") ;音節の末尾を整形
		表示テキスト += GEN_ONOMATOPE(@"%TEXTR("ぢゅ/ぢゅっ/ぬちゅ")%/%TEXTR("ぢゅる/ぬちゃ/ちゅぷ")%/%TEXTR("ちゅく/じゅる/じゅる")%",5,3,1,5,0,カナ,ハート無し)
	CASE "足コキ_潤"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("ぐじゅ/じゅっ/ぬちゅ")%/%TEXTR("しゅる/じゅる！/ぬちゅ")%/%TEXTR("ちゅる/じゅっ/ぬちゅ")%",5,8,5,1,1,カナ,ハート無し)
		表示テキスト '= ARRANGE_ONOMATOPE(表示テキスト,"文中") ;音節の末尾を整形
		表示テキスト += GEN_ONOMATOPE(@"%TEXTR("ぐじゅ/じゅっ❤/ぬちゅ")%/%TEXTR("ぐじゅ！/じゅる❤/ぬちゅ")%/%TEXTR("ちゅる❤/じゅっ/ぬちゅ❤")%",5,3,5,5,0,カナ,ハート無し)
	CASE "髪コキ_潤"
		表示テキスト '= GEN_ONOMATOPE(@"%TEXTR("しゅる/しゅっ/しゅる")%/%TEXTR("しゅる！/じゅる❤/ぬちゅ/")%/%TEXTR("しゅっ/じゅっ/しゅる❤/")%",2,5,7,1,1,カナ,ハート無し)
		表示テキスト '= ARRANGE_ONOMATOPE(表示テキスト,"文中") ;音節の末尾を整形
		表示テキスト += GEN_ONOMATOPE(@"%TEXTR("しゅる/じゅっ❤/ぬちゅ")%/%TEXTR("じゅる❤/しゅる/ぬちゅ")%/%TEXTR("しゅる❤/じゅっ/ぬちゅ❤")%",2,5,7,1,1,カナ,ハート無し)
	CASEELSE
ENDSELECT

RETURNF 表示テキスト

;─────────────────────────────────────── 
;機械的に生成した文字列を接続する際、文として自然になるように整形して返す
; 第二引数は"文中"か"文末"を指定(未実装)
;─────────────────────────────────────── 
@ARRANGE_ONOMATOPE(文字列,位置)
#FUNCTIONS 
#DIMS 文字列
#DIMS 位置
;末尾が以下の文字に該当する場合は処理しない
#DIMS CONST 区切り文字 = "…、。！❤♥♪"

STRLENFORMU %文字列%
LOCAL:0 = RESULT
IF STRFINDU(区切り文字, SUBSTRINGU(文字列, LOCAL:0 - 1, LOCAL:0) ) == -1
	文字列 '= 文字列 + "、"
ENDIF

RETURNF 文字列
;─────────────────────────────────────── 
;与えられた4文字までの文字列をもとにオノマトペを生成し文字列を返す
; ・文字列は平仮名で指定
; ・文字列は"/"で区切り3候補まで指定可
; ・４文字目は記号指定にのみ使用(！❤♥♪)
; ・第2 ~ 6引数は0~10で指定
; ・濁音頻度	1文字目が清音の場合どの程度濁音に置換するか
; ・促音頻度	"っ"をどの程度付加するか
; ・付帯音頻度	3文字が"る"の時に音を足す。びゅるるっ
; ・端音頻度	3文字目をどの程度の頻度で出力するか
; ・テンポ　通常 = 0,2個以上の3点リーダを付加しない = 1
; ・カナ 平仮名 = 0 カタカナ = 1
; ・ハート無し = 1 でハートマークを追加しない
;─────────────────────────────────────── 
@GEN_ONOMATOPE(文字列, 濁音頻度, 促音頻度, 付帯音頻度, 端音頻度, テンポ, カナ, ハート無し = 0)
#FUNCTIONS
#DIMS 文字列
#DIM テンポ
#DIM カナ
#DIM ハート無し

#DIMS 表示テキスト, 2
#DIM 表示テキスト文字数
#DIMS 基本音, 3 
#DIM 候補数
#DIMS 選択基本音
#DIM 濁音頻度
#DIM 促音頻度
#DIM 付帯音頻度
#DIM 端音頻度

#DIM 子音
#DIM 促音フラグ
#DIM 濁音フラグ
#DIM 端音フラグ

#DIMS CHAR, 4
#DIM  CNT_A
#DIM  CNT_B

#DIMS CONST 濁音変換候補 = "くしちぴ"
#DIMS CONST 濁音変換対応文字列 = "ぐじぢび"
#DIMS CONST 拗音表 = "ゃゅょ"
#DIMS CONST 半濁音表, 3 = "ぱ","ぷ","ぽ"
#DIMS CONST 付帯音表, 3 = "る","るっ","るぅ"
#DIMS CONST 記号表 = "！❤♥♪"

候補数 = 0
VARSET 表示テキスト, ""

;生成のベースとする基本音を配列に格納
SPLIT 文字列, "/", 基本音
FOR CNT_A, 0, 3
	IF 基本音:CNT_A == ""
		ARRAYREMOVE 基本音, CNT_A, 1
	ENDIF
NEXT
FOR CNT_A, 0, 3
	SIF 基本音:CNT_A != ""
		候補数 += 1
NEXT


FOR CNT_A, 0, 候補数
	VARSET CHAR, ""
	選択基本音 '= 基本音:CNT_A 
	促音フラグ = 0
	濁音フラグ = 0
	端音フラグ = 0

	FOR CNT_B, 0, STRLENSU(選択基本音)
		CHAR:(CNT_B) '= SUBSTRINGU(選択基本音, CNT_B, 1) 
	NEXT

	;1文字目の処理
	;特定の文字のみ確率で濁音に変換する
	LOCAL = STRFINDU(濁音変換候補, CHAR:0) 
	IF RAND:10 < 濁音頻度 && LOCAL != -1
		CHAR:0 '= CHARATU(濁音変換対応文字列,LOCAL)
		SIF STRFINDU(濁音変換対応文字列, CHAR:0) != -1
			濁音フラグ = 1
	ENDIF
	表示テキスト '= 表示テキスト + CHAR:0

	
	表示テキスト '= 表示テキスト + CHAR:1
	;基本音の2音目が拗音（ゃ、ゅ、ょ）以外の場合;確率で2音目の後に促音（っ）を付加
	IF STRFINDU(拗音表, CHAR:1) != -1 && FINDELEMENT(付帯音表, CHAR:1) != -1 
		IF RAND:10 < 促音頻度
			表示テキスト '= 表示テキスト + "っ"
			促音フラグ = 1
		ENDIF
	ELSE
		;例えば「ちゅぱ」と「くちゅ」では3文字目の処理を変えないと不自然になるのでこのフラグで制御する
		端音フラグ = 1
	ENDIF

	;３音目の処理
	;確率で３文字目を消去
	;子音をランダムでずらす。ex.ちゅぷ -> ちゅぱ or ちゅぽ
	{
		SELECTCASE IFRAND(
			"99",1,
			"D99,1", 端音フラグ == 1,
			"D99,1",RAND:10 < 端音頻度 && 濁音フラグ != 1 && 端音フラグ != 1
			)
	}
		CASE 1
			IF FINDELEMENT(半濁音表, CHAR:2) != -1 && CHAR:2 != ""
				子音 = FINDELEMENT(半濁音表, CHAR:2)
				IF 子音 != -1 && RAND:3 
					IF 子音 == 1
						IF RAND:3 
							子音 += 1
						ELSEIF RAND:2
							子音 -= 1
						ENDIF
					ELSEIF 子音 == 0
						SIF RAND:3 
							子音 += 1
					ELSEIF 子音 != 0
						SIF RAND:3 
							子音 -= 1
					ENDIF
				ENDIF
				CHAR:2 '= 半濁音表:子音 
			ENDIF
			表示テキスト '= 表示テキスト + CHAR:2
		CASEELSE
	ENDSELECT 

	;3文字目以降に付帯する音の処理(る、るる)
	IF FINDELEMENT(付帯音表, CHAR:2) != -1 && 促音フラグ == 0  ;ちゅっるるのような音は不自然なので出現しないように
		IF RAND:10 < 付帯音頻度
			表示テキスト '= 表示テキスト + TEXTR(@"%付帯音表:0%/%付帯音表:1%/%付帯音表:2%")
		ENDIF
	ENDIF
	;確率で末尾に促音を負荷
	IF RAND:10 < 促音頻度 && 促音フラグ == 0
		表示テキスト '= 表示テキスト + TEXTR("っ/っ/っ/っ/っ！/っっ/っっ/っっ――！")
		促音フラグ = 1
	ENDIF

	;4文字目が存在しない場合、句点か三点リーダを付加
	IF CHAR:3 == ""
		;テンポに応じて三点リーダの量を調整する
		IF テンポ == 0;通常
			表示テキスト '= 表示テキスト + TEXTR("、/、/、/…/…/……/……/……/……………")
		ELSE ;早い(二個以上の三点リーダを打たない)
			表示テキスト '= 表示テキスト + TEXTR("/、/、/…/…/…/……/……")
		ENDIF
	ENDIF 
	;4文字目の処理
	;特定の記号が指定さている場合は0～3個つける
	IF  CHAR:3 != ""
		IF (CHAR:3 == "❤" || CHAR:3 == "♥") && ハート無し != 1
			表示テキスト '= 表示テキスト + TEXTR("、/、/❤/❤/❤/…❤/……❤/……❤/❤❤/…❤❤/……❤❤/❤❤❤")
		ELSEIF FINDELEMENT(記号表, CHAR:3) != -1 
			表示テキスト '= 表示テキスト + TEXTR(@"、/、/%CHAR:3%/%CHAR:3%/%CHAR:3%%CHAR:3%")
		ENDIF
		IF CNT_A:0 < 候補数 - 1 
			IF テンポ == 0;通常
				表示テキスト '= 表示テキスト + TEXTR("/…/……/……/…………")
			ELSE
				表示テキスト '= 表示テキスト + TEXTR("//…/…/…/……/……")
			ENDIF
		ENDIF
	ENDIF
	;音節の末尾に応じた処理
	STRLENFORMU %表示テキスト%
	表示テキスト文字数 = RESULT
	IF SUBSTRINGU(表示テキスト, 表示テキスト文字数 - 1, 表示テキスト文字数) == "っ"
		表示テキスト '= 表示テキスト + "、"
	ENDIF
	;ハートありならハートマーク付加
	STRLENFORMU %表示テキスト%
	表示テキスト文字数 = RESULT
	IF STRFINDU("、…", SUBSTRINGU(表示テキスト, 表示テキスト文字数 - 1, 表示テキスト文字数) ) == 1
		表示テキスト '= SUBSTRINGU(表示テキスト, 0, 表示テキスト文字数 - 1)  + TEXTR("❤/❤/❤❤/❤❤/❤❤❤")
	ENDIF
NEXT

;生成文字列の末尾が句点なら除去
STRLENFORMU %表示テキスト%
表示テキスト文字数 = RESULT
IF SUBSTRINGU(表示テキスト,  表示テキスト文字数 - 1, 表示テキスト文字数) == "、"
	表示テキスト '= SUBSTRINGU(表示テキスト,0 , 表示テキスト文字数 - 1)
ENDIF

IF カナ == 1
	表示テキスト = %TO_KANA(表示テキスト)%
ENDIF

RETURNF 表示テキスト


;●平仮名カナ変換
@TO_KANA(ARGS:0)
#FUNCTIONS 
#DIMS CONST 平仮名対応表 	= "ぁあぃいぅうぇえぉおかがきぎくぐけげこごさざしじすずせぜそぞただちぢっつづてでとどなにぬねのはばぱひびぴふぶぷへべぺほぼぽまみむめもゃやゅゆょよらりるれろゎわゐゑをん"
#DIMS CONST カナ対応表	= "ァアィイゥウェエォオカガキギクグケゲコゴサザシジスズセゼソゾタダチヂッツヅテデトドナニヌネノハバパヒビピフブプヘベペホボポマミムメモャヤュユョヨラリルレロヮワヰヱヲン"

#DIM CNT_A	
#DIM IDX
#DIMS 対象文字
#DIMS 変換後文字列

対象文字 '= ""
変換後文字列 '= "" 

FOR CNT_A, 0, STRLENSU(ARGS:0)
	対象文字 '= SUBSTRINGU(ARGS:0, CNT_A, 1)
	IDX = STRFINDU(平仮名対応表, 対象文字)
	IF IDX != -1 
		対象文字 '= CHARATU(カナ対応表, IDX)
	ENDIF
	変換後文字列 '= 変換後文字列 + 対象文字
NEXT
変換後文字列 '= 変換後文字列

RETURNF 変換後文字列



