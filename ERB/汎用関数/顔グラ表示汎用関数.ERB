@顔グラ表示用文字列関数(ARG, g_loop = 0, y_pos = 0, size = 1125, 文字列記録フラグ = 0, デフォルト生成 = 0)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔
#DIM g_loop
#DIM y_pos
#DIM size
#DIM 文字列記録フラグ
#DIM デフォルト生成

;リソース解放
SPRITEDISPOSE @"顔グラ{g_loop}"
GDISPOSE 1000 + g_loop

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(ARG)


VARSET 差分エフェクト

; Add Ahyanosia
IF デフォルト生成 == 0
	IF ダンジョン表示モード == ""
		;ゆきずり確認
		グラ表示用一時文字列_短冊 = %ゆきずりセックス立ち絵更新(ARG, "短冊_")%
		SIF グラ表示用一時文字列_短冊 == ""
			グラ表示用一時文字列_短冊 = %グラ表示用文字列算出(ARG, "短冊_")%

		グラ表示用一時文字列_顔 = %ゆきずりセックス立ち絵更新(ARG, "顔_")%
		SIF グラ表示用一時文字列_顔 == ""
			グラ表示用一時文字列_顔 = %グラ表示用文字列算出(ARG, "顔_")%
	ELSE
		;グラ指定があるかどうかで分岐
		IF 探索中顔グラ保存:(キャラ隊列検索(ARG)):グラ名指定 != "" && EXIST画像FILE(@"resources/%CSTR:ARG:画像フォルダ%/%探索中顔グラ保存:(キャラ隊列検索(ARG)):グラ名指定%")
			グラ表示用一時文字列_顔 '= 探索中顔グラ保存:(キャラ隊列検索(ARG)):グラ名指定
		ELSE
			SIF 探索中顔グラキー:(キャラ隊列検索(ARG)) == ""
				探索中顔グラキー:(キャラ隊列検索(ARG)) = 通常時
			グラ表示用一時文字列_顔 = %GETVARS(@"探索中顔グラ保存:(キャラ隊列検索(ARG)):%探索中顔グラキー:(キャラ隊列検索(ARG))%")%
		ENDIF
	ENDIF
ENDIF

IF グラ表示用一時文字列_顔 == "" || デフォルト生成
	RESULTS:(2 + g_loop) = 顔_デフォルト
	CALLF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト")
	SIF 文字列記録フラグ
		RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_デフォルト
ELSE
	RESULTS:(2 + g_loop) = %グラ表示用一時文字列_顔%
	CALLF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%")
	SIF 文字列記録フラグ
		RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%
ENDIF

IF RESULT == 0 && GCREATED(1000 + g_loop) == 0
	;該当画像が無い場合
	IF グラ表示用一時文字列_顔 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す。差分エフェクトも無し
		CALLF GCREATE_拡張子F(1000 + g_loop, @"Noimage_顔")
		RESULTS:(2 + g_loop) = Noimage_顔
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = 
		立ち絵リソース記録 = 顔ノーイメ
		RETURNF @"<img src='顔ノーイメ' width='{size}' height='{size}' ypos='{y_pos}'>"
	ELSEIF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%")
		;設定中の短冊グラが存在すればそちらをトリミングで表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 0
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
		RESULTS:(2 + g_loop) = %グラ表示用一時文字列_短冊%
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%
	ELSEIF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト")
		;変更後画像も設定中の顔グラも無い場合はデフォルト顔を表示させる
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
		RESULTS:(2 + g_loop) = 顔_デフォルト
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_デフォルト
	ELSE
		IF CSTR:ARG:汎用立ち絵登録文字列 == "" || OPTION変数:汎用立ち絵制御 == 0
			CALLF GCREATE_拡張子F(1000 + g_loop, @"Noimage_顔")
			;どれもない場合はNoimage画像で返す。差分エフェクトも無し
			RESULTS:(2 + g_loop) = Noimage_顔
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = 
			立ち絵リソース記録 = 顔ノーイメ
			RETURNF @"<img src='顔ノーイメ' width='{size}' height='{size}' ypos='{y_pos}'>"
		ELSEIF GCREATE_拡張子F(1000 + g_loop, @"汎用グラフィック/顔_%CSTR:ARG:汎用立ち絵登録文字列%")
			;汎用立ち絵登録されてる場合はそれを表示させる
			SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
				LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
			RESULTS:(2 + g_loop) = 顔_%CSTR:ARG:汎用立ち絵登録文字列%
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_%CSTR:ARG:汎用立ち絵登録文字列%
		ENDIF
	ENDIF
ELSEIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
	;該当画像がある場合、そのまま作成
	LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
ELSE
ENDIF

SIF デフォルト生成
	GOTO 文字列返し

SIF 差分エフェクト != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")
SIF  差分エフェクト:1 != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト:1)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト:1, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")
SIF  差分エフェクト:2 != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト:2)

IF 文字列記録フラグ
	RSTR:表示グラ記録 += @"+++%差分エフェクト%"
	RSTR:表示グラ記録 += @"+++%差分エフェクト:1%"
	RSTR:表示グラ記録 += @"+++%差分エフェクト:2%"
ENDIF

$文字列返し
立ち絵リソース記録 = 顔グラ{g_loop}
RETURNF LOCALS



@顔グラ単発表示_デフォルト(対象キャラ)
#DIM 対象キャラ

SIF 対象キャラ < 0
	RETURN 0

HTML_PRINT 顔グラ表示用文字列関数(対象キャラ,,,,,1)
描画開始行数 = LINECOUNT
CALL ピクセル自動改行(180)



@任意顔グラ表示用文字列関数(ARG, 表示画像名, g_loop = 0, y_pos = 0, size = 1125, 任意エフェクト = "")
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔
#DIMS 表示画像名
#DIM g_loop
#DIM y_pos
#DIM size
#DIM 文字列記録フラグ
#DIM デフォルト生成
#DIMS 任意エフェクト

;リソース解放
SPRITEDISPOSE @"顔グラ{g_loop}"
GDISPOSE 1000 + g_loop
GDISPOSE 1001 + g_loop

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(ARG)

RESULTS:(2 + g_loop) = %表示画像名%
CALLF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%表示画像名%")
IF RESULT && 任意エフェクト != ""
	;エフェクト乗せ
	;基本的にエフェクトは顔グラと同サイズとし、別サイズのパターンは考慮しない
	CALLF GCREATE_拡張子F(1001 + g_loop, @"顔グラ漫画符/%任意エフェクト%")
	GDRAWG 1000 + g_loop, 1001 + g_loop, 0, 0, size, size, 0, 0, size, size
ENDIF

IF RESULT == 0 && GCREATED(1000 + g_loop) == 0
	;該当画像が無い場合
	IF GCREATE_拡張子F(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト")
		;デフォルト顔を表示させる
		IF 任意エフェクト != ""
			;エフェクト乗せ
			;基本的にエフェクトは顔グラと同サイズとし、別サイズのパターンは考慮しない
			CALLF GCREATE_拡張子F(1001 + g_loop, @"顔グラ漫画符/%任意エフェクト%")
			GDRAWG 1000 + g_loop, 1001 + g_loop, 0, 0, size, size, 0, 0, size, size
		ENDIF
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
		RESULTS:(2 + g_loop) = 顔_デフォルト
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_デフォルト
	ELSE
		IF CSTR:ARG:汎用立ち絵登録文字列 == "" || OPTION変数:汎用立ち絵制御 == 0
			CALLF GCREATE_拡張子F(1000 + g_loop, @"Noimage_顔")
			;どれもない場合はNoimage画像で返す。差分エフェクトも無し
			RESULTS:(2 + g_loop) = Noimage_顔
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = 
			立ち絵リソース記録 = 顔ノーイメ
			RETURNF @"<img src='顔ノーイメ' width='{size}' height='{size}' ypos='{y_pos}'>"
		ELSEIF GCREATE_拡張子F(1000 + g_loop, @"汎用グラフィック/顔_%CSTR:ARG:汎用立ち絵登録文字列%")
			;汎用立ち絵登録されてる場合はそれを表示させる
			IF 任意エフェクト != ""
				;エフェクト乗せ
				;基本的にエフェクトは顔グラと同サイズとし、別サイズのパターンは考慮しない
				CALLF GCREATE_拡張子F(1001 + g_loop, @"顔グラ漫画符/%任意エフェクト%")
				GDRAWG 1000 + g_loop, 1001 + g_loop, 0, 0, size, size, 0, 0, size, size
			ENDIF
			SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
				LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
			RESULTS:(2 + g_loop) = 顔_%CSTR:ARG:汎用立ち絵登録文字列%
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_%CSTR:ARG:汎用立ち絵登録文字列%
		ENDIF
	ENDIF
ELSEIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
	;該当画像がある場合、そのまま作成
	LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
ELSE
ENDIF

$文字列返し
立ち絵リソース記録 = 顔グラ{g_loop}
RETURNF LOCALS
