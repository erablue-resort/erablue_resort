;********************************************************************************************
;2019/8/14 作成 ロウ@TW夢子口上の人
;
;Licence  :ライセンスフリー(CC0)。
;
;グラフィック表示簡易化ライブラリ
;なんかややこしいグラフィック表示関連を簡単に表示できるようにまとめたもの。
;HTMLもどきだし、可読性悪いし、冗長なので「もう画像指定したら縦幅とか勝手に揃えろよ」っていうことで作りました。
;********************************************************************************************

;********************************************************************************************
;画像合成関数
;指定したグラフィックIDにリソース画像を重ねて合成する関数
;グラフィックIDが0の変数を渡すと勝手に使用可能なグラフィックIDを走査してその変数に渡す
;その場合は初めに合成されたリソースのサイズに合わせてグラフィックを作成する
;横幅か縦幅に0が入っている場合には自動でリソース画像のサイズを取得してサイズを合わせる
;ブレンド名はリソースを合成する際に適用するカラーブレンドの名称を入れる　例="赤"
;********************************************************************************************
@画像合成(グラフィックID, スプライト名, POS_X = 0, POS_Y = 0, 横幅 = 0, 縦幅 = 0, カラーブレンド名 = "", アルファ値 = 256)
#DIM REF グラフィックID		;合成先のグラフィックID 参照渡し
#DIMS スプライト名			;合成元のスプライト名
#DIM POS_X					;リソースを配置するX座標
#DIM POS_Y					;リソースを配置するY座標
#DIM 横幅					;合成するリソースの横幅
#DIM 縦幅					;合成するリソースの縦幅
#DIMS カラーブレンド名		;カラーブレンドの名称
#DIM カラーマトリクス, 5, 5	;カラーブレンドに使用するマトリクス
#DIM アルファ値				;カラーブレンドの不透明度
VARSET カラーマトリクス
SIF !SPRITECREATED(スプライト名)
	;スプライトが存在しない場合0を返す
	RETURN 0
IF 横幅 == 0 || 縦幅 == 0
	横幅 = SPRITEWIDTH(スプライト名)
	縦幅 = SPRITEHEIGHT(スプライト名)
ENDIF
IF グラフィックID == 0
	;前回呼び出されていれば続きから始める
	グラフィックID = NEXT_GID
	WHILE GCREATED(グラフィックID)
		;空いているグラフィックIDを走査する
		グラフィックID ++
	WEND
	;次回は続きから走査するように設定
	NEXT_GID = グラフィックID + 1
	
	GCREATE グラフィックID, 横幅, 縦幅
	GPLグラフィックID:GPL保有GID数 = グラフィックID
	GPLリソース名:GPL保有GID数 = 合成
	GPL保有GID数++
ENDIF
IF カラーブレンド名 == ""
	GDRAWSPRITE グラフィックID, スプライト名, POS_X, POS_Y,  横幅, 縦幅
	DEBUGPRINTFORML 画像合成：%スプライト名%:{グラフィックID},{POS_X},{POS_Y},{横幅},{縦幅}
ELSE
	;カラーブレンドが設定されている場合、適用して合成
	CALL カラーブレンド取得(カラーマトリクス, カラーブレンド名, アルファ値)
	GDRAWSPRITE グラフィックID, スプライト名, POS_X, POS_Y,  横幅, 縦幅, カラーマトリクス:0:0
	DEBUGPRINTFORML 画像合成：%スプライト名%:{グラフィックID},{POS_X},{POS_Y},{横幅},{縦幅},%カラーブレンド名%
ENDIF
SIF RESULT == 0
	RETURN 0
RETURN 1

;********************************************************************************************
;画像カラーブレンド複製関数
;指定したスプライトを用いてカラーブレンドを施したものを複製する。
;この関数が呼ばれた場合、自動で参照するスプライト画像のサイズを取得して合わせる
;ブレンド名はリソースを合成する際に適用するカラーブレンドの名称を入れる　例="赤"　"無し"
;このスプライトを作成した際に生成されたGIDを削除する場合、複製先のスプライト名を画像廃棄関数に渡す
;********************************************************************************************
@画像カラーブレンド複製(複製先スプライト名, 元スプライト名, カラーブレンド名, アルファ値 = 256)
#DIM グラフィックID		;複製先のグラフィックID 参照渡し
#DIMS 元スプライト名		;複製元のスプライト名
#DIMS 複製先スプライト名	;複製先のスプライト名
#DIM 横幅					;複製するリソースの横幅
#DIM 縦幅					;複製するリソースの縦幅
#DIMS カラーブレンド名		;カラーブレンドの名称
#DIM カラーマトリクス, 5, 5	;カラーブレンドに使用するマトリクス
#DIM アルファ値				;カラーブレンドの不透明度
VARSET カラーマトリクス
SIF !SPRITECREATED(元スプライト名)
	;スプライトが存在しない場合0を返す
	RETURN 0
横幅 = SPRITEWIDTH(元スプライト名)
縦幅 = SPRITEHEIGHT(元スプライト名)
;グラフィックIDを新規取得する
IF グラフィックID == 0
	;前回呼び出されていれば続きから始める
	グラフィックID = NEXT_GID
	WHILE GCREATED(グラフィックID)
		;空いているグラフィックIDを走査する
		グラフィックID ++
	WEND
	;次回は続きから走査するように設定
	NEXT_GID = グラフィックID + 1
	
	GCREATE グラフィックID, 横幅, 縦幅
	GPLグラフィックID:GPL保有GID数 = グラフィックID
	GPLリソース名:GPL保有GID数 '= 複製先スプライト名
	GPL保有GID数++
ENDIF
;カラーブレンドを適用して複製
CALL カラーブレンド取得(カラーマトリクス, カラーブレンド名, アルファ値)
GDRAWSPRITE グラフィックID, 元スプライト名, 0, 0,  横幅, 縦幅, カラーマトリクス:0:0
SPRITECREATE @"%複製先スプライト名%", グラフィックID, 0, 0, 横幅, 縦幅
DEBUGPRINTFORML 画像カラーブレンド複製：%元スプライト名%:%複製先スプライト名%:{グラフィックID},0,0,{横幅},{縦幅},%カラーブレンド名%
SIF RESULT == 0
	RETURN 0
RETURN 1

;********************************************************************************************
;リソース新規作成関数
;引数に渡した名称のスプライトリソースを、第二引数に渡したグラフィックIDで作成する
;********************************************************************************************
@リソース登録(スプライト名, グラフィックID)
#DIMS スプライト名
#DIM グラフィックID
SIF SPRITECREATED(スプライト名)
	;すでにその名前のリソースが存在すると失敗する。
	RETURN 0
SIF !GCREATED(グラフィックID)
	;元となるグラフィックIDが未作成、または廃棄済みであった場合失敗する。
	RETURN 0
SPRITECREATE スプライト名, グラフィックID
RETURN 1

;********************************************************************************************
;カラーブレンド取得関数
;カラーブレンド名を取得する関数
;カラーマトリクスの初期化関数はこの下にあります
;********************************************************************************************
@カラーブレンド取得(カラーマトリクス, カラーブレンド名, アルファ値 = 256)
#DIM REF カラーマトリクス, 0, 0	;使用するカラーマトリクスを格納する変数 参照渡し
#DIMS カラーブレンド名			;カラーブレンドの名称
#DIM アルファ値				;カラーブレンドの不透明度
SIF !カラーマトリクス初期化フラグ
	;今回起動時にカラーマトリクスが初期化されていない場合は初期化を行う
	CALL カラーマトリクス初期化
;カラーマトリクス参照変数にベースから取得したマトリクスを代入する
SELECTCASE カラーブレンド名
	CASE "青", "無し", "無", "0"
		CALL カラーマトリクス取得(カラーマトリクス, 0, アルファ値)
	CASE "紫"
		CALL カラーマトリクス取得(カラーマトリクス, 1, アルファ値)
	CASE "赤"
		CALL カラーマトリクス取得(カラーマトリクス, 2, アルファ値)
	CASE "黄"
		CALL カラーマトリクス取得(カラーマトリクス, 3, アルファ値)
	CASE "緑"
		CALL カラーマトリクス取得(カラーマトリクス, 4, アルファ値)
	CASE "水"
		CALL カラーマトリクス取得(カラーマトリクス, 5, アルファ値)
	CASE "白"
		CALL カラーマトリクス取得(カラーマトリクス, 6, アルファ値)
	CASE "灰"
		CALL カラーマトリクス取得(カラーマトリクス, 7, アルファ値)
	CASE "黒"
		CALL カラーマトリクス取得(カラーマトリクス, 8, アルファ値)
	CASE "栗"
		CALL カラーマトリクス取得(カラーマトリクス, 9, アルファ値)
	CASE "桃"
		CALL カラーマトリクス取得(カラーマトリクス, 10, アルファ値)
	CASE "橙"
		CALL カラーマトリクス取得(カラーマトリクス, 11, アルファ値)
	CASE "普通"
		CALL カラーマトリクス取得(カラーマトリクス, 12, アルファ値)
	CASE "褐色"
		CALL カラーマトリクス取得(カラーマトリクス, 13, アルファ値)
	CASE "黄色"
		CALL カラーマトリクス取得(カラーマトリクス, 14, アルファ値)
	CASE "日焼け"
		CALL カラーマトリクス取得(カラーマトリクス, 15, アルファ値)
	CASE "不健康"
		CALL カラーマトリクス取得(カラーマトリクス, 16, アルファ値)
	CASE "悪魔"
		CALL カラーマトリクス取得(カラーマトリクス, 17, アルファ値)
	CASEELSE
		CALL カラーマトリクス取得(カラーマトリクス, 12, アルファ値)
ENDSELECT
RETURN 1

;********************************************************************************************
;カラーマトリクス取得関数
;ディクショナリーライブラリから引用
;********************************************************************************************
@カラーマトリクス取得(カラーマトリクス, MID, アルファ値 = 256)
#DIM REF カラーマトリクス, 0, 0
#DIM MID ;マトリクスID
#DIM アルファ値				;カラーブレンドの不透明度
FOR LOCAL, 0, 5
	FOR LOCAL:1, 0, 5
		カラーマトリクス:LOCAL:(LOCAL:1) = カラーマトリクスベース:MID:LOCAL:(LOCAL:1)
	NEXT
NEXT
;不透明度を上書き設定
カラーマトリクス:3:3 = アルファ値
RETURN 1


;********************************************************************************************
;カラーマトリクス初期化関数
;ディクショナリーライブラリから引用
;基本青色素材用なので、色々試してカラーマトリクス作ってみたよ！って人はフィードバックくれると
;嬉しいです。
;********************************************************************************************
@カラーマトリクス初期化
#DIM _CNT
;"[青:0][紫:1][赤:2][黄:3][緑:4][水:5][白:6][黒:7][灰:8][栗:9][桃:10][橙:11]"
;"[普通:12][褐色:13][黄色:14][日焼:15][不健康:16][悪魔:17]"
_CNT = 0;"青"
	カラーマトリクスベース:_CNT:0:0 = 256,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,256,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 1;"紫"
	カラーマトリクスベース:_CNT:0:0 = 196,196,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,196,196,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 = 196,  0,196,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 2;"赤"
	カラーマトリクスベース:_CNT:0:0 =   0,256,  0,  0,  0
	カラーマトリクスベース:_CNT:1:0 =   0,  0,256,  0,  0
	カラーマトリクスベース:_CNT:2:0 = 256,  0,  0,  0,  0
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256
_CNT = 3;"黄"
	カラーマトリクスベース:_CNT:0:0 =   0,196,196,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 196,  0,196,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 196,196,  0,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 4;"緑"
	カラーマトリクスベース:_CNT:0:0 =   0,  0,256,  0,  0
	カラーマトリクスベース:_CNT:1:0 = 256,  0,  0,  0,  0
	カラーマトリクスベース:_CNT:2:0 =   0,256,  0,  0,  0
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256
_CNT = 5;"水"
	カラーマトリクスベース:_CNT:0:0 = 196,  0,196,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 196,196,  0,  0,  0;
	カラーマトリクスベース:_CNT:2:0 =   0,196,196,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 6;"白"//元絵が青一色だから
	カラーマトリクスベース:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 7;"灰"
	カラーマトリクスベース:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 = -64,-64,-64,  0,256;
_CNT = 8;"黒"
	カラーマトリクスベース:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 = -128,-128,-128,  0,256;
_CNT = 9;"栗"
	カラーマトリクスベース:_CNT:0:0 =  56, 30,  6,  0,  0;
	カラーマトリクスベース:_CNT:1:0 =  56, 30,  6,  0,  0;
	カラーマトリクスベース:_CNT:2:0 =  56, 30,  6,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 10;"桃"
	カラーマトリクスベース:_CNT:0:0 = 126, 58, 76,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 126, 58, 76,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 126, 58, 76,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   3,  3,  3,  0,256;
_CNT = 11;"橙"
	カラーマトリクスベース:_CNT:0:0 = 128, 71,  0,  0,  0;
	カラーマトリクスベース:_CNT:1:0 = 128, 71,  0,  0,  0;
	カラーマトリクスベース:_CNT:2:0 = 128, 71,  0,  0,  0;
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 12;"普通"
	カラーマトリクスベース:_CNT:0:0 = 256,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,256,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 13;"褐色" (0.9, 0.7, 0.7)
	カラーマトリクスベース:_CNT:0:0 = 231,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,180,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,180,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 14;"黄色" (1.05, 1.05, 1)
	カラーマトリクスベース:_CNT:0:0 = 269,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,269,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 15;"日焼" (1.2, 0.95, 0.95)
	カラーマトリクスベース:_CNT:0:0 = 308,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,244,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,244,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 16;"不健康" (1.2, 1.05, 1.2)
	カラーマトリクスベース:_CNT:0:0 = 308,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,269,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,308,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 17;"悪魔" (0.75, 0.75, 1.5)
	カラーマトリクスベース:_CNT:0:0 = 192,  0,  0,  0,  0;R
	カラーマトリクスベース:_CNT:1:0 =   0,192,  0,  0,  0;G
	カラーマトリクスベース:_CNT:2:0 =   0,  0,384,  0,  0;B
	カラーマトリクスベース:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクスベース:_CNT:4:0 =   0,  0,  0,  0,256;平行

カラーマトリクス初期化フラグ = 1
RETURN 1
