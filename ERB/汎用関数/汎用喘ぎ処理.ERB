@汎用喘ぎ処理(キャラ番号)
#DIM キャラ番号

SIF キャラ番号 == 0
	RETURN 0

CALL 口上変数初期化
CALL 喘ぎ内容取得(キャラ番号)
IF RESULT
	CALL 口上変数表示(キャラ番号, 1)
	CALL 口上変数初期化
ENDIF

@喘ぎ内容取得(キャラ番号)
#DIM キャラ番号
#DIM 文節数
#DIM 基準快楽量
#DIM 喘ぎレベル
#DIMS 自称単語
#DIMS 他称単語
#DIM 実行者番号


基準快楽量 = CUP:キャラ番号:快Ｃ + CUP:キャラ番号:快Ｂ + CUP:キャラ番号:快Ｖ + CUP:キャラ番号:快Ａ + CUP:キャラ番号:快Ｓ
IF 基準快楽量 < 1
	IF TCVAR:キャラ番号:前ターン絶頂 && CFLAG:キャラ番号:うふふ
		;快楽が発生していないが、絶頂直後なら余韻喘ぎ
		LOCALS = %汎用喘ぎ基礎文字(0)%
		SELECTCASE RAND:2
			CASE 0
				KSTR:(K++) = 「%LOCALS%────っ_H_ %LOCALS%────っ_H_」
			CASE 1
				KSTR:(K++) = 「%LOCALS%っ……_H_ %LOCALS%っ……_H_ %LOCALS%っ……_H_」
		ENDSELECT
		RETURN 1
	ENDIF
	;快楽が発生していないなら戻る
	RETURN 0
ENDIF

基準快楽量 = MAX(CUP:キャラ番号:快Ｃ, CUP:キャラ番号:快Ｂ, CUP:キャラ番号:快Ｖ, CUP:キャラ番号:快Ａ, CUP:キャラ番号:快Ｓ)
SELECTCASE 基準快楽量
	CASE 1 TO 999
		喘ぎレベル = 1
	CASE 1000 TO 2999
		喘ぎレベル = 2
	CASE 3000 TO 5999
		喘ぎレベル = 3
	CASE 6000 TO 9999
		喘ぎレベル = 4
	CASE IS >= 10000
		喘ぎレベル = 5
ENDSELECT
IF PALAM:キャラ番号:欲情 >= PALAMLV:8
	喘ぎレベル = MIN(5, 喘ぎレベル + 2)
ELSEIF PALAM:キャラ番号:欲情 >= PALAMLV:4
	喘ぎレベル = MIN(5, 喘ぎレベル + 1)
ENDIF

IF CFLAG:キャラ番号:うふふ && RCVAR:キャラ番号:汎用喘ぎ_文節低減フラグ == 0
	SELECTCASE PALAM:キャラ番号:欲情
		CASE IS <= PALAMLV:2
			文節数 = RAND:2 + 1
		CASE IS <= PALAMLV:4
			文節数 = LIMIT(RAND:喘ぎレベル, 2, 3)
		CASEELSE
			文節数 = LIMIT(RAND:喘ぎレベル, 2, 4)
	ENDSELECT
ELSE
	文節数 = 1
ENDIF

;快Ｓは通常性感帯ではないので、強い快感を覚えない限りは喘がない
IF CUP:キャラ番号:快Ｓ == MAX(CUP:キャラ番号:快Ｃ, CUP:キャラ番号:快Ｂ, CUP:キャラ番号:快Ｖ, CUP:キャラ番号:快Ａ, CUP:キャラ番号:快Ｓ)
	喘ぎレベル = MAX(1, 喘ぎレベル - 2)
	文節数 = 1
	IF CUP:キャラ番号:快Ｓ < 1000
		RETURN 0
	ENDIF
ENDIF
;睡眠時は喘ぎレベル１固定
IF CFLAG:キャラ番号:睡眠
	喘ぎレベル = 1
	文節数 = 1
ENDIF

;汎用喘ぎに一人称・二人称を導入
;DEBUGPRINTFORM 対象:{TARGET} 実行:{PLAYER} A対象:{AUTOCOM_対象番号保存} A実行:{AUTOCOM_実行番号保存}
実行者番号 = -1
;あなた以外に快楽を与えられた場合、与えてきた相手を特定
IF AUTOCOM_対象番号保存 == キャラ番号 && INRANGE(AUTOCOM_実行番号保存, 0 , CHARANUM-1) && AUTOCOM_実行番号保存 != キャラ番号
	実行者番号 = AUTOCOM_実行番号保存
ELSEIF AUTOCOM_実行番号保存 == キャラ番号 && INRANGE(AUTOCOM_対象番号保存, 0 , CHARANUM-1) && AUTOCOM_対象番号保存 != キャラ番号
	実行者番号 = AUTOCOM_対象番号保存
ELSEIF !(TARGET == キャラ番号 && PLAYER == MASTER) && !(TARGET == MASTER && PLAYER == キャラ番号)
	IF TARGET == キャラ番号 && INRANGE(PLAYER, 0 , CHARANUM-1) && PLAYER != キャラ番号
		実行者番号 = PLAYER
	ELSEIF PLAYER == キャラ番号 && INRANGE(TARGET, 0 , CHARANUM-1) && TARGET != キャラ番号
		実行者番号 = TARGET
	ENDIF
ENDIF

TRYCCALLFORM 汎用喘ぎ時使用時設定_{NO:キャラ番号}(キャラ番号, 実行者番号, 自称単語, 他称単語)
CATCH
	自称単語 '= CSTR:キャラ番号:一人称
	他称単語 '= CSTR:キャラ番号:二人称
	SIF INRANGE(実行者番号, 0, CHARANUM-1)
		他称単語 '= CALLNAME:実行者番号
ENDCATCH

;DEBUGPRINTFORML 　%自称単語%/%他称単語%
KSTR:(K++) = %汎用喘ぎ基礎文節(文節数, 喘ぎレベル, 自称単語, 他称単語)%
RETURN 1


@汎用喘ぎ基礎文字(喘ぎレベル)
#FUNCTIONS
#DIM 喘ぎレベル
#DIMS 文字返り値

SELECTCASE 喘ぎレベル
	CASE 0
		STRDATA 文字返り値
			DATA は
			DATA ふ
		ENDDATA
	CASE 1 TO 3
		STRDATA 文字返り値
			DATA は
			DATA ふ
			DATA あ
			DATA ん
			DATA んぅ
			DATA んぁ
			DATA ぁん
			DATA っ
			DATA ぅっ
			DATA ぁっ
			DATA ッ
			DATA ひぁ
			DATA ふっ
			DATA やっ
		ENDDATA
	CASE 4 TO 5
		STRDATA 文字返り値
			DATA あ゛
			DATA あ゛っ
			DATA ッッ
			DATA う゛
			DATA う゛ッ
			DATA お゛
			DATA ぉ゛っ
			DATA ぉう゛っ
			DATA ふ、ぅぅ……っ
			DATA あっあっ
			DATA ぁッ
			DATA は、ぁ
			DATA あ、ぁッ
			DATA んうぅぅ゛
			DATA あーッ
			DATA やっ
			DATA ぅっ
			DATA ぁっ
		ENDDATA
ENDSELECT

RETURNF 文字返り値

@汎用喘ぎ接続文字()
#FUNCTIONS
#DIMS 文字返り値


STRDATA 文字返り値
	DATA ん
	DATA ッ
	DATA っ
	DATA ──
ENDDATA


RETURNF 文字返り値

@汎用喘ぎ基礎文節(文節数, 喘ぎレベル, 一人称, 二人称)
#FUNCTIONS
#DIM 喘ぎレベル
#DIM 文節数
#DIMS 文字返り値
#DIMS 文節保存
#DIMS 一人称
#DIMS 二人称


SELECTCASE 文節数
	CASE 1
		STRDATA LOCALS
			DATAFORM 
			DATAFORM 
			DATAFORM %二人称%… 
			DATAFORM %二人称%_H_ 
			DATAFORM %二人称%…だめっ… 
			DATAFORM %二人称%…やっ_H_ 
		ENDDATA
		STRDATA 文字返り値
			DATAFORM 「%汎用喘ぎ基礎文字(喘ぎレベル)%っ……%LOCALS%%汎用喘ぎハート数返り(喘ぎレベル)%」
			DATAFORM 「%汎用喘ぎ基礎文字(喘ぎレベル)%%汎用喘ぎ接続文字()% %LOCALS%%汎用喘ぎハート数返り(喘ぎレベル)%」
			DATAFORM 「%LOCALS%%汎用喘ぎ接続文字()%%汎用喘ぎ基礎文字(喘ぎレベル)%%汎用喘ぎハート数返り(喘ぎレベル)%」
			DATAFORM 「%LOCALS%%汎用喘ぎ接続文字()%っ%汎用喘ぎハート数返り(喘ぎレベル)%」
		ENDDATA
	CASEELSE
		文字返り値 = 
		IF 一人称 != ""
			STRDATA LOCALS
				DATAFORM 
				DATAFORM 
				DATAFORM %一人称%っ 
				DATAFORM %一人称%_H_ 
				DATAFORM %一人称%、もぅ… 
				DATAFORM %SUBSTRINGU(一人称, 0, 1)%っ %一人称%_H_ 
				DATAFORM %二人称%… 
				DATAFORM %二人称%_H_ 
				DATAFORM %二人称%…だめっ… 
				DATAFORM %二人称%…やっ_H_ 
			ENDDATA
		ELSE
			STRDATA LOCALS
				DATAFORM 
				DATAFORM 
				DATAFORM %二人称%… 
				DATAFORM %二人称%_H_ 
				DATAFORM %二人称%…だめっ… 
				DATAFORM %二人称%…やっ_H_ 
			ENDDATA
		ENDIF
		LOCAL:1 = RAND:文節数
		FOR LOCAL, 0, 文節数
			STRDATA 文節保存
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%ッ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%っ……%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%ッ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ接続文字()%、%汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ基礎文字(喘ぎレベル)%っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ接続文字()%、%汎用喘ぎ接続文字()%、%汎用喘ぎ基礎文字(喘ぎレベル)%っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%、%汎用喘ぎ接続文字()%、%汎用喘ぎ接続文字()%っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%──っ%汎用喘ぎハート数返り(喘ぎレベル)%
				DATAFORM %汎用喘ぎ基礎文字(喘ぎレベル)%……っ%汎用喘ぎハート数返り(喘ぎレベル)%
			ENDDATA
			SIF LOCAL == LOCAL:1 && LOCALS != ""
				文字返り値 += @"　%LOCALS%"
			文字返り値 += @"　%文節保存%"
		NEXT
		;先頭の全角スペースを削除
		SUBSTRING 文字返り値, 2, -1
		文字返り値 = 「%RESULTS%」
ENDSELECT

RETURNF 文字返り値

@汎用喘ぎハート数返り(喘ぎレベル)
#FUNCTIONS
#DIM 喘ぎレベル
#DIMS 文字返り値

SELECTCASE 喘ぎレベル
	CASE 1
		RETURNF "_H_"
	CASE 2
		IF RAND:2 == 0
			文字返り値 = _H_
		ELSE
			文字返り値 = _H_H_
		ENDIF
		RETURNF 文字返り値
	CASE 3
		RETURNF "_H_H_"
	CASE 4
		IF RAND:2 == 0
			文字返り値 = _H_H_
		ELSE
			文字返り値 = _H_H_H_
		ENDIF
		RETURNF 文字返り値
	CASE 5
		RETURNF "_H_H_H_"
ENDSELECT

