@初体験日登録処理(体験キャラ, 相手キャラ, 体験内容, 体験日時, 体験場所, 体験方法 = "", その他備考 = "", 初回フラグ = 0)
#DIM 体験キャラ
#DIM 相手キャラ
#DIMS 体験内容
#DIM 体験日時
#DIMS 体験場所
#DIMS 体験方法
#DIMS その他備考
#DIM id記録
#DIM 初回フラグ
#DIM 相手登録番号

;体験キャラが不明なのはありえないはず
SIF 体験キャラ < 0
	RETURN 0

IF 相手キャラ < 0
	相手登録番号 = -1
ELSE
	相手登録番号 = CFLAG:相手キャラ:人物番号
ENDIF

IF 初回フラグ
	;相手が誰かにかかわらず、体験キャラだけに依存する重複判定
	;ファーストキスとかその辺
	IF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 体験名 = '%体験内容%'")
		;まったく同じ体験が既に登録されてるならスキップ
		RETURN 0
	ENDIF
ELSE
	;こっちは相手ごとの初回を記録
	IF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 相手キャラ人物番号 = {相手登録番号} And 体験名 = '%体験内容%'")
		;まったく同じ体験が既に登録されてるならスキップ
		RETURN 0
	ENDIF
ENDIF

{
DT_ROW_ADD "初体験データベース", "体験キャラ人物番号", CFLAG:体験キャラ:人物番号, "相手キャラ人物番号", 相手登録番号, "体験名", 体験内容,
			"体験日時", 体験日時, "体験場所", 体験場所, "体験方法", 体験方法, "その他", その他備考
}
id記録 = RESULT

RETURN id記録


@初体験日取得(体験内容, 体験キャラ, 相手キャラ)
#FUNCTION
#DIM 体験キャラ
#DIM 相手キャラ
#DIMS 体験内容
#DIM 相手登録番号
;体験が存在するかどうかをチェック

IF 相手キャラ < 0
	相手登録番号 = -1
ELSE
	相手登録番号 = CFLAG:相手キャラ:人物番号
ENDIF

IF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 相手キャラ人物番号 = {相手登録番号} And 体験名 = '%体験内容%'")
	RETURNF DT_CELL_GET("初体験データベース", RESULT:1, "体験日時", 1)
ELSE
	RETURNF 0
ENDIF


@初体験方法取得(体験内容, 体験キャラ, 相手キャラ)
#FUNCTIONS
#DIM 体験キャラ
#DIM 相手キャラ
#DIMS 体験内容
#DIM 相手登録番号
;体験が存在するかどうかをチェック

IF 相手キャラ < 0
	相手登録番号 = -1
ELSE
	相手登録番号 = CFLAG:相手キャラ:人物番号
ENDIF

IF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 相手キャラ人物番号 = {相手登録番号} And 体験名 = '%体験内容%'")
	RETURNF DT_CELL_GETS("初体験データベース", RESULT:1, "体験方法", 1)
ELSE
	RETURNF 
ENDIF


@初体験済みチェック(体験内容, 体験キャラ)
#FUNCTION
#DIM 体験キャラ
#DIM 相手キャラ
#DIMS 体験内容
#DIM 相手登録番号
;体験が存在するかどうかをチェック、相手不問Ver

RETURNF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 体験名 = '%体験内容%'")


@初体験相手_存在チェック(体験内容, 体験キャラ, 相手キャラ)
#FUNCTION
#DIM 体験キャラ
#DIM 相手キャラ
#DIMS 体験内容
#DIM 相手登録番号
;任意の体験に任意の相手がいるかどうかのチェック

IF 相手キャラ < 0
	相手登録番号 = -1
ELSE
	相手登録番号 = CFLAG:相手キャラ:人物番号
ENDIF

RETURNF DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:体験キャラ:人物番号} And 相手キャラ人物番号 = {相手登録番号} And 体験名 = '%体験内容%'")
