

@所持画像詳細表示(対象キャラ)
#DIM 対象キャラ
#DIM 画像数
#DIM 衣装数
#DIM 関数総数
#DIM 配列番号
#DIM 画像番号
#DIMS 衣装登録, 500
#DIMS 判定衣装
#DIMS 表示人数
#DIM 判定数, 4
#DIM 複数人一枚絵キャラ, 5

;画像フォルダに所持している画像を分類わけして表示する

画像数 = 0
;; status画面からくる場合はすでにセットされているので念のため
SIF CSTR:対象キャラ:画像フォルダ == ""
	CSTR:対象キャラ:画像フォルダ = {NO:対象キャラ}%CSTR:対象キャラ:フォルダ判定キー%

VARSET RESULTS
衣装数 = ENUMFUNCBEGINSWITH("CLOTHES_CHANGE_")
VARSET 衣装登録
ARRAYCOPY "RESULTS", "衣装登録"
ARRAYSORT 衣装登録, FORWARD, 0, 衣装数
ARRAYSHIFT 衣装登録, 1, "デフォルト"
VARSET RESULTS
画像数 = ENUMFILES(@"resources/%CSTR:対象キャラ:画像フォルダ%", "*.*", 0)

FOR 配列番号, 0, 衣装数
	SIF 衣装登録:配列番号 == ""
		BREAK
	判定衣装 '= REPLACE(衣装登録:配列番号, "CLOTHES_CHANGE_", "")
	VARSET 判定数
	FOR 画像番号, 0, 画像数
		IF STRFIND(RESULTS:画像番号, @"短冊_%判定衣装%") >= 0
			判定数:0 ++
		ELSEIF STRFIND(RESULTS:画像番号, @"短冊_妊娠%判定衣装%") >= 0
			判定数:1 ++
		ELSEIF STRFIND(RESULTS:画像番号, @"コマンド実行時_%判定衣装%") >= 0
			判定数:2 ++
		ELSEIF STRFIND(RESULTS:画像番号, @"コマンド実行時_妊娠%判定衣装%") >= 0
			判定数:3 ++
		ENDIF
		IF 判定衣装 == "デフォルト" && STRFIND(RESULTS:画像番号, @"コマンド実行時_") >= 0 && STRCOUNT(RESULTS:画像番号, "_") < 2
			判定数:2 ++
		ENDIF
	NEXT
	IF SUMARRAY(判定数) > 0
		PRINTFORML ■%判定衣装%画像
		PRINT 　　
		IF 判定数:0 > 0
			PRINTFORM 立ち絵：{判定数:0, 4}枚　　
		ENDIF
		IF 判定数:1 > 0
			PRINTFORM 妊娠立ち絵：{判定数:1, 4}枚　　
		ENDIF
		IF 判定数:2 > 0
			PRINTFORM コマンド画像：{判定数:2, 4}枚　　
		ENDIF
		IF 判定数:3 > 0
			PRINTFORM 妊娠コマンド画像：{判定数:3, 4}枚　　
		ENDIF
		PRINTL
	ENDIF
NEXT

IF 対象キャラ != MASTER
	画像数 = 0
	VARSET RESULTS
	ENUMFUNCBEGINSWITH "複数人一枚絵_登録データ_"
	関数総数 = RESULT

	FOR 配列番号, 0, 関数総数
		;複数人一枚絵関数を全部見る
		VARSET 複数人一枚絵キャラ
		CALLFORM %RESULTS:配列番号%("初期登録", 複数人一枚絵キャラ)
		;キャラが条件内に含まれていたら表示
		IF MATCH(複数人一枚絵キャラ, 対象キャラ)
			IF 画像数 < 1
				PRINTL
				PRINTL ■複数人一枚絵
			ENDIF
			PRINTFORM 　　組み合わせ%TOFULL(TOSTR(画像数 + 1))%：
			表示人数 = 0
			FOR 画像番号, 0, VARSIZE("複数人一枚絵キャラ")
				SIF 複数人一枚絵キャラ:画像番号 < 1
					CONTINUE
				SIF 複数人一枚絵キャラ:画像番号 == 対象キャラ
					CONTINUE
				PRINTFORM %NAME:(複数人一枚絵キャラ:画像番号)%　
				表示人数 ++
			NEXT
			SIF 表示人数 < 1
				PRINTFORM %CALLNAME:対象キャラ%単独
			PRINTL 
			画像数 ++
		ENDIF
	NEXT
ENDIF

WAIT
