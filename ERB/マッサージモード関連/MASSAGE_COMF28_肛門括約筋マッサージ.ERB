;-------------------------------------------------
;肛門括約筋マッサージ
;-------------------------------------------------
@MASSAGE_COMNAME28
#FUNCTIONS
TSTR:コマンド名受渡 = 肛門括約筋マッサージ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_28
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_28
LOCALS = <br>■肛門括約筋マッサージ<br>
LOCALS += "尻たぶを割り開いて、オイルをつけた指先で肛門括約筋を外側から揉みほぐす。"
LOCALS += "<br>使用頻度のわりにケアされない筋肉なので、プロによる施術が必要…ということになっている。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ａ・接触・歓楽・液体"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM28
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

;外側から揉みほぐすだけなので苦痛はない。
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 120)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;ローションは別のコマンドになるっぽいので気持ち程度
CALL SOURCE_CALC_液体(TARGET, PLAYER, 1000)

;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり == 1
	TEQUIP:下半身下着あり = 0
	TEQUIP:下半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:上半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 120
	BASE:TARGET:警戒 -= 40
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「流石にこれはダメなやつなのでは？」と思いはじめる。が、時既に遅し。
;警戒90で理性が45上がり、そこから理性が80下がるイメージでこの数値。ここにきて未だに警戒90なのが無知シチュの『味』。
;尻穴で性感に？？？となっているので性欲の上昇量はちょっとだけ抑えめ。
	BASE:TARGET:理性 -= 35
	BASE:TARGET:性欲 += 80
	BASE:TARGET:警戒 += 90
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 650
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒160で理性が80回復するも、煽られる性欲と与えられる刺激で理性が120削られて理性変動は-40。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒64で理性32回復、そこから理性120低下でこの数字となる。理性と警戒の合計値は少しだけ下がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 -= 88
	BASE:TARGET:性欲 += 120
	BASE:TARGET:警戒 += 64
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSE
;実行に際し十分な理性・警戒削りがなされていない場合。
;性欲は煽れるが、警戒と理性が跳ねる。警戒心がビンっ！ってなるので、煽られる性欲の理性低下を撥ね退けてしまう。
	BASE:TARGET:理性 += 80
	BASE:TARGET:性欲 += 100
	BASE:TARGET:警戒 += 160
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE28
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;仰向けだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 0
	RETURN 0


;理性警戒の合計が1000以下じゃないとダメ
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM28

PRINTFORM %CALLNAME:PLAYER%は、

	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM %CALLNAME:TARGET%の
			;Ｖがあり欲情している場合
			SIF TALENT:TARGET:Ｖ感度 >= -1 && PALAM:欲情  >= PALAMLV:3
				PRINTFORM ぐっしょりと愛液を吸った
		PRINTFORM 施術着を脱がし、
	ENDIF
	PRINTFORML オイルをたっぷりとつけた指で%CALLNAME:TARGET%の%TEXTR("肛門/お尻の穴/アヌス")%を優しく揉みほぐした。

	
;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 650
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%は%TEXTR("とろんと目元を蕩けさせ/蕩けた顔でだらしなく口をあけて/法悦に目元を蕩けさせて")%、
	CASE 1
		PRINTFORM %CALLNAME:TARGET%はだらしなく緩んだ口から%TEXTR("あられもない声/快楽に染まった嬌声/淫らな声")%を%TEXTR("あげて/こぼして/こぼしながら")%、
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("の『マッサージ』/の手技/が与える快楽")%%TEXTR("にアナルを差し出してしまっている/に酔いしれている/にされるがままになっている/を受け入れている/を甘受している")%…
ENDIF
