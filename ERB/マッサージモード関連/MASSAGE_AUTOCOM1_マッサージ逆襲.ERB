;------------------------------------------------------------------------------
;マッサージ中、
;------------------------------------------------------------------------------
@MASSAGE_AUTOCOM1
;処理はすべてCOMENDで行う
;処理の発動キー用にSELECTCOMを代入
SELECTCOM = 1001
SIF TARGET == MASTER
	SWAP TARGET, PLAYER

RETURN 1

@MASSAGE_AUTOCOM_ABLE1(キャラ番号)
#DIM キャラ番号

;オプション制御
SIF OPTION変数:マッサージ_逆襲禁止
	RETURN 0
;性知識
SIF 知識素質:キャラ番号:性知識 < -1
	RETURN 0
IF フィールド展開:淫化マッサージ
	;発情期のみ
	IF CFLAG:キャラ番号:発情期フラグ >= 0 || BASE:キャラ番号:ちんイラ < 600
		;アウト条件に引っかかったら通常条件を見る
		;ターゲットが陥落済みであること
		SIF 陥落チェック_性的(キャラ番号) == 0
			RETURN 0
		;初うふふ済み必須
		SIF 初体験日取得("うふふ", キャラ番号, PLAYER) == 0
			RETURN 0
		;専用ステータスを参照
		SIF BASE:キャラ番号:ちんイラ < 200
			RETURN 0
	ENDIF
ELSE
	;ターゲットが陥落済みであること
	SIF 陥落チェック_性的(キャラ番号) == 0
		RETURN 0
	;初うふふ済み必須
	SIF 初体験日取得("うふふ", キャラ番号, PLAYER) == 0
		RETURN 0
	;専用ステータスを参照
	SIF BASE:キャラ番号:ちんイラ < 200
		RETURN 0
ENDIF
;毎回は出さないように
IF TCVAR:キャラ番号:強制発情フラグ
;強制発情時はガチ発情よりもちょっと弱めになるように。テストしてみた感じあんまかわらんかったけど一応
	SIF BASE:キャラ番号:ちんイラ < RAND:1300
		RETURN 0
ELSE
	SIF BASE:キャラ番号:ちんイラ < RAND:1000
		RETURN 0
ENDIF
;理性は削れてること
SIF BASE:キャラ番号:理性 > 1
	RETURN 0
;二人きりのみ
SIF GET_TARGETNUM() > 1
	RETURN 0
;通常マッサージ直後には発動しない
TRYCALLFORMF MASSAGE_COMTYPE_{コマンド履歴:0}
SIF TFLAG:コマンドタイプ受渡 == 0
	RETURN 0

RETURN 1

@MESSAGE_MASSAGE_AUTOCOM1
#DIM キャラ番号
PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%は息を荒くし、もう我慢が出来ない様子で%CALLNAME:PLAYER%に襲いかかってきた！
PRINTFORML どうやらきわどいマッサージで情欲を煽りすぎたようだ
PRINTL 満たされるまでは解放されないだろう…
PRINTW （なすがまま状態でうふふを開始　うふふ終了条件：絶頂３回）
IF 初体験日取得("初うふふ", TARGET, MASTER) == 0
	IF 知識素質:キャラ番号:性知識 == -1
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、昂る本能%TEXTR("の導く/が求める/の")%ままに%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/との交尾を求めてしまった")%</font><img src='えっちハート'>","うふふ")
	ELSEIF TALENT:TARGET:恋人持ち == 2
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、%TEXTR("煽られた生殖本能/昂った性欲/めくるめく快楽への欲求")%%TEXTR("を抑えきれずに/に抗えず/のままに")%伴侶%TEXTR("を裏切って/のいる身でありながら")%%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/との不貞交尾を望んでしまった/との浮気えっちを求めてしまった")%</font><img src='えっちハート'>","うふふ")
	ELSEIF TALENT:TARGET:恋人持ち == 1
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、%TEXTR("煽られた生殖本能/昂った性欲/めくるめく快楽への欲求")%%TEXTR("を抑えきれずに/に抗えず/のままに")%恋人%TEXTR("を裏切って/のいる身でありながら")%%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/との不貞交尾を望んでしまった/との浮気えっちを求めてしまった")%</font><img src='えっちハート'>","うふふ")
	ELSEIF アイドル判定(TARGET)
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、%TEXTR("煽られた生殖本能/昂った性欲/めくるめく快楽への欲求")%%TEXTR("を抑えきれずに/に抗えず/のままに")%%TEXTR("ファンの想い/プロデューサー")%を裏切って%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/とのえっちを求めてしまった")%</font><img src='えっちハート'>","うふふ")
	ELSEIF TALENT:TARGET:恋人持ち == -1
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、%TEXTR("煽られた生殖本能/昂った性欲/めくるめく快楽への欲求")%%TEXTR("を抑えきれずに/に抗えず/のままに")%%TEXTR("好きな人への想い/胸に抱く恋心")%を裏切って%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/とのえっちを求めてしまった")%</font><img src='えっちハート'>","うふふ")
	ELSE
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>発情しきった体を%TEXTR("きわどい/スケベな")%マッサージで弄ばれ、性感と欲望に煽られるまま%CALLNAME:PLAYER%%TEXTR("と一線を越えてしまった/を押し倒してしまった/との交尾を望んでしまった")%</font><img src='えっちハート'>","うふふ")
	ENDIF
ENDIF

@ちんイラ増加処理(対象キャラ, 基礎値)
#DIM 対象キャラ
#DIM 基礎値
#DIM 補正量

SIF 対象キャラ == MASTER
	RETURN 0

;素質で基礎値に補正を掛ける

IF TALENT:対象キャラ:性欲 == -2
	BASE:対象キャラ:ちんイラ = 0
	RETURN 0
ENDIF

補正量 = 0

補正量 += TALENT:対象キャラ:態度 * 3
補正量 += TALENT:対象キャラ:応答 * 5
補正量 += TALENT:対象キャラ:プライド * 3

SIF TALENT:対象キャラ:精神的余裕 > 0
	補正量 -= 10
SIF TALENT:対象キャラ:精神的余裕 < 0
	補正量 += 5
SIF TALENT:対象キャラ:無関心 > 0
	補正量 -= 10
SIF TALENT:対象キャラ:感情乏しい > 0
	補正量 -= 10

補正量 -= TALENT:対象キャラ:自制心 * 3

SIF TALENT:対象キャラ:性欲 > 0
	補正量 += 10
SIF TALENT:対象キャラ:性欲 < 0
	補正量 -= 5

補正量 += TALENT:対象キャラ:経験豊富 * 5
補正量 += TALENT:対象キャラ:淫乱 * 50
補正量 -= TALENT:対象キャラ:貞操 * 10
補正量 += TALENT:対象キャラ:快感応答 * 10

SIF CFLAG:発情期フラグ == -1 && (TCVAR:強制発情フラグ == 1 || TALENT:発情期あり >= 1)
	補正量 += 20

SIF TALENT:対象キャラ:性別嗜好 == 1 && TALENT:PLAYER:性別 == 2
	補正量 -= 20
SIF TALENT:対象キャラ:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1
	補正量 -= 20
SIF TALENT:対象キャラ:性別嗜好 == 3
	補正量 -= 20

SIF TALENT:PLAYER:印象 == 4
	補正量 += 5

BASE:対象キャラ:ちんイラ = MIN(MAXBASE:対象キャラ:ちんイラ, BASE:対象キャラ:ちんイラ + 基礎値 * (100 + 補正量) / 100)

RETURN 基礎値 * (100 + 補正量) / 100
