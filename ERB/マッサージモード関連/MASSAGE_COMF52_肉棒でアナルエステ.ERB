;-------------------------------------------------
;肉棒でアナルエステ♡
;-------------------------------------------------
@MASSAGE_COMNAME52
#FUNCTIONS
TSTR:コマンド名受渡 = 肉棒でアナルエステ_H_
TSTR:コマンドカラー受渡 = FFD900

;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_52
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_52
LOCALS = <br>■肉棒でアナルエステ<br>
LOCALS += "とろとろにほぐれて食べ頃になったアナルを、専用の『施術道具』を使ってマッサージする。"
LOCALS += "<br>挿入するまでは一兆歩譲ってエステだったかもしれないが、挿入してしまったらそれはもうセックスなので、うふふモードに突入する。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ａ・露出・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ａ性交経験(TARGET)・Ｃ性交経験_Ａ挿入(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@MASSAGE_COM52
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ
;まずキャラの状態によって３つに分岐する

CALL MASSAGE_分岐_COM52(TARGET)
RFLAG:コマンド結果受渡し変数 = RESULT
;RFLAG:コマンド結果受渡し変数に結果を入れる
; 1:通常許可
; 2:なし崩し許可
; 3:レイプ
;失敗はそもそもコマンドを出さないので分岐しない

;ここでレイプ時の記憶喪失手段使うよ～とか
;レイプするけどOK～？　みたいな確認分岐を入れる
IF RFLAG:コマンド結果受渡し変数 == 2
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%が何をしようとしているのかに気付き、やめるように言ってくる。
	PRINTW しかしその拒否は弱々しく、強く押せばなし崩しで許可してくれそうだ……
	PRINTL このまま実行しますか？
	PRINTL
	PRINTBUTTONLC "[0] はい", 0
	PRINTBUTTONLC "[1] いいえ", 1
	BINPUT
	IF RESULT == 1
		PRINTFORMW %CALLNAME:PLAYER%は思い直すことにした。
		RETURN -1
	ENDIF
	;履歴セット
	CFLAG:TARGET:泥酔ックス回数 += 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中に快楽でとろとろにした%CALLNAME:TARGET%を手籠めにした</font><img src='えっちハート'>","うふふ")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中に快楽に負け、%CALLNAME:PLAYER%と一線を越えてしまった</font><img src='えっちハート'>","うふふ")
	

	IF (CFLAG:TARGET:お願いックス回数 + CFLAG:TARGET:泥酔ックス回数 / 3) >= 4 || TALENT:淫乱
		SETBIT CFLAG:1 , 0
		TALENT:セフレ = 1
		初体験日:セフレ = DAY
		RFLAG:コマンド結果受渡し変数４ = TALENT:陥落不可
		CLEARBIT TALENT:陥落不可, 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%を[セフレ]陥落させた</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[セフレ]を得た</font><img src='えっちハート'>","うふふ")
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == 3
	;レイプしないを選んだ時はキャンセル扱いでコマンド選択に戻す
	PRINTFORM 『マッサージ』を建前にしながら下半身の服を脱ぎ
	IF MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
		PRINTFORM %TEXTR("ガチガチに勃起し/硬くそそり立っ")%た%TEXTR("陰茎/チンポ/ペニス/肉棒")%を出した
	ELSE
		PRINTFORM ペニスバンドをつけた
	ENDIF
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("が、今から何をしようとしているのか/に、これから何をされてしまうのか")%に気付いた%CALLNAME:TARGET%は
	IF TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
		PRINTFORM 夢見心地にぼんやりとしながらも
		IF TALENT:TARGET:度胸 == -1 || TALENT:TARGET:応答 == -1 || TALENT:TARGET:プライド == -1 || TALENT:TARGET:陽気／陰気 == -1
		;臆病、大人しい、プライド低い、陰気
			PRINTFORML 懸命に、やめて欲しいと懇願する。
		ELSE
			PRINTFORML 懸命に、やめ%TEXTR("るように言ってくる/て欲しいと口にする")%。
		ENDIF
		PRINTFORMW 無理やりコトに及ぼうとすれば、きっと普段の%CALLNAME:TARGET%であれば%TEXTR("強く/激しく")%抵抗していただろう……。
	ELSE
		PRINTFORML やめるように言ってくる。
		PRINTFORMW 無理やりコトに及ぼうとすれば、きっと%CALLNAME:TARGET%は強く抵抗してくるだろう……。
	ENDIF
	PRINTL
	;香使用から20分以上経ってると許可
	IF TFLAG:マッサージ_鬼灯の香使用時間 > 0 && TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
		;ここで記憶を曖昧にする薬の所持分岐
		PRINTFORML しかし「鬼灯の香」が十分に効いている今ならば、快楽と薬効で朦朧とした%CALLNAME:TARGET%にはもはや赤子程度の抵抗力しか残っていない。
		PRINTFORML おまけに香の効果で行為中の記憶は曖昧になり、「よく覚えていないけど気持ちよかった」程度にしか残らない……。
		PRINTFORM これならば%CALLNAME:PLAYER%の力でも容易に抵抗を封じ、
		SELECTCASE RAND:5
		CASE 0
			IF TALENT:TARGET:恋人持ち == 1 || TALENT:TARGET:恋人持ち == 2
				PRINTFORM %CALLNAME:TARGET%の尻の一番奥に無理やり%TEXTR("不貞の/浮気の/浮気セックスの/想い人以外に犯される")%快楽を%TEXTR("覚えこませる/教えてやる/刻み付けてやる")%
			ELSE
				PRINTFORM 無理やり%TEXTR("に関係を持つ/犯す")%
			ENDIF
		CASE 1
			IF アイドル判定(TARGET)
				PRINTFORM %TEXTR("犯され/朦朧レイプ/セックス/望まぬ")%アナルスキャンダルでファンを裏切らせる
			ELSE
				PRINTFORM 合意のないままに%CALLNAME:TARGET%の体を貪る
			ENDIF
		CASE 2
			PRINTFORM 穴の『使い心地』を存分に楽しむ
		CASE 3
			PRINTFORM マッサージで%CALLNAME:TARGET%の体に触れたせいで散々ムラつかされた%TEXTR("獣欲/欲望")%%TEXTR("を思う存分その体にぶつける/を思う存分その体にぶちまける/の責任を取らせる")%
		CASE 4
			PRINTFORM ノーリスクでアナルレイプを楽しむ
		ENDSELECT
		PRINTFORMW ことが出来るだろう。
		PRINTL
		PRINTL このまま実行しますか？
		PRINTL
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		BINPUT
		IF RESULT == 1
			PRINTFORMW %CALLNAME:PLAYER%は思いとどまることにした。
			RETURN -1
		ENDIF
		CFLAG:TARGET:泥酔ックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中、快楽とお香で抵抗力を奪った%CALLNAME:TARGET%のアナルを無理やり手籠めにした</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中、快楽とお香で朦朧とする中で抵抗むなしく%CALLNAME:PLAYER%にアナルを%TEXTR("犯/レイプ")%されてしまった</font><img src='えっちハート'>","うふふ")
		IF TALENT:TARGET:Ａ処女
			IF 知識素質:TARGET:高貴 >= 1
				CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%の高貴な尻穴処女を無理やり奪ってやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>快楽とお香で朦朧とする意識の中、%CALLNAME:PLAYER%に無理やり犯され、高貴な身でありながら後ろの処女を%TEXTR("奪われ/散らされ")%てしまった</font><img src='えっちハート'>","うふふ")
			ELSE
				CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%のＡ処女を無理やり奪ってやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>快楽とお香で朦朧とする意識の中、%CALLNAME:PLAYER%に無理やり犯され後ろの処女を%TEXTR("奪われ/散らされ")%てしまった</font><img src='えっちハート'>","うふふ")
			ENDIF
		ENDIF
		;うわべだけ実行判定
		IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
		;うつ伏せだと寝バック
			PRINTL A寝バック
		ELSE
		;仰向けだと正常位
			PRINTL Ａ正常位
		ENDIF

		実行値 = 0
		＋表示フラグ = 0

		;すべての命令に共通の要素を考慮
		CALL COM_ORDER
		実行値 = RESULT
		＋表示フラグ = RESULT:1

		;ABL:欲望
		IF ABL:欲望
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += ABL:欲望 * 3
			PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 3})
			＋表示フラグ = 1
		ENDIF
		IF RFLAG:指示モード対象あなたフラグ
			;ABL:Ｃ感覚
			IF ABL:Ｃ感覚
				SIF ＋表示フラグ
					PRINT  + 
				実行値 += ABL:Ｃ感覚 * 3
				PRINTFORM Ｃ感覚LV{ABL:Ｃ感覚}({ABL:Ｃ感覚 * 3})
				＋表示フラグ = 1
			ENDIF
		ELSE
			;ABL:Ａ感覚
			IF ABL:Ａ感覚
				SIF ＋表示フラグ
					PRINT  + 
				実行値 += ABL:Ａ感覚 * 2
				PRINTFORM Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚 * 2})
				＋表示フラグ = 1
			ENDIF
		ENDIF
		;快楽刻印
		IF MARK:快楽刻印
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += MARK:快楽刻印 * 3
			PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 3})
			＋表示フラグ = 1
		ENDIF
		;PALAM:潤滑（の不足）
		IF RFLAG:指示モード対象あなたフラグ
			LOCAL = PALAM:MASTER:潤滑
		ELSE
			LOCAL = PALAM:潤滑
		ENDIF
		IF LOCAL < PALAMLV:3
			PRINT  - 
			実行値 -= 5
			PRINT 潤滑不足
			PRINT (5)
			＋表示フラグ = 1
		ENDIF
		;PALAM:欲情
		IF GETPALAMLV(PALAM:欲情, 5)
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += GETPALAMLV(PALAM:欲情, 5) * 3
			PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 3})
			＋表示フラグ = 1
		ENDIF
		;恥じらい
		IF TALENT:羞恥心 > 0
			PRINT  - 
			実行値 -= 2
			PRINT 羞恥心(2)
			＋表示フラグ = 1
		ENDIF
		;快感の否定
		IF TALENT:快感応答 < 0 && !TALENT:恋慕
			PRINT  - 
			実行値 -= 5
			PRINT 快感の否定(5)
			＋表示フラグ = 1
		ENDIF
		;恋慕
		IF TALENT:恋慕
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 15
			PRINT 恋慕(15)
			＋表示フラグ = 1
		ELSE
			;男嫌い（恋慕で無効）
			IF TALENT:性別嗜好 & 1 && TALENT:PLAYER:性別 == 2
				PRINT  - 
				実行値 -= 12
				PRINT 男嫌い(12)
				＋表示フラグ = 1
			ENDIF
			;女嫌い（恋慕で無効）
			IF TALENT:性別嗜好 & 2 && TALENT:PLAYER:性別 == 1
				PRINT  - 
				実行値 -= 12
				PRINT 女嫌い(12)
				＋表示フラグ = 1
			ENDIF
		ENDIF
		;セフレ
		IF TALENT:セフレ
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 10
			PRINT セフレ(10)
			＋表示フラグ = 1
		ENDIF
		;未陥落
		IF TALENT:セフレ == 0 && TALENT:恋慕 == 0
			PRINT  - 
			実行値 -= 10
			PRINT 未陥落(10)
			＋表示フラグ = 1
		ENDIF
		IF RFLAG:指示モード対象あなたフラグ == 0
			;処女
			LOCAL = MAX(10, 40 - EXP:Ａ経験)
			IF TALENT:Ａ処女
				PRINT  - 
				実行値 -= LOCAL
				PRINT 
				PRINTFORM Ａ処女({LOCAL})
				＋表示フラグ = 1
			;V経験
			ELSEIF EXP:Ａ経験 < EXPLV:2
				PRINT  - 
				実行値 -= 15
				PRINT Ａ経験不足(15)
				＋表示フラグ = 1
			ENDIF
		ELSE
			;C経験
			IF EXP:Ｃ経験 < EXPLV:2
				PRINT  - 
				実行値 -= 5
				PRINT Ｃ経験不足(5)
				＋表示フラグ = 1
			ENDIF
		ENDIF
		;調教者がふたなり
		IF (TALENT:PLAYER:性別 & 3) == 3
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 4
			PRINT ふたなり(4)
			＋表示フラグ = 1
		ENDIF
		;媚薬
		IF TEQUIP:媚薬
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 6
			PRINT 媚薬(6)
			＋表示フラグ = 1
		ENDIF
		;発情期
		IF CFLAG:発情期フラグ < 0
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 6
			PRINT 発情中(6)
			＋表示フラグ = 1
		ENDIF

		;合計を表示(20以上で実行…？)
		PRINT  = {実行値}
		SIF 実行値 < 必要実行値
			PRINT  < 
		SIF 実行値 == 必要実行値
			PRINT  = 
		SIF 実行値 > 必要実行値
			PRINT  > 
		PRINT 実行値{必要実行値}
		WAIT
		PRINTL

		;ここまでの実行値
		PRINTFORM %CALLNAME:TARGET%の尊厳({実行値})

		;鬼灯の香
		PRINT  + 
		実行値 += 999
		SETCOLOR カラーパレット("赤")
		PRINTFORM 鬼灯の香(999)
		RESETCOLOR

		;再度合計を表示()
		PRINT  = 
		SETCOLOR カラーパレット("赤")
		PRINTFORM {実行値}
		RESETCOLOR
		PRINT  >>>> 
		PRINT 実行値
		PRINTV 必要実行値
		WAIT
		PRINTL

		IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
		;うつ伏せだと寝バック
			CALL PRINT_STR(@"_FS_4_Ａ寝バック_")
		ELSE
		;仰向けだと正常位
			CALL PRINT_STR(@"_FS_4_Ａ正常位_")
		ENDIF
		SETCOLOR カラーパレット("赤")
		PRINTFORML 　Ａ強姦成功
		RESETCOLOR
		SIF TALENT:Ａ処女
			PRINT アナルレイプで
	ELSEIF TFLAG:マッサージ_鬼灯の香使用時間
		PRINTL 鬼灯の香が効いてくるまでは、まだもう少し時間が必要だろう。
		PRINTFORMW %CALLNAME:PLAYER%はまだ待つことにした。
		RETURN -1
	ELSE
		PRINTL このままでは実行出来ない。何か抵抗を封じるための道具が必要になるだろう。
		PRINTFORMW %CALLNAME:PLAYER%は思い直すことにした。
		RETURN -1
	ENDIF
	

ENDIF

;通常モードに移行はターンエンド時に行う
IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
	;うつ伏せだと寝バック
	CALL MASSAGE_COM52_1
ELSE
	;仰向けだと正常位
	CALL MASSAGE_COM52_0
ENDIF


@MASSAGE_COM52_0
;引数は前から快楽量、露出量、接触量
CALL MASSAGE_A体位COM(400, 50, 150)
RETURN RESULT

@MASSAGE_COM52_1
;寝バック
CALL MASSAGE_A体位COM(400, 70, 100)
RETURN RESULT

@MASSAGE_A体位COM(ARG, ARG:1, ARG:2)
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, ARG)
CALL SOURCE_CALC_露出(TARGET, PLAYER, ARG:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, ARG:2)

IF !MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, ARG)
ELSE
	CALL SOURCE_CALC_快Ａ(PLAYER, TARGET, ARG / 3)
ENDIF

;苦痛
LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF
;処女喪失
CALL LOST_A_VIRGIN(TARGET, PLAYER, 1)
CALL LOST_MAN_A_VIRGIN(PLAYER, TARGET)

;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり || TEQUIP:下半身服あり || TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || TEQUIP:下タイツあり
	TEQUIP:下半身下着あり = 0
	TEQUIP:下半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:上半身服あり = 0
	TEQUIP:下腹部後鎧あり = 0
	TEQUIP:下腹部前鎧あり = 0
	TEQUIP:下タイツあり = 0
	RFLAG:コマンド結果受渡し変数２ = 1
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

EXP:Ａ性交経験 ++
EXP:PLAYER:Ｃ性交経験_Ａ挿入 ++

;同性愛
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4

	EXP:愛情経験 += TALENT:恋慕 * 2
	;処女
	SIF TALENT:Ａ処女 && TALENT:恋慕
		EXP:愛情経験 += ARG:2 / 15

RETURN 1

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
@MASSAGE_分岐_COM52(ARG)
#DIM 判定値

;分岐を判定する
;RETURN 1:通常許可
;RETURN 2:なし崩し許可
;RETURN 3:レイプ
;拒否はそもそもコマンドを出さないので分岐しない

IF 陥落チェック(ARG)
	RETURN 1
ENDIF

;性知識-2の場合、親友以上なら基本OK
IF 知識素質:TARGET:性知識 < -1 && CFLAG:好感度レベル > 関係閾値:3
	RETURN 2 
ENDIF

;性交渉お願いの判定を流用する
判定値 = COM353_SUB(ARG)
判定値 += CFLAG:ARG:お願いックス回数 * 30
判定値 += CFLAG:ARG:泥酔ックス回数 * 10

;好感度が無二の友未満である場合、大きく成功度と下限が減少
SELECTCASE CFLAG:ARG:好感度レベル
	CASE IS <= 関係閾値:2
		;顔見知り以下
		判定値 -= 55
		SIF 判定値 > 0
			判定値 /= 2
	CASE IS <= 関係閾値:3
		;友人
		判定値 -= 40
		SIF 判定値 > 0
			LOCAL /= 2
	CASE IS <= 関係閾値:4
		;親友
		判定値 -= 20
		SIF 判定値 > 0
			LOCAL /= 2
ENDSELECT

;理性が無い場合は追加で補正
SIF BASE:理性 <= 0
	判定値 += 20

;判定値が50未満ならレイプ
;緩すぎるか？　条件の数値は様子見する感じで
IF 判定値 >= 50
	RETURN 2
ELSE
	RETURN 3
ENDIF


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE52
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(52)
	RETURN RESULT

;Ａがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:性別 & 2) && !MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
	RETURN 0
;処女かつ潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:3 && TALENT:Ａ処女
	RETURN 0
;処女でなくとも潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0

;理性警戒の合計が1以下じゃないとダメ
;エロマッサージの終着点ポジションなのに、香さえ回れば他のエロマッサージより先に解禁されるのは風情に欠けるため
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM52
#DIM 対象キャラ
#DIM 対象キャラ２
;RFLAG:コマンド結果受渡し変数に結果を入れる
; 1:通常許可
; 2:なし崩し許可
; 3:レイプ

;下着がある場合はRFLAG:コマンド結果受渡し変数２に1が入り、下着を剥ぎ取っている

PRINTL
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がせ、
		ENDIF
		IF PALAM:TARGET:潤滑 > PALAMLV:3
			PRINT とろとろに濡れた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%のアナルに、
		IF MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
			PRINT ペニスバンド
		ELSE
			PRINT 自らの肉棒
		ENDIF
		PRINTL をゆっくりと挿入した……。
	CASE 2
		PRINTFORML %CALLNAME:PLAYER%がどうしてもと頼み込むと、%CALLNAME:TARGET%は迷いながらも『マッサージ』ではなくなることに頷いた。
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がせ、
		ENDIF
		IF PALAM:TARGET:潤滑 > PALAMLV:3
			PRINT とろとろに濡れた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%のアナルに、
		IF MODE_存在判定_ターゲット側("ペニスバンド", PLAYER)
			PRINT ペニスバンド
		ELSE
			PRINT 自らの肉棒
		ENDIF
		PRINTL をゆっくりと挿入した……。

		IF (CFLAG:TARGET:お願いックス回数 + CFLAG:TARGET:泥酔ックス回数 / 3) >= 4
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%と幾度か体を重ね、抵抗感が薄れてきたようだ…
			PRINTFORML %CALLNAME:TARGET%は[セフレ]を得た
			IF GETBIT(RFLAG:コマンド結果受渡し変数４, 1)
				IF GETBIT(TALENT:TARGET:陥落不可, 0)
					PRINTFORML %CALLNAME:TARGET%の[陥落不可]が[恋慕不可]に変化しました。
				ELSE
					PRINTFORML %CALLNAME:TARGET%の[セフレ不可]が消滅しました。
				ENDIF
			ENDIF
		ENDIF
	CASE 3
	;むりやり犯すコマンドから持ってきた
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がし、
		ENDIF

		IF TCVAR:TARGET:マッサージ_うつ伏せフラグ
		;うつ伏せだと寝バック
			PRINTFORML %TEXTR("うつ伏せの/うつ伏せに押さえつけた")%%CALLNAME:TARGET%に%TEXTR("背中から覆いかぶさって/後ろからのしかかって")%、
		ELSE
		;仰向けだと正常位
			PRINTFORM 仰向けのまま%TEXTR("か弱い力で手を突っ張って抵抗する/微かな力でもがいて抵抗しようとする/ぼんやりした頭でイヤイヤとかぶりを振る/ろくな抵抗すらままならない/抵抗力を奪われ言いなりの身となった")%
			PRINTFORML %CALLNAME:TARGET%の%TEXTR("脚を割り開いて/太腿を割り開いて/太腿を掴んで股を広げさせ")%、
		ENDIF

		IF TALENT:TARGET:恋人持ち == 1
			;恋人持ち
			IF TALENT:TARGET:Ａ処女
				PRINTFORM %TEXTR("まだ恋人の侵入も許していない/まだ恋人にも手を付けられていない/恋人に捧げるはずだったであろう/恋人のために膜を残しておいたであろう")%
			ELSE
				PRINTFORM 恋人にしか許してはいけないはずだった
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == 2
			;人妻
				IF TALENT:TARGET:Ａ処女
				PRINTFORM 結婚していながら、いまだ夫の侵入も許していなかった
			ELSE
				PRINTFORM 夫にしか許してはいけないはずだった
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == 3
		;未亡人
			IF TALENT:TARGET:Ａ処女
				PRINTFORM かつての夫に%TEXTR("捧げることすら叶わなかった/操をたてていただろう")%未亡人の
			ELSE
				PRINTFORM 夫を亡くして%TEXTR("男日照り/欲求不満")%だったであろう熟れた
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == -1
		;想い人あり
			IF TALENT:TARGET:Ａ処女
				PRINTFORM %TEXTR("まだ想い人にも侵入を許していない/まだ誰にも手を付けられていない/想い人に捧げるはずだったであろう/想い人のために膜を残しておいたであろう")%
			ELSE
				PRINTFORM 想い人にしか許してはいけないはずだった
			ENDIF
		ELSEIF キャラ一致チェック(TARGET, "[蒼天の剣閃]モニカ") || キャラ一致チェック(TARGET, "[スカイセイバー]リーシャ")
			IF TALENT:TARGET:Ａ処女
				PRINTFORM 秩序の如くかたく守ってきたであろう
			ELSE
				PRINTFORM 秩序の如く乱れてなどいないはずの
			ENDIF
		ELSEIF TALENT:TARGET:種族 == 5 && TALENT:TARGET:Ａ処女
		;星晶獣かつ処女
			PRINTFORM %TEXTR("不老/長命")%の存在でありながら純潔を保ったままの
		ELSEIF TALENT:TARGET:Ａ処女
			IF RAND:3 == 0
				PRINTFORM %CALLNAME:PLAYER%に犯されるために今まで純潔を守ってくれていた
			ELSE
				PRINTFORM %TEXTR("純潔を守っていた/まだ穢れを知らなかった")%
			ENDIF
		ELSE
		;その他　犯す側の、自分勝手であまりにも都合の良い解釈
			PRINTFORM %CALLNAME:PLAYER%%TEXTR("のペニスをいやらしく誘う/の股間をムラつかせる/の獣欲をムラつかせてやまない")%
		ENDIF

		PRINTFORM %CALLNAME:TARGET%の

		;おまんこ冠詞群
		;範囲が狭くシコリティの高そうなものから順につけていく
		IF 知識素質:TARGET:高貴 >= 1
		;高貴持ち
			PRINTFORM %TEXTR("高貴な/ロイヤル")%
		ELSEIF アイドル判定(TARGET)
		;アイドル勢
			PRINTFORM アイドル
		ELSEIF 固有素質チェック(TARGET, "女子高生")
			PRINTFORM 女子高生
		ELSEIF 固有素質チェック(TARGET, "中学生")
			PRINTFORM 中学生
		ELSEIF 固有素質チェック(TARGET, "小学生")
			PRINTFORM 小学生
		ELSEIF 固有素質チェック(TARGET, "サーヴァント")
		;包括して英霊という表現を使っていいものかは……まぁ文句来たら変えればヨシ
			PRINTFORM %TEXTR("英霊/サーヴァント")%
		ELSEIF TALENT:TARGET:ギャル
		;ギャル
			PRINTFORM ギャル
		ELSEIF TALENT:TARGET:子持ち
		;子持ち
			PRINTFORM 経産婦の
		ELSEIF TALENT:TARGET:経験豊富
		;経験豊富
			PRINTFORM %TEXTR("経験豊富な/使いこまれた")%
		ELSEIF TALENT:TARGET:性別嗜好 == 1
		;男嫌い
			PRINTFORM 男嫌いな
		ELSEIF TALENT:TARGET:種族 == 4
		;ハーヴィン
			PRINTFORM ハーヴィンの小さな
		ELSEIF TALENT:TARGET:種族 == 5
		;星晶獣
			IF TALENT:TARGET:Ａ処女
				PRINTFORM %TEXTR("いき遅れ/年増/新古品")%
			ELSE
				PRINTFORM 強大な星晶獣の
			ENDIF
		ELSEIF TALENT:TARGET:年齢層 == 10 || (BASE:TARGET:年齢 <= 13 && BASE:TARGET:年齢 != 0)
		;幼い
			PRINTFORM 幼く未熟な
		ELSEIF TALENT:TARGET:体格 <= -1
		;小柄以下
			PRINTFORM %TEXTR("小さく狭い/狭くてきつい")%
		ELSEIF TALENT:TARGET:Ａ処女 && TALENT:TARGET:性的興味 == 1
		;処女かつ好奇心
			PRINTFORM むっつりスケベな
		ENDIF
		IF RISKY_DAY(TARGET) >= 1
			IF CFLAG:TARGET:発情期フラグ < 0
				PRINTFORM 発情
			ENDIF
		ENDIF
	
		SIF TALENT:TARGET:恋人持ち == 3
			PRINTFORM %TEXTR("未亡人/独り身")%
		SIF TALENT:TARGET:恋人持ち == 2
			PRINTFORM 人妻

		SIF (TALENT:TARGET:年齢層 == 16 && TALENT:TARGET:外見年齢 == 10) || (TALENT:TARGET:年齢層 == 16 && TALENT:TARGET:外見年齢 == 11)		
			PRINTFORM ロリババア
		SIF TALENT:TARGET:Ａ処女
			PRINTFORM %TEXTR("未通/処女/未使用")%
		SIF TALENT:TARGET:年齢層 == 10 || (BASE:TARGET:年齢 <= 13 && BASE:TARGET:年齢 != 0)
			PRINTFORM %TEXTR("ロリ/こども/ガキ")%
	
		PRINTFORM アナルに

		IF MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
			PRINTFORM %TEXTR("ガチガチに勃起し/硬くそそり立っ")%た%TEXTR("陰茎/チンポ/ペニス/肉棒")%
		ELSE
			PRINTFORM ペニスバンド
		ENDIF

		PRINTFORML を%TEXTR("ゆっくり/ぬぷぬぷ")%と%TEXTR("挿入していった/押し込み征服していった/ねじ入れて腹の最奥を踏み荒らした/ねじ入れて尊厳を無残に踏み躙ってやった/ねじ込み征服していった/押し込んでいった/ねじ込んでいった/埋めていった")%…
		PRINTL

		;おまけ
		;キャラにそれ用の口上ができたら自由に消してＯＫ
		IF キャラ一致チェック(TARGET, "[炎舞の紅脚]アリーザ")
			PRINTW 
			PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのようにスタン%TEXTR("の名前を呼んでい/への謝罪を繰り返してい")%
			IF PALAM:欲情 >= PALAMLV:3
				PRINTFORML たが、
				PRINTFORML %CALLNAME:PLAYER%が腰を突き込んでやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
			ELSE
				PRINTFORML る…
			ENDIF
			PRINTW 
			PRINTFORM 　　　　　　　　「%TEXTR("ゴメン、スタン/スタン……アタシ、もう")%……っ
			CALL PRINT_STRL(@"_H_」")
		ELSEIF キャラ一致チェック(TARGET, "[狂恋の華鎧]ヴィーラ")
			対象キャラ２ = キャラ検索("[蒼天の守護騎士]カタリナ・アリゼ")
			PRINTW 
			IF STRFIND(経験相手:対象キャラ２:Ａ挿入初体験, @"-{CFLAG:PLAYER:人物番号}-") >= 0
			;カタリナとヤったことがある場合。ド級の畜生、「ド畜生」である。
				PRINTFORM %CALLNAME:PLAYER%が戯れに
				IF RAND:2 == 0
					PRINTFORML 「%TEXTR("カタリナより気持ちいい/カタリナよりよく締まる")%」と最愛のお姉様との関係を仄めかすと、
				ELSE
					PRINTFORML 「%TEXTR("これならカタリナのほうが気持ちよかった/これならカタリナのほうがよく締まって気持ちよかった")%」と最愛のお姉様をダシにヴィーラを貶すと、
				ENDIF
				PRINTFORML ヴィーラは朦朧と%TEXTR("しながら/甘い声をあげながら/嬌声をあげながら")%も懸命に%CALLNAME:PLAYER%を睨みつけた……
			ELSE
				PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのようにカタリナ%TEXTR("の名前を呼んでい/への謝罪を繰り返してい")%
				IF PALAM:欲情 >= PALAMLV:3
					PRINTFORML たが、
					PRINTFORML %CALLNAME:PLAYER%が腰を突き込んでやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
				ELSE
					PRINTFORML る…
				ENDIF
				PRINTW 
				PRINTFORM 　　　　　　　　「あぁ……お姉、さまぁ……っ
				CALL PRINT_STRL(@"_H_」")
			ENDIF
		ELSEIF キャラ一致チェック(TARGET, "[純白の花嫁]ジュリエット")
			PRINTW 
			PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのようにロミオ%TEXTR("の名前を呼んでい/への謝罪を繰り返してい")%
			IF PALAM:欲情 >= PALAMLV:3
				PRINTFORML たが、
				PRINTFORML %CALLNAME:PLAYER%が腰を突き込んでやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
			ELSE
				PRINTFORML る…
			ENDIF
			PRINTW 
			PRINTFORM 　　　　　　　　「あぁ……ロミオ……ごめんなさい……っ
			CALL PRINT_STRL(@"_H_」")
		ELSEIF キャラ一致チェック(TARGET, "[奮い立つ決意の盾]マシュ・キリエライト")
			PRINTW 
			PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのように「先輩」とやら%TEXTR("を呼んでい/への謝罪を繰り返してい")%
			IF PALAM:欲情 >= PALAMLV:3
				PRINTFORML たが、
				PRINTFORML %CALLNAME:PLAYER%が腰を突き込んでやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
			ELSE
				PRINTFORML る…
			ENDIF
			PRINTW 
			PRINTFORM 　　　　　　　　「あぁ、ぁ……せん、ぱい……
			CALL PRINT_STRL(@"_H_」")
		ELSEIF TALENT:TARGET:恋人持ち == 2
			PRINTW 
			PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのように伴侶への謝罪を繰り返してい
			IF PALAM:欲情 >= PALAMLV:3
				PRINTFORML たが、
				PRINTFORML %CALLNAME:PLAYER%が腰を突き込んでやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
			ELSE
				PRINTFORML る…
			ENDIF
		ENDIF
ENDSELECT

