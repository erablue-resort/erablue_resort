;-------------------------------------------------
;乳腺開発マッサージ
;-------------------------------------------------
@MASSAGE_COMNAME19
;20に排卵のツボとか来てもいいようにこの位置
#FUNCTIONS
TSTR:コマンド名受渡 = 秘孔・発情のツボ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_19
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("マッサージ")

@MASSAGE_COM_TOOLTIP_19
LOCALS = <br>■秘孔・発情のツボ<br>
LOCALS += "マッサージを極めし者のみがたどりつける奥義。"
LOCALS += "<br>優しく、それでいて力強くそのツボを刺激された者は、"
LOCALS += "<br>血の巡りが良くなるとともに性欲が昂進され、肉体が繁殖の本能に目覚めるという……。"
LOCALS += "<br>局部に触れるわけではないためエロマッサージではない。"
LOCALS += "<br>既に発情している相手には実行できない。"
LOCALS += "<br>COMタイプ：マッサージ<br>取得ソース：露出・接触・歓楽"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM19
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)

;性欲をとても煽る。
BASE:TARGET:性欲 += 200

;局部に触れるわけではないため警戒されず、理性がゴリっと削れる
BASE:TARGET:理性 += 理性変動値算出(TARGET, -100)
BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -50)

TCVAR:強制発情フラグ = 1
CFLAG:発情期フラグ = -1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE19
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(19)
	RETURN RESULT

;ＣかＶがない場合駄目。要はロボ想定
SIF TALENT:Ｃ感度 == -2 && TALENT:Ｖ感度 == -2
	RETURN 0

;うつ伏せだとダメ。発情のツボ、あるとしても背中じゃなさそうな気がしない？
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;既に発情してるとダメ。実質一回限り
SIF CFLAG:TARGET:発情期フラグ < 0
	RETURN 0

;マッサージ技能4以下はダメ
SIF ABL:MASTER:マッサージ技能 < 5
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM19
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%のお腹をマッサージしながら、こっそりと発情するツボを刺激した。
PRINTL 
PRINTFORM %CALLNAME:PLAYER%の絶妙な指技によって%TEXTR("体の奥底/本能の隙間")%から
SETCOLOR カラーパレット("えっちな色")
PRINT 発情
RESETCOLOR
PRINTFORML を引き出されてしまった%CALLNAME:TARGET%はその肌にじんわりと汗をにじませ、
PRINTFORML 夢見心地にぼーっと%CALLNAME:PLAYER%に体を揉みほぐされながら、無意識に吐息を熱くし鼓動を高鳴らせている……。

