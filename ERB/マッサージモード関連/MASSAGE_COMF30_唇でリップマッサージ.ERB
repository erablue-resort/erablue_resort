;-------------------------------------------------
;唇でリップマッサージ
;-------------------------------------------------
@MASSAGE_COMNAME30
#FUNCTIONS
TSTR:コマンド名受渡 = 唇でリップマッサージ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_30
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_30
LOCALS = <br>■唇でリップマッサージ<br>
LOCALS += "キスモードには突入しない単発コマンド。"
LOCALS += "お客様の唇を、同じくらい柔らかく繊細な施術具──つまりあなたの唇で『マッサージ』してあげる。"
LOCALS += "<br>あくまでこれはマッサージであってキスではなく、したがって浮気とかにもならないはずである。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：露出・接触・快Ｓ・不潔・苦痛(一定条件時)"
LOCALS += "<br>固有の取得経験：キス経験(PLAYER＆TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM30
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

;誰がどう見たってただのキスなので、キスのコマンドからそのまま持ってきた。
;どう考えてもマッサージじゃないので歓楽は上がらない

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
ENDIF

IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
ENDIF

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

;同性愛
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 3
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 3

SIF TALENT:恋慕
	EXP:愛情経験 += 1

SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF

IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 40
	BASE:TARGET:警戒 -= 40
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「流石にこれはダメなやつなのでは？」と思いはじめる。が、まぁいいかとなる。
;心地いいので警戒と理性は下がり続ける。
	BASE:TARGET:理性 -= 50
	BASE:TARGET:性欲 += 20
	BASE:TARGET:警戒 -= 30
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 400
;実行に際し十分な理性・警戒削りがなされている場合。
;激しい快楽はないが、浮つくような心地の中で唇を奪われて抵抗する気持ちがうまく芽生えない状態。100警戒してるはずなのに、100絆されてプラマイゼロ。
	BASE:TARGET:理性 -= 100
	BASE:TARGET:性欲 += 40
	BASE:TARGET:警戒 += 100
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
ELSE
;実行に際し十分な理性・警戒削りがなされていない場合。
;どう言い訳しようとキスであることは間違いないので警戒と理性が跳ねる。
	BASE:TARGET:理性 += 125
	BASE:TARGET:性欲 += 40
	BASE:TARGET:警戒 += 250
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>エステ中に%CALLNAME:TARGET%の[唇]にファーストキスを捧げた</font><img src='えっちハート'>","うふふ")
ENDIF

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>エステ中に%CALLNAME:PLAYER%の[唇]にファーストキスを奪われた</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE30
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(30)
	RETURN RESULT

;キスモードには入らない単発コマンド

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;理性警戒の合計が700以下じゃないとダメ
;実行自体はできるが、この時点だと警戒と理性が跳ねる。されるがままになっちゃうのは合計値400以下くらいを想定。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 700
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM30

PRINTFORM %CALLNAME:PLAYER%は%TEXTR("「これはマッサージだから」と偽って/「これはマッサージだ」と強弁で押し切って/マッサージにかこつけて")%、
PRINTFORML %CALLNAME:TARGET%%TEXTR("と唇を重ね合わせた/の唇を奪った/の唇を堪能した/の甘い唇を吸った/の唇に吸いついた/の唇を舐めまわした/とのキスを堪能した")%…

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 400
	PRINTW 
	PRINTFORM %CALLNAME:TARGET%は%TEXTR("とろんと目元を蕩けさせ/蕩けた顔でだらしなく口をあけて/法悦に目元を蕩けさせて")%、
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("に唇を差し出してしまっている/自らも唇を捧げてしまっている/とのキスに酔いしれている/にされるがままになっている/のキスをを受け入れている/のキスをを甘受している/とのキスに没頭している")%…
ENDIF
