@PREGNANCY_CHECK(ARG)
#DIM 種親精液量
#DIM 妊娠確率
#DIM 種親
#DIM 命中強度1
#DIM 命中強度2
#DIM 危険度
種親 = -1
命中強度1 = 0
命中強度2 = 0

SIF 妊娠切り替えOPTION記憶
	RETURN 0
SIF TCVAR:ARG:膣内精液量 <= 0
	RETURN 0
SIF TCVAR:ARG:避妊薬
	RETURN 0
SIF TALENT:ARG:妊娠
	RETURN 0
SIF TALENT:ARG:妊娠不可 > 0
	RETURN 0

;種親の確定
FOR LOCAL,0,19
	IF 性交相手:ARG:LOCAL == -1
		CONTINUE
	ELSE
		SIF 種親 == -1
			種親 = 性交相手:ARG:LOCAL
		IF 命中強度1 == 0
			命中強度1 = RAND:(相手ごとの射精量:ARG:LOCAL)
			種親精液量 = 相手ごとの射精量:ARG:LOCAL
		ELSE
			命中強度2 = RAND:(相手ごとの射精量:ARG:LOCAL)
			IF 命中強度2 > 命中強度1
				命中強度1 = 命中強度2
				種親 = 性交相手:ARG:LOCAL
				種親精液量 = 相手ごとの射精量:ARG:LOCAL
			ENDIF
		ENDIF
	ENDIF
NEXT

;超危険日,20回膣内射精で6割、+好条件で確定くらい
妊娠確率 = MIN(20, 種親精液量 / 2 + 10)
SIF CFLAG:ARG:発情期フラグ < 0
	妊娠確率 += 5
;異種妊娠
IF TALENT:種親:種族 != TALENT:ARG:種族 && キャラ一致チェック(ARG, "[六竜の『金』]ガレヲン")
	妊娠確率 -= 5
ENDIF
SIF TALENT:ARG:恋慕
	妊娠確率 += 5
SIF CSTR:ARG:着せ替え服 == "バニー服"
	妊娠確率 += 3
SIF CSTR:ARG:着せ替え服 == "逆バニー"
	妊娠確率 -= 3

危険度 = RISKY_DAY(ARG) + TCVAR:ARG:排卵誘発薬
SIF TCVAR:ARG:排卵誘発薬 && 危険度 <= 0
	危険度 = 1
SELECTCASE 危険度
	CASE 3
		TIMES 妊娠確率, 7.5
	CASE 2
		TIMES 妊娠確率, 4.0
	CASE 1
		TIMES 妊娠確率, 2.0
	CASE 0
		TIMES 妊娠確率, 0.5
	CASE -1
		TIMES 妊娠確率, 0.1
ENDSELECT
LOCAL:0 = RAND:100

DRAWLINE
SETCOLOR カラーパレット("クリーム色")
LOCALS = \@ INRANGE(種親, 0, CHARANUM - 1)? %CALLNAME:種親%# 得体のしれぬ者\@
PRINTFORM %CALLNAME:ARG%の子宮の中
IF TCVAR:ARG:膣内精液量 >= 20
	PRINTFORML は、注ぎこまれた%LOCALS%の精で満たされている……
ELSEIF TCVAR:ARG:膣内精液量 >= 10
	PRINTFORML を、%LOCALS%の精が我が物顔で蹂躙している……
ELSE
	PRINTFORML では、%LOCALS%の精が卵子めがけて必死に泳いでいる……
ENDIF
RESETCOLOR
SIF FLAG_アニメーション
	CALL 受精アニメ(0)
;妊娠した
IF LOCAL:0 <= 妊娠確率 && !TALENT:ARG:妊娠
	IF 種親 <= -1
	ELSE
		CFLAG:ARG:子の父親 = 種親
	ENDIF
	SIF TCVAR:ARG:膣内精液量 >= 15
		PRINT 精液をたっぷりと注がれた
	PRINTFORML %CALLNAME:ARG%の\@RISKY_DAY(ARG)?危険日の#\@\@TALENT:ARG:種族 == 種族_ハーヴィン || TALENT:ARG:体格 == -2?小さな#\@子宮が、痺れるように甘く疼く…
	;MASTERの子を妊娠した時だけ追加で表示
	IF 種親 == MASTER
		IF TALENT:ARG:恋慕 || CFLAG:ARG:恋慕レベル >= 関係閾値:4
			PRINTFORML %CALLNAME:ARG%の卵子は、愛する%CALLNAME:MASTER%の精子を嬉々として受け入れたようだ…
		ELSEIF INRANGE(CFLAG:ARG:恋慕レベル,関係閾値:2,関係閾値:4)
			PRINTFORML %CALLNAME:ARG%の卵子は淡い憧れを抱いている%CALLNAME:MASTER%の精子を拒み切れず、受精を受け入れてしまった…
		ELSEIF CFLAG:ARG:発情期フラグ < 0
			PRINTFORML 発情期の%CALLNAME:ARG%の卵子は勝手に受精の準備を整えており、簡単に%CALLNAME:MASTER%の精子を受け入れてしまった…
		ELSEIF 知識素質:ARG:性知識 == -2
			PRINTFORML %CALLNAME:ARG%の無垢な卵子は、何が起こってるかも分からないままに受精させられてしまった…
		ELSE
			PRINTFORML %CALLNAME:ARG%の卵子は結合を迫る%CALLNAME:MASTER%の精子たちの勢いに流され、\@異種妊娠(ARG)?異種間#\@受精を許してしまった…
		ENDIF
		CFLAG:ARG:MASTERの子供妊娠回数 ++
	ENDIF
	SIF FLAG_アニメーション
		CALL 受精アニメ(1)
	SETCOLOR カラーパレット("ピンク")
	PRINTFORM %CALLNAME:ARG%は
	IF 種親 >= 0
		PRINTFORM %CALLNAME:種親%の
	ELSE
		PRINTFORM 得体のしれぬ相手の
	ENDIF
	PRINTFORMW 子供を妊娠したようです
	RESETCOLOR
	PRINTW
	TALENT:ARG:妊娠 = 1
	EXP:ARG:妊娠経験 ++
	SIF 種親 >= 0
		EXP:種親:孕ませ経験 ++
	CFLAG:ARG:妊娠前体力 = MAXBASE:ARG:体力
	CFLAG:ARG:産後経過日 = 0
ELSE
	PRINTL
ENDIF
TCVAR:ARG:膣内精液量 = 0


;出産 とりあえずの実装
@BIRTHING(MOTHER)
#DIM MOTHER
#DIM FATHER
#DIM DOMINANT
#DIM 子供性別候補, 2
#DIM 子供性別
#DIM 子供人数

FATHER = CFLAG:MOTHER:子の父親
;見た目をよく引き継ぐ側の親を1/2ずつで選択(ただしあなたは優先度低)
IF FATHER < 0 || FATHER == MASTER || (MOTHER != MASTER && RAND:2 == 0)
	DOMINANT = MOTHER
ELSE
	DOMINANT = FATHER
ENDIF

;子供の性別の決定:両親の性別に依存（父親不明の場合は男性扱いとする）
子供性別候補:0 = TALENT:MOTHER:性別
IF FATHER < 0
	子供性別候補:1 = 2
ELSE
	子供性別候補:1 = TALENT:FATHER:性別
ENDIF
子供性別 = 子供性別候補:(RAND:2)

;子供の人数の決定
SELECTCASE TALENT:MOTHER:受胎体質
	CASE 1
		;双子胎
		子供人数 = 1
		SIF RAND:2
			子供人数 = 2
		SIF RAND:16 == 0
			子供人数 = 3
	CASE 2
		;多産
		子供人数 = 5 - SQRT(RAND:16)
	CASE 3
		;畜生胎
		子供人数 = 12 - SQRT(RAND:49)
	CASEELSE
		子供人数 = 1
		SIF RAND:100 == 0
			子供人数 = 2
ENDSELECT

DRAWLINE
IF CFLAG:MOTHER:滞在期間 == -1 && MOTHER != MASTER
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %CALLNAME:MOTHER%が出産したという連絡がありました
	RESETCOLOR

	;子供の姿の描写
	PRINTFORML %子供描写(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)%のようです…
	PRINTL 

	CALL CREATE_CHILDREN(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)
	TRYCALLFORM KOJO_出産_リゾート外_{NO:MOTHER}(MOTHER)
ELSE
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %CALLNAME:MOTHER%は産気づいた…
	RESETCOLOR
	SIF 知識素質:MOTHER:性知識 == -2
		PRINTFORML %CALLNAME:MOTHER%は何が起こったか分からない様子で、破水の痛みに混乱している…
	SIF MOTHER != MASTER
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:MOTHER%を\@ TALENT:MASTER:種族 == 種族_ハーヴィン || TALENT:MOTHER:体格 == -2 ? 支えながら # 抱き上げて \@、医務室へと急いだ…
	PRINTL ……
	PRINTL …
	PRINTL 
	SETCOLOR カラーパレット("ピンク")
	PRINTFORM %CALLNAME:MOTHER%は
	IF (TALENT:MOTHER:種族 == 種族_ハーヴィン || TALENT:MOTHER:体格 == -2) && 異種妊娠(MOTHER)
		PRINTL 大変な苦労の末、なんとか出産を終えました
	ELSE
		PRINTFORML \@ 知識素質:MOTHER:性知識 == -2 ? 大変な混乱と痛みの中で、なんとか # 苦労の末、\@出産を終えました
	ENDIF
	RESETCOLOR

	;子供の姿と母親の反応の描写
	PRINTFORML %子供描写(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)%です…
	IF MOTHER == MASTER
		PRINTFORML %CALLNAME:MOTHER%は優しく自分の子供\@ (子供人数 > 1) ? たち # \@を撫でた…
	ELSE
		PRINTFORML %CALLNAME:MOTHER%は疲れ切った様子ながら優しい微笑みを浮かべ、自らの子供\@ (子供人数 > 1) ? たち # \@を撫でている…
		IF 知識素質:MOTHER:性知識 == -2
			SETCOLOR 0xffd900
			PRINTFORML %CALLNAME:MOTHER%は出産を経て最低限の性知識を身に着けた！
			RESETCOLOR
			知識素質:MOTHER:性知識 = 0
			性知識経験値:MOTHER:0 = 0
		ENDIF
	ENDIF
	PRINTL 

	CALL CREATE_CHILDREN(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)
	TRYCALLFORM KOJO_出産_リゾート内_{NO:MOTHER}(MOTHER)
ENDIF

IF TALENT:MOTHER:子持ち == 0
	PRINTFORM %CALLNAME:MOTHER%は
	SETCOLOR カラーパレット("えっちな色")
	PRINT [子持ち]
	RESETCOLOR
	PRINT を得た！
	TALENT:MOTHER:子持ち = 1
ENDIF

PRINTFORML 
PRINTW
SIF CFLAG:MOTHER:子の父親 == MASTER
	MASTERの子供の数:0 += 子供人数
SIF MOTHER == MASTER
	MASTERの子供の数:1 += 子供人数
TALENT:MOTHER:妊娠 = 0
CFLAG:MOTHER:妊娠経過日 = 0
CFLAG:MOTHER:子の父親 = 0
CFLAG:MOTHER:妊娠発覚 = 0
EXP:MOTHER:出産経験 += 子供人数
CFLAG:MOTHER:産後経過日 = 1


@子供描写(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)
#FUNCTIONS
#DIM MOTHER
#DIM FATHER
#DIM DOMINANT
#DIM 子供性別
#DIM 子供人数

LOCALS '= @"%CALLNAME:DOMINANT%に似て"
;似てる相手が母親でもあなたでもない場合は名前が唐突に出るので説明を一言加える
SIF DOMINANT == FATHER && DOMINANT != MASTER
	LOCALS '= "父親の" + LOCALS

IF GETBIT(子供性別, 1)
	LOCALS += "元気な"
ELSE
	LOCALS += "かわいらしい"
ENDIF

IF 子供人数 > 2
	LOCALS += @"%TOFULL(TOSTR(子供人数))%つ子の"
ELSEIF 子供人数 == 2
	LOCALS += "双子の"
ENDIF

IF 子供性別 == 2
	LOCALS += "男の子"
ELSE
	LOCALS += "女の子"
	SIF 子供性別 != 1
		LOCALS += "？"
ENDIF

SIF 子供人数 >= 2
	LOCALS += "たち"

RETURNF LOCALS


;子供たちのデータ作成
@CREATE_CHILDREN(MOTHER, FATHER, DOMINANT, 子供性別, 子供人数)
#DIM MOTHER
#DIM FATHER
#DIM DOMINANT
#DIM 子供人数
#DIM 子供性別
#DIM 命名フラグ
#DIM 子供ID

命名フラグ = (MOTHER == MASTER || FATHER == MASTER)
IF 命名フラグ
	CALL CHILDREN_NAMING_CHECK(DOMINANT, 子供人数)
	命名フラグ = RESULT
ENDIF

IF 子供人数 >= 2
	FOR LOCAL, 0, 子供人数
		CALL CREATE_CHILD(MOTHER, FATHER, DOMINANT, 子供性別, 命名フラグ, LOCAL + 1)
		SIF LOCAL == 0
			子供ID = RESULT
	NEXT
ELSE
	CALL CREATE_CHILD(MOTHER, FATHER, DOMINANT, 子供性別, 命名フラグ)
	子供ID = RESULT
ENDIF

IF 命名フラグ
	PRINTFORML %GET_PERSON_呼び名(子供ID)%\@ (子供人数 >= 2) ? たち # \@はリゾート併設の保育園で育てられるようです
ELSE
	PRINTFORML 子供\@ (子供人数 >= 2) ? たち # \@は%CALLNAME:MOTHER%の元で育てられるようです
ENDIF

RETURN 子供人数


;子供たちに一人一人名前を付けるかの選択
@CHILDREN_NAMING_CHECK(PARENT, 子供人数)
#DIM PARENT
#DIM 子供人数
#DIM 命名フラグ

PRINTFORML 子供\@ (子供人数 >= 2) ? たち # \@の名前はどうしようか？
PRINTBUTTONLC "[0]よく考える", 0
PRINTBUTTONLC "[1]なんでもいい", 1
BINPUT
SELECTCASE RESULT
	CASE 0
		SIF 子供人数 >= 2
			PRINT 一人ずつ、
		PRINTFORML 自分\@ (PARENT != MASTER) ? と%CALLNAME:PARENT% # \@で考えます
		命名フラグ = 1
	CASE 1
		PRINTL 名前を決めず、以降の登場シーンでは「（○○の）子供」とだけ表記します
		命名フラグ = 0
ENDSELECT

PRINT よろしいですか？　
SETCOLOR カラーパレット("真っ赤")
PRINTL ※後から変更できません！
RESETCOLOR

PRINTBUTTONLC "[0]はい", 0
PRINTBUTTONLC "[1]いいえ", 1
BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 命名フラグ
	CASE 1
		RESTART
ENDSELECT


;子供のデータ作成
@CREATE_CHILD(MOTHER, FATHER, DOMINANT, 子供性別, 命名フラグ, 子供順番 = 0)
#DIM MOTHER
#DIM FATHER
#DIM DOMINANT
#DIM 命名フラグ
#DIM 子供順番
#DIM 子供ID
#DIMS 子供名前
#DIM 子供性別
#DIM 子供種族
#DIM 命名種族
#DIMS 子供プレゼント好み
#DIMS 子供経歴

子供ID = ADD_PERSON()

;性別
CALL SET_PERSON_性別(子供ID, 子供性別)

;種族 親の種族が一致していればその種族、それ以外はその他
IF FATHER < 0 || (TALENT:FATHER:種族 != TALENT:MOTHER:種族)
	子供種族 = 6
	命名種族 = TALENT:DOMINANT:種族
ELSE
	子供種族 = TALENT:MOTHER:種族
	命名種族 = 子供種族
ENDIF
CALL SET_PERSON_種族(子供ID, 子供種族)

;名前/呼び名
子供名前 '= ""
IF 命名フラグ
	CALL CHILD_NAMING(DOMINANT, 子供性別, 命名種族, 子供順番)
	子供名前 '= RESULTS
	CALL SET_PERSON_名前(子供ID, @"[%CALLNAME:MOTHER%の子供]" + 子供名前)
	CALL SET_PERSON_呼び名(子供ID, 子供名前)
ELSE
	CALL SET_PERSON_名前(子供ID, @"[%CALLNAME:MOTHER%の子供]%CALLNAME:MOTHER%の{DAY}" + ((子供順番 > 0) ? @"-{子供順番}" # ""))
ENDIF

;プレゼント好み
STRDATA 子供プレゼント好み
	DATA 食べ物
	DATA 宝石
	DATA アクセサリ
	DATA 実用品
	DATA 本
	DATA 花
ENDDATA
CALL SET_PERSON_プレゼント好み(子供ID, 子供プレゼント好み)

;出会った周回/生年日
CALL SET_PERSON_出会った周回(子供ID, 周回回数累計:0)
CALL SET_PERSON_生年日(子供ID, DAY)

;母親/父親
CALL SET_PERSON_母親(子供ID, CFLAG:MOTHER:人物番号)
SIF FATHER >= 0
	CALL SET_PERSON_父親(子供ID, CFLAG:FATHER:人物番号)

;経歴
子供経歴 '= @"%CALLNAME:MOTHER%が"
SIF 周回回数累計:0 > 0
	子供経歴 += @"{周回回数累計:0 + 1}周目の"
LOCALS = %月算出()%
子供経歴 += @"{RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)に産んだ"
CALL SET_PERSON_経歴(子供ID, 子供経歴)

RETURN 子供ID


@CHILD_NAMING(PARENT, 子供性別, 命名種族, 子供順番)
#DIM PARENT
#DIM 子供性別
#DIM 命名種族
#DIM 子供順番
#DIMS 子供名前
#DIM ランダムネームフラグ

PRINTFORML \@ (子供順番 > 0) ? {子供順番}人目の # \@名前は…
PRINTBUTTONLC "[0]自分で考える", 0
IF PARENT != MASTER
	PRINTBUTTONLC @"[1]%CALLNAME:PARENT%に考えてもらう", 1
ELSE
	PRINTBUTTONLC "[1]ふと頭に浮かんできた名前にする", 1
ENDIF
BINPUT
SELECTCASE RESULT
	CASE 0
		PRINTL 名前を入力してください。（９文字まで）
		INPUTS
		IF RESULTS == ""
			PRINTL 名前が入力されていません
			RESTART
		ENDIF
		子供名前 = %SUBSTRING(RESULTS, 0, 18)%
		PRINTFORML %子供名前%でよろしいですか？
		PRINTBUTTONLC "[0]はい", 0
		PRINTBUTTONLC "[1]いいえ", 1
		BINPUT
		SIF RESULT == 1
			RESTART
	CASE 1
		ランダムネームフラグ = 0
		$INPUT_LOOP1
		TRYCCALLFORM 固有子供命名設定_{NO:PARENT}(PARENT, 子供性別, 子供順番)
			;RESULTSに命名が返ってくる
			子供名前 = %RESULTS%
			SIF ランダムネームフラグ
				子供名前 = %ランダムキャラ名前生成(命名種族, 子供性別)%
		CATCH
			子供名前 = %ランダムキャラ名前生成(命名種族, 子供性別)%
		ENDCATCH
		PRINTFORML %CALLNAME:PARENT%は%子供名前%と名付けたいようだ…
		PRINTBUTTONLC "[0]それにする", 0
		PRINTBUTTONLC "[1]別の名前を考えてもらう", 1
		PRINTBUTTONLC "[2]やっぱり自分で考えようかな", 2
		BINPUT
		IF RESULT == 1
			ランダムネームフラグ = 1
			GOTO INPUT_LOOP1
		ENDIF
		SIF RESULT == 2
			RESTART
ENDSELECT
SIF PARENT != MASTER
	PRINTFORM %CALLNAME:PARENT%と相談した結果、
PRINTFORML 子供の名前は%子供名前%になりました！
RESULTS '= 子供名前
RETURN


;危険日とかの設定 一日の初めに呼ばれる
@SET_PREGNANCY_PALAM(ARG)
#DIM 消耗度_体力
;危険日がない場合セット

IF RISKY_DAY(ARG) != -2 && !INRANGE(CFLAG:ARG:危険日,1,10)
	CFLAG:ARG:危険日 = RAND:10 + 1
ENDIF
;性交相手のリセット
VARSET 性交相手:ARG:0, -1
VARSET 相手ごとの射精量:ARG:0
SIF !TALENT:ARG:妊娠 && CFLAG:ARG:子の父親 != -1
	CFLAG:ARG:子の父親 = -1
;疲れる処理（臨月、産後）
IF 妊娠時疲労(ARG) || (CFLAG:ARG:産後経過日 >= PREG_DAY(8) && CFLAG:ARG:妊娠前体力)
	消耗度_体力 = 100 * BASE:ARG:体力 / MAXBASE:ARG:体力
	MAXBASE:ARG:体力 = CFLAG:ARG:妊娠前体力  - 妊娠時疲労(ARG,1)
	BASE:ARG:体力 = MAXBASE:ARG:体力 * 消耗度_体力 / 100
	IF !妊娠時疲労(ARG)
		CFLAG:ARG:妊娠前体力 = 0
	ENDIF
ENDIF


;妊娠の進行による諸々 一日の終わりに呼ばれる
@PREGNANCY_COUNTUP(ARG)
#DIM 変化前バスト
#DIM ループ回数

;妊娠知覚イベント
CALL DETECT_PREGNANCY(ARG)
;素の体力を更新
IF 妊娠時疲労(ARG)
	CFLAG:ARG:妊娠前体力 = MAXBASE:ARG:体力 + 妊娠時疲労(ARG,1)
ENDIF
;色々な経過日時のカウント
;妊娠経過日 妊娠後それなりに経つと母乳が出る
IF TALENT:ARG:妊娠
	CFLAG:ARG:妊娠経過日  ++
	IF !CFLAG:ARG:妊娠時バストサイズ増加 && !素質切り替えOPTION記憶:母乳体質_なし && PREGNANCY_PROGRESS(ARG) >= 3
		CFLAG:ARG:妊娠時バストサイズ増加 = 1
		;元から最大の場合のフラグ立て
		SIF TALENT:ARG:バストサイズ == バストサイズ_最大
			CFLAG:ARG:妊娠時バストサイズ増加 = 2
		変化前バスト = BASE:ARG:バスト
		;一旦、どのケースでもTALENTを大きくする
		TALENT:ARG:バストサイズ ++
		;BASEが実際に大きくなるまでループ
		ループ回数 = 0
		DO
			CALL SET_BODYSIZE(ARG,2)
			;なんか知らんけどここで無限ループになるパターンがあるっぽいので１００回ループしてもダメなら変化前バスト+1にする
			ループ回数 ++
			IF ループ回数 > 100
				BASE:ARG:バスト = 変化前バスト + 1
				BREAK
			ENDIF
		LOOP BASE:ARG:バスト < 変化前バスト
		TALENT:ARG:バストサイズ = LIMIT(TALENT:ARG:バストサイズ, バストサイズ_最小, バストサイズ_最大)

		SIF TALENT:ARG:母乳体質 != 1
			TALENT:ARG:母乳体質 = 3
		IF CFLAG:ARG:滞在期間 >= 0
			DRAWLINE
			IF TALENT:ARG:バストサイズ == 3
				PRINTFORML 母乳をたっぷりと湛えた%CALLNAME:ARG%の乳房は、もはや妊娠前とは見違えたサイズになっている…
			ELSE
				PRINTFORML 妊娠して腹部が目立つようになってきた%CALLNAME:ARG%の乳房から、母乳が滲み出てきた…
			ENDIF
			SETCOLOR カラーパレット("ピンク")
				PRINTFORML %CALLNAME:ARG%の乳房は母乳を蓄えるようになり、より大きくなりました
			RESETCOLOR
			PRINTW
		ENDIF
	ENDIF
	SIF CFLAG:ARG:妊娠経過日 >= 妊娠期間
		CALL BIRTHING(ARG)
;産後経過日 産後それなりに経つと母乳が止まる
ELSEIF CFLAG:ARG:産後経過日
	CFLAG:ARG:産後経過日 ++
	IF CFLAG:ARG:妊娠時バストサイズ増加 && CFLAG:ARG:妊娠前体力 == 0 && CFLAG:ARG:産後経過日 > PREG_DAY(25 + (MAX(5,TALENT:ARG:バストサイズ * 10)))
		IF CFLAG:ARG:妊娠時バストサイズ増加
			;元が魔乳未満の場合、TALENTも小さくする（魔乳の場合は下駄履かせて計算させてるのでそのままでOK）
			SIF CFLAG:ARG:妊娠時バストサイズ増加 == 1
				TALENT:ARG:バストサイズ --
			;BASEが実際に小さくなるまでループ
			変化前バスト = BASE:ARG:バスト
			DO
				CALL SET_BODYSIZE(ARG,2)
			LOOP BASE:ARG:バスト > 変化前バスト
		ENDIF
		TALENT:ARG:バストサイズ = LIMIT(TALENT:ARG:バストサイズ, バストサイズ_最小, バストサイズ_最大)
		CFLAG:ARG:妊娠時バストサイズ増加 = 0

		SIF TALENT:ARG:母乳体質 != 1
			TALENT:ARG:母乳体質 = 0
		IF CFLAG:ARG:滞在期間 >= 0
			DRAWLINE
			PRINTFORML 授乳を終えた%CALLNAME:ARG%の乳房は元の大きさに戻り\@TALENT:ARG:母乳体質 != 1?、母乳の分泌も止まり#\@ました
			PRINTW
		ENDIF
		CFLAG:ARG:産後経過日 = 0
	ENDIF
ENDIF


;妊娠時疲労計算処理
;ARG:0 キャラ
;ARG:1 0(default)=妊娠時疲労の存在判定 1=計算した体力を返す
@妊娠時疲労(ARG:0,ARG:1 = 0)
#FUNCTION
#DIM 倍率
倍率 = 0
IF INRANGE(CFLAG:(ARG:0):産後経過日, 1, PREG_DAY(3))
	倍率 = 30
ELSEIF PREGNANCY_PROGRESS(ARG:0) >= 5
	倍率 = 20
ELSEIF PREGNANCY_PROGRESS(ARG:0) == 4 || INRANGE(CFLAG:(ARG:0):産後経過日, PREG_DAY(4), PREG_DAY(7))
	倍率 = 10
ELSE
	倍率 = 0
ENDIF
SELECTCASE ARG:1
	CASE 0
		SIF 倍率
			RETURNF 1
		RETURNF 0
	CASE 1
		RETURNF CFLAG:(ARG:0):妊娠前体力 * 倍率 / 100
	CASEELSE
		RETURNF 0
ENDSELECT


;危険日と安全日の算出
;戻り値: 超危険日2,危険日1,通常日0,安全日-1,妊娠しない-2
@RISKY_DAY(ARG)
#FUNCTION
#DIM C_危険日
#DIM C_安全日
#DIM 妊娠危険度

IF 発情期判定(ARG) == 0
	SIF GETBIT(TALENT:ARG:性別, 0) == 0
		RETURNF -2
	SIF TALENT:ARG:Ｖ感度 == -2
		RETURNF -2
ENDIF
SIF TALENT:ARG:妊娠不可
	RETURNF -2

C_危険日 = CFLAG:ARG:危険日 % 10
C_安全日 = (CFLAG:ARG:危険日 + 4) % 10
IF C_危険日 == DAY % 10
	妊娠危険度 = 2
ELSEIF (DAY-1) % 10 == C_危険日 || (DAY+1) % 10 == C_危険日
	妊娠危険度 = 1
ELSEIF DAY % 10 == C_安全日 || (DAY+1) % 10 == C_安全日 || (DAY+2) % 10 == C_安全日
	妊娠危険度 = -1
ELSE
	妊娠危険度 = 0
ENDIF
RETURNF 妊娠危険度


;妊娠の進行度
;0=非妊娠 1=見た目変化なし（2ヶ月） 2=僅かに腹部に変化（4ヶ月） 3=明確に腹部が膨らみ始める（5ヶ月半） 4=妊婦 5=臨月
@PREGNANCY_PROGRESS(ARG)
#FUNCTION
#DIM 進行度
IF CFLAG:ARG:妊娠経過日 > PREG_DAY(85)
	進行度 = 5
ELSEIF CFLAG:ARG:妊娠経過日 > PREG_DAY(70)
	進行度 = 4
ELSEIF CFLAG:ARG:妊娠経過日 > PREG_DAY(55)
	進行度 = 3
ELSEIF CFLAG:ARG:妊娠経過日 > PREG_DAY(40)
	進行度 = 2
ELSEIF CFLAG:ARG:妊娠経過日 > PREG_DAY(20)
	進行度 = 1
ELSE
	進行度 = 0
ENDIF
RETURNF 進行度


;特殊な状況でお腹が大きくなる場合なども含めた最終的なお腹の大きさ
@PREGNANCY_SIZE(ARG)
#FUNCTION
#DIM 進行度
進行度 = PREGNANCY_PROGRESS(ARG)
;異種妊娠したハーヴィンの場合、お腹が一段階大きくなる
SIF 異種妊娠(ARG) && (TALENT:ARG:種族 == 種族_ハーヴィン || TALENT:ARG:体格 == -2) && CFLAG:ARG:妊娠経過日 > PREG_DAY(10)
	進行度 ++
RETURNF 進行度


@異種妊娠(ARG)
#FUNCTION
SIF CFLAG:ARG:子の父親 == -1
	RETURNF 0
SIF TALENT:ARG:種族 != TALENT:(CFLAG:ARG:子の父親):種族
	RETURNF 1
RETURNF 0


;妊娠発覚
@DETECT_PREGNANCY(ARG)
IF CFLAG:ARG:滞在期間 < 0
	RETURN 0
ELSEIF PREGNANCY_PROGRESS(ARG) >= 2 && CFLAG:ARG:妊娠発覚 == 0
	IF 知識素質:ARG:性知識 == -2
		;無知シチュ
		DRAWLINE
		PRINTFORML %CALLNAME:ARG%が%CALLNAME:MASTER%に話しかけてきた…
		PRINTFORML %CALLNAME:ARG%はふっくらと膨らみつつある自身のお腹を不思議に思っているようだ…
		PRINTFORML 腹部はささやかながら明確に膨らんでおり、新たな生命がその奥で芽吹いているのは明らかだ…
		TRYCALLFORM KOJO_無知_妊娠発覚_1_{NO:ARG}(ARG)
		PRINTBUTTONLC "[0]正しい性知識を教える", 0
		PRINTBUTTONLC "[1]誤魔化す", 1
		$INPUT_LOOP
		INPUT
		IF RESULT == 0
			PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ARG%に、そのお腹の中には%CALLNAME:ARG%の赤ちゃんが居ると教えた
			PRINTFORML %CALLNAME:ARG%は自分が既に母となりつつある事にとても驚いているようだ…
			TRYCALLFORM KOJO_無知_妊娠発覚_2_{NO:ARG}(ARG)
			SETCOLOR 0xffd900
			PRINTFORML %CALLNAME:ARG%は最低限の性知識を身に着けた！
			RESETCOLOR
			知識素質:ARG:性知識 = 0
			性知識経験値:ARG:0 = 0
		ELSEIF RESULT == 1
			PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ARG%の頭を撫で、何も問題はないと誤魔化した
			PRINTFORML %CALLNAME:ARG%は安心したように頷き、自分の部屋へと帰った…
			TRYCALLFORM KOJO_無知_妊娠発覚_3_{NO:ARG}(ARG)
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF TALENT:ARG:恋慕
		;恋慕時
		DRAWLINE
		PRINTFORML %CALLNAME:ARG%が%CALLNAME:MASTER%に話しかけてきた…
		PRINTFORML %CALLNAME:ARG%はふっくらと膨らみつつある自身のお腹を愛おしそうに撫でている…
		PRINTFORML 腹部はささやかながら明確に膨らんでおり、新たな生命がその奥で芽吹いているのは明らかだ
		TRYCALLFORM KOJO_恋慕_妊娠発覚_{NO:ARG}(ARG)
	ELSE
		;その他時
		DRAWLINE
		PRINTFORML %CALLNAME:ARG%が%CALLNAME:MASTER%に話しかけてきた…
		PRINTFORML %CALLNAME:ARG%はふっくらと膨らみつつある自身のお腹を抱えている…
		PRINTFORML 腹部はささやかながら明確に膨らんでおり、新たな生命がその奥で芽吹いているのは明らかだ
		TRYCALLFORM KOJO_その他_妊娠発覚_{NO:ARG}(ARG)
	ENDIF
	PRINTW
	CFLAG:ARG:妊娠発覚 = 1
ENDIF


;招待されたキャラが孕まされてからお腹が大きくなるまでリゾートに来てなかったら特殊メッセージが出る
@招待時妊娠メッセージ(ARG,ARG:1 = 0)
#DIM MEMBER,10
#DIM NUM = 1
IF ARG:1 == 1
	SELECTCASE NUM - 1
		CASE 1
			SETCOLOR カラーパレット("ピンク")
			PRINTFORML %CALLNAME:(MEMBER:1)%のお腹が大きく膨らんでいる…！
			IF 知識素質:(MEMBER:1):性知識 < 0 || TALENT:(MEMBER:1):無関心 > 0
				PRINTFORML %CALLNAME:(MEMBER:1)%は何事もないかのように挨拶をしてきたが、どうしてもお腹に目が行ってしまう…
			ELSEIF TALENT:(MEMBER:1):恋慕 && RAND:2
				PRINTFORML %CALLNAME:(MEMBER:1)%は満面の笑みを浮かべて%CALLNAME:MASTER%に抱き着いた…
			ELSE
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML %CALLNAME:(MEMBER:1)%は恥ずかしそうに%CALLNAME:MASTER%に手を振った
					CASE 1
						PRINTFORML 妊婦らしい服装をした%CALLNAME:(MEMBER:1)%の姿が新鮮で、%CALLNAME:MASTER%はつい目を惹かれてしまった
					CASE 2
						PRINTFORML %CALLNAME:MASTER%が%CALLNAME:(MEMBER:1)%のお腹を撫でると、%CALLNAME:(MEMBER:1)%は仕方なさそうに微笑んで身体を寄せてきた…
				ENDSELECT
			ENDIF
		CASE 2
			SETCOLOR カラーパレット("ピンク")
			PRINTFORML %CALLNAME:(MEMBER:1)%と%CALLNAME:(MEMBER:2)%のお腹は大きく膨らんでいる…！
			PRINTFORML %CALLNAME:(MEMBER:1)%と%CALLNAME:(MEMBER:2)%はそれぞれのお腹を押し付けるように、%CALLNAME:MASTER%を左右で挟んでハグしてくれた
		CASE IS >= 3
			SETCOLOR カラーパレット("ピンク")
			LOCALS:0 = 
			FOR LOCAL,1,10
				IF MEMBER:LOCAL
					SIF LOCAL != 1
						LOCALS:0 += "と"
					LOCALS:0 += CALLNAME:(MEMBER:LOCAL)
				ENDIF
			NEXT
			PRINTFORML %LOCALS:0%のお腹は大きく膨らんでいる…
			PRINTFORML %CALLNAME:(MEMBER:1)%達は%CALLNAME:MASTER%にそれぞれのお腹を撫でさせてくれた
	ENDSELECT
	RESETCOLOR
	IF NUM > 1
		FOR LOCAL, 1, NUM
			TRYCCALLFORM KOJO_来島時_妊娠発覚_{NO:(MEMBER:NUM)}(MEMBER:NUM)
				DRAWLINE
			CATCH
			ENDCATCH
		NEXT
	ENDIF
	VARSET MEMBER
	NUM = 1
ELSE
	IF !MEMBER:NUM && CFLAG:ARG:妊娠発覚 == 0 && PREGNANCY_PROGRESS(ARG) >= 3
		MEMBER:NUM = ARG
		NUM ++
		CFLAG:ARG:妊娠発覚 = 1
	ENDIF
ENDIF


;膣内射精した相手の取得
@TAKE_SPERM(ARG,射精した人)
#DIM 射精した人
TCVAR:ARG:膣内精液量 += RFLAG:フィニッシュフラグ
FOR LOCAL,0,19
	SIF 性交相手:ARG:LOCAL == -1
		性交相手:ARG:LOCAL = 射精した人
	IF 性交相手:ARG:LOCAL == 射精した人
		相手ごとの射精量:ARG:LOCAL += RFLAG:フィニッシュフラグ
		BREAK
	ENDIF
NEXT

;妊娠の経過度を百分率で返す
@PREG_DAY(ARG)
#FUNCTION
RETURNF MAX(1,ARG * 妊娠期間 / 100)
