;-------------------------------------------------------------------------------------------
;射精先の選択肢
;-------------------------------------------------------------------------------------------
@オート射精先設定
;射精先をいちいち選ばなくてもいいように設定する
DRAWLINE
PRINTL 射精時、選択なしで射精する設定を行います。
PRINTL 設定されている射精先が選択可能な場合、自動的にその射精先を選択します。
PRINTL 複数の選択可能な射精先が設定されている場合、優先度の数字が小さい方が優先して選択されます。
DRAWLINE

FOR LOCAL, 0, 31
	IF オート射精先オプション:LOCAL == -1
		PRINTBUTTON @"[{LOCAL}] オート射精先を設定する", LOCAL
		PRINTL
		BREAK
	ELSE
		TSTR:コマンド名受渡 = 
		TRYCALLFORM SHOOT_NAME_{オート射精先オプション:LOCAL}(-1)
		SIF TSTR:コマンド名受渡 == ""
			CONTINUE

		PRINTFORM 優先度{LOCAL}：%TSTR:コマンド名受渡, 20, LEFT%　　
		IF LOCAL > 0
			PRINTBUTTON "[↑]", LOCAL + 100
		ELSE
			PRINT     
		ENDIF
		IF LOCAL < 30 && オート射精先オプション:(LOCAL + 1) > -1
			PRINTBUTTON "[↓]", LOCAL + 200
		ELSE
			PRINT     
		ENDIF
		PRINT 　　
		PRINTBUTTON "[変更]", LOCAL
		PRINT   
		PRINTBUTTON "[解除]", LOCAL + 300
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTBUTTON @"[999] 戻る", 999
PRINTL
DRAWLINE
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE IS < 100
		DRAWLINE
		FOR LOCAL:1, 0, 31
			TSTR:コマンド名受渡 = 
			TRYCALLFORM SHOOT_NAME_{LOCAL:1}(-1)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			PRINTBUTTON @"[{LOCAL:1}] %TSTR:コマンド名受渡%", LOCAL:1
			PRINTL
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 戻る", 999
		PRINTL
		DRAWLINE
		BINPUT
		IF RESULT != 999
			オート射精先オプション:LOCAL = RESULT
		ENDIF
	CASE IS < 200
		;上に移動
		SWAP オート射精先オプション:(LOCAL - 100), オート射精先オプション:(LOCAL - 101)
	CASE IS < 300
		;下に移動
		SWAP オート射精先オプション:(LOCAL - 200), オート射精先オプション:(LOCAL - 199)
	CASEELSE
		ARRAYSHIFT オート射精先オプション, -1, -1, LOCAL - 300
ENDSELECT

RESTART


;-------------------------------------------------------------------------------------------
;SHOOT_0:膣内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_0(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 膣内射精
RETURN 1


@SHOOT_PALAM_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｖ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "膣内射精回数")
履歴用実績:対象キャラ:膣内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):膣内射精フラグ = 1

CALL SOURCE_CALC_快Ｖ(対象キャラ, 実行キャラ, 200 * (RCVAR:対象キャラ:被精液濃縮 / 4 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;妊娠処理用に膣内射精された精液を計上
CALL TAKE_SPERM(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)


@SHOOT_ABLE_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;選択肢に出る条件
;Ｖ挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ｖ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＶ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の膣奥で
;以上元部分 以下加筆部分
PRINTFORM %CALLNAME:実行キャラ%は
IF CFLAG:対象キャラ:睡眠
	PRINTFORM 眠った
ELSEIF TCVAR:対象キャラ:泥酔眠姦
	PRINTFORM 酔いつぶれた
ENDIF
PRINTFORM %CALLNAME:(対象キャラ)%の
PRINTDATA
	DATA 膣奥
;	DATA 最奥
;	DATA 肉穴の最奥
;教えの最奥がチラつく
	DATA 膣の一番奥
	DATA 最も深い場所
	DATA 一番深い場所
ENDDATA
SELECTCASE RAND:2
	CASE 0
		PRINT に
		IF RAND:2 == 0
			PRINTDATA
				DATA 亀頭を
				DATA ペニスの先端を
			ENDDATA
			PRINTDATA
				DATA べったりと
				DATA ぴったりと
				DATA ぐいぐいと
				DATA ぐりぐりと
				DATA 
			ENDDATA
			PRINT 押し付けながら
		ENDIF
	CASEELSE
		PRINT で
ENDSELECT
IF RFLAG:フィニッシュフラグ > 1
	PRINTDATA
		DATA 大量の
		DATA 濃厚な
		DATA おびただしい量の
	ENDDATA
ENDIF
PRINTDATA
	DATA 精液
	DATA 子種
;	DATA 種汁
;オラッ！APとBP満タンにしてやる！
	DATA ザーメン
ENDDATA
PRINT を
PRINTDATA
	DATA 吐き出した
	DATA 吐き散らした
	DATA 放った
ENDDATA
PRINT …
PRINTL 
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 1

;-------------------------------------------------------------------------------------------
;SHOOT_1:腸内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_1(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 腸内射精
RETURN 1


@SHOOT_PALAM_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ａ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "腸内射精回数")
履歴用実績:対象キャラ:腸内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):腸内射精フラグ = 1

CALL SOURCE_CALC_快Ａ(対象キャラ, 実行キャラ, 200 * (RCVAR:対象キャラ:被精液濃縮 / 4 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;腸内射精された精液を計上
CALL TAKE_SPERM_A(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)

@SHOOT_ABLE_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;A挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ａ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＡ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＡ", 実行キャラ, 対象キャラ) 
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＡ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の腸内で
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 2

;-------------------------------------------------------------------------------------------
;SHOOT_2:口
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_2(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 口内射精
RETURN 1


@SHOOT_PALAM_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｓ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "口内射精回数")
履歴用実績:対象キャラ:口内射精回数 += 1

;ここで精飲するかどうかをRCVAR:(対象キャラ):精飲フラグで渡す
;喜んで飲む=2 なんとか飲み込む=1 吐き出す=0
RCVAR:(対象キャラ):精飲フラグ = 精飲フラグ(対象キャラ)
IF RCVAR:(対象キャラ):精飲フラグ > 0
	EXP:(対象キャラ):精飲経験 += RFLAG:フィニッシュフラグ
	;ミルク濃厚率を上げる
	;濃厚率補正は30％まで
	SIF TCVAR:対象キャラ:ミルク濃厚率補正 < 30
		TCVAR:対象キャラ:ミルク濃厚率補正 += 10
ENDIF

;精飲時、絶頂していると精飲快楽経験取得
IF RCVAR:(対象キャラ):絶頂の強度 && RCVAR:(対象キャラ):精飲フラグ
	EXP:(対象キャラ):精飲快楽経験 += RCVAR:(対象キャラ):絶頂の強度
ENDIF

CALL SOURCE_CALC_快Ｓ(対象キャラ, 実行キャラ, 100 * (RCVAR:対象キャラ:被精液濃縮 / 7 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)


@SHOOT_ABLE_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;フェラチオ系モードじゃない時はアウト
SIF MODE_存在判定_完全一致("フェラチオ系", 対象キャラ, 実行キャラ)
	RETURN 1
;パイズリ系モードなら技巧3以上かつ飲んでくれる場合のみOK
SIF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && 精飲フラグ(対象キャラ) && ABL:(対象キャラ):技巧 > 2 && !MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 1
RETURN 0


@MESSAGE_SHOOT_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF TCVAR:対象キャラ:泥酔眠姦
	PRINTFORM %CALLNAME:実行キャラ%は酔いつぶれた%CALLNAME:(対象キャラ)%の口にペニスを押し込み、
ELSEIF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	PRINTFORML %CALLNAME:(対象キャラ)%は射精の兆候を感じ取ると、すかさず%CALLNAME:実行キャラ%のペニスを咥えこんだ
	PRINTFORM %CALLNAME:実行キャラ%はそのまま%CALLNAME:(対象キャラ)%の口に
ELSE
	PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の口に
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
SELECTCASE RCVAR:(対象キャラ):精飲フラグ
	CASE 0
		IF ABL:対象キャラ:技巧 > 2
			IF TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
				PRINTFORML 朦朧としている%CALLNAME:対象キャラ%は、口内に出された精液をそのまま吐き出してしまった
			ELSE
				PRINTFORML %CALLNAME:対象キャラ%は手慣れた様子で口内の精液をティッシュに吐き出した
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%は咳き込んで、口内に出された精液を吐き出してしまった
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:対象キャラ%は慣れない様子で口内の精液を、少しずつ飲み下した
	CASE 2
		IF 知識素質:対象キャラ:性知識 > 1
			PRINTFORM %CALLNAME:対象キャラ%は口に出された精液を飲み下した証を見せるように、
			PRINTL 口を開いて全部飲み干した咥内を見せてきた
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%は喉を鳴らし、口内の精液を飲み下した
		ENDIF
ENDSELECT
TCVAR:(対象キャラ):被射精位置 = 3

;-------------------------------------------------------------------------------------------
;SHOOT_3:乳内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_3(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 乳内射精
SIF 対象キャラ < 0 || TALENT:対象キャラ:バストサイズ < 1
	TSTR:コマンド名受渡 = 胸に射精
RETURN 1


@SHOOT_PALAM_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_快Ｂ(対象キャラ, 実行キャラ, 10 * (RCVAR:対象キャラ:被精液濃縮 / 7 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):Ｂ経験 += 1
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "乳内射精回数")
履歴用実績:対象キャラ:乳内射精回数 += 1

@SHOOT_ABLE_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の
SELECTCASE TALENT:(対象キャラ):バストサイズ
	CASE IS < 1
		PRINT 胸に
	CASE 1
		PRINT 乳の谷間に
	CASE 2
		PRINT 大きな乳の谷間に
	CASE 3
		PRINT とても豊かに育った乳の谷間に
ENDSELECT
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
TCVAR:(対象キャラ):被射精位置 = 4

;-------------------------------------------------------------------------------------------
;SHOOT_5:顔射
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_5(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 顔射
RETURN 1


@SHOOT_PALAM_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "顔射回数")
履歴用実績:対象キャラ:顔射回数 += 1


@SHOOT_ABLE_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードでも口戯系モードでもない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && !MODE_存在判定_完全一致("口戯系", 対象キャラ, 実行キャラ)
	RETURN 0
;縦パイズリは駄目
SIF MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の顔に向かって
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
TCVAR:(対象キャラ):被射精位置 = 5

;-------------------------------------------------------------------------------------------
;SHOOT_6:腹射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_6(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = お腹に出す
RETURN 1


@SHOOT_PALAM_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;仰向けの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_順不同("兜合わせ", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("ボテ腹ズリ", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の\@PREGNANCY_SIZE(対象キャラ) >= 3?ボテ腹#へそ\@に向かって
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
TCVAR:(対象キャラ):被射精位置 = 6

;-------------------------------------------------------------------------------------------
;SHOOT_7:背中射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_7(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 背中に出す
RETURN 1


@SHOOT_PALAM_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;うつぶせの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の背中に向かって
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
TCVAR:(対象キャラ):被射精位置 = 7

;-------------------------------------------------------------------------------------------
;SHOOT_8:太ももに射精
;-------------------------------------------------------------------------------------------

@SHOOT_NAME_8(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 太ももに射精
SIF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	TSTR:コマンド名受渡 = 太ももに中出し
RETURN 1


@SHOOT_PALAM_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;素股、疑似性交と足が閉じてる体位
LOCAL = 0
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("素股", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:対象キャラ%の
IF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	PRINT むちむちの太ももをぴったりと閉じさせると、その中に
ELSE
	PRINT 太ももに向かって
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…
TCVAR:(対象キャラ):被射精位置 = 8

;-------------------------------------------------------------------------------------------
;SHOOT_9:手のひらに出す
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_9(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 手のひらに出す
RETURN 1


@SHOOT_PALAM_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;手コキ系モードじゃない時はアウト
LOCAL = 0
SIF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ｃ愛撫", 対象キャラ, 実行キャラ) && GETBIT(TALENT:実行キャラ:性別, 1)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2 && TCVAR:対象キャラ:泥酔眠姦 != 1
	PRINTFORM %CALLNAME:(対象キャラ)%は射精の兆候を感じ取ると、%CALLNAME:実行キャラ%の
	PRINTDATA
		DATA 亀頭を
		DATA ペニスの先端を
	ENDDATA
	PRINTL 手のひらで包み込んだ
	PRINTFORM %CALLNAME:実行キャラ%はそのまま%CALLNAME:(対象キャラ)%の手のひらの中に
ELSE
	PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の手のひらに
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER
	PRINTFORM %CALLNAME:対象キャラ%は手のひらを広げ、
	IF RFLAG:フィニッシュフラグ >= 3
		PRINT 手から溢れ出すほどおびただしい量の
	ELSEIF RFLAG:フィニッシュフラグ > 1
		PRINT べっとりとこびりついた
	ELSE
		PRINT 広がった
	ENDIF
	IF TCVAR:対象キャラ:泥酔眠姦 == 1
		PRINT 精液を焦点の定まらない目でぼーっと見つめている…
	ELSEIF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:サド性感 > 2
		PRINTFORML 精液を楽しそうに%CALLNAME:実行キャラ%に見せつけた
	ELSEIF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:奉仕精神 > 5
		PRINTFORML 精液を誇らしげに%CALLNAME:実行キャラ%に見せつけた
	ELSEIF CFLAG:対象キャラ:発情期フラグ < 0
		IF 知識素質:対象キャラ:性知識 == -2
			PRINT 精液に本能的な何かを感じたのか、
		ELSE
			PRINT 精液を
		ENDIF
		PRINTL 熱っぽい表情で眺めている
	ELSEIF 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTL 精液を愛おしそうに眺めている
	ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
		PRINTL 精液を興味津々な様子で弄んでいる
	ELSE
		PRINT 精液をティッシュで
		IF TALENT:対象キャラ:汚臭耐性 < 0
			PRINTDATA
				DATA 何度も
				DATA 丁寧に
				DATA 綺麗に
			ENDDATA
		ENDIF
		PRINTL 拭った
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 9

;-------------------------------------------------------------------------------------------
;SHOOT_10:足に出す
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_10(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 足に出す
RETURN 1


@SHOOT_PALAM_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;足コキモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
#DIM フィニッシュ反応

フィニッシュ反応 = 0
IF MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2 && TCVAR:対象キャラ:泥酔眠姦 != 1
	IF ABL:対象キャラ:サド性感 > 2
		フィニッシュ反応 = 2
		PRINTFORML %CALLNAME:対象キャラ%は射精の兆候を感じ取ると、%CALLNAME:実行キャラ%のペニスを力いっぱい踏みつけた
	ELSEIF ABL:対象キャラ:奉仕精神 > 5
		フィニッシュ反応 = 1
		PRINTFORML %CALLNAME:対象キャラ%は射精の兆候を感じ取ると、%CALLNAME:実行キャラ%のペニスを両足で包み込んだ
	ENDIF
ENDIF
PRINTFORM %CALLNAME:実行キャラ%は\@ フィニッシュ反応 ? そのまま # \@%CALLNAME:対象キャラ%の足に
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量の
PRINTL 精液を吐き出した…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER && TCVAR:対象キャラ:泥酔眠姦 != 1
	IF フィニッシュ反応 == 1
		PRINTFORM %CALLNAME:対象キャラ%は両足をペニスから離し、その間に
		IF RFLAG:フィニッシュフラグ > 1
			PRINT べっとりと白い橋をかける精液を誇らしげに
		ELSE
			PRINT 広がった精液を
		ENDIF
		PRINTFORML %CALLNAME:実行キャラ%に見せつけた
	ELSEIF フィニッシュ反応 != 2 && 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTFORM %CALLNAME:実行キャラ%が射精する間、%CALLNAME:対象キャラ%は足に
		SIF RFLAG:フィニッシュフラグ > 1
			PRINT 大量の
		PRINTL 精液が降りかかる感触を楽しんでいる
	ELSE
		PRINTFORM %CALLNAME:対象キャラ%は足をペニスから離し、
		IF RFLAG:フィニッシュフラグ > 1
			PRINT べっとりとこびりついた
		ELSE
			PRINT 付着した
		ENDIF
		IF フィニッシュ反応 == 2
			PRINTFORML 精液を意地悪く笑いながら%CALLNAME:実行キャラ%に見せつけた
		ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
			PRINTL 精液を興味津々な様子で両足で弄んでいる
		ELSE
			PRINT 精液をティッシュで
			IF TALENT:対象キャラ:汚臭耐性 < 0
				PRINTDATA
					DATA 何度も
					DATA 丁寧に
					DATA 綺麗に
				ENDDATA
			ENDIF
			PRINTL 拭った
		ENDIF
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 10

;-------------------------------------------------------------------------------------------
;SHOOT_30:他にどこにも射精せない時用
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_30(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 何もない場所に射精
RETURN 1


@SHOOT_PALAM_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ


@SHOOT_ABLE_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
RETURN 1


@MESSAGE_SHOOT_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF 対象キャラ == -1
	;非うふふ時射精
	IF RFLAG:コマンド結果受渡し変数 == 23 && 実行キャラ == MASTER
		;モブ逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%は誰とも知らぬ相手の膣に精液を吐き出しながら絶頂した…
	ELSEIF RFLAG:コマンド結果受渡し変数 == 24 && 実行キャラ == MASTER
		;モブA逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%は誰とも知らぬ相手の尻穴に精液を吐き出しながら絶頂した…
	ELSEIF TEQUIP:実行キャラ:下半身下着あり > 0
		PRINTFORML %CALLNAME:実行キャラ%は下着の中に精液を吐き出しながら絶頂した…
	ELSEIF TEQUIP:実行キャラ:下半身服あり
		PRINTFORML %CALLNAME:実行キャラ%は服の中に精液を吐き出しながら絶頂した…
	ELSE
		PRINTFORML %CALLNAME:実行キャラ%は陰茎から精液を吐き出しながら絶頂した…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:実行キャラ%は陰茎から精液を吐き出しながら絶頂した…
ENDIF
