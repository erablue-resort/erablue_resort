
@イベント追加チェック
#DIM イベント数
#DIM ソート計算値
#DIMS イベント名保存
#DIMS 既存イベント名配列, 5000
VARSET LOCAL
VARSET 既存イベント名配列

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	既存イベント名配列:LOCAL = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_メインシナリオ_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 28)%
	IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "メインシナリオ"
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_サブイベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 26)%
	IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "サブイベント"
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_キャライベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 28)%
	IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
		RESULTS = 
		TRYCALLFORM イベントカテゴリ登録_キャライベント_%イベント名保存%
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "キャライベント", "特殊イベント", RESULTS
	ENDIF
NEXT

;汎用イベントを突っ込む
FOR LOCAL, 0, CHARANUM
	IF GETBIT(TALENT:LOCAL:陥落不可, 0) && !CFLAG:LOCAL:恋慕不可_汎用解除禁止
		イベント名保存 = %ESCAPE(@"【汎用】%NAME:LOCAL%[恋慕不可]解除")%
		IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
			IF NO:LOCAL == 999
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%NAME:LOCAL%[恋慕不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", LOCAL + 90000
			ELSE
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%NAME:LOCAL%[恋慕不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", NO:LOCAL
			ENDIF
		ENDIF
	ENDIF
	IF GETBIT(TALENT:LOCAL:陥落不可, 1) && !CFLAG:LOCAL:セフレ不可_汎用解除禁止
		イベント名保存 = %ESCAPE(@"【汎用】%NAME:LOCAL%[セフレ不可]解除")%
		IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
			IF NO:LOCAL == 999
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%NAME:LOCAL%[セフレ不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", LOCAL + 90000
			ELSE
				DT_ROW_ADD "各イベント用変数配列", "イベント名", @"【汎用】%NAME:LOCAL%[セフレ不可]解除", "イベントカテゴリ", "キャライベント", "汎用イベ用_キャラNO保存", NO:LOCAL
			ENDIF
		ENDIF
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_突発イベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 26)%
	IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
		DT_ROW_ADD "各イベント用変数配列", "イベント名", イベント名保存, "イベントカテゴリ", "突発イベント"
	ENDIF
NEXT

VARSET LOCAL
VARSET 既存イベント名配列

FOR LOCAL, 0, DT_ROW_LENGTH("エンディング用変数配列")
	既存イベント名配列:LOCAL = %DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%
	;ここでついでにソートキーを再セット
	CALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%("設定取得", DT_CELL_GET("エンディング用変数配列", LOCAL, "id"))
	IF STRFIND(TSTR:コマンド名受渡, "MA") > -1
		ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2))
	ELSEIF STRFIND(TSTR:コマンド名受渡, "CH") > -1
		ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 10000
	ELSEIF STRFIND(TSTR:コマンド名受渡, "EX") > -1
		ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 20000
	ELSEIF STRFIND(TSTR:コマンド名受渡, "GE") > -1
		ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 30000
	ENDIF
	IF ソート計算値 != DT_CELL_GET("エンディング用変数配列", LOCAL, "ソート値")
		DT_CELL_SET "エンディング用変数配列", LOCAL, "ソート値", ソート計算値
	ENDIF
NEXT

イベント数 = ENUMFUNCBEGINSWITH("イベント関数_エンディングイベント_")
FOR LOCAL, 0, イベント数
	イベント名保存 = %SUBSTRING(RESULTS:LOCAL, 34)%
	IF FINDELEMENT(既存イベント名配列, イベント名保存, , , 1) < 0
		CALLFORM イベント関数_エンディングイベント_%イベント名保存%("設定取得")
		IF STRFIND(TSTR:コマンド名受渡, "MA") > -1
			ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2))
		ELSEIF STRFIND(TSTR:コマンド名受渡, "CH") > -1
			ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 10000
		ELSEIF STRFIND(TSTR:コマンド名受渡, "EX") > -1
			ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 20000
		ELSEIF STRFIND(TSTR:コマンド名受渡, "GE") > -1
			ソート計算値 = TOINT(SUBSTRING(TSTR:コマンド名受渡, 2)) + 30000
		ENDIF
		DT_ROW_ADD "エンディング用変数配列", "イベント名", イベント名保存, "ソート値", ソート計算値
	ENDIF
NEXT

@イベント削除チェック
#DIM イベント数
#DIM 対象キャラ
#DIMS イベント名保存
VARSET LOCAL

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	イベント名保存 = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	イベント数 = ENUMFUNCBEGINSWITH(@"イベント関数_メインシナリオ_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_サブイベント_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_キャライベント_%イベント名保存%")
	イベント数 += ENUMFUNCBEGINSWITH(@"イベント関数_突発イベント_%イベント名保存%")
	;汎用イベを見る
	IF STRCOUNT(イベント名保存, "【汎用】")
		対象キャラ = DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存")
		IF EXISTCSV(DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存"))
			IF 対象キャラ > 90000
				対象キャラ = 対象キャラ - 90000
			ELSE
				対象キャラ = GETCHARA(対象キャラ)
			ENDIF
			IF STRCOUNT(イベント名保存, "恋慕不可]解除") && !CFLAG:対象キャラ:恋慕不可_汎用解除禁止
				イベント数 = 1
			ELSEIF STRCOUNT(イベント名保存, "セフレ不可]解除") && !CFLAG:対象キャラ:セフレ不可_汎用解除禁止
				イベント数 = 1
			ELSEIF STRCOUNT(イベント名保存, "結婚イベント") && !CFLAG:対象キャラ:結婚イベント禁止
				イベント数 = 1
			ENDIF
		ENDIF
	ENDIF
	IF イベント数 < 1
		DT_ROW_REMOVE "各イベント用変数配列", DT_CELL_GET("各イベント用変数配列", LOCAL, "id")
		LOCAL --
	ENDIF
NEXT
