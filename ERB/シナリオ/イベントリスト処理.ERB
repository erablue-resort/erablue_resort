
@起床前イベントリスト
#DIMS 表示用リスト, 100
#DIMS NEWフラグ用リスト, 100
#DIM 表示候補数
VARSET LOCAL
VARSET LOCALS
VARSET NEWフラグ用リスト

表示候補数 = DT_ROW_LENGTH("各イベント用変数配列")
SPLIT TSTR:NEWイベントフラグ, "_", NEWフラグ用リスト

DRAWLINE
IF FLAG:クリア済み視聴フラグ
	PRINTL 再視聴するクリア済みイベントを選んでください。
	PRINTL ※基本的に再視聴では報酬などが取得出来ません。イベント中に「◯◯を入手した」と出ても取得されません。
	PRINTBUTTON "[9998] イベントリストへ戻る", 9998
ELSE
	PRINTL 開始するイベントを選んでください。
	PRINTBUTTON "[9998] クリア済みイベントを再視聴する", 9998
ENDIF
PRINTL
DRAWLINE
IF FLAG:クリア済み視聴フラグ
	FOR LOCAL, 0, 表示候補数
		;発生してないものは出さない
		SIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") == 0
			CONTINUE
		;再発生中だと出さない
		SIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -2
			CONTINUE
		IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 1
			SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
				CASE "メインシナリオ"
					LOCALS:1 += @"<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button><br>"
				CASE "サブイベント"
					LOCALS:2 += @"<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button><br>"
				CASE "キャライベント"
					LOCALS:3 += @"<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button><br>"
				CASE "突発イベント"
					LOCALS:4 += @"<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button><br>"
			ENDSELECT
		ENDIF
	NEXT
ELSE
	FOR LOCAL, 0, 表示候補数
		SIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント消滅CT") == -2
			CONTINUE
		SIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") == 0
			CONTINUE
		IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 0
			LOCALS:10 = 
			LOCALS:11 = 
			IF MATCH(NEWフラグ用リスト, @"EVENT%TOSTR(LOCAL)%")
				LOCALS:10 += @"<font color='#%カラーパレット_HTML("黄")%'>"
				LOCALS:11 += @"</font><font color='#FF5555'> NEW!!</font>"
			ENDIF
			SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
				CASE "メインシナリオ"
					LOCALS:1 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%<br>"
				CASE "サブイベント"
					LOCALS:2 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%<br>"
				CASE "キャライベント"
					LOCALS:3 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%<br>"
				CASE "突発イベント"
					LOCALS:4 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%<br>"
			ENDSELECT
		ENDIF
		IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -2
			;CT-2は再発生でないと起こり得ないはず
			LOCALS:10 = 
			LOCALS:11 = 
			IF MATCH(NEWフラグ用リスト, @"EVENT%TOSTR(LOCAL)%")
				LOCALS:10 += @"<font color='#%カラーパレット_HTML("黄")%'>"
				LOCALS:11 += @"</font><font color='#FF5555'> NEW!!</font>"
			ENDIF
			SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
				CASE "メインシナリオ"
					LOCALS:1 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%　(再発生)</button>%LOCALS:11%<br>"
				CASE "サブイベント"
					LOCALS:2 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%　(再発生)</button>%LOCALS:11%<br>"
				CASE "キャライベント"
					LOCALS:3 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%　(再発生)</button>%LOCALS:11%<br>"
				CASE "突発イベント"
					LOCALS:4 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%　(再発生)</button>%LOCALS:11%<br>"
			ENDSELECT
		ENDIF
	NEXT
ENDIF

LOCALS = <div rect='81,31,1937,1250' border='31' bcolor='#C0C0C0'></div><div rect='156,156,1837,1250'><nobr>%LOCALS:1%</nobr></div>
LOCALS += @"<div rect='81,1331,1937,1531' border='31' bcolor='#C0C0C0'></div><div rect='156,1425,1837,1531'><nobr>%LOCALS:4%</nobr></div>"
LOCALS += @"<div rect='2081,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2156,156,1837,2812'><nobr>%LOCALS:2%</nobr></div>"
LOCALS += @"<div rect='4081,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='4156,156,1837,2812'><nobr>%LOCALS:3%</nobr></div>"
LOCALS += "<div width='750' height='125' xpos='337' ypos='0' color='#101010'>メインシナリオ</div>"
LOCALS += "<div width='662' height='125' xpos='337' ypos='1300' color='#101010'>突発イベント</div>"
LOCALS += "<div width='662' height='125' xpos='2337' ypos='0' color='#101010'>サブイベント</div>"
LOCALS += "<div width='762' height='125' xpos='4337' ypos='0' color='#101010'>キャライベント</div>"
HTML_PRINT LOCALS

FOR LOCAL, 0, 25
	PRINTL
NEXT
PRINTBUTTON "[9997]システムの説明", 9997
PRINTL
PRINTBUTTON "[9999]戻る", 9999
PRINTL

IF チュートリアルフラグ:イベントリスト == 0
	LOCAL = 9997
ELSE
	BINPUT
	LOCAL = RESULT
ENDIF
SELECTCASE LOCAL
	CASE IS < 表示候補数
		SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
			CASE "メインシナリオ"
				CALLFORM イベント関数_メインシナリオ_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%(LOCAL)
			CASE "サブイベント"
				CALLFORM イベント関数_サブイベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%(LOCAL)
			CASE "キャライベント"
				IF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "【汎用】")
					IF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "恋慕不可]解除")
						CALL 汎用イベント関数_キャライベント_恋慕不可解除(DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存"))
					ELSEIF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "セフレ不可]解除")
						CALL 汎用イベント関数_キャライベント_セフレ不可解除(DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存"))
					ENDIF
				ELSE
					CALLFORM イベント関数_キャライベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%(LOCAL)
				ENDIF
			CASE "突発イベント"
				CALLFORM イベント関数_突発イベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%(LOCAL)
		ENDSELECT
		IF MATCH(NEWフラグ用リスト, @"EVENT%TOSTR(LOCAL)%")
			REPLACE TSTR:NEWイベントフラグ, @"EVENT%TOSTR(LOCAL)%_", ""
			TSTR:NEWイベントフラグ = %RESULTS%
		ENDIF
		RESTART
	CASE 9997
		CALL イベントリストチュートリアル()
		RESTART
	CASE 9998
		INVERTBIT FLAG:クリア済み視聴フラグ, 0
		RESTART
	CASE 9999
		FLAG:クリア済み視聴フラグ = 0
		RETURN 0
ENDSELECT

@イベント発生条件チェック
FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	;初回発生チェック
	イベント消滅日数渡し = 0
	イベント消滅確率渡し = 0
	イベント再発生日数渡し = 0
	イベント再発生確率渡し = 0
	IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") == 0
		RESULT = 0
		SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
			CASE "メインシナリオ"
				TRYCALLFORM イベント発生条件_メインシナリオ_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
			CASE "サブイベント"
				TRYCALLFORM イベント発生条件_サブイベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
			CASE "キャライベント"
				TRYCCALLFORM イベント発生条件_キャライベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
				CATCH
					IF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "【汎用】")
						IF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "恋慕不可]解除")
							CALL 汎用イベント発生条件_キャライベント_恋慕不可解除(DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存"))
						ELSEIF STRCOUNT(DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名"), "セフレ不可]解除")
							CALL 汎用イベント発生条件_キャライベント_セフレ不可解除(DT_CELL_GET("各イベント用変数配列", LOCAL, "汎用イベ用_キャラNO保存"))
						ENDIF
					ENDIF
				ENDCATCH
			CASE "突発イベント"
				TRYCALLFORM イベント発生条件_突発イベント_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
		ENDSELECT
		IF RESULT
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント発生フラグ", 1
			IF LOCAL:1 == 0
				PRINTL
				PRINTL
				DRAWLINE
			ENDIF
			SELECTCASE DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")
				CASE "メインシナリオ"
					PRINTFORML メインシナリオ［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が新たに発生した！
				CASE "サブイベント"
					PRINTFORML サブイベント［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が新たに発生した！
				CASE "キャライベント"
					PRINTFORML キャライベント［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が新たに発生した！
				CASE "突発イベント"
					PRINTFORML 突発イベント［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が新たに発生した！
			ENDSELECT
			FORCEWAIT
			LOCAL:1 = 1
			TSTR:NEWイベントフラグ += @"EVENT{LOCAL}_"
			;消失確率があるなら入れ込む
			イベント消滅日数渡し = 0
			イベント消滅確率渡し = 0
			TRYCALLFORM イベント発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
			IF イベント消滅確率渡し
				DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", イベント消滅日数渡し
				DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅確率", イベント消滅確率渡し
			ENDIF
		ENDIF
	;再発生チェック
	ELSEIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -1
		;既に発生済みかつクリア済みの突発イベントの再発生
		IF LOCAL:1 == 0
			PRINTL
			PRINTL
			DRAWLINE
		ENDIF
		PRINTFORML %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が再発生した！
		FORCEWAIT
		;再発生済みと再発生前を区別するためにCTに-2を入れる
		DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", -2
		LOCAL:1 = 1
		;消失確率があるなら入れ込む
		イベント消滅日数渡し = 0
		イベント消滅確率渡し = 0
		TRYCALLFORM イベント発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
		IF イベント消滅確率渡し
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", イベント消滅日数渡し
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅確率", イベント消滅確率渡し
		ENDIF
	ENDIF
	IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") == 1 && DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 0
		;条件消失チェック、単純にフラグを戻すだけなので条件が整った時に即再発生する
		RESULT = 0
		TRYCALLFORM イベント消滅条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
		IF RESULT
			;消失文章は出さない
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント発生フラグ", 0
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", 0
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅確率", 0
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", 0
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生確率", 0
		ENDIF
	ELSEIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント消滅CT") == -1
		;自然消滅チェック、こっちは再発生CTや確率がセット出来る
		IF LOCAL:1 == 0
			PRINTL
			PRINTL
			DRAWLINE
		ENDIF
		PRINTFORML %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%［%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%］が消失した…
		FORCEWAIT
		;消失済みと消失前を区別するためにCTに-2を入れる
		DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", -2
		LOCAL:1 = 1
		;再発生確率渡しを見て、存在するなら入れ込む
		イベント再発生日数渡し = 0
		イベント再発生確率渡し = 0
		TRYCALLFORM イベント発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
		IF イベント再発生確率渡し
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", イベント再発生日数渡し
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生確率", イベント再発生確率渡し
		ENDIF
	ENDIF
NEXT
FOR LOCAL, 0, DT_ROW_LENGTH("エンディング用変数配列")
	IF DT_CELL_GET("エンディング用変数配列", LOCAL, "イベント発生フラグ") == 0
		RESULT = 0
		TRYCALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%("発生条件", LOCAL)
		IF RESULT
			DT_CELL_SET "エンディング用変数配列", LOCAL, "イベント発生フラグ", 1
			IF LOCAL:1 == 0
				PRINTL
				PRINTL
				DRAWLINE
			ENDIF
				PRINTFORML エンディング［%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%］が新たに発生した！
			FORCEWAIT
			LOCAL:1 = 1
			TSTR:NEWイベントフラグ += @"ENDHING{LOCAL}_"
		ENDIF
	ENDIF
NEXT



@イベント完了フラグ汎用処理(ARGS)
#DIM 行番号

SIF FLAG:クリア済み視聴フラグ
	RETURN 0

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	LOCALS = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	IF LOCALS == ARGS
		行番号 = LOCAL
		BREAK
	ENDIF
NEXT
DT_CELL_SET "各イベント用変数配列", 行番号, "イベント完了フラグ", 1

;再発生確率渡しを見て、存在するなら入れ込む
イベント消滅日数渡し = 0
イベント消滅確率渡し = 0
イベント再発生日数渡し = 0
イベント再発生確率渡し = 0
TRYCALLFORM イベント発生条件_%DT_CELL_GETS("各イベント用変数配列", 行番号, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", 行番号, "イベント名")%
IF イベント再発生確率渡し
	DT_CELL_SET "各イベント用変数配列", 行番号, "イベント再発生CT", イベント再発生日数渡し
	DT_CELL_SET "各イベント用変数配列", 行番号, "イベント再発生確率", イベント再発生確率渡し
	;再発生処理を毎日呼ぶために消失CTを-2にセット
	DT_CELL_SET "各イベント用変数配列", 行番号, "イベント消滅CT", -2
ENDIF


@イベント完了フラグ確認(ARGS)
#FUNCTION
#DIM 行番号
VARSET LOCAL

FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	LOCALS = %DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	IF LOCALS == ARGS
		行番号 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1
	RETURNF DT_CELL_GET("各イベント用変数配列", 行番号, "イベント完了フラグ")
ELSE
	RETURNF -1
ENDIF


@イベント中戦闘用処理()

;再視聴中は常に勝利を返す
IF FLAG:クリア済み視聴フラグ
	PRINTL 視聴パターンを選択してください。
	PRINTBUTTONLC "[0] 勝利", 0
	PRINTBUTTONLC "[1] 敗北", 1
	PRINTBUTTONLC "[-1] 逃走", -1
	BINPUT
	RETURN RESULT
ELSE
	CALL 雑魚敵遭遇汎用処理()
	RETURN RESULT
ENDIF
