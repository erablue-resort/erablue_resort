@汎用イベント発生条件_キャライベント_恋慕不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:恋心度 < 経験値テーブル:99
	RETURN 0

RETURN 1

@汎用イベント関数_キャライベント_恋慕不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

DRAWLINE
PRINTFORML ［【汎用】%NAME:対象キャラ%[恋慕不可]解除］
DRAWLINE
PRINTW
PRINTFORMW %CALLNAME:対象キャラ%の様子がなんだかおかしい。
PRINTFORMW 話しかけると少し顔を赤くして、%CALLNAME:PLAYER%のことを潤んだ瞳で見つめている…
PRINTL 
PRINTFORMW 交流を重ねた結果、拒み続けていた恋心が溢れてしまったようだ。
PRINTFORMW 今後はもっと関係を深めることが出来るかもしれない。
PRINTL 
SETCOLOR 0xFFFF00
IF GETBIT(TALENT:対象キャラ:陥落不可, 1)
	PRINTFORML %CALLNAME:対象キャラ%の[陥落不可]が[セフレ不可]に変化しました。
ELSE
	PRINTFORML %CALLNAME:対象キャラ%の[恋慕不可]が消滅しました。
ENDIF
CLEARBIT TALENT:対象キャラ:陥落不可, 0
RESETCOLOR
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%[恋慕不可]解除")





@汎用イベント発生条件_キャライベント_セフレ不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:性愛度 < 経験値テーブル:99
	RETURN 0

RETURN 1

@汎用イベント関数_キャライベント_セフレ不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

DRAWLINE
PRINTFORML ［【汎用】%NAME:対象キャラ%[セフレ不可]解除］
DRAWLINE
PRINTW
PRINTFORMW %CALLNAME:対象キャラ%の様子がなんだかおかしい。
PRINTFORMW 話しかけると少し顔を赤くして、%CALLNAME:PLAYER%のことを妖艶な笑みで見つめている…
PRINTL 
PRINTFORMW 交流を重ねた結果、抑え続けていた性欲が溢れてしまったようだ。
PRINTFORMW 今後はもっと関係を深めることが出来るかもしれない。
PRINTL 
SETCOLOR 0xFFFF00
IF GETBIT(TALENT:対象キャラ:陥落不可, 0)
	PRINTFORML %CALLNAME:対象キャラ%の[陥落不可]が[恋慕不可]に変化しました。
ELSE
	PRINTFORML %CALLNAME:対象キャラ%の[セフレ不可]が消滅しました。
ENDIF
CLEARBIT TALENT:対象キャラ:陥落不可, 1
RESETCOLOR
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%[セフレ不可]解除")




@汎用イベント発生条件_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 0
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 0

RETURN 1

@汎用イベント消滅条件_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

;結婚相手は滞在していること
SIF CFLAG:対象キャラ:滞在期間 < 1
	RETURN 1
;一応フラグもチェック
SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 1
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 1

RETURN 0

@汎用イベント関数_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
#DIM 花嫁キャラ
#DIM 花婿キャラ
#DIM レイヤー記録

IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

IF TALENT:MASTER:性別 == 2 && TALENT:対象キャラ:性別 != 2
	花嫁キャラ = 対象キャラ
	花婿キャラ = MASTER
ELSEIF TALENT:MASTER:性別 != 2 && TALENT:対象キャラ:性別 == 2
	花嫁キャラ = MASTER
	花婿キャラ = 対象キャラ
ELSEIF RAND:2
	花嫁キャラ = 対象キャラ
	花婿キャラ = MASTER
ELSE
	花嫁キャラ = MASTER
	花婿キャラ = 対象キャラ
ENDIF


DRAWLINE
PRINTFORML ［【汎用】%NAME:対象キャラ%結婚イベント］
DRAWLINE
PRINTW
PRINTFORMW 快晴の吉日、午前の日差しの中。
PRINTFORMW 結婚式場の扉が開くと、%CALLNAME:花嫁キャラ%が白いドレスに身を包み、柔らかな光の中を歩み始めた。
PRINTFORMW 祭壇には%CALLNAME:花婿キャラ%が微笑み、緊張と喜びが入り混じった表情で相手を見つめる。
レイヤー記録 = TFLAG:口上用画像表示番号
CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/汎用結婚イベント一枚絵")
IF レイヤー記録 == TFLAG:口上用画像表示番号
	;専用画像が表示されてないということなので、ドレス画像を見る
	IF TALENT:花嫁キャラ:性別 == 2
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/短冊_ホワイトタキシード")
	ELSE
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/短冊_ウエディングドレス")
	ENDIF
ENDIF
レイヤー記録 = TFLAG:口上用画像表示番号
CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/汎用結婚イベント一枚絵")
IF レイヤー記録 == TFLAG:口上用画像表示番号
	;専用画像が表示されてないということなので、ドレス画像を見る
	IF TALENT:花婿キャラ:性別 == 2
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/短冊_ホワイトタキシード")
	ELSE
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/短冊_ウエディングドレス")
	ENDIF
ENDIF
PRINTL 
PRINTFORMW ステンドグラスの色鮮やかな光が、木のベンチに座る参列者たちを温かく照らす。
PRINTFORMW オルガンの荘厳な音色が響き合い、%CALLNAME:花嫁キャラ%のベールがそよ風に揺れている。
PRINTFORMW %CALLNAME:花嫁キャラ%の手には、純白のバラと淡いピンクの芍薬のブーケ。
PRINTL 
PRINTFORMW 牧師の穏やかな声が誓いの言葉を導き、二人の愛を祝福する。
PRINTFORMW 指輪交換の瞬間、会場は静寂に包まれ、ただ二人の呼吸だけが聞こえる。
PRINTFORMW %CALLNAME:花婿キャラ%が%CALLNAME:花嫁キャラ%の手を取り、優しくキスを交わすと、拍手と歓声が一斉に沸き起こった。
PRINTL 
PRINTFORMW 花びらが舞い、鐘の音が鳴り響き、新たな門出を告げる。
PRINTFORMW %CALLNAME:花婿キャラ%と%CALLNAME:花嫁キャラ%は、末永く幸せな未来を築くことだろう────。
PRINTL 

IF !FLAG:クリア済み視聴フラグ
	IF TALENT:対象キャラ:恋慕
		TALENT:対象キャラ:恋慕 = 2
	ELSEIF TALENT:対象キャラ:疑似恋慕
		TALENT:対象キャラ:疑似恋慕 = 2
		;固有の疑似恋慕素質名を取得、ない場合はデフォルト名
		;一応該当文字列を空っぽにしてから呼ぶ
		CSTR:対象キャラ:擬似恋慕陥落名称 = 
		TRYCALLFORM 固有擬似恋慕陥落_陥落名変更_結婚_{NO:対象キャラ}(対象キャラ)
		IF CSTR:対象キャラ:擬似恋慕陥落名称 == ""
			CSTR:対象キャラ:擬似恋慕陥落名称 = およめさん
		ENDIF
	ENDIF
	初体験日:対象キャラ:伴侶 = DAY
	IF CSTR:対象キャラ:擬似恋慕陥落名称 != ""
		CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:対象キャラ%を[擬似恋慕（%CSTR:対象キャラ:擬似恋慕陥落名称%）]陥落させた</font><img src='えっちハート'>","うふふ実績")
		CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>擬似恋慕陥落素質[%CSTR:対象キャラ:擬似恋慕陥落名称%]を得た</font><img src='えっちハート'>","うふふ実績")
	ELSE
		CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:対象キャラ%を[伴侶]陥落させた</font><img src='えっちハート'>","うふふ実績")
		CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[伴侶]を得た</font><img src='えっちハート'>","うふふ実績")
	ENDIF
	IF TALENT:対象キャラ:浮気 > 0
		TALENT:対象キャラ:浮気 = 0
	ENDIF
	; IF TALENT:対象キャラ:恋人持ち != 2
	; 	TALENT:対象キャラ:恋人持ち = 2
	; ENDIF
ENDIF

SETCOLOR 0xFFFF00
IF CSTR:対象キャラ:擬似恋慕陥落名称 != ""
	PRINTFORMW %CALLNAME:対象キャラ%は［%CSTR:対象キャラ:擬似恋慕陥落名称%］を得ました。
ELSE
	PRINTFORMW %CALLNAME:対象キャラ%は［伴侶］を得ました。
ENDIF
RESETCOLOR
IF TALENT:対象キャラ:浮気 > 0
	PRINTFORM %CALLNAME:対象キャラ%は
	SETCOLOR カラーパレット("えっちな色")
	PRINT [浮気]
	RESETCOLOR
	PRINT を失った……
ENDIF
IF TALENT:対象キャラ:恋人持ち != 2
	SIF TALENT:対象キャラ:浮気 > 0
		WAIT
	PRINTFORM %CALLNAME:対象キャラ%は
	SETCOLOR カラーパレット("えっちな色")
	IF TALENT:対象キャラ:性別 == 1 || TALENT:対象キャラ:性別 == 3
		PRINT [人妻]
	ELSE
		PRINT [既婚者]
	ENDIF
	RESETCOLOR
	PRINT を得た！
ENDIF

PRINTL
PRINTL
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%結婚イベント")

