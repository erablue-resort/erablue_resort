
;----------------------------------------------------------------------------
;・イベント中うふふ使い方
;呼びたいキャラの「CFLAG:(キャラ番号):イベントうふふ対象フラグ」を１にしてから下の関数を呼ぶことで
;起床前の段階（イベント視聴など）からうふふに移行することが可能
;うふふが終了した時点で元の場所に戻る……はず。
;
;以下はオブション設定用フラグ
; STR:イベントうふふ場所名 = （イベントうふふ中に表示する現在位置名）
; 
; FLAG:イベントうふふベッドルーム = 1にするとベッドルームになる
; というか、１にしないと野外扱いになってしまうので基本１推奨
;----------------------------------------------------------------------------


@イベント中うふふ移行処理(イベント名 = "", ゲームフェイズ指定 = "")
#DIM 現在位置保存, 100
#DIM 現在マップ種別保存, 100
#DIMS イベント名
#DIMS ゲームフェイズ指定

VARSET 現在位置保存, 2000
VARSET 現在マップ種別保存

SIF ゲームフェイズ指定 == ""
	ゲームフェイズ指定 = 通常モード

SAVESTR:ゲームフェイズ管理 '= ゲームフェイズ指定

CFLAG:MASTER:うふふ = 1

SWAP CFLAG:MASTER:現在位置, 現在位置保存:0
SWAP CFLAG:MASTER:現在マップ種別, 現在マップ種別保存:0

LOCAL:1 = 1
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:イベントうふふ対象フラグ == 0
		CONTINUE
	SWAP CFLAG:LOCAL:現在位置, 現在位置保存:(LOCAL:1)
	SWAP CFLAG:LOCAL:現在マップ種別, 現在マップ種別保存:(LOCAL:1)
	
	CALL うふふ開始キャラ処理(LOCAL, @"イベント_%イベント名%")
	SIF ゲームフェイズ指定 == "むりやりうふふモード"
		TSTR:むりやりごっこ時シチュ保存 '= CSTR:LOCAL:うふふシチュ記録
	CFLAG:LOCAL:睡眠 = 0
	CFLAG:LOCAL:同室 = 1
	LOCAL:1 ++
NEXT

FLAG:イベントTIME記録 = TIME
;処理の誤爆を防ぐためオートコマンド周りリセット
TFLAG:オートコマンドフラグ = 0
AUTOCOM_実行番号保存 = -1
AUTOCOM_対象番号保存 = -1
自慰ターゲット保存 = -1

CALL SORT_CHARALIST()
CALL TRAIN_イベントうふふ_LOOP()

SWAP CFLAG:MASTER:現在位置, 現在位置保存:0
SWAP CFLAG:MASTER:現在マップ種別, 現在マップ種別保存:0

FLAG:イベントうふふベッドルーム = 0

LOCAL:1 = 1
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:イベントうふふ対象フラグ == 0
		CONTINUE
	SWAP CFLAG:LOCAL:現在位置, 現在位置保存:(LOCAL:1)
	SWAP CFLAG:LOCAL:現在マップ種別, 現在マップ種別保存:(LOCAL:1)
	CFLAG:LOCAL:同室 = 0
	CFLAG:LOCAL:同行 = 0
	CFLAG:LOCAL:睡眠 = 1
	CFLAG:LOCAL:イベントうふふ対象フラグ = 0
	LOCAL:1 ++
NEXT
