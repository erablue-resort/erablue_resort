;-------------------------------------------------
;タイトル画面
;-------------------------------------------------
@SYSTEM_TITLE
DRAWLINE
ALIGNMENT CENTER
PRINT_IMG "タイトルロゴ", 5330, 1520
PRINTL
PRINTL 
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
PRINTL
ALIGNMENT LEFT
DRAWLINE
[IF_DEBUG]
PRINTBUTTON "Ver" + TOSTR(GAMEBASE_VERSION, "0,.000"), 5656
[ELSE]
PRINTS "Ver" + TOSTR(GAMEBASE_VERSION, "0,.000")
[ENDIF]
PRINTL
PRINTFORML %GAMEBASE_INFO%
DRAWLINE
PRINTBUTTON "[0] 最初から始める", 0
PRINTL
PRINTBUTTON "[1] ロードして始める", 1
PRINTL
PRINTL
PRINTL
PRINTL ■データエディタ機能
PRINT 　├
PRINTBUTTON "[2] CSVエディタを起動する", 2
PRINTL
PRINT 　├
PRINTBUTTON "[3] 服ERBエディタを起動する", 3
PRINTL
PRINT 　├
PRINTBUTTON "[4] 弱点コマンド設定ERBエディタを起動する", 4
PRINTL
PRINT 　├
PRINTBUTTON "[5] 一言コメント/解説エディタを起動する", 5
PRINTL
PRINT 　└
PRINTBUTTON "[6] アビリティエディタを起動する", 6
PRINTL
PRINTL
PRINTL
PRINTL ■erablue_resort開発Discord
PRINT 　└
PRINTBUTTON "[100] Discordチャンネルに飛ぶ", 100





[IF_DEBUG]
PRINTL
PRINTL
PRINTL
PRINTL ■デバッグ用機能
PRINT 　├
PRINTBUTTON "[ 9] 画像ファイル名のチェック", 9
PRINTL
PRINT 　├
PRINTBUTTON "[11] 画像ファイル名のチェック(CSV番号指定)", 11
PRINTL
PRINT 　└
PRINTBUTTON "[10] CSV初期同行率のチェック", 10
[ENDIF]

BINPUT
SELECTCASE RESULT
	CASE 0
		BEGIN FIRST
	CASE 1
		LOADGAME
	CASE 2
		CSVエディタフラグ = 1
		CALL 従業員素質指定雇用
		CSVエディタフラグ = 0
	CASE 3
		CALL 服ERB生成処理
	CASE 4
		CALL 弱点コマンド設定ERB生成処理
	CASE 5
		CALL 一言コメント解説エディタ
	CASE 6
		CALL アビリティエディタ
	CASE 100
		CALLSHARP LAUNCH_BROWSER("https://discord.gg/zCWzA8kbgp")
[IF_DEBUG]
	CASE 9
		CALL デバッグ用_画像ファイル名チェック()
	CASE 11
		CALL デバッグ用_画像ファイル名チェック_キャラ指定()
	CASE 10
		CALL デバッグ用_初期同行率チェック()
	CASE 5656
		CALL バージョンアップ準備処理
[ENDIF]
ENDSELECT

RESTART


;-------------------------------------------------
;ニューゲーム
;-------------------------------------------------
@EVENTFIRST
#DIM 引き継いだキャラ人数
#DIMS 分割文字列, 3
#DIM era番号

VARSET LOCAL
VARSET LOCALS
TARGET = 0

CALL DEFAULT_OPTION

;モード選択（後々使うかもしれないから消さずにコメントアウト）
;PRINTL ★★モードを選択してください★★
;PRINTL [0]START
;
;$INPUT_LOOP
;INPUT
;IF RESULT == 0
;	FLAG:5 = 0
;ELSE
;	GOTO INPUT_LOOP
;ENDIF

;キャラの作成前に必要なデータベース
IF 引き継ぎ中フラグ
	LOADTEXT "dat/人物DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/人物DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "人物データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/履歴DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/履歴DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "履歴データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/エンディングDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/エンディングDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "エンディング用変数配列", LOCALS:1, LOCALS:0
	LOADTEXT "dat/Rキャラ初期値DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/Rキャラ初期値DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML Ｒキャラ初期値記録, LOCALS:1, LOCALS:0
	SIF リゾート名引き継ぎ != ""
		リゾート名称 '= リゾート名引き継ぎ
ELSE
	CALL 人物データベースセット
	CALL 履歴データベース作成
	CALL ランダムキャラデータベースセット(Ｒキャラ初期値記録)
ENDIF

初期設定中 = 1
RFLAG:キャラ初期登録済み = 0
IF あなた引き継ぎ == 2
	;あなた登録
	LOADCHARA "あなた"
	FOR LOCAL, 0, 100
		あなた特殊能力:LOCAL = あなた特殊能力引き継ぎ:LOCAL
	NEXT
	FOR LOCAL, 0, 20
		ABL:MASTER:(LOCAL + 20) = あなた技巧引き継ぎ:LOCAL
	NEXT
ELSE
	;あなた登録
	ADDCHARA 0
	CALL ADD_PERSON_FROM_CHARA(CHARANUM - 1)
	CALL CUSTOM_CHARAMAKE(MASTER)
	SIF TALENT:MASTER:2 & 2
		MAXBASE:MASTER:5 = 1500
	IF あなた引き継ぎ == 1
		FOR LOCAL, 0, 100
			あなた特殊能力:LOCAL = あなた特殊能力引き継ぎ:LOCAL
		NEXT
		FOR LOCAL, 0, 20
			ABL:MASTER:(LOCAL + 20) = あなた技巧引き継ぎ:LOCAL
		NEXT
	ENDIF
ENDIF

CALL キャラクター初期登録()
引き継いだキャラ人数 = RESULT
CALL コマンド存在チェック


スイートルーム部屋数 = 1
開発済み料理名フラグ文字列 += "_"

CALL ユニークキャラ親子関係セット
CALL エンディングイベントデータベースセット
CALL イベントデータベースセット
CALL 戦闘用データベースセット
CALL シリーズ出典マップセット
IF 引き継ぎ中フラグ
	CALL 人物データベースGC

	LOADTEXT "dat/素材DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/素材DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持素材データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/証DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/証DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持ジョブ証データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/指輪DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/指輪DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持指輪データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/耳飾りDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/耳飾りDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持耳飾りデータベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/アイテムDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/アイテムDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "所持アイテムデータベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/弱点の欠片DT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/弱点の欠片DT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "弱点の欠片データベース", LOCALS:1, LOCALS:0
	LOADTEXT "dat/ミルクDT_XML.txt"
	LOCALS:0 = %RESULTS%
	LOADTEXT "dat/ミルクDT_schema.txt"
	LOCALS:1 = %RESULTS%
	DT_FROMXML "ミルクデータベース", LOCALS:1, LOCALS:0

	所持制服文字列 = %制服引き継ぎ%
	所持エロ衣装文字列 = %エロ衣装引き継ぎ%
	MONEY = 所持ルピ引き継ぎ
	VARSET オート射精先オプション, -1
	ARRAYCOPY "オート射精設定引き継ぎ", "オート射精先オプション"
	ARRAYCOPY "オプション引き継ぎ", "OPTION変数"
	ARRAYCOPY "素質切り替え引き継ぎ", "素質切り替えOPTION記憶"
	ARRAYCOPY "画像サイズ切り替え引き継ぎ", "画像サイズ切り替えOPTION記憶"
	ARRAYCOPY "一日終了時オナニー引き継ぎ", "一日終了時オナニーOPTION"
	ARRAYCOPY "アイテム引き継ぎ", "ITEM"
ELSE

	CALL アイテムデータベースセット
	CALL 指輪データベースセット
	CALL 耳飾りデータベースセット
	CALL ジョブ証データベースセット
	CALL 素材アイテムデータベースセット
	CALL 弱点の欠片データベースセット
	CALL ミルクデータベースセット

	CALL 制服登録処理("リゾート従業員制服")
	CALL 制服登録処理("バニー服")
	CALL 制服登録処理("メイド服")
	CALL 制服登録処理("チャイナドレス")
	CALL 制服登録処理("ミニチャイナ")

	CALL エロ衣装登録処理("逆バニー")
	CALL エロ衣装登録処理("マイクロビキニ")
	CALL エロ衣装登録処理("セクシーランジェリー")

	LOCAL:1 = ENUMFUNCENDSWITH("CLOTHES_初期所持関数_")
	IF LOCAL:1 > 0
		FOR LOCAL, 0, LOCAL:1
			CALLFORM %RESULTS:LOCAL%
		NEXT
	ENDIF
	
	MONEY = 5000
	OPTION変数:身体項目表示切り替え = 1p0 | 1p1 | 1p2 | 1p3 | 1p5
	VARSET オート射精先オプション, -1
	EXシナリオ解放記録 = ___
ENDIF

CALL 食堂メニューデータベースセット
CALL 写真データベースセット
CALL 乗機データベースセット
CALL 所持兵器データベースセット
CALL 従業員業務用マップセット
CALL 自動探索用マップセット
CALL 体位モードデータベースセット
CALL 縁結びデータベースセット
CALL 関係性データベースセット
CALL 初体験データベースセット
CALL デートスコアデータベースセット
CALL 複数人一枚絵データベースセット
CALL 複数人一枚絵_組み合わせ登録処理
ZPキャラ無料回数 = 3

CALL 素材アイテム登録処理

従業員部屋数 = 2
従業員部屋_増築数 = 0
施設改造度:0:0 = 1
AWAIT
CALL RESET_GRAPH_0
AWAIT

PRINTW パラメータの初期化処理を行います
PRINTL 
SAVESTR:10 = /
LOCAL:1 = CHARANUM / 100
REDRAW 0
IF 引き継ぎ中フラグ && キャラ同士関係性データ != ""
	VARSET LOCALS
	SPLIT キャラ同士関係性データ, "+++", LOCALS
	FOR LOCAL, 0, RESULT
		SPLIT LOCALS:LOCAL, "_", 分割文字列
		LOCAL:2 = キャラ検索(分割文字列:0)
		LOCAL:3 = キャラ検索(分割文字列:1)
		SIF LOCAL:2 > 0 && LOCAL:3 > 0
			DT_ROW_ADD "関係性データベース", "対象キャラ１", CFLAG:(LOCAL:2):人物番号, "対象キャラ２", CFLAG:(LOCAL:3):人物番号, "関係性種別", 分割文字列:2
	NEXT
ENDIF
FOR LOCAL, 0, CHARANUM
	IF LOCAL > 0 && LOCAL <= 引き継いだキャラ人数
		CALL キャラクター初期化(LOCAL, 1)
	ELSEIF NO:LOCAL == 999
		CALL キャラクター初期化(LOCAL, 1)
	ELSE
		CALL キャラクター初期化(LOCAL)
	ENDIF
	IF LOCAL % LOCAL:1 == 0
		CLEARLINE 1
		PRINTFORML 進行度……{LOCAL / LOCAL:1}\%
		AWAIT
		;TWAIT 100, 1
	ENDIF
NEXT
CLEARLINE 1
PRINTL 進行度……100%

REDRAW 1



PRINTW パラメータの初期化処理が終了しました

;呼び出し設定
PRINTL
PRINTW 初期設定オプションを呼び出します。
PRINTL
IF SAVESTR:あなたスタイル != "グランくんスタイル"
	CALL OPTION_命のリンク初期切り替え
ENDIF
CALL OPTION_キャラ制限初期切り替え
PRINTL

;呼び出し設定
PRINTL
PRINTW キャラクターの招待制御オプションを呼び出します。
PRINTL
CALL OPTION_招待切り替え
PRINTL
PRINTW このオプションはゲーム開始後もOPTIONから呼び出すことが出来ます。

;素質設定
PRINTL
PRINTW 素質の切り替えオプションを呼び出します。
PRINTL
CALL OPTION_素質切り替え
PRINTL
PRINTW このオプションはゲーム開始後もOPTIONから呼び出すことが出来ます。

;フレーバー設定
PRINTL
PRINTW フレーバー素質の設定を呼び出します。
PRINTL
;先に口上側の設定を読み込んでおく
FOR COUNT, 1, CHARANUM
	TRYCALLFORM フレーバー素質初期設定変更_{COUNT}
NEXT
CALL フレーバー素質初期設定
PRINTL

IF SAVESTR:あなたスタイル == "グランくんスタイル"
	CALL グランくんスタイルセット(0)
	CALL シナリオ_OP_グランくんスタイル(0)
ELSEIF SAVESTR:あなたスタイル == "ジータちゃんスタイル"
	CALL グランくんスタイルセット(1)
	CALL シナリオ_OP_グランくんスタイル(1)
ELSEIF SAVESTR:あなたスタイル == "任意キャラスタイル"
	CALL 任意キャラスタイル_キャラ選定()
	SAVESTR:あなたスタイル = 任意キャラスタイル_{RESULT}
ELSEIF SAVESTR:あなたスタイル == "スタートダッシュモード"
	従業員部屋数 = 4
	従業員部屋_増築数 = 2
	PRINTW 初期追加する従業員を設定します。
	CALL 初期従業員選択(4)
	
	IF 引き継ぎ中フラグ == 0
		PRINTL メインシナリオ進行度を特定のところまでスキップできます
		PRINTL 
		PRINTL ※注意※
		PRINTL 後から変更することはできません
		PRINTBUTTON "[0] スキップしない", 0
		PRINTL 
		PRINTBUTTON "[1] 人食い鳥の島 クリアまでスキップ（ＥＸキャラ解放可能）", 1
		PRINTL 
		PRINTBUTTON "[2] 温泉解放までスキップ（マッサージ解放可能）", 2
		$INPUT_LOOP_Q
		BINPUT
		SELECTCASE RESULT
		CASE 0
		CASE 1 TO 2
			CALL QUICK_START(RESULT)
		CASEELSE
			GOTO INPUT_LOOP_Q
		ENDSELECT
	ENDIF
	
	CALL シナリオ_OP
ELSE
	CALL シナリオ_OP
ENDIF

CALL リゾート名決定処理()

;起床時刻
CFLAG:MASTER:自室位置 = 20
CFLAG:MASTER:現在位置 = CFLAG:MASTER:自室位置
CFLAG:MASTER:現在マップ種別 = 0
CFLAG:MASTER:滞在期間 = 999
CFLAG:MASTER:起床予定時刻 = 420
CFLAG:MASTER:睡眠 = 1
初体験日:MASTER:初対面 = 999
依頼同時最大数 = 1
DAY = 1
OPTION変数:汎用喘ぎ使用 = 3
OPTION変数:汎用喘ぎ使用_男 = 0
CALL 最大レベル算出保存
VARSET 同時モード_選択キャラ, -1
USERCOM表示文字列 = 804_820_999_998_830_831_840_841_850_856_892_893_860_861_895

ボタン化用カラーマトリクス:0:0 = 768,256,256,  0,  0
ボタン化用カラーマトリクス:1:0 = 256,768,256,  0,  0
ボタン化用カラーマトリクス:2:0 = 256,256,256,  0,  0
ボタン化用カラーマトリクス:3:0 =   0,  0,  0,256,  0
ボタン化用カラーマトリクス:4:0 =   0,  0,  0,  0,256

VARSET 通常キャラ招待
VARSET ＤＭキャラ招待

;初期値設定
滞在キャラ数 = 0
滞在キャラ上限 = 20
モブ観光客数 = 0
;あなたは初期状態で雑務に従事している
現在仕事:0:0 = 1
;eraエンドを見てるならeraをキャラ解放
IF エンディングイベント完了フラグ確認("era") > 0
	era番号 = GETCHARA(998)
	CFLAG:era番号:招待不可フラグ = 0
	CFLAG:era番号:滞在期間 = 999
	TALENT:era番号:妊娠不可 = 1
ENDIF

;編成画面初期表示
メンバーリスト表示欄:1 = Ｌｖ
メンバーリスト表示欄:2 = 属性
メンバーリスト表示欄:3 = 固有

初期設定中 = 0
IF 引き継ぎ中フラグ

	;あなた以外をMASTERにして、そのキャラが引き継ぎキャラを妊娠させていた場合の処理
	;@キャラクター初期化内でやるようにしたのでコメアウト
	;IF !GROUPMATCH(SAVESTR:あなたスタイル, "","スタートダッシュモード") && 引き継いだキャラ人数
	;	FOR LOCAL, 1, 引き継いだキャラ人数 + 1
	;		SIF TALENT:LOCAL:妊娠 && CFLAG:LOCAL:子の父親 > 0 && NO:(CFLAG:LOCAL:子の父親) == FLAG:あなたキャラ化記録
	;			CFLAG:LOCAL:子の父親 = 0
	;	NEXT
	;ENDIF

	;使用する変数を掃除
	VARSET 周回回数累計_退避
	VARSET キャラ変数引き継ぎ
	VARSET キャラ文字列引き継ぎ
	制服引き継ぎ = 
	所持ルピ引き継ぎ = 0
	VARSET 引き継ぎキャラ番号
	VARSET あなた特殊能力引き継ぎ
	引き継ぎ中フラグ = 0
	あなた引き継ぎ = 0
	キャラ同士関係性データ = 

	最大レベル保存_周回 = 最大レベル引き継ぎ
	;最大レベルに応じて経験値アイテム配布
	CALL 素材入手処理("星晶の欠片", 最大レベル保存_周回, 1)

	;引き継ぎ時のクイックスタート処理
	SIF 周回後スタート地点
		CALL QUICK_START(周回後スタート地点 + 100)
ENDIF

;バージョンアップ時の処理を不要としてマーク
FOR LOCAL:1, GAMEBASE_ALLOWVERSION, GAMEBASE_VERSION + 1
	SIF IS_SKIPPED_VERSION(LOCAL:1)
		CONTINUE
	CALLFORM VERUP_{LOCAL:1}_FINISH()
NEXT


;変数リセット
CALL TURN_RESET
CALL 最大レベル算出保存

;OPTION数値のチェック
SIF OPTION変数:招待画像枚数閾値 < 1
	OPTION変数:招待画像枚数閾値 = 1

;ログボ処理
LOCAL = GETSECOND() / 3600
;日単位
LOCAL:1 = (LOCAL + 19) / 24

IF (REAL_TIME < LOCAL:1)
	REAL_TIME = LOCAL:1
	CALL ログインボーナス処理
ENDIF

BEGIN SHOP

@EVENTLOAD
CALL VERSION_UP



@キャラクター初期登録()
#DIM 設定中キャラ番号
#DIM 弱点配列
#DIM 引き継ぎ番号
#DIM ランダム引き継ぎ番号
#DIM キャラ番号
#DIMS 大分割文字列, 10
#DIMS 小分割文字列, 1000
#DIM 引き継いだランダムキャラ, 10
#DIM 引き継いだキャラ人数

VARSET 引き継いだランダムキャラ, -1
VARSET 大分割文字列
VARSET 小分割文字列
引き継いだキャラ人数 = 0

IF 引き継ぎ中フラグ
	キャラ番号 = 0
	ランダム引き継ぎ番号 = 0
	FOR 引き継ぎ番号, 0, 引き継ぎ人数
		IF 引き継ぎキャラ番号:引き継ぎ番号 > 0
			;引き継ぎキャラは先に登録
			LOADCHARA TOSTR(引き継ぎ番号)
			キャラ番号 += 1
			IF NO:キャラ番号 == 999
				引き継いだランダムキャラ:ランダム引き継ぎ番号 = ランダムキャラデータ_GET_KEY_FROM_PERSON(, CFLAG:キャラ番号:人物番号)
				ランダム引き継ぎ番号 += 1
			ENDIF
			VARSET 現在仕事:キャラ番号:0
			引き継いだキャラ人数 ++
		ENDIF
	NEXT
ENDIF

FOR キャラ番号, 1, キャラクタ数上限
	SIF !EXISTCSV(キャラ番号)
		CONTINUE
	;ランダムキャラ排除
	SIF キャラ番号 == 999
		CONTINUE
	;登録済みのキャラ(=引き継ぎキャラ)はスキップ
	GETCHARA キャラ番号
	SIF RESULT > -1
		CONTINUE
	ADDCHARA キャラ番号
	設定中キャラ番号 = CHARANUM - 1
	CALL ADD_PERSON_FROM_CHARA(設定中キャラ番号)
	IF 引き継ぎ中フラグ && キャラ番号 != 0
		FOR 引き継ぎ番号, 0, キャラクタ数上限
			IF キャラ変数引き継ぎ:引き継ぎ番号:キャラナンバー == NO:設定中キャラ番号
				EXP:設定中キャラ番号:肉体熟知 = キャラ変数引き継ぎ:引き継ぎ番号:肉体熟知
				FOR 弱点配列, 0, 50
					弱点看破:設定中キャラ番号:弱点配列 = キャラ変数引き継ぎ:引き継ぎ番号:(弱点配列 + 2)
					弱点コマンド枠:設定中キャラ番号:弱点配列 = %キャラ文字列引き継ぎ:引き継ぎ番号:弱点配列%
				NEXT
				BREAK
			ENDIF
		NEXT
		CALL 弱点妥当性チェック(設定中キャラ番号)
	ENDIF
NEXT

;引き継ぎランダムキャラの追加
IF 引き継ぎ中フラグ
	LOCAL = 1
	WHILE ランダムキャラデータ_IS_KEY_EXIST(, LOCAL)
		IF MATCH(引き継いだランダムキャラ, LOCAL) > 0 || ランダムキャラデータ_IS_KEY_IGNORED(, LOCAL)
			LOCAL ++
			CONTINUE
		ENDIF
		ADDCHARA 999
		設定中キャラ番号 = CHARANUM - 1
		FOR 引き継ぎ番号, 0, キャラクタ数上限
			IF キャラ変数引き継ぎ:引き継ぎ番号:キャラナンバー == LOCAL * -1
				EXP:設定中キャラ番号:肉体熟知 = キャラ変数引き継ぎ:引き継ぎ番号:肉体熟知
				FOR 弱点配列, 0, 50
					弱点看破:設定中キャラ番号:弱点配列 = キャラ変数引き継ぎ:引き継ぎ番号:(弱点配列 + 2)
					弱点コマンド枠:設定中キャラ番号:弱点配列 = %キャラ文字列引き継ぎ:引き継ぎ番号:弱点配列%
				NEXT
				BREAK
			ENDIF
		NEXT
		CALL ランダムキャラデータ復元(, 設定中キャラ番号, LOCAL)
		CALL 初期ステータス設定_テンプレート適用(タイプ数値文字列変換(基礎BATTLE_STATE:設定中キャラ番号:ステータスタイプ保存), 設定中キャラ番号)
		CALL ADD_PERSON_FROM_CHARA(設定中キャラ番号)
		CALL ランダムキャラデータ_SET(, LOCAL, "人物番号", 0, CFLAG:設定中キャラ番号:人物番号)

		;素質の調整
		NAME:設定中キャラ番号 = [一般人]%CALLNAME:設定中キャラ番号%
		TALENT:設定中キャラ番号:従業員 = 0
		TALENT:設定中キャラ番号:定住者 = 0
		CFLAG:設定中キャラ番号:招待不可フラグ = 0

		LOCAL ++
	WEND
ENDIF

RFLAG:キャラ初期登録済み = 1
RETURN 引き継いだキャラ人数


@キャラクター初期化(ARG, 引き継ぎキャラフラグ = 0)
#DIM 引き継ぎキャラフラグ
#DIM 得意武器記録
#DIM キャラ番号記録
#DIMS CSTR切り分け文字列格納, 2
VARSET LOCALS
VARSET CSTR切り分け文字列格納


;全員睡眠状態
CFLAG:ARG:睡眠 = 1
CFLAG:ARG:滞在期間 = -1
CFLAG:ARG:現在位置 = 999
CFLAG:ARG:現在マップ種別 = 0
BASE:ARG:酩酊 = 0
MAXBASE:ARG:酩酊 = 1000
BASE:ARG:母乳 = 0
MAXBASE:ARG:母乳 = 10000
BASE:ARG:性欲 = 0
MAXBASE:ARG:性欲 = 1200
BASE:ARG:満足 = 0
MAXBASE:ARG:満足 = 1200
BASE:ARG:警戒 = 0
MAXBASE:ARG:警戒 = 1001
CALL CLOTHES_RESET(ARG)
SIF CSTR:ARG:二人称 == ""
	CSTR:ARG:二人称 = %CALLNAME:PLAYER%
CALL 精力ゲージ_初期化(ARG)

;勃起、ムード、理性、怒り
BASE:ARG:勃起 = 0
BASE:ARG:ムード = 0
BASE:ARG:理性 = MAXBASE:ARG:理性
BASE:ARG:怒り = 0
BASE:ARG:勃起 = 0
CFLAG:ARG:勃起度 = 0
CFLAG:ARG:同行 = 0
CFLAG:ARG:デート = 0
CFLAG:ARG:酔いすぎ = 0
;発情期
; SIF 発情期存在判定(ARG)
; 	CFLAG:ARG:発情期フラグ = RAND:9 + 1

IF 引き継ぎキャラフラグ == 0
	;招待時同行キャラ
	IF CSTR:ARG:同行キャラ != ""
		VARSET LOCALS
		REPLACE CSTR:ARG:同行キャラ, " ", ""
		SPLIT RESULTS, "%", LOCALS
		LOCAL:2 = RESULT - 1
		FOR LOCAL, 0, LOCAL:2
			SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
			;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
			LOCAL:10 = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
			IF LOCAL:10 >= 0
				同行候補キャラ番号:ARG:LOCAL = LOCAL:10
				同行候補キャラ確率:ARG:LOCAL = TOINT(CSTR切り分け文字列格納:1)
			ENDIF
		NEXT
	ENDIF

	;性別保存
	CFLAG:ARG:元性別保存 = TALENT:ARG:性別

	;知識素質
	IF CSTR:ARG:知識素質設定 != ""
		VARSET LOCALS
		SUBSTRING CSTR:ARG:知識素質設定, 1, -1
		SPLIT RESULTS, " ", LOCALS
		LOCAL:2 = RESULT - 1
		FOR LOCAL, 0, LOCAL:2
			SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
			;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
			知識素質:ARG:(CSTR切り分け文字列格納:0) = TOINT(CSTR切り分け文字列格納:1)
		NEXT
	ENDIF

	;性癖素質
	IF CSTR:ARG:性癖素質設定 != ""
		VARSET LOCALS
		SUBSTRING CSTR:ARG:性癖素質設定, 1, -1
		SPLIT RESULTS, " ", LOCALS
		LOCAL:2 = RESULT - 1
		FOR LOCAL, 0, LOCAL:2
			SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
			;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
			性癖素質:ARG:(CSTR切り分け文字列格納:0) = TOINT(CSTR切り分け文字列格納:1)
		NEXT
	ENDIF

	;素質切り替え
	IF TALENT:ARG:陥没乳首 && 素質切り替えOPTION記憶:陥没乳首_なし
		TALENT:ARG:陥没乳首 = 0
	ENDIF
	IF TALENT:ARG:母乳体質 && 素質切り替えOPTION記憶:母乳体質_なし
		TALENT:ARG:母乳体質 = 0
	ENDIF
	IF TALENT:ARG:性別 == 3 && 素質切り替えOPTION記憶:ふたなり_女性
		TALENT:ARG:性別 = 1
	ENDIF

	IF EXP:ARG:キス経験
		CALL 初体験日登録処理(ARG, -1, "ファーストキス", -1, "不明", "唇", "", 1)
	ENDIF

	SIF ARG == MASTER
		RETURN 0

	;戦闘ステータス
	IF CSTR:ARG:戦闘基礎ステータス設定 != "" || 基礎BATTLE_STATE:ARG:得意武器 == 0
		VARSET LOCALS
		SUBSTRING CSTR:ARG:戦闘基礎ステータス設定, 1, -1
		SPLIT RESULTS, " ", LOCALS

		;属性
		SPLIT LOCALS:0, "_", CSTR切り分け文字列格納
		基礎BATTLE_STATE:ARG:属性 = 属性文字列数値変換(CSTR切り分け文字列格納:1)
		;得意武器
		SPLIT LOCALS:1, "_", CSTR切り分け文字列格納
		VARSET RESULTS
		SPLIT CSTR切り分け文字列格納:1, "・", RESULTS
		得意武器記録 = 0
		FOR LOCAL, 0, RESULT
			SETBIT 得意武器記録, 得意武器文字列数値変換(RESULTS:LOCAL)
		NEXT
		基礎BATTLE_STATE:ARG:得意武器 = 得意武器記録
		;初期ステータス
		SPLIT LOCALS:2, "_", CSTR切り分け文字列格納
		CALL 初期ステータス設定_テンプレート適用(CSTR切り分け文字列格納:1, ARG)
	ELSE
		CALL 初期ステータス設定_テンプレート適用(タイプ数値文字列変換(基礎BATTLE_STATE:ARG:ステータスタイプ保存), ARG)
	ENDIF
	TRYCALLFORM 戦闘初期ステータス設定_{NO:ARG}(ARG)
	;表示奥義設定
	IF CSTR:ARG:表示奥義設定 != ""
		VARSET LOCALS
		SPLIT CSTR:ARG:表示奥義設定, "_", LOCALS
		表示奥義:ARG:0 '= LOCALS:0
		表示奥義:ARG:1 '= LOCALS:1
		表示奥義:ARG:2 '= LOCALS:2
	ENDIF
	IF CSTR:ARG:陥落奥義設定 != ""
		VARSET LOCALS
		SPLIT CSTR:ARG:陥落奥義設定, "_", LOCALS
		陥落奥義:ARG:0 '= LOCALS:0
		陥落奥義:ARG:1 '= LOCALS:1
		陥落奥義:ARG:2 '= LOCALS:2
	ENDIF

	CALL 弱点コマンド初期設定(ARG)
	TRYCALLFORM 口上側初期パラメータ変更_{NO:ARG}
	;この時点で身長・スリーサイズが入っている場合、それを初期値として登録
	SIF BASE:ARG:身長
		CFLAG:ARG:身長初期値 = BASE:ARG:身長
	SIF BASE:ARG:バスト
		CFLAG:ARG:バスト初期値 = BASE:ARG:バスト
	SIF BASE:ARG:ウエスト
		CFLAG:ARG:ウエスト初期値 = BASE:ARG:ウエスト
	SIF BASE:ARG:ヒップ
		CFLAG:ARG:ヒップ初期値 = BASE:ARG:ヒップ

	;正の年齢が入っている場合、それをもとに年齢層を計算
	SIF BASE:ARG:年齢 > 0
		TALENT:ARG:年齢層 = 年齢層算出処理(BASE:ARG:年齢)

	SIF 従業員追加中フラグ
		従業員追加中フラグ = 0

	;身長・体格決定
	IF !BASE:ARG:身長 || !TALENT:ARG:体格
		CALL INIT_BODYSIZE_HEIGHT(ARG)
	ENDIF
	;ウエスト・体型決定
	IF !BASE:ARG:ウエスト || !TALENT:ARG:体型
		CALL INIT_BODYSIZE_WAIST(ARG)
	ENDIF
	;バスト決定
	IF !BASE:ARG:バスト || !TALENT:ARG:バストサイズ
		CALL INIT_BODYSIZE_BUST(ARG)
	ENDIF
	;ヒップ決定
	IF !BASE:ARG:ヒップ || !TALENT:ARG:下半身
		CALL INIT_BODYSIZE_HIP(ARG)
	ENDIF


	; IF !BASE:ARG:身長 || (!BASE:ARG:バスト && TALENT:ARG:性別 != 2) || !BASE:ARG:ウエスト || !BASE:ARG:ヒップ
	; 	CALL INIT_BODYSIZE(ARG)
	; ELSE
	; 	IF BASE:ARG:身長
	; 		;身長があるなら体格の設定
	; 		CALL INIT_BODYTALENT(ARG)
	; 	ENDIF
	; ENDIF

	;画像フォルダで有効なものを探す
	IF STRFIND(CSTR:ARG:汎用立ち絵登録文字列, "そっくりさん設定_") >= 0
		CSTR:ARG:画像フォルダ '= REPLACE(CSTR:ARG:汎用立ち絵登録文字列, "そっくりさん設定_", "")
	ELSE
		CALL FIX_RESOURCES_FOLDER(ARG)
	ENDIF

ELSE
	SIF !BASE:ARG:身長
		CALL INIT_BODYSIZE(ARG)

	;同行キャラをCSVを見て入れ直す
	VARSET LOCAL
	VARSET LOCALS
	;まず引き継ぎキャラ同士の同行記録を見る
	IF CSTR:ARG:引き継ぎ用同行キャラ確率保存 != ""
		SPLIT CSTR:ARG:引き継ぎ用同行キャラ確率保存, "_", LOCALS
		FOR LOCAL, 0, 20
			SIF LOCALS:LOCAL == ""
				BREAK
			キャラ番号記録 = FINDCHARA(NAME, LOCALS:LOCAL)
			SIF キャラ番号記録 < 0
				CONTINUE
			同行候補キャラ番号:ARG:(LOCAL:1) = キャラ番号記録
			同行候補キャラ確率:ARG:(LOCAL:1) = TOINT(LOCALS:(LOCAL + 1))
			LOCAL:1 += 1
			LOCAL += 1
		NEXT
	ENDIF
	;招待時同行キャラをCSVから見直す
	VARSET LOCALS
	CSTR:ARG:同行キャラ = %CSVCSTR(NO:ARG, GETNUM(CSTR, "同行キャラ"))%
	REPLACE CSTR:ARG:同行キャラ, " ", ""
	SPLIT RESULTS, "%", LOCALS
	FOR LOCAL, 0, 100
		SIF LOCALS:LOCAL == ""
			BREAK
		SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
		キャラ番号記録 = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
		SIF キャラ番号記録 < 0
			CONTINUE
		SIF FINDELEMENT(同行候補キャラ番号:ARG:0, キャラ番号記録) > -1
			CONTINUE
		;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
		同行候補キャラ番号:ARG:(LOCAL:1) = キャラ番号記録
		同行候補キャラ確率:ARG:(LOCAL:1) = TOINT(CSTR切り分け文字列格納:1)
		LOCAL:1 += 1
	NEXT
	;キャラ妊娠引き継ぎ時
	IF TALENT:ARG:妊娠
		;引き継ぎキャラが種親の場合、-10000するとキャラIDになるようにいじってある
		IF CFLAG:ARG:子の父親 > 10000
			CFLAG:ARG:子の父親 -= 10000
		;引き継ぎありでランダムキャラの場合
		ELSEIF CFLAG:ARG:子の父親 < 0
			CFLAG:ARG:子の父親 = FIND_CHARA_FROM_PERSON_ID(CFLAG:ARG:子の父親 * -1)
		;あなたかつMASTERを別キャラにした場合、不明にしようとも思ったけどそのキャラにしたのでコメアウト
		;ELSEIF !CFLAG:ARG:子の父親 && !GROUPMATCH(SAVESTR:あなたスタイル, "","スタートダッシュモード")
		;	CFLAG:ARG:子の父親 = -1
		;いないキャラ(引き継がなかったランダムキャラなど)が種親の場合、-1(親不明？)に
		ELSEIF CFLAG:ARG:子の父親 == 999
			CFLAG:ARG:子の父親 = -1
		;MASTERにしたキャラだった場合
		ELSEIF FLAG:あなたキャラ化記録 == CFLAG:ARG:子の父親
			CFLAG:ARG:子の父親 = 0
		ELSEIF CFLAG:ARG:子の父親 > 0
			CFLAG:ARG:子の父親 = GETCHARA(CFLAG:ARG:子の父親)
		ENDIF
	ENDIF
ENDIF
CALL 口上変数初期化

CALL 体力変動素質適用処理(ARG)
BASE:ARG:体力 = MAXBASE:ARG:体力

;引き継ぎバグ対応のため関係性はリセットしてあるので、全員セットし直す
CALL INIT_RELATION(ARG)

;初期関係性のセット、引き継ぎキャラの場合は先に設定してあるので同じ処理でOK
CALL 初期関係性データベース登録(ARG)

;性癖の設定
IF TALENT:ARG:性愛傾向 == 3
	性癖素質:ARG:同性愛 = 2
ENDIF


@弱点コマンド初期設定(ARG)
#DIMS 弱点候補_セクハラ, 200
#DIMS 弱点候補_うふふ, 200
#DIMS 敏感弱点候補_セクハラ, 200
#DIMS 敏感弱点候補_うふふ, 200
#DIM 敏感セクハラ候補数
#DIM 敏感うふふ候補数
#DIM セクハラ候補数
#DIM うふふ候補数
#DIM チェック完了

;弱点が全て埋まっている場合（=周回中）はここで処理終了
SIF MATCH(弱点コマンド枠:ARG:0, "", 0, 5) == 0 && MATCH(弱点コマンド枠:ARG:0, "-1", 0, 5) == 0
	RETURN

;弱点初期化
VARSET 弱点コマンド枠:ARG:0

;まず口上側で弱点の設定があるか見に行く
TRYCCALLFORM 弱点コマンド変更_{NO:ARG}
	;弱点が全て埋まった場合はここで処理終了
	SIF MATCH(弱点コマンド枠:ARG:0, "", 0, 5) == 0 && MATCH(弱点コマンド枠:ARG:0, "-1", 0, 5) == 0
		RETURN
CATCH
ENDCATCH

セクハラ候補数 = 0
うふふ候補数 = 0
敏感セクハラ候補数 = 0
敏感うふふ候補数 = 0


;敏感素質に対応した弱点候補をリスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合+敏感素質に対応してる場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}") && 敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}")
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
				CASE 1
					敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}
					敏感セクハラ候補数 += 1
				CASE 2
					敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}
					敏感うふふ候補数 += 1
			ENDSELECT
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるOR敏感素質に対応してないなら飛ばす
			SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}") || !敏感素質対応判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CONTINUE
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CASE 1
					敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{LOCAL:1}
					敏感セクハラ候補数 += 1
				CASE 2
					敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{LOCAL:1}
					敏感うふふ候補数 += 1
			ENDSELECT
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるOR敏感素質に対応してないなら飛ばす
		SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}") || !敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CASE 1
				敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{NO:ARG}
				敏感セクハラ候補数 += 1
			CASE 2
				敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{NO:ARG}
				敏感うふふ候補数 += 1
		ENDSELECT
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:5, 0, 1
				FOR LOCAL:2, 0, 20
					SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
						CONTINUE
					LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
					;口上側と被ってるなら飛ばす
					SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}") || !敏感素質対応判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
						CONTINUE
					SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
						CASE 1
							敏感弱点候補_セクハラ:敏感セクハラ候補数 = {LOCAL}_{LOCAL:3}
							敏感セクハラ候補数 += 1
						CASE 2
							敏感弱点候補_うふふ:敏感うふふ候補数 = {LOCAL}_{LOCAL:3}
							敏感うふふ候補数 += 1
					ENDSELECT
				NEXT
			NEXT
		NEXT
	ENDIF
NEXT


;敏感対応の候補からセクハラとうふふに一つずつ設定
;ただし、以下の条件を守る
;・弱点コマンド枠は最低一つ空ける
;・うふふとセクハラのどちらか一つしか入らないならうふふ優先

;うふふ設定
LOCAL = 0
WHILE 1
	SIF LOCAL > 3 || !敏感うふふ候補数
		BREAK
	IF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		LOCAL ++
		CONTINUE
	ENDIF
	LOCAL:1 = RAND:敏感うふふ候補数
	弱点コマンド枠:ARG:LOCAL = %敏感弱点候補_うふふ:(LOCAL:1)%
	BREAK
WEND

;セクハラ設定
LOCAL = 0
WHILE 1
	SIF LOCAL > 3 || !敏感セクハラ候補数
		BREAK
	IF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		LOCAL ++
		CONTINUE
	ENDIF
	LOCAL:1 = RAND:敏感セクハラ候補数
	弱点コマンド枠:ARG:LOCAL = %敏感弱点候補_セクハラ:(LOCAL:1)%
	BREAK
WEND

;弱点候補リスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}")
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
				CASE 1
					弱点候補_セクハラ:セクハラ候補数 = {LOCAL}
					セクハラ候補数 += 1
				CASE 2
					弱点候補_うふふ:うふふ候補数 = {LOCAL}
					うふふ候補数 += 1
			ENDSELECT
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるなら飛ばす
			SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}")
				CONTINUE
			SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				CASE 1
					弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{LOCAL:1}
					セクハラ候補数 += 1
				CASE 2
					弱点候補_うふふ:うふふ候補数 = {LOCAL}_{LOCAL:1}
					うふふ候補数 += 1
			ENDSELECT
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるなら飛ばす
		SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}")
			CONTINUE
		SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			CASE 1
				弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{NO:ARG}
				セクハラ候補数 += 1
			CASE 2
				弱点候補_うふふ:うふふ候補数 = {LOCAL}_{NO:ARG}
				うふふ候補数 += 1
		ENDSELECT
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
					CONTINUE
				LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
				;口上側と被ってるなら飛ばす
				SIF MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					CONTINUE
				SELECTCASE 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					CASE 1
						弱点候補_セクハラ:セクハラ候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
						セクハラ候補数 += 1
					CASE 2
						弱点候補_うふふ:うふふ候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
						うふふ候補数 += 1
				ENDSELECT
			NEXT
		NEXT
	ENDIF
NEXT

;空いている枠にセクハラ系から２つ、うふふ系から２つ、完全ランダムで１つ設定する

FOR LOCAL, 0, 5
	SIF 弱点コマンド枠:ARG:LOCAL != "" && 弱点コマンド枠:ARG:LOCAL != "-1"
		CONTINUE
	SELECTCASE LOCAL
		CASE 0 TO 1
			LOCAL:1 = RAND:セクハラ候補数
			弱点コマンド枠:ARG:LOCAL = %弱点候補_セクハラ:(LOCAL:1)%
			FOR LOCAL:2, LOCAL:1, セクハラ候補数
				弱点候補_セクハラ:(LOCAL:2) = %弱点候補_セクハラ:(LOCAL:2 + 1)%
			NEXT
			セクハラ候補数 -= 1
		CASE 2 TO 3
			LOCAL:1 = RAND:うふふ候補数
			弱点コマンド枠:ARG:LOCAL = %弱点候補_うふふ:(LOCAL:1)%
			FOR LOCAL:2, LOCAL:1, うふふ候補数
				弱点候補_うふふ:(LOCAL:2) = %弱点候補_うふふ:(LOCAL:2 + 1)%
			NEXT
			うふふ候補数 -= 1
		CASEELSE
			LOCAL:1 = RAND:(セクハラ候補数 + うふふ候補数)
			IF LOCAL:1 < セクハラ候補数
				弱点コマンド枠:ARG:LOCAL = %弱点候補_セクハラ:(LOCAL:1)%
			ELSE
				弱点コマンド枠:ARG:LOCAL = %弱点候補_うふふ:(LOCAL:1 - セクハラ候補数)%
			ENDIF
	ENDSELECT
NEXT

@弱点コマンド候補判定(ARG, ARGS)
;返り値
;0=候補外
;1=弱点候補_セクハラ
;2=弱点候補_うふふ
#FUNCTION
弱点コマンドカテゴリ = 
TFLAG:コマンドタイプ受渡 = 999
CALLFORMF %ARGS%
SIF 弱点コマンドカテゴリ == ""
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "角") >= 0 && TALENT:ARG:角あり == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "尻尾") >= 0 && TALENT:ARG:尻尾あり == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "ちんこ") >= 0 && GETBIT(TALENT:ARG:性別, 1) == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "女") >= 0 && GETBIT(TALENT:ARG:性別, 0) == 0
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "V") >= 0 && GETBIT(TALENT:ARG:性別, 0) == 0
	RETURNF 0
IF GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), ARG) == "異性愛"
	;性癖名が異性愛＝[女性愛]持ち女性or[男性愛]持ち男性
	SIF STRFIND(弱点コマンドカテゴリ, "異性") >= 0 && 性癖素質:ARG:同性愛 <= 0
		RETURNF 0
ELSE
	SIF STRFIND(弱点コマンドカテゴリ, "同性") >= 0 && 性癖素質:ARG:同性愛 <= 0
		RETURNF 0
ENDIF
SELECTCASE TFLAG:コマンドタイプ受渡
	CASE 1 TO 3
		RETURNF 1
	CASE 100, 101
		;複数コマンド
		SIF STRFIND(TSTR:複合コマンドタイプ受け渡し, "セクハラ") >= 0
			RETURNF 1
		SIF STRFIND(TSTR:複合コマンドタイプ受け渡し, "うふふ") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し, "Ｖ系") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し,"Ａ系") >= 0 || STRFIND(TSTR:複合コマンドタイプ受け渡し, "道具系") >= 0
			RETURNF 2
	CASEELSE
		;うふふ扱い
		RETURNF 2
ENDSELECT

@弱点コマンドランダム選出(ARG)
#DIMS 弱点候補, 1000
#DIM 弱点候補数

VARSET 弱点候補
弱点候補数 = 0
;弱点候補リスト化
FOR LOCAL, 0, 1000
	;お祭りコマンドは弱点候補にしない
	SIF INRANGE(LOCAL, 385, 389)
		CONTINUE
	;通常コマンド
	IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
		;口上側と被ってない場合のみ
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}")
			弱点候補:弱点候補数 = {LOCAL}
			弱点候補数 += 1
		ENDIF
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
				CONTINUE
			;口上側と被ってるなら飛ばす
			IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:1}") && 弱点コマンド候補判定(ARG, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				弱点候補:弱点候補数 = {LOCAL}_{LOCAL:1}
				弱点候補数 += 1
			ENDIF
		NEXT
	ENDIF
	;専用コマンド
	IF INRANGE(LOCAL, 370, 374)
		SIF !EXISTFUNCTION(@"COMTYPE_{LOCAL}_{NO:ARG}")
			CONTINUE
		;口上側と被ってるなら飛ばす
		IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{NO:ARG}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{NO:ARG}")
			弱点候補:弱点候補数 = {LOCAL}_{NO:ARG}
			弱点候補数 += 1
		ENDIF
	;場所固有コマンド
	ELSEIF INRANGE(LOCAL, 380, 384)
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
					CONTINUE
				LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
				;口上側と被ってるなら飛ばす
				IF !MATCH(弱点コマンド枠:ARG:0, @"{LOCAL}_{LOCAL:5}_{LOCAL:3}") && 弱点コマンド候補判定(ARG, @"COMTYPE_{LOCAL}_{LOCAL:5}_{LOCAL:3}")
					弱点候補:弱点候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
					弱点候補数 += 1
				ENDIF
			NEXT
		NEXT
	ENDIF
NEXT


SIF 弱点候補数 > 0
	RESULTS = %弱点候補:(RAND:弱点候補数)%


@敏感素質対応判定(ARG, ARGS)
#FUNCTION
;敏感素質を検出
弱点コマンドカテゴリ = 
CALLFORMF %ARGS%
;弱点コマンドカテゴリに対応する敏感素質があれば1を返す
SIF 弱点コマンドカテゴリ == ""
	RETURNF 0
SIF STRFIND(弱点コマンドカテゴリ, "C") >= 0 && TALENT:ARG:Ｃ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "V") >= 0 && TALENT:ARG:Ｖ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "A") >= 0 && TALENT:ARG:Ａ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "B") >= 0 && TALENT:ARG:Ｂ感度 > 0
	RETURNF 1
SIF STRFIND(弱点コマンドカテゴリ, "S") >= 0 && TALENT:ARG:Ｓ感度 > 0
	RETURNF 1
;ここまで来た時点で該当するものがないのでオワリ
RETURNF 0

@コマンド存在チェック
#DIM コマンド総数
#DIM ループ用

コマンド総数 = ENUMFUNCBEGINSWITH("COMTYPE_")

;通常コマンド
FOR LOCAL, 0, 1000
	IF MATCH(RESULTS, @"COMTYPE_{LOCAL}")
		SETBIT 通常コマンド存在フラグ:LOCAL, 0
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF MATCH(RESULTS, @"COMTYPE_S_{LOCAL}_{LOCAL:1}")
				SETBIT 通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1
		NEXT
	ENDIF
NEXT
;場所固有コマンド
FOR LOCAL, 0, 5
	FOR LOCAL:3, 0, 1
		FOR LOCAL:1, 0, 20
			FOR LOCAL:2, 0, 20
				SIF MATCH(RESULTS, @"COMTYPE_{LOCAL + 380}_{LOCAL:3}_{LOCAL:1 * 100 + LOCAL:2 + 1}")
					SETBIT 場所固有コマンド存在フラグ:LOCAL:(LOCAL:3):(LOCAL:1), LOCAL:2
			NEXT
		NEXT
	NEXT
NEXT

VARSET RESULTS
コマンド総数 = ENUMFUNCBEGINSWITH("MASSAGE_COMTYPE_")

;マッサージコマンド
FOR LOCAL, 0, コマンド総数
	IF MATCH(RESULTS, @"MASSAGE_COMTYPE_{LOCAL}")
		SETBIT マッサージコマンド存在フラグ:LOCAL, 0
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF MATCH(RESULTS, @"MASSAGE_COMTYPE_S_{LOCAL}_{LOCAL:1}")
				SETBIT マッサージコマンド存在フラグ:LOCAL, LOCAL:1 + 1
		NEXT
	ENDIF
NEXT

VARSET RESULTS
コマンド総数 = ENUMFUNCBEGINSWITH("RAPE_COMTYPE_")

;むりやりうふふコマンド
FOR LOCAL, 0, コマンド総数
	IF MATCH(RESULTS, @"RAPE_COMTYPE_{LOCAL}")
		SETBIT むりやりうふふコマンド存在フラグ:LOCAL, 0
		;派生コマンド
		FOR LOCAL:1, 0, 20
			SIF MATCH(RESULTS, @"RAPE_COMTYPE_S_{LOCAL}_{LOCAL:1}")
				SETBIT むりやりうふふコマンド存在フラグ:LOCAL, LOCAL:1 + 1
		NEXT
	ENDIF
NEXT

@年齢層算出処理(年齢)
#FUNCTION
#DIM 年齢
#DIM ループ用

FOR ループ用, 年齢層_幼い, 年齢層_人外 + 1
	SIF 年齢 < 年齢ボーダー:ループ用
		RETURNF ループ用
NEXT
RETURNF 年齢層_不詳

@体力変動素質適用処理(キャラ番号)
#DIM キャラ番号
#DIM 体力計算値
#DIM 性欲計算値

体力計算値 = CSVBASE(NO:キャラ番号, 0)

;素質によって体力を加算減算する
SIF GETBIT(TALENT:キャラ番号:性別, 0) && TALENT:キャラ番号:処女 == 0
	体力計算値 += 200
SIF GETBIT(TALENT:キャラ番号:性別, 1) && TALENT:キャラ番号:非童貞 > 0
	体力計算値 += 200
体力計算値 += TALENT:キャラ番号:度胸 * 50
SIF TALENT:キャラ番号:精神的余裕 > 0
	体力計算値 += 100
体力計算値 += TALENT:キャラ番号:性的興味 * 100
体力計算値 += TALENT:キャラ番号:性欲 * 100
体力計算値 += TALENT:キャラ番号:経験豊富 * 200
体力計算値 += TALENT:キャラ番号:自慰しやすい * 50
体力計算値 += TALENT:キャラ番号:快感応答 * 50
体力計算値 += TALENT:キャラ番号:絶倫 * 400
体力計算値 += TALENT:キャラ番号:子持ち * 400
IF TALENT:キャラ番号:恋人持ち > 0
	体力計算値 += 300
ELSEIF TALENT:キャラ番号:セフレあり
	体力計算値 += 700
ENDIF

SIF 知識素質:キャラ番号:性知識 > 0
	体力計算値 += 知識素質:キャラ番号:性知識 * 500

SIF CFLAG:キャラ番号:元気になる薬フラグ > 0
	体力計算値 += 1000

SIF 陥落チェック_性的(キャラ番号)
	体力計算値 += 1000

SELECTCASE あなた特殊能力:体力成長の加護
	CASE 1
		体力計算値 += EXP:キャラ番号:ダウン経験 * 50
	CASE 2
		体力計算値 += EXP:キャラ番号:ダウン経験 * 75
	CASE 3
		体力計算値 += EXP:キャラ番号:ダウン経験 * 100
	CASE 4
		体力計算値 += EXP:キャラ番号:ダウン経験 * 150
ENDSELECT

;EXP変動分
体力計算値 += EXP:キャラ番号:滞在日数 * 5
体力計算値 += EXP:キャラ番号:従業員日数 * 10
体力計算値 += EXP:キャラ番号:定住者日数 * 10
体力計算値 += EXP:キャラ番号:探索経験 * 5

;体力成長分適用
体力計算値 += CFLAG:キャラ番号:体力成長値

MAXBASE:キャラ番号:体力 = MIN(体力計算値, 9999)
SIF 妊娠時疲労(キャラ番号)
	MAXBASE:キャラ番号:体力 -= 妊娠時疲労(キャラ番号,1)
;性欲は体力と同値
;発情期は性欲ゲージにのみ掛かる
性欲計算値 = 体力計算値
IF CFLAG:キャラ番号:発情期フラグ < 0
	性欲計算値 = MIN(体力計算値 * 3 / 2, 9999)
ENDIF
MAXBASE:キャラ番号:性欲 = MIN(性欲計算値, 9999)

RESULT = 体力計算値
TRYCALLFORM 固有体力補正_{NO:キャラ番号}(キャラ番号, 体力計算値)
MAXBASE:キャラ番号:体力 = MIN(RESULT, 9999)

RESULT = 性欲計算値
TRYCALLFORM 固有性欲補正_{NO:キャラ番号}(キャラ番号, 性欲計算値)
MAXBASE:キャラ番号:性欲 = MIN(RESULT, 9999)

BASE:キャラ番号:体力 = MIN(MAXBASE:キャラ番号:体力, BASE:キャラ番号:体力)
BASE:キャラ番号:性欲 = MIN(MAXBASE:キャラ番号:性欲, BASE:キャラ番号:性欲)
MAXBASE:キャラ番号:満足 = MAXBASE:キャラ番号:性欲
BASE:キャラ番号:満足 = 0

@フレーバー素質初期設定
#DIM 全体設定
#DIM 残りキャラ設定

DRAWLINE
PRINTL 陰毛の濃さや乳輪の大きさなど、快楽発生量に直接関わらない素質を設定します。
PRINTL （口上の変動やコマンド発生条件には関わる可能性があります）
PRINTL 
PRINTL またこれらの設定にこだわりがない場合、［全て表示しない］を選ぶことでキャラのイメージを壊さないことが可能です。
DRAWLINE
PRINTL 
PRINT 現在の設定：
SELECTCASE 全体設定
	CASE 0
		PRINTBUTTON "[全て表示しない]", 100
		PRINTL
	CASE 1
		PRINTBUTTON "[初対面時に設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[初対面時に設定]", 101
		ENDIF
		PRINTL
	CASE 2
		PRINTBUTTON "[初うふふ時に設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[初うふふ時に設定]", 101
		ENDIF
		PRINTL
	CASE 3
		PRINTBUTTON "[ランダムに設定]", 100
		PRINTL
		PRINT 口上側で設定があるキャラについて：
		IF 残りキャラ設定 == 0
			PRINTBUTTON "[口上側の設定を維持]", 101
		ELSE
			PRINTBUTTON "[ランダムに設定]", 101
		ENDIF
		PRINTL
ENDSELECT

PRINTL
PRINTBUTTON "[998] 口上側の設定があるキャラの一覧を見る", 998
PRINTL
PRINTBUTTON "[999] 設定を終える", 999

BINPUT
SELECTCASE RESULT
	CASE 100
		DRAWLINE
		PRINTBUTTON "[全て表示しない]", 0
		PRINTL
		PRINTBUTTON "[初対面時に設定]", 1
		PRINTL
		PRINTBUTTON "[初うふふ時に設定]", 2
		PRINTL
		PRINTBUTTON "[ランダムに設定]", 3
		PRINTL
		DRAWLINE
		PRINTBUTTON "[戻る]", 999
		BINPUT
		IF RESULT == 999
			RESTART
		ENDIF
		残りキャラ設定 = 0
		全体設定 = RESULT
		RESTART
	CASE 101
		DRAWLINE
		SELECTCASE 全体設定
			CASE 1
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[初対面時に設定]", 1
				PRINTL
			CASE 2
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[初うふふ時に設定]", 1
				PRINTL
			CASE 3
				PRINTBUTTON "[口上側・引き継ぎキャラの設定を維持]", 0
				PRINTL
				PRINTBUTTON "[ランダムに設定]", 1
				PRINTL
		ENDSELECT
		DRAWLINE
		PRINTBUTTON "[戻る]", 999
		BINPUT
		IF RESULT == 999
			RESTART
		ENDIF
		残りキャラ設定 = RESULT
		RESTART
	CASE 998
		CALL フレーバー素質口上設定者表示
		RESTART
ENDSELECT

SELECTCASE 全体設定
	CASE 0
		PRINTL [全て表示しない]の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			フレーバー素質:COUNT:素質表示設定 = 0
		NEXT
		フレーバー素質設定 = 1
	CASE 1
		PRINT [初対面時に設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[初対面時に設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 2
		NEXT
		フレーバー素質設定 = 2
	CASE 2
		PRINT [初うふふ時に設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[初うふふ時に設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 3
		NEXT
		フレーバー素質設定 = 3
	CASE 3
		PRINT [ランダムに設定]、
		IF 残りキャラ設定 == 0
			PRINT 口上存在キャラ・引き継ぎキャラは[設定を維持]
		ELSE
			PRINT 口上存在キャラ・引き継ぎキャラも[ランダムに設定]
		ENDIF
		PRINTL の設定でゲームを開始します。
		FOR COUNT, 1, CHARANUM
			IF 残りキャラ設定 == 0 && フレーバー素質:COUNT:素質表示設定 == 1
				CONTINUE
			ENDIF
			フレーバー素質:COUNT:素質表示設定 = 1
			フレーバー素質:COUNT:陰毛濃さ = RAND:5 + 1
			フレーバー素質:COUNT:乳首大きさ = RAND:5 + 1
			フレーバー素質:COUNT:乳輪大きさ = RAND:5 + 1
			IF IS_FEMALE(COUNT)
				フレーバー素質:COUNT:性器種類 = RAND:5 + 1
				フレーバー素質:COUNT:クリ大きさ = RAND:3 + 1
			ENDIF
			IF TALENT:COUNT:性別 == 3
				フレーバー素質:COUNT:ふたなり時_玉ありなし = RAND:2 + 1
			ENDIF
			;ほくろはbit管理でいくつでも設定可能
			フレーバー素質:COUNT:ほくろ位置 |= !RAND:5 << 0                        ;乳首付近
			フレーバー素質:COUNT:ほくろ位置 |= (!RAND:5 && HAS_VAGINA(COUNT)) << 1 ;女性器付近
			フレーバー素質:COUNT:ほくろ位置 |= !RAND:5 << 2                        ;アナル付近
			フレーバー素質:COUNT:ほくろ位置 |= (!RAND:5 && HAS_PENIS(COUNT)) << 3  ;陰茎
		NEXT
ENDSELECT


@フレーバー素質個別設定画面(キャラ番号)
#DIM キャラ番号
#DIM フレーバー素質数値保存, 10
VARSET フレーバー素質数値保存

FOR COUNT, 1, 10
	IF フレーバー素質:キャラ番号:COUNT
		フレーバー素質数値保存:COUNT = フレーバー素質:キャラ番号:COUNT
	ENDIF
NEXT

$描画_LOOP
DRAWLINE
PRINTL キャラクターのフレーバー素質を手動で設定します。
DRAWLINE
LOCALS = 
;画像があるなら突っ込んでおく
SIF CSTR:キャラ番号:画像フォルダ == ""
	CSTR:キャラ番号:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(キャラ番号)


LOCALS += @"<div rect='4300,0,1500,3000'>%短冊グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += "<div rect='62,0,4175,3750' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='137,125,3975,3750'>"

LOCALS += @"■%NAME表示(キャラ番号)%<br>"
IF 装備ステータス補正保存:キャラ番号:属性
	LOCALS:1 = %属性数値文字色変換_HTML(装備ステータス補正保存:キャラ番号:属性 - 10)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(装備ステータス補正保存:キャラ番号:属性 - 10)%</font>"
ELSE
	LOCALS:1 = %属性数値文字色変換_HTML(基礎BATTLE_STATE:キャラ番号:属性)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(基礎BATTLE_STATE:キャラ番号:属性)%</font>"
ENDIF
LOCALS += @"　得意武器：%得意武器表示文字列(基礎BATTLE_STATE:キャラ番号:得意武器)%　"
LOCALS += @"　タイプ：%タイプ数値文字列変換(基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存)%"
LOCALS += "<br><br>"
LOCALS += "□ 情報 □---------------------------------------------------------------------<br>"
LOCALS += @"　種族：%GET_TALENTNAME(200,TALENT:キャラ番号:種族,,キャラ番号)%"
IF CSTR:キャラ番号:特殊種族 != ""
	LOCALS +=  @"[%CSTR:キャラ番号:特殊種族%]"
ENDIF
SIF TALENT:キャラ番号:角あり
	LOCALS += " （角あり）"
SIF TALENT:キャラ番号:尻尾あり
	LOCALS += " （尻尾）"
SIF TALENT:キャラ番号:エルーン耳
	LOCALS += " （エルーン耳）"
SIF TALENT:キャラ番号:ハーヴィン耳
	LOCALS += " （ハーヴィン耳）"
SIF TALENT:キャラ番号:発情期あり > 0
	LOCALS += " （発情期あり）"
SIF TALENT:キャラ番号:発情期あり < 0
	LOCALS += " （発情期なし）"
LOCALS += "<br>"
LOCALS += "　性別："
IF TALENT:キャラ番号:性別 == 1
	LOCALS += "女性"
	SIF TALENT:キャラ番号:処女
		LOCALS += "（処女）"
ELSEIF TALENT:キャラ番号:性別 == 2
	LOCALS += "男性"
	SIF TALENT:キャラ番号:非童貞 == 0
		LOCALS += "（童貞）"
ELSEIF TALENT:キャラ番号:性別 == 3
	LOCALS += "ふたなり"
	SIF TALENT:キャラ番号:非童貞 == 0
		LOCALS += "(童貞)"
	SIF TALENT:キャラ番号:処女
		LOCALS += "（処女）"
ELSE
	LOCALS += "その他"
ENDIF
IF GETBIT(TALENT:キャラ番号:性別, 0)
LOCALS += @"バストサイズ：%GET_TALENTNAME(205,TALENT:キャラ番号:バストサイズ,,キャラ番号)%"
ENDIF
LOCALS += "<br>"

LOCALS += @"　性知識レベル：%性知識段階表示(知識素質:キャラ番号:性知識, キャラ番号), 17, LEFT%"
LOCALS += "<br>"

IF TALENT:キャラ番号:性別 & 2
	LOCALS += @"　男性器サイズ：%GET_TALENTNAME(162,TALENT:キャラ番号:男性器サイズ,,キャラ番号)%"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"

CALL PRINT_STATE_TALENT(キャラ番号)
LOCALS += @"%RESULTS%"
LOCALS += "<br>"

LOCALS += "□ フレーバー素質 □-----------------------------------------------------------<br>"

FOR COUNT, 1, 6
	SIF ERDNAME(フレーバー素質, COUNT) == ""
		CONTINUE
	SIF IS_MALE(キャラ番号) && COUNT >= 1
		BREAK
	LOCALS += @"　%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
	FOR LOCAL:1, 0, 6
		LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
		IF LOCAL:1 == 0
			LOCALS:2 = 非表示
		ENDIF
		IF COUNT == 5 && LOCAL:1 == 4
			CONTINUE
		ELSEIF LOCALS:2 == ""
			CONTINUE
		ENDIF
		IF フレーバー素質数値保存:COUNT == LOCAL:1
			LOCALS:1 = C0C0C0
		ELSE
			LOCALS:1 = 333333
		ENDIF
		LOCALS += @"<font color='#%LOCALS:1%'><button value='{COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
	NEXT
	LOCALS += "<br>"
NEXT
LOCALS += @"　%ERDNAME(フレーバー素質, フレーバー素質_ほくろ位置), 12, LEFT%：　　　　"
FOR LOCAL:1, 0, 4
	LOCALS:2 = %GET_フレーバー素質NAME(フレーバー素質_ほくろ位置, POWER(2, LOCAL:1))%
	SIF LOCALS:2 == ""
		CONTINUE
	SIF LOCAL:1 == 1 && !HAS_VAGINA(キャラ番号) ;ビット0：乳首付近　1：女性器付近　2：アナル付近　3：陰茎（ふたなり時のみ）
		CONTINUE
	SIF LOCAL:1 == 3 && !HAS_PENIS(キャラ番号)
		CONTINUE
	IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
		LOCALS:1 = C0C0C0
	ELSE
		LOCALS:1 = 333333
	ENDIF
	LOCALS += @"<font color='#%LOCALS:1%'><button value='{60 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
NEXT
LOCALS += "<br>"
IF TALENT:キャラ番号:性別 == 3 && OPTION変数:ふたなり玉設定 == 0
	LOCALS += @"　%ERDNAME(フレーバー素質, フレーバー素質_ふたなり時_玉ありなし), 12, LEFT%：　　　　"
	FOR LOCAL:1, 0, 3
		LOCALS:2 = %GET_フレーバー素質NAME(フレーバー素質_ふたなり時_玉ありなし, LOCAL:1)%
		IF LOCALS:2 == ""
			CONTINUE
		ENDIF
		IF フレーバー素質数値保存:7 == LOCAL:1
			LOCALS:1 = C0C0C0
		ELSE
			LOCALS:1 = 333333
		ENDIF
		LOCALS += @"<font color='#%LOCALS:1%'><button value='{70 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
	NEXT
ENDIF
LOCALS += "</div>"
LOCALS += "<div rect='2812,3562,1875,312'><button value='999'>[999] これで決定</button></div>"

HTML_PRINT LOCALS, 1
FOR COUNT, 0, 35
	PRINTL
NEXT

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0 TO 59, 70 TO 79
		フレーバー素質数値保存:(LOCAL / 10) = LOCAL % 10
		GOTO 描画_LOOP
	CASE 60 TO 69
		INVERTBIT フレーバー素質数値保存:(LOCAL / 10), LOCAL % 10
		GOTO 描画_LOOP
	CASE 999
		LOCAL:1 = 0
		FOR COUNT, 1, フレーバー素質_項目数 + 1
			IF フレーバー素質数値保存:COUNT
				フレーバー素質:キャラ番号:COUNT = フレーバー素質数値保存:COUNT
				LOCAL:1 = 1
			ENDIF
		NEXT
		IF LOCAL:1
			フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 = 1
		ELSE
			フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 = 0
		ENDIF
ENDSELECT



@フレーバー素質口上設定者表示
#DIM 現在ページ
#DIM 表示候補, 500
#DIM 候補人数
現在ページ = 0

$最初から
;表示候補選定＆ソート処理
LOCAL:1 = 0
VARSET 表示候補
VARSET LOCALS
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	SIF フレーバー素質:LOCAL:素質表示設定 != 1
		CONTINUE
	SIF MAXARRAY(フレーバー素質:LOCAL:0, 1, VARSIZE("フレーバー素質", 1)) == 0
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
候補人数 = LOCAL:1
LOCAL:1 = 1

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, 候補人数
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %GET_NAME(表示候補:LOCAL, 1)%</button>"
	LOCALS += "　　　　　　　　"
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 29
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [900]前のページへ
ELSE
	PRINTLC  
ENDIF
IF 現在ページ < 候補人数 / 30
	PRINTLC  [901]次のページへ
ENDIF
PRINTL
DRAWLINE
PRINTBUTTONLC "[1000]戻る", 1000
PRINTL

$INPUT_LOOP
BINPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 900
	現在ページ = MAX(現在ページ - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 901
	現在ページ = MIN(現在ページ + 1, 候補人数 / 30)
	GOTO 表示処理_LOOP
ENDIF
LOCAL = RESULT
$再表示
IF 候補人数 == 1
	CALL PRINT_STATE(LOCAL, 0)
ELSEIF 表示候補:0 == LOCAL
	CALL PRINT_STATE(LOCAL, 2)
ELSEIF 表示候補:(候補人数 - 1) == LOCAL
	CALL PRINT_STATE(LOCAL, 1)
ELSE
	CALL PRINT_STATE(LOCAL, 3)
ENDIF
IF RESULT == 100
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 - 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ELSEIF RESULT == 101
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 + 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ENDIF

GOTO 最初から


; @初期従業員選択(追加人数)
; #DIM 追加人数
; #DIM 追加候補, 10
; #DIM 表示候補, 5000
; #DIM 配列番号
; #DIM 表示最大数
; #DIM 現在ページ
; #DIM 対象キャラ

; VARSET 追加候補, -1
; VARSET 表示候補

; 表示最大数 = 0
; FOR 配列番号, 1, CHARANUM
; 	SIF CFLAG:配列番号:招待不可フラグ == 2
; 		CONTINUE
; 	表示候補:表示最大数 = 配列番号
; 	表示最大数 ++
; NEXT

; $表示LOOP
; DRAWLINE
; PRINTL 初期追加する従業員を選択してください。
; DRAWLINE

; FOR 配列番号, 現在ページ * 30, 現在ページ * 30 + 30
; 	SIF 表示候補:配列番号 <= 0
; 		BREAK
; 	SIF MATCH(追加候補, 表示候補:配列番号)
; 		SETCOLOR カラーパレット("黄")
; 	PRINTBUTTON @"[{配列番号}]%NAME表示((表示候補:配列番号), 41, LEFT)%", 配列番号
; 	RESETCOLOR
; 	SIF 配列番号 % 2
; 		PRINTL 
; NEXT
; PRINTL
; PRINTL 
; IF 現在ページ > 0
; 	PRINTBUTTONLC "[-1] 前ページへ", -1
; ELSE
; 	PRINTLC  
; ENDIF
; IF 表示候補:(現在ページ * 30 + 30) > 0
; 	PRINTBUTTONLC "[-2] 次ページへ", -2
; ENDIF
; PRINTL 
; DRAWLINE
; PRINTL ・
; DRAWLINE
; PRINTL ・現在の選択キャラ
; FOR LOCAL, 0, 追加人数
; 	IF 追加候補:LOCAL < 0
; 		PRINTL 未選択
; 	ELSE
; 		PRINTFORML %NAME表示((追加候補:LOCAL))%
; 	ENDIF
; NEXT
; PRINTL 
; IF 10 - MATCH(追加候補, -1) == 追加人数
; 	PRINTBUTTON "[9999] これで決定する", 9999
; ELSE
; 	SETCOLOR カラーパレット("グレーアウト")
; 	PRINTPLAIN [9999] これで決定する
; 	RESETCOLOR
; ENDIF
; PRINTL 
; DRAWLINE
; BINPUT
; LOCAL = RESULT
; SELECTCASE LOCAL
; 	CASE -1
; 		現在ページ --
; 	CASE -2
; 		現在ページ ++
; 	CASE 9999
; 		IF 10 - MATCH(追加候補, -1) != 追加人数
; 			PRINTL 設定人数が合っていません
; 			GOTO 表示LOOP
; 		ENDIF
; 		FOR 配列番号, 0, 追加人数
; 			対象キャラ = 追加候補:配列番号
; 			SIF 対象キャラ < 1
; 				CONTINUE
; 			CFLAG:対象キャラ:信頼度 = 5000
; 			CFLAG:対象キャラ:招待不可フラグ = 0
; 			TALENT:対象キャラ:従業員 = 1
; 			CFLAG:対象キャラ:滞在期間 = 999
; 			CFLAG:対象キャラ:探索参加許可フラグ = 1
; 			FOR LOCAL, 0, 従業員部屋数
; 				IF 従業員部屋割り配列:LOCAL < 1
; 					従業員部屋割り配列:LOCAL = 対象キャラ
; 					BREAK
; 				ENDIF
; 			NEXT
; 			初体験日:対象キャラ:初対面 = -1
; 			CFLAG:対象キャラ:現在位置 = キャラクター部屋検索(対象キャラ)
; 			CFLAG:対象キャラ:現在マップ種別 = 0
; 			;初期状態雑務
; 			現在仕事:対象キャラ:0 = 1
; 		NEXT
; 		PRINTW 初期従業員キャラを設定しました。
; 		RETURN 0
; 	CASEELSE
		
; 		;- キャラ招待ループ
; 		WHILE 1
; 			CALL 初期従業員セット確認(表示候補:LOCAL, MATCH(追加候補, 表示候補:LOCAL))
; 			SELECTCASE RESULT
; 				CASE 0
; 					;- キャンセル
; 					; リスタートしてリスト表示へ戻る
; 					BREAK
; 				CASE 1
; 					;- キャラ追加後
; 					; リスタートしてリスト表示へ戻る
; 					IF MATCH(追加候補, 表示候補:LOCAL)
; 						追加候補:(FINDELEMENT(追加候補, 表示候補:LOCAL)) = -1
; 					ELSE
; 						IF 10 - MATCH(追加候補, -1) < 追加人数
; 							追加候補:(FINDELEMENT(追加候補, -1)) = 表示候補:LOCAL
; 						ELSE
; 							PRINTW 追加枠が一杯です。
; 						ENDIF
; 					ENDIF
; 					BREAK
; 				CASE 10
; 					;- 前のキャラ
; 					; 候補番号を更新、ループして前のキャラを表示
; 					LOCAL -=  1
; 					SIF LOCAL < 0
; 						LOCAL = 表示最大数 - 1
; 				CASE 11
; 					;- 次のキャラ
; 					; 候補番号を更新、ループして次のキャラを表示
; 					LOCAL +=  1
; 					SIF LOCAL >= 表示最大数
; 						LOCAL = 0
; 			ENDSELECT
; 		WEND
; ENDSELECT

; GOTO 表示LOOP

@初期従業員セット確認(対象キャラ, セットフラグ)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 返り値
#DIM 描画開始行
#DIM 対象画像数
#DIM リロールボタン有効
#DIM 実行結果
#DIM セットフラグ
#DIM リスタートフラグ
#DIM TFLAG保存
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	; 所持ZPチェック
	; ・ZPが足りなくても簡易紹介を表示したいのでZPの確認は後に移動
;	IF CFLAG:対象キャラ:ZPショップ値段 > FLAG:ZP所持量
;		PRINTW ZPが足りません
;		RETURN
;	ENDIF
	描画開始行 = LINECOUNT
ENDIF

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
IF セットフラグ
	PRINTFORML %NAME表示(対象キャラ)%を初期従業員から外しますか？
ELSE
	PRINTFORML %NAME表示(対象キャラ)%を初期従業員にセットしますか？
ENDIF
; ボタン表示
PRINTBUTTONLC "[0]はい", 0
PRINTBUTTONLC "[1]いいえ", 1
PRINTBUTTONLC "[10]詳細能力を表示", 10
PRINTL
PRINTBUTTONLC "[2]前のキャラ", 2
PRINTBUTTONLC "[3]次のキャラ", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]画像のランダム表示をリロール", 4
PRINTL

; 入力ループ
WHILE 1
	BINPUT
	SELECTCASE RESULT
		CASE 0
			返り値 = 1
			BREAK
		CASE 1
			返り値 = 0
			BREAK
		CASE 4
			RESTART
		CASE 2
			;- [2]前のキャラ
			返り値 = 10
			BREAK
		CASE 3
			;- [3]次のキャラ
			返り値 = 11
			BREAK
		CASE 10
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					返り値 = 10
					BREAK
				CASE 101
					;次のキャラへ
					返り値 = 11
					BREAK
				CASE 1
					;はい
					返り値 = 1
					BREAK
				CASE 0
					;いいえ
					返り値 = 0
					BREAK
			ENDSELECT
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)

RETURN 返り値




@初期従業員選択(追加人数)
#DIM 追加人数
#DIM 追加候補, 10
#DIM 現在ページ
#DIM 表示候補, キャラクタ数上限
#DIM ソート基準値, キャラクタ数上限
#DIM 候補人数
#DIM 口上ファイル数
#DIM アビ奥義関数
#DIM 対象キャラ
#DIM スキップフラグ
#DIM 得意武器番号
#DIM 配列番号
#DIMS ソート情報
#DIM 表示キャラ数 = 30
現在ページ = 0
VARSET 追加候補, -1

$最初から
;表示候補選定＆ソート処理

VARSET 表示候補
VARSET ソート基準値
VARSET LOCALS

候補人数 = 0
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	CALL 装備再計算処理(LOCAL)
	SIF CFLAG:LOCAL:招待不可フラグ == 2
		CONTINUE
	SIF CFLAG:LOCAL:招待不可フラグ == -1
		CONTINUE
	SIF 滞在者のみフィルタ == 1 && CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:候補人数 = LOCAL
	候補人数 += 1
NEXT
CALL キャラ配列フィルタ処理(表示候補)
候補人数 = RESULT

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 項目表示数, 候補人数
	LOCALS += @"<button value = '{LOCAL}'>[{LOCAL, 4}] %GET_NAME(表示候補:LOCAL, 1)%</button>"
	ソート情報 '= ソート情報表示(表示候補:LOCAL, 1)
	SIF ソート情報 != ""
		LOCALS += @"　%ソート情報%"
	LOCALS += "　"
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 項目表示数 - 1
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
;IF 現在ページ > 0
;	PRINTLC  [-1]前のページへ
;ELSE
;	PRINTLC  
;ENDIF
;IF 現在ページ < 候補人数 / 30
;	PRINTLC  [-2]次のページへ
;ENDIF
;PRINTL
;ページめくり表示
CALL ページ送り共通表示(現在ページ, 項目表示数, 最大表示数, 9000, 9003)
DRAWLINE
CALL フィルタソートボタンセット表示(2000, 3000, 3100, 4000)
DRAWLINE
PRINTL ・現在の選択キャラ
FOR LOCAL, 0, 追加人数
	IF 追加候補:LOCAL < 0
		PRINTL 未選択
	ELSE
		PRINTBUTTON "[外す]", 9900 + LOCAL
		PRINTFORML 　%GET_NAME(追加候補:LOCAL, 1, 24)%
	ENDIF
NEXT
PRINTL 
PRINTBUTTON "[9998] おすすめセットを選ぶ", 9998
PRINTL 
IF 10 - MATCH(追加候補, -1) == 追加人数
	PRINTBUTTON "[9999] これで決定する", 9999
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [9999] これで決定する
	RESETCOLOR
ENDIF

PRINTL 
DRAWLINE
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9900 TO 9910
		追加候補:(LOCAL - 9900) = -1
	CASE 9000 TO 9007
		CALL ページ送り共通実行(LOCAL, 現在ページ, 項目表示数, 最大表示数, 9000, 9003)
	CASE 2000, 3000, 3100, 4000
		;出典フィルタに全作品を強制表示させる(通常表示だとグラブルしか表示されないため)
		出典フィルタ強制全表示フラグ = 1
		CALL フィルタソートボタンセット実行部(LOCAL, 2000, 3000, 3100, 4000)
		;他処理に影響しないようにすぐにOFF
		出典フィルタ強制全表示フラグ = 0
		現在ページ = 0
		;最初に戻ってリスト再生成
		GOTO 最初から
	CASE -1
		現在ページ --
	CASE -2
		現在ページ ++
	CASE 9998
		CALL 初期従業員オススメセット(追加候補)
	CASE 9999
		IF 10 - MATCH(追加候補, -1) != 追加人数
			PRINTL 設定人数が合っていません
			GOTO 表示処理_LOOP
		ENDIF
		FOR 配列番号, 0, 追加人数
			対象キャラ = 追加候補:配列番号
			SIF 対象キャラ < 1
				CONTINUE
			CFLAG:対象キャラ:信頼度 = 5000
			CFLAG:対象キャラ:招待不可フラグ = 0
			TALENT:対象キャラ:従業員 = 1
			CFLAG:対象キャラ:滞在期間 = 999
			CFLAG:対象キャラ:探索参加許可フラグ = 1
			FOR LOCAL, 0, 従業員部屋数
				IF 従業員部屋割り配列:LOCAL < 1
					従業員部屋割り配列:LOCAL = 対象キャラ
					BREAK
				ENDIF
			NEXT
			初体験日:対象キャラ:初対面 = -1
			CFLAG:対象キャラ:現在位置 = キャラクター部屋検索(対象キャラ)
			CFLAG:対象キャラ:現在マップ種別 = 0
			;初期状態雑務
			現在仕事:対象キャラ:0 = 1
		NEXT
		PRINTW 初期従業員キャラを設定しました。
		RETURN 0
	CASEELSE
		
		;- キャラ招待ループ
		WHILE 1
			CALL 初期従業員セット確認(表示候補:LOCAL, MATCH(追加候補, 表示候補:LOCAL))
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスタートしてリスト表示へ戻る
					BREAK
				CASE 1
					;- キャラ追加後
					; リスタートしてリスト表示へ戻る
					IF MATCH(追加候補, 表示候補:LOCAL)
						追加候補:(FINDELEMENT(追加候補, 表示候補:LOCAL)) = -1
					ELSE
						IF 10 - MATCH(追加候補, -1) < 追加人数
							追加候補:(FINDELEMENT(追加候補, -1)) = 表示候補:LOCAL
						ELSE
							PRINTW 追加枠が一杯です。
						ENDIF
					ENDIF
					BREAK
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補人数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補人数
						LOCAL = 0
			ENDSELECT
		WEND
ENDSELECT

GOTO 表示処理_LOOP


@初期従業員オススメセット(追加候補)
#DIM REF 追加候補
#DIM 選択セット番号
#DIM 関数数
#DIMS 関数配列, 100

選択セット番号 = -1
VARSET RESULTS
VARSET 関数配列
ENUMFUNCBEGINSWITH "オススメセット処理_"
ARRAYCOPY "RESULTS", "関数配列"
関数数 = RESULT

$表示ループ
DRAWLINE
PRINTL 任意のセットを選択してください。
DRAWLINE

;左側
LOCALS = <div rect='81,31,1937,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>
FOR LOCAL, 0, 関数数
	LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %REPLACE(関数配列:LOCAL, "オススメセット処理_", "")%</button><br>"
NEXT
LOCALS += "</div>"

;右側
LOCALS += "<div rect='2050,31,3875,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
RSTR:コマンド結果受渡し文字列 = 
SIF 選択セット番号 >= 0
	CALLFORM %関数配列:選択セット番号%("説明文表示", 追加候補)
LOCALS += @"%RSTR:コマンド結果受渡し文字列%"
LOCALS += "</div>"

;戻る
LOCALS += "<div rect='131,2631,1000,200'><button value='999'>[999] 戻る</button></div>"

HTML_PRINT LOCALS

FOR LOCAL, 0, 27
	PRINTL
NEXT

BINPUT 999
SELECTCASE RESULT
	CASE 998
		CALLFORM %関数配列:選択セット番号%("キャラセット", 追加候補)
		PRINTFORMW %REPLACE(関数配列:選択セット番号, "オススメセット処理_", "")%を適用しました。
		RETURN 0
	CASE 999
		RETURN 0
	CASE IS >= 1000
		; キャラ確認用の簡易紹介
		CALL キャラ簡易紹介(RESULT - 1000)
		WAIT
	CASEELSE
		選択セット番号 = RESULT
ENDSELECT
GOTO 表示ループ


@初期関係性データベース登録(対象キャラ)
#DIM 対象キャラ
#DIM キャラ番号記録
#DIMS CSTR切り分け文字列格納, 2

IF CSTR:対象キャラ:初期関係性 != ""
	VARSET LOCALS
	REPLACE CSTR:対象キャラ:初期関係性, " ", ""
	SPLIT RESULTS, "%", LOCALS
	FOR LOCAL, 0, 100
		SIF LOCALS:LOCAL == ""
			BREAK
		SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
		キャラ番号記録 = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
		SIF キャラ番号記録 < 0
			CONTINUE
		;同一組み合わせはスキップ
		SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And (対象キャラ１ = {CFLAG:キャラ番号記録:人物番号} Or 対象キャラ２ = {CFLAG:キャラ番号記録:人物番号})")
			CONTINUE
		;既に恋慕があるなら恋慕初期はスキップ
		SIF CSTR切り分け文字列格納:1 == "恋慕" && (DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ１ = {CFLAG:キャラ番号記録:人物番号} Or 対象キャラ２ = {CFLAG:キャラ番号記録:人物番号}) And 関係性種別 = '恋慕'") || TALENT:対象キャラ:恋慕 || TALENT:キャラ番号記録:恋慕)
			CONTINUE
		;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
		DT_ROW_ADD "関係性データベース", "対象キャラ１", CFLAG:対象キャラ:人物番号, "対象キャラ２", CFLAG:キャラ番号記録:人物番号, "関係性種別", CSTR切り分け文字列格納:1
		SELECTCASE CSTR切り分け文字列格納:1
			CASE "恋慕"
				SIF TALENT:対象キャラ:恋人持ち != 2
					TALENT:対象キャラ:恋人持ち = 1
				SIF TALENT:キャラ番号記録:恋人持ち != 2
					TALENT:キャラ番号記録:恋人持ち = 1
				SETBIT TALENT:対象キャラ:陥落不可, 0
				SETBIT TALENT:キャラ番号記録:陥落不可, 0
			CASE "セフレ"
				TALENT:対象キャラ:セフレあり = 1
				TALENT:キャラ番号記録:セフレあり = 1
		ENDSELECT
	NEXT
ENDIF



@リゾート名決定処理()

DRAWLINE
PRINTL リゾートの名前を決定してください。
PRINTL ※リゾートの名前はゲーム開始後、オプションからいつでも変更可能です。
DRAWLINE

PRINT 現在の名前：
IF リゾート名称 == ""
	PRINTL （未定）
ELSE
	PRINTFORML %リゾート名称%
ENDIF
DRAWLINE

PRINTBUTTON @"[0] 『%CALLNAME:MASTER%リゾート』にする", 0
PRINTL
PRINTBUTTON @"[1] 『eraリゾート』にする", 1
PRINTL
PRINTBUTTON @"[2] 『スパリゾート%CALLNAME:MASTER%』にする", 2
PRINTL
PRINTBUTTON @"[3] 『スパリゾートera』にする", 3
PRINTL
PRINTBUTTON @"[10] 任意入力する", 10
PRINTL
PRINTL
PRINTBUTTON @"[999] この名前で決定する", 999
PRINTL
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 0
		リゾート名称 = %CALLNAME:MASTER%リゾート
	CASE 1
		リゾート名称 = eraリゾート
	CASE 2
		リゾート名称 = スパリゾート%CALLNAME:MASTER%
	CASE 3
		リゾート名称 = スパリゾートera
	CASE 10
		$名称ループ
		PRINTL
		PRINTL
		DRAWLINE
		PRINTL 任意の名称を入力してください。
		CALL 入力処理_HTML記法警告付き()
		IF RESULTS == ""
			PRINTW 空白は入力できません。
			PRINTL
			PRINTL
			DRAWLINE
			GOTO 名称ループ
		ELSE
			リゾート名称 '= RESULTS
		ENDIF
	CASE 999
		IF リゾート名称 == ""
			PRINTW 空白は入力できません。
			PRINTL
			PRINTL
		ELSE
			PRINTFORMW リゾートの名前を『%リゾート名称%』に決定しました。
			RETURN 0
		ENDIF
ENDSELECT

RESTART

