@MOVE_ROUTINE_FESTIVAL_ハロウィンパーティ(対象キャラ, ARGS)
#DIM 対象キャラ
#DIM 時間計算
#DIM CONST 開催費用 = 10000

SELECTCASE ARGS
	CASE "お祭り説明"
		LOCALS = <div rect='2125,500,3762,2812'>
		LOCALS += "■ハロウィンパーティ<br>"
		LOCALS += "<br>"
		LOCALS += "リゾート全体をハロウィンムードで盛り上げます。<br>"
		LOCALS += "お祭り専用コマンド「お菓子をあげる」「いたずらされる」が実行可能になります。<br>"
		LOCALS += "同じキャラへは２時間に１回、どちらかのみ実行可能です。<br>"
		LOCALS += "<br>"
		LOCALS += "お菓子をあげる：好感度要素：友情が上昇します<br>"
		LOCALS += "いたずらされる：好感度要素：友情が上昇します。性的ないたずらになる可能性があります。<br>"
		LOCALS += "口上で設定されたキャラがハロウィンの衣装を着ることがあります。<br>"
		LOCALS += "<br>"
		IF 10000 > MONEY
			LOCALS += @"ルピが不足しています。（必要ルピ：%NUM_FORMAT(開催費用)%）<br>"
		ELSE
			LOCALS += @"<button value='0'>[0] このお祭りを企画する（必要ルピ：%NUM_FORMAT(開催費用)%）</button><br>"
		ENDIF
		LOCALS += "</div>"
		HTML_PRINT LOCALS, 1
	CASE "出現条件"
		;既に予定に入ってたらアウト
		SIF 開催予定祭り名 == "FESTIVAL_ハロウィンパーティ"
			RETURN 0
		;秋の月限定
		SIF DAY % 120 / 30 != 2
			RETURN 0
		RETURN 1
	CASE "参加判定"
		;ルーチンは変動させないので0を返す
		RETURN 0
	CASE "特設マップ"
		;存在しない
		RETURN 0
	CASE "起床時刻"
		;ルーチンは変動しないので必要なし
	CASE "就寝時刻"
		;ルーチンは変動しないので必要なし
	CASE "就寝場所"
		;ルーチンは変動しないので必要なし
	CASE "初期位置"
		;ルーチンは変動しないので必要なし
	CASE "お祭り補正条件"
		;条件なし
		RETURN 1
	CASE "お祭り補正内容"
		;コマンドのみなのでなし
	CASE "服装変更"
		;口上設定があればそれ
		TRYCCALLFORM KOJO_CLOTHES_FESTIVAL_ハロウィンパーティ_{NO:対象キャラ}(対象キャラ)
		CATCH
			;ない場合はトリック・オア・トリートメントの画像を見る
			IF EXISTFILE("resources/" + CSTR:AUTOCOM_相手番号保存:画像フォルダ + "/短冊_トリック・オア・トリートメント.png")
				RSTR:服装名受け渡し = トリック・オア・トリートメント
			ENDIF
		ENDCATCH
ENDSELECT


;-------------------------------------------------
;お菓子をあげる
;-------------------------------------------------
@COMNAME_PLACE_385_FESTIVAL_ハロウィンパーティ
#FUNCTION
TSTR:コマンド名受渡 = お菓子をあげる
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_385_FESTIVAL_ハロウィンパーティ
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_385_FESTIVAL_ハロウィンパーティ
LOCALS = <br>■お菓子をあげる<br>
LOCALS += "TARGETにお菓子をあげるコマンド。<br>トリック・オア・トリート！"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM385_FESTIVAL_ハロウィンパーティ

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 800)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 500)
TCVAR:TARGET:ハロウィンコマンド時刻 = TIME
TIME += 5
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE385_FESTIVAL_ハロウィンパーティ
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(385)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密が一致してないとダメ
SIF CFLAG:PLAYER:隠密 != CFLAG:TARGET:隠密
	RETURN 0
;２時間に１回
SIF TCVAR:TARGET:ハロウィンコマンド時刻 + 120 > TIME
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM385_FESTIVAL_ハロウィンパーティ

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%にハロウィンのお菓子をあげた

@MESSAGE_COM385_FESTIVAL_ハロウィンパーティ_非口上時
SELECTCASE RAND:3
	CASE 2
		PRINTFORML %CALLNAME:TARGET%はいたずらをしそこねた、と冗談を言っている
	CASE 1
		PRINTFORML %CALLNAME:TARGET%はお返しにと、お菓子を%CALLNAME:PLAYER%に渡してきた
	CASE 0
		PRINTFORML %CALLNAME:TARGET%は嬉しそうにお礼を言ってくれた
ENDSELECT


;-------------------------------------------------
;いたずらされる
;-------------------------------------------------
@COMNAME_PLACE_386_FESTIVAL_ハロウィンパーティ
#FUNCTION
TSTR:コマンド名受渡 = いたずらされる
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_386_FESTIVAL_ハロウィンパーティ
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_386_FESTIVAL_ハロウィンパーティ
LOCALS = <br>■いたずらされる<br>
LOCALS += "TARGETにいたずらされるコマンド。<br>相手が陥落済み＆うふふ経験済みの場合、1/2で性的ないたずらになる。<br>トリック・オア・トリート！"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽・征服"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM386_FESTIVAL_ハロウィンパーティ
#DIM セクハラ部位
#DIM セクハラ快楽量
セクハラ快楽量 = 0

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 700)
CALL SOURCE_CALC_征服(TARGET, PLAYER, 200)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 400)

TCVAR:TARGET:ハロウィンコマンド時刻 = TIME
TIME += 5

IF 陥落チェック(TARGET) && 初体験日:TARGET:初うふふ && RAND:2
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
	セクハラ部位 = RANDOM_PART(PLAYER)
	SIF セクハラ部位 == -1
		RETURN 1

	;快楽量決定、基本値から諸々補正
	セクハラ快楽量 = RAND:100 + 100

	SELECTCASE セクハラ部位
		CASE 0
			CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, セクハラ快楽量)
		CASE 1
			CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, セクハラ快楽量)
		CASE 2
			CALL SOURCE_CALC_快Ａ(PLAYER, TARGET, セクハラ快楽量)
		CASE 3
			CALL SOURCE_CALC_快Ｂ(PLAYER, TARGET, セクハラ快楽量)
	ENDSELECT

	;口上用に数値受け渡し
	RFLAG:コマンド結果受渡し変数 = セクハラ部位
	RFLAG:コマンド結果受渡し変数２ = セクハラ快楽量
	;非うふふ時射精口上用に保存
	AUTOCOM_相手番号保存 = TARGET
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE386_FESTIVAL_ハロウィンパーティ
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(386)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密が一致してないとダメ
SIF CFLAG:PLAYER:隠密 != CFLAG:TARGET:隠密
	RETURN 0
;２時間に１回
SIF TCVAR:TARGET:ハロウィンコマンド時刻 + 120 > TIME
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM386_FESTIVAL_ハロウィンパーティ

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%からいたずらを受けた
IF RFLAG:コマンド結果受渡し変数２ > 0
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の
	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE 0
			IF GETBIT(TALENT:PLAYER:性別, 1)
				PRINT 陰茎に
			ELSE
				PRINT クリトリスに
			ENDIF
		CASE 1
			PRINT 股間に
		CASE 2
			PRINT 尻に
		CASE 3
			PRINT 胸に
	ENDSELECT
	PRINTL 優しく手を這わせた…
ENDIF

@MESSAGE_COM386_FESTIVAL_ハロウィンパーティ_非口上時

IF RFLAG:コマンド結果受渡し変数２ < 1
	SELECTCASE RAND:3
		CASE 2
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%をくすぐってきた！
		CASE 1
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手に大きなカエルを置いた！　……と思ったら、おもちゃだった。
		CASE 0
			PRINTFORML %CALLNAME:TARGET%はどこからかクラッカーを取り出し、ぱぁん！　と大きな音を響かせた。
	ENDSELECT
ENDIF
