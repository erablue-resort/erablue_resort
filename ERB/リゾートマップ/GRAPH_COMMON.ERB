;-----------------------------------------------------------
;グラフ共通処理
;-----------------------------------------------------------

@GET_DEST(スタート地点, 目的地, _NODE, _NODE_REV, _NEXT)
#FUNCTION
#DIM スタート地点
#DIM 目的地
#DIM REF _NODE, 0
#DIM REF _NODE_REV, 0
#DIM REF _NEXT, 0, 0
#DIM START_NODE
#DIM GOAL_NODE
#DIM DEST_NODE
#DIM 経由地

START_NODE = _NODE_REV:スタート地点
IF START_NODE == -1
	RESULTS '= @"スタート地点:{スタート地点}は存在しません。"
	RETURNF -1
ENDIF
GOAL_NODE = _NODE_REV:目的地
IF GOAL_NODE == -1
	RESULTS '= @"ゴール地点:{目的地}は存在しません。"
	RETURNF -1
ENDIF
DEST_NODE = _NEXT:START_NODE:GOAL_NODE
IF DEST_NODE == -1
	RESULTS '= @"スタート地点:{スタート地点}からゴール地点:{目的地}への経路が見つかりません。"
	RETURNF -1
ENDIF
経由地 = _NODE:DEST_NODE
IF 経由地 == -1
	RESULTS '= @"スタート地点:{スタート地点}からゴール地点:{目的地}への経路が不正です。"
	RETURNF -1
ENDIF
RETURNF 経由地

@FLOYD_WARSHALL(NODE_COUNT, _DIST, _NEXT)
#DIM NODE_COUNT
#DIM REF _DIST, 0, 0
#DIM REF _NEXT, 0, 0
#DIM I_始点
#DIM J_終点
#DIM K_経由点

FOR K_経由点, 0, NODE_COUNT
	FOR I_始点, 0, NODE_COUNT
		FOR J_終点, 0, NODE_COUNT
			IF _DIST:I_始点:J_終点 > _DIST:I_始点:K_経由点 + _DIST:K_経由点:J_終点
				_DIST:I_始点:J_終点 = _DIST:I_始点:K_経由点 + _DIST:K_経由点:J_終点
				_NEXT:I_始点:J_終点 = _NEXT:I_始点:K_経由点
			ENDIF
		NEXT
	NEXT
NEXT
