;------------------------------------------------------------------------------
;汎用ルーチン プールで遊ぶ
;------------------------------------------------------------------------------
@MOVE_ROUTINE_プールで遊ぶ(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 480 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0
ENDSELECT

@ROUTINE_SCHEDULE_プールで遊ぶ(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_プールで遊ぶ(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_プールで遊ぶ(キャラ番号)

@ROUTINE_SCHEDULE_AM_プールで遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後はロビーへ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "交流")
スケジュール番号++

;9時からプール
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "プールで遊ぶ", "所要時間", "120")
スケジュール番号++


IF TALENT:キャラ番号:飲食不可 == 0
	;12時の15分前(着替え時間)から食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 705 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_プールで遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半からプール
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "プールで遊ぶ", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時の15分前(着替え時間)から食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1065 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF


;------------------------------------------------------------------------------
;汎用ルーチン 外で遊ぶ
;------------------------------------------------------------------------------
@MOVE_ROUTINE_外で遊ぶ(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 480 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1290 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		;キャンプ場の時、メインテントで眠る
		IF 施設改造度:3:0
			RESULT:1 = 0
			RESULT:2 = 303
			RETURN 1
		ELSE
			RETURN 0
		ENDIF

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0
ENDSELECT

@ROUTINE_SCHEDULE_外で遊ぶ(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_外で遊ぶ(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_外で遊ぶ(キャラ番号)

@ROUTINE_SCHEDULE_AM_外で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後はロビーへ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "交流")
スケジュール番号++

;9時から外へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "外で遊ぶ", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から食堂（キャンプ場改造済ならバーベキューエリア）
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "バーベキュー", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_外で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半から外へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "外で遊ぶ", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から食堂（キャンプ場改造済ならバーベキューエリア）
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "バーベキュー", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室（キャンプ場改造済ならメインテント）
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩", "小分類", "キャンプ")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室（キャンプ場改造済ならメインテント）
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩", "小分類", "キャンプ")
	スケジュール番号++
ENDIF


;------------------------------------------------------------------------------
;汎用ルーチン 遊戯室で遊ぶ
;------------------------------------------------------------------------------
@MOVE_ROUTINE_遊戯室で遊ぶ(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 510 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0
ENDSELECT

@ROUTINE_SCHEDULE_遊戯室で遊ぶ(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_遊戯室で遊ぶ(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_遊戯室で遊ぶ(キャラ番号)

@ROUTINE_SCHEDULE_AM_遊戯室で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後はロビーへ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "交流")
スケジュール番号++

;9時から遊戯室へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "遊戯室で遊ぶ", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_遊戯室で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半から遊戯室へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "遊戯室で遊ぶ", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF


;------------------------------------------------------------------------------
;汎用ルーチン 空艇港でショッピング
;------------------------------------------------------------------------------
@MOVE_ROUTINE_空艇港でショッピング(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0

ENDSELECT

@ROUTINE_ABLE_空艇港でショッピング(キャラ番号)
;ルーチンが候補に入る条件
#DIM キャラ番号
#DIM 店候補

店候補 = 0
IF ダンジョンクリアフラグ_沿岸部の森
	FOR LOCAL:1, 3, 9
		IF 空艇港店舗状況:(LOCAL:1):0 > 0 && 空艇港店舗状況:(LOCAL:1):0 != 3
			店候補 += 1
			BREAK
		ENDIF
	NEXT
	IF 店候補 > 0
		RETURN 1
	ENDIF
ENDIF

@ROUTINE_SCHEDULE_空艇港でショッピング(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_空艇港でショッピング(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_空艇港でショッピング(キャラ番号)

@ROUTINE_SCHEDULE_AM_空艇港でショッピング(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後はロビーへ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "交流")
スケジュール番号++

;9時から店へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "空艇港でショッピング", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から食事処、無いなら食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "外食", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_空艇港でショッピング(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半から店へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "空艇港でショッピング", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から食事処、無いなら食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "外食", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF



;------------------------------------------------------------------------------
;汎用ルーチン 居住区エリアで日常生活
;------------------------------------------------------------------------------
@MOVE_ROUTINE_居住区エリアで日常生活(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 1
		CFLAG:キャラ番号:現在マップ種別 = 2
ENDSELECT

@ROUTINE_SCHEDULE_居住区エリアで日常生活(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_居住区エリアで日常生活(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_居住区エリアで日常生活(キャラ番号)

@ROUTINE_SCHEDULE_AM_居住区エリアで日常生活(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;9時から居住区へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "居住区でのんびり", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時になったら自室に戻って自炊
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_居住区エリアで日常生活(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半から居住区へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "居住区でのんびり", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から帰宅して自炊
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "所要時間", "90", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "自室入浴", "所要時間", "60", "優先度", "2")
スケジュール番号++

;あとは寝るまで自室でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
スケジュール番号++

;------------------------------------------------------------------------------
;汎用ルーチン 海で遊ぶ
;------------------------------------------------------------------------------
@MOVE_ROUTINE_海で遊ぶ(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0

ENDSELECT

@ROUTINE_ABLE_海で遊ぶ(キャラ番号)
#DIM キャラ番号
;ルーチンが候補に入る条件

IF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0
	;海があるとOK
	RETURN 1
ENDIF

@ROUTINE_SCHEDULE_海で遊ぶ(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_海で遊ぶ(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_海で遊ぶ(キャラ番号)

@ROUTINE_SCHEDULE_AM_海で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は海へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "海入口")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0
	;9時から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 555 + TCVAR:キャラ番号:行動時間ゆらぎ, "海で遊ぶ", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:PMルーチンID != "" && 移動ルーチン:キャラ番号:PMルーチンID != "海で遊ぶ"
	;11時45分から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 705 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_海で遊ぶ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時15分から着替え
IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:ルーチンID != "海で遊ぶ"
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 795 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "海で遊ぶ", "所要時間", "180")
スケジュール番号++

;17時45分から着替え
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1065 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF

;------------------------------------------------------------------------------
;汎用ルーチン 遊覧船に乗る
;------------------------------------------------------------------------------
@MOVE_ROUTINE_遊覧船に乗る(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0

ENDSELECT

@ROUTINE_ABLE_遊覧船に乗る(キャラ番号)
#DIM キャラ番号
;ルーチンが候補に入る条件

IF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0 && 海改造度:6:1
	;遊覧船があるとOK
	RETURN 1
ENDIF

@ROUTINE_SCHEDULE_遊覧船に乗る(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_遊覧船に乗る(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_遊覧船に乗る(キャラ番号)

@ROUTINE_SCHEDULE_AM_遊覧船に乗る(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は海へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "海入口")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0
	;9時から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 555 + TCVAR:キャラ番号:行動時間ゆらぎ, "遊覧船に乗る", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:PMルーチンID != "" && 移動ルーチン:キャラ番号:PMルーチンID != "遊覧船に乗る"
	;11時45分から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 705 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_遊覧船に乗る(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時15分から着替え
IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:ルーチンID != "遊覧船に乗る"
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 795 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "遊覧船に乗る", "所要時間", "180")
スケジュール番号++

;17時45分から着替え
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1065 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF

;------------------------------------------------------------------------------
;汎用ルーチン 海底散歩をする
;------------------------------------------------------------------------------
@MOVE_ROUTINE_海底散歩をする(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0

ENDSELECT

@ROUTINE_ABLE_海底散歩をする(キャラ番号)
#DIM キャラ番号
;ルーチンが候補に入る条件

IF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0 && 海改造度:6:2
	;海底散歩があるとOK
	RETURN 1
ENDIF

@ROUTINE_SCHEDULE_海底散歩をする(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_海底散歩をする(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_海底散歩をする(キャラ番号)

@ROUTINE_SCHEDULE_AM_海底散歩をする(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は海へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "海入口")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0
	;9時から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 555 + TCVAR:キャラ番号:行動時間ゆらぎ, "海底散歩をする", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:PMルーチンID != "" && 移動ルーチン:キャラ番号:PMルーチンID != "海底散歩をする"
	;11時45分から着替え
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 705 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_海底散歩をする(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時15分から着替え
IF TALENT:キャラ番号:性別 != 0 && 移動ルーチン:キャラ番号:ルーチンID != "海底散歩をする"
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 795 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、水着へ", "所要時間", "15", "優先度", "4")
	スケジュール番号++
ENDIF

;遊ぶ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "海底散歩をする", "所要時間", "180")
スケジュール番号++

;17時45分から着替え
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1065 + TCVAR:キャラ番号:行動時間ゆらぎ, "海更衣室、普段着へ", "所要時間", "15", "優先度", "4")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時から海の家
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "小分類", "海の家", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF


;------------------------------------------------------------------------------
;汎用ルーチン あなた自宅で日常生活
;------------------------------------------------------------------------------
@MOVE_ROUTINE_あなた自宅で日常生活(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = キャラクター部屋検索(キャラ番号)
		CFLAG:キャラ番号:現在マップ種別 = 部屋検索_マップ種別
ENDSELECT

@ROUTINE_SCHEDULE_あなた自宅で日常生活(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_あなた自宅で日常生活(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_あなた自宅で日常生活(キャラ番号)

@ROUTINE_SCHEDULE_AM_あなた自宅で日常生活(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;9時から自宅でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "あなた自宅でのんびり", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時になったら自炊
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "小分類", "同棲", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_あなた自宅で日常生活(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時半から自宅でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810 + TCVAR:キャラ番号:行動時間ゆらぎ, "あなた自宅でのんびり", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;18時になったら自炊
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "小分類", "同棲", "所要時間", "90", "優先度", "4")
	スケジュール番号++
ENDIF

;19時半からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "小分類", "同棲", "所要時間", "60", "優先度", "2")
スケジュール番号++

;あとは寝るまで自室でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
スケジュール番号++


;------------------------------------------------------------------------------
;汎用ルーチン 商業区で店番
;------------------------------------------------------------------------------
@MOVE_ROUTINE_商業区で店番(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 480 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1380 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = キャラクター部屋検索(キャラ番号)
		CFLAG:キャラ番号:現在マップ種別 = 部屋検索_マップ種別
ENDSELECT

@ROUTINE_SCHEDULE_商業区で店番(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_商業区で店番(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_商業区で店番(キャラ番号)

@ROUTINE_SCHEDULE_AM_商業区で店番(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室にいる
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;8時半から移動
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 510, "商業区店番")
スケジュール番号++

;9時から開店
;店の時間フルで所要時間を入れておく
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540, "商業区店番", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;外食
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720, "食事", "小分類", "外食", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_商業区で店番(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時から移動
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 780, "商業区店番")
スケジュール番号++

;13時半から開店
;店の時間フルで所要時間を入れておく
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 810, "商業区店番", "所要時間", "390")
スケジュール番号++

IF TALENT:キャラ番号:定住者 == 2
	IF TALENT:キャラ番号:飲食不可 == 0
		;20時から帰宅して自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1200, "自炊", "小分類", "同棲", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;21時からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260, "お風呂", "小分類", "同棲", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1320, "休憩")
	スケジュール番号++
ELSE
	IF TALENT:キャラ番号:飲食不可 == 0
		;20時から帰宅して自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1200, "自炊", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;21時からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260, "自室入浴", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1320, "休憩")
	スケジュール番号++
ENDIF


;------------------------------------------------------------------------------
;汎用ルーチン 学校で勉強
;------------------------------------------------------------------------------
@MOVE_ROUTINE_学校で勉強(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 450 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = キャラクター部屋検索(キャラ番号)
		CFLAG:キャラ番号:現在マップ種別 = 部屋検索_マップ種別
ENDSELECT

@ROUTINE_SCHEDULE_学校で勉強(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_学校で勉強(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_学校で勉強(キャラ番号)

@ROUTINE_SCHEDULE_AM_学校で勉強(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室にいる
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;8時半から学校へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 510 + TCVAR:キャラ番号:行動時間ゆらぎ, "学校に滞在")
スケジュール番号++

;9時から勉強
;授業時間は90分×2
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540, "学校で勉強", "所要時間", "180")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;弁当
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720, "食事", "小分類", "お弁当", "所要時間", "40", "優先度", "4")
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_学校で勉強(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;昼食が終わり次第すぐに学校へ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720, "学校に滞在")
スケジュール番号++

;13時から勉強
;授業時間は90分×3+休憩15分
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 780, "学校で勉強", "所要時間", "285")
スケジュール番号++

IF TALENT:キャラ番号:定住者 == 2
	IF TALENT:キャラ番号:飲食不可 == 0
		;18時から帰宅して自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "小分類", "同棲", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;19時半からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "小分類", "同棲", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	IF TALENT:キャラ番号:飲食不可 == 0
		;18時から帰宅して自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;19時半からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "自室入浴", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF

;------------------------------------------------------------------------------
;汎用ルーチン 自室でうふふ
;------------------------------------------------------------------------------
@MOVE_ROUTINE_自室でうふふ(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 510 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = キャラクター部屋検索(キャラ番号)
		CFLAG:キャラ番号:現在マップ種別 = 部屋検索_マップ種別
ENDSELECT

@ROUTINE_SCHEDULE_自室でうふふ(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_自室でうふふ(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_自室でうふふ(キャラ番号)

@ROUTINE_SCHEDULE_AM_自室でうふふ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室にいる
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;9時からうふふ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 540 + TCVAR:キャラ番号:行動時間ゆらぎ, "自室でうふふ")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;食事
	IF TALENT:キャラ番号:定住者 == 2
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "小分類", "同棲", "所要時間", "60", "優先度", "4")
	ELSEIF TALENT:キャラ番号:定住者 == 1
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "所要時間", "60", "優先度", "4")
	ELSE
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	ENDIF
	スケジュール番号++
ENDIF

@ROUTINE_SCHEDULE_PM_自室でうふふ(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;昼食が終わり次第すぐに自室でうふふ
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 780 + TCVAR:キャラ番号:行動時間ゆらぎ, "自室でうふふ")
スケジュール番号++

IF TALENT:キャラ番号:定住者 == 2
	IF TALENT:キャラ番号:飲食不可 == 0
		;18時から自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "小分類", "同棲", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

		;19時半からお風呂
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "小分類", "同棲", "所要時間", "60", "優先度", "2")
		スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSEIF TALENT:キャラ番号:定住者 == 1
	IF TALENT:キャラ番号:飲食不可 == 0
		;18時から自炊
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "自炊", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;19時半からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "自室入浴", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	IF TALENT:キャラ番号:飲食不可 == 0
		;18時から食事
		CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1080 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
		スケジュール番号++
	ENDIF

	;19時半からお風呂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1170 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	;あとは寝るまで自室でのんびり
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1230 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF

;------------------------------------------------------------------------------
;汎用ルーチン おこたでまったり
;------------------------------------------------------------------------------
@MOVE_ROUTINE_おこたでまったり(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "起床時刻"
		時間計算 = 540 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算 + TCVAR:キャラ番号:行動時間ゆらぎ

	CASE "就寝場所"
		RETURN 0

	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = キャラクター部屋検索(キャラ番号)
		CFLAG:キャラ番号:現在マップ種別 = 部屋検索_マップ種別

ENDSELECT

@ROUTINE_ABLE_おこたでまったり(キャラ番号)
#DIM キャラ番号
;ルーチンが候補に入る条件

;冬のみ
SIF 月算出() != "冬の月"
	RETURN 0
;こたつある？
SIF ITEM:おこた == 0
	RETURN 0
;こたつあるってことは大丈夫だと思うけど一応自宅があるかどうかもチェック
SIF 施設改造度:20:0 < 1
	RETURN 0
;口上側制御
RESULT = 0
TRYCALLFORM おこた参加固有条件_{NO:キャラ番号}(キャラ番号)
IF RESULT == 0
	;通常条件
	;好感度一定以上
	SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:4
		RETURN 0
	;最大人数設定
	SIF TFLAG:おこた人数_AM >= 10 || TFLAG:おこた人数_PM >= 10
		RETURN 0
ELSEIF RESULT == -1
	;条件を無視しておこたに絶対入らない
	RETURN 0
ENDIF

RETURN 1

@ROUTINE_SCHEDULE_おこたでまったり(キャラ番号)
#DIM キャラ番号
CALL ROUTINE_SCHEDULE_AM_おこたでまったり(キャラ番号)
CALL ROUTINE_SCHEDULE_PM_おこたでまったり(キャラ番号)

@ROUTINE_SCHEDULE_AM_おこたでまったり(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

CALL RESET_SCHEDULE(キャラ番号)
スケジュール番号 = 0

;起床後は自室でのんびり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 0, "休憩")
スケジュール番号++

;10時からおこたでまったり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 600 + TCVAR:キャラ番号:行動時間ゆらぎ, "おこたでまったり", "所要時間", "120")
スケジュール番号++

IF TALENT:キャラ番号:飲食不可 == 0
	;12時から食堂
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 720 + TCVAR:キャラ番号:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++
ENDIF

TFLAG:おこた人数_AM ++

@ROUTINE_SCHEDULE_PM_おこたでまったり(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号

スケジュール番号 = FIND_BLANK_SCHEDULE(キャラ番号)

;13時からおこたでまったり
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 600 + TCVAR:キャラ番号:行動時間ゆらぎ, "おこたでまったり", "所要時間", "120")
スケジュール番号++

;19時からお風呂
CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1140 + TCVAR:キャラ番号:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
スケジュール番号++

IF TALENT:キャラ番号:早寝 > 0
	;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1245 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ELSE
	;21時からロビーへ
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1260 + TCVAR:キャラ番号:行動時間ゆらぎ, "交流")
	スケジュール番号++

	;21時30分から寝るまでは自室
	CALL SET_SCHEDULE(キャラ番号, スケジュール番号, 1290 + TCVAR:キャラ番号:行動時間ゆらぎ, "休憩")
	スケジュール番号++
ENDIF

TFLAG:おこた人数_PM ++
