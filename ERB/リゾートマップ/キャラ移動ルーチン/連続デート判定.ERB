@連続デート判定(デートキャラ１, デートキャラ２, DB番号, 縁結びフラグ = 0)
#FUNCTION
#DIM DB番号
#DIM デートキャラ１
#DIM デートキャラ２
#DIM 関係数
#DIM 対象配列, 500
#DIM 恋慕関係数
#DIM 恋慕優先フラグ
#DIM 恋慕対象配列, 500
#DIM 縁結び関係数
#DIM 縁結び対象配列, 500
#DIM 確率値
#DIM リセットフラグ
#DIM 比較フラグ
#DIM 回数比率
#DIM 回数比率比較用
#DIM 最大連続回数
#DIM 連続回数
;縁結びが多受け攻め反転で重登録される場合がある為
#DIM 縁結びID, 5
#DIM 縁結びID数
#DIM 縁結びフラグ

VARSET 対象配列
VARSET 恋慕対象配列
VARSET 縁結び対象配列
VARSET 縁結びID


関係数 = DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号}) And (Not((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号}))) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')", , 対象配列)
縁結び関係数 = DT_SELECT("縁結びデータベース", @"(対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号}) And (Not((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号}))) And (縁結び種別 = '恋慕' Or 縁結び種別 = 'セフレ')", , 縁結び対象配列)
縁結びID数 = DT_SELECT("縁結びデータベース", @"((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号})) And (縁結び種別 = '恋慕' Or 縁結び種別 = 'セフレ')", , 縁結びID)


IF (関係数 + 縁結び関係数) < 1
	IF 縁結びフラグ == 1
		SIF DT_SELECT("関係性データベース", @"((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号})) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')")
			DT_CELL_SET "関係性データベース", (RESULT:1), "連続デート回数", (DT_CELL_GET("関係性データベース", (RESULT:1), "連続デート回数", 1) +1), 1
	ELSE
		DT_CELL_SET "関係性データベース", DB番号, "連続デート回数", (DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1) +1), 1
	ENDIF
	IF 縁結びID数 > 0
		FOR LOCAL, 0, 縁結びID数
			DT_CELL_SET "縁結びデータベース", (縁結びID:LOCAL), "連続デート回数", (DT_CELL_GET("関係性データベース", (縁結びID:LOCAL), "連続デート回数", 1) +1), 1
		NEXT
	ENDIF
	RETURNF 0
ENDIF

IF 縁結びフラグ == 1
	DT_CELL_SET "縁結びデータベース", DB番号, "関係数比率", ((DT_ROW_LENGTH("縁結びデータベース") * 100)/(関係数 + 縁結び関係数)), 1
	確率値 = MAX(0, 3 + DT_CELL_GET("縁結びデータベース", DB番号, "連続デート回数", 1) - (DT_CELL_GET("縁結びデータベース", DB番号, "関係数比率", 1) / 50)) 
	回数比率 = (DT_CELL_GET("縁結びデータベース", DB番号, "連続デート回数", 1) * 5000) / DT_CELL_GET("縁結びデータベース", DB番号, "関係数比率", 1)
	最大連続回数 = DT_CELL_GET("縁結びデータベース", DB番号, "関係数比率", 1) / 50
	連続回数 = DT_CELL_GET("縁結びデータベース", DB番号, "連続デート回数", 1)
ELSE
	DT_CELL_SET "関係性データベース", DB番号, "関係数比率", ((DT_ROW_LENGTH("関係性データベース") * 100)/(関係数 + 縁結び関係数)), 1
	確率値 = MAX(0, 3 + DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1) - (DT_CELL_GET("関係性データベース", DB番号, "関係数比率", 1) / 50)) 
	回数比率 = (DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1) * 5000) / DT_CELL_GET("関係性データベース", DB番号, "関係数比率", 1)
	最大連続回数 = DT_CELL_GET("関係性データベース", DB番号, "関係数比率", 1) / 50
	連続回数 = DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1)
ENDIF

リセットフラグ = 1
比較フラグ = 1

FOR LOCAL, 0, 関係数
	SIF DT_CELL_GET("関係性データベース", (対象配列:LOCAL), "連続デート回数", 1) < (DT_CELL_GET("関係性データベース", (対象配列:LOCAL), "関係数比率", 1) / 50)
		リセットフラグ = 0
	IF DT_CELL_GET("関係性データベース", (対象配列:LOCAL), "関係数比率", 1) != 0
		回数比率比較用 = (DT_CELL_GET("関係性データベース", (対象配列:LOCAL), "連続デート回数", 1) * 5000) / DT_CELL_GET("関係性データベース", (対象配列:LOCAL), "関係数比率", 1)
		IF 回数比率比較用 < 回数比率
			比較フラグ = 0
			確率値 = MAX(確率値 , (最大連続回数 * (回数比率 - 回数比率比較用)) / 100 )
		ENDIF
	ENDIF
NEXT

FOR LOCAL, 0, 縁結び関係数
	SIF DT_CELL_GET("縁結びデータベース", (縁結び対象配列:LOCAL), "連続デート回数", 1) < (DT_CELL_GET("縁結びデータベース", (縁結び対象配列:LOCAL), "関係数比率", 1) / 50)
		リセットフラグ = 0
	IF DT_CELL_GET("縁結びデータベース", (縁結び対象配列:LOCAL), "関係数比率", 1) != 0
		回数比率比較用 = (DT_CELL_GET("縁結びデータベース", (縁結び対象配列:LOCAL), "連続デート回数", 1) * 5000) / DT_CELL_GET("縁結びデータベース", (縁結び対象配列:LOCAL), "関係数比率", 1)
		IF 回数比率比較用 < 回数比率
			比較フラグ = 0
			確率値 = MAX(確率値 , (最大連続回数 * (回数比率 - 回数比率比較用)) / 100 )
		ENDIF
	ENDIF
NEXT


IF (DT_CELL_GETS("関係性データベース", DB番号, "関係性種別", 1) == "恋慕") && 縁結びフラグ != 1
	恋慕関係数 = DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号}) And (関係性種別 = '恋慕')", , 恋慕対象配列)
	恋慕優先フラグ = 1
	IF 恋慕関係数 > 1
		FOR LOCAL, 0, 恋慕関係数
			SIF DT_CELL_GET("関係性データベース", (恋慕対象配列:LOCAL), "連続デート回数", 1) < DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1)
				恋慕優先フラグ = 0
		NEXT
	ENDIF
	SIF 恋慕優先フラグ == 1
		確率値 = 確率値 / 2
ENDIF

IF RAND:4 >=  確率値
	IF 縁結びフラグ == 1
		SIF DT_SELECT("関係性データベース", @"((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号})) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')")
			DT_CELL_SET "関係性データベース", (RESULT:1), "連続デート回数", (DT_CELL_GET("関係性データベース", (RESULT:1), "連続デート回数", 1) +1), 1
	ELSE
		DT_CELL_SET "関係性データベース", DB番号, "連続デート回数", (DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1) +1), 1
	ENDIF
	IF 縁結びID数 > 0
		FOR LOCAL, 0, 縁結びID数
			DT_CELL_SET "縁結びデータベース", (縁結びID:LOCAL), "連続デート回数", (DT_CELL_GET("関係性データベース", (縁結びID:LOCAL), "連続デート回数", 1) +1), 1
		NEXT
	ENDIF
	RETURNF 0
ENDIF

IF リセットフラグ == 1 && 連続回数 >= 最大連続回数
	FOR LOCAL, 0, 関係数
		DT_CELL_SET "関係性データベース", (対象配列:LOCAL), "連続デート回数", 0, 1
	NEXT
	FOR LOCAL, 0, 縁結び関係数
		DT_CELL_SET "縁結びデータベース", (縁結び対象配列:LOCAL), "連続デート回数", 0, 1
	NEXT
	IF 縁結びフラグ == 1
		SIF DT_SELECT("関係性データベース", @"((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号})) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')")
			DT_CELL_SET "関係性データベース", (RESULT:1), "連続デート回数", 1, 1
	ELSE
		DT_CELL_SET "関係性データベース", DB番号, "連続デート回数", 1, 1
	ENDIF
	IF 縁結びID数 > 0
		FOR LOCAL, 0, 縁結びID数
			DT_CELL_SET "縁結びデータベース", (縁結びID:LOCAL), "連続デート回数", 1, 1
		NEXT
	ENDIF
	RETURNF 0
ENDIF

IF 比較フラグ == 1
	IF 縁結びフラグ == 1
		SIF DT_SELECT("関係性データベース", @"((対象キャラ１ = {CFLAG:デートキャラ１:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ１:人物番号}) And (対象キャラ１ = {CFLAG:デートキャラ２:人物番号} Or 対象キャラ２ = {CFLAG:デートキャラ２:人物番号})) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')")
			DT_CELL_SET "関係性データベース", (RESULT:1), "連続デート回数", (DT_CELL_GET("関係性データベース", (RESULT:1), "連続デート回数", 1) +1), 1
	ELSE
		DT_CELL_SET "関係性データベース", DB番号, "連続デート回数", (DT_CELL_GET("関係性データベース", DB番号, "連続デート回数", 1) +1), 1
	ENDIF
	IF 縁結びID数 > 0
		FOR LOCAL, 0, 縁結びID数
			DT_CELL_SET "縁結びデータベース", (縁結びID:LOCAL), "連続デート回数", (DT_CELL_GET("関係性データベース", (縁結びID:LOCAL), "連続デート回数", 1) +1), 1
		NEXT
	ENDIF
	RETURNF 0
ENDIF

RETURNF 1

