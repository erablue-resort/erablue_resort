@発注依頼_素材収集

依頼文章保存 = 
依頼文章保存 += "任意の素材を指定し、集めてきてもらいます。<br>"
依頼文章保存 += "（一度でも入手したことのある素材のみ）<br>"
依頼文章保存 += "素材の量・必要報酬額は素材のランクに従って変動します。<br>"
依頼文章保存 += "必要報酬額は素材の相場によって変動します。<br>"
依頼文章保存 += "<br>"
依頼文章保存 += @"依頼アイテム："
IF 依頼アイテム名 != ""
	LOCALS = %カラーパレット_HTML(@"%属性数値文字列変換(素材アイテム_数値データ取得("素材属性", 依頼アイテム名))%属性")%
	依頼文章保存 += @"<font color='#%LOCALS%'>%依頼アイテム名%</font><br>"
	LOCAL = 素材アイテム_数値データ取得("素材ランク", 依頼アイテム名)
	LOCAL:1 = 素材アイテム_数値データ取得("素材値相場", 依頼アイテム名)
	依頼報酬額 = (LOCAL + 1) * (LOCAL + 1) * 500 * LOCAL:1 / 100
	依頼文章保存 += @"必要報酬額：%NUM_FORMAT(依頼報酬額)%<br>"
ENDIF
依頼文章保存 += "<br>"
依頼文章保存 += "<button value='200'>[200] 依頼アイテムを変更する</button><br>"
依頼文章保存 += "<button value='201'>[201] これで発注する</button><br><br>"

@依頼追加表示情報_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

詳細文文字列受け渡し変数 = 
IF 遂行中依頼_対象名:表示依頼枠番号 !=""
	LOCALS = %カラーパレット_HTML(@"%属性数値文字列変換(素材アイテム_数値データ取得("素材属性", 遂行中依頼_対象名:表示依頼枠番号))%属性")%
	詳細文文字列受け渡し変数 = %STRFORM(@"依頼アイテム：<font color='#%LOCALS%'>%遂行中依頼_対象名:表示依頼枠番号%</font>")%
ENDIF

@発注実処理_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 200
		CALL 素材アイテム一覧表示("累計入手素材数 > 0", 1)
		IF RESULT > -1
			依頼アイテム名 = %DT_CELL_GETS("所持素材データベース", RESULT, "素材アイテム名", 1)%
		ENDIF
	CASE 201
		IF 依頼アイテム名 != ""
			IF MONEY < 依頼報酬額
				PRINTL 
				PRINTW ルピが足りません。
				RETURN 0
			ENDIF
			PRINTL
			PRINTFORMW 依頼：素材収集　　依頼アイテム：%依頼アイテム名%　　を発注しました。
			遂行中依頼_対象名:表示依頼枠番号 = %依頼アイテム名%
			LOCAL = 素材アイテム_数値データ取得("素材ランク", 依頼アイテム名)
			依頼完了確率_初期値:表示依頼枠番号 = MAX(0, (5 - LOCAL) * 10)
			依頼完了確率_進数:表示依頼枠番号 = MAX(5, (5 - LOCAL) * 3)
			RETURN 1
		ELSE
			PRINTL
			PRINTFORMW アイテムが指定されていません。
			RETURN 0
		ENDIF
	CASE 999
		依頼アイテム名 = 
		RETURN -1
ENDSELECT

@依頼完了時処理_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

LOCAL = 素材アイテム_数値データ取得("素材ランク", 遂行中依頼_対象名:表示依頼枠番号)
LOCAL = RAND:2 + (6 - LOCAL)
PRINTFORML %遂行中依頼_対象名:表示依頼枠番号% を {LOCAL}個手に入れた！
CALL 素材入手処理(遂行中依頼_対象名:表示依頼枠番号, LOCAL)
