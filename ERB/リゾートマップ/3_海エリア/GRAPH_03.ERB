@EVENTLOAD
CALL RESET_GRAPH_3

;-----------------------------------------------------------
;空挺港エリア グラフ化
;-----------------------------------------------------------
@RESET_GRAPH_3
#DIM 場所番号, 2
#DIM 始点ノード
#DIM 終点ノード
#DIM DISTANCE_MAP3, MAX_NODE, MAX_NODE

;各変数の初期化
NODE_COUNT_MAP3 = 0
VARSET NODE_MAP3, -1
VARSET NODE_REVERSE_MAP3, -1
VARSET DISTANCE_MAP3, WEIGHT_DISCONNECTED
VARSET NEXT_MAP3, -1

;ノードの定義
FOR 場所番号:0, 0, マップ部屋番号上限
	SIF 存在しない移動先_3(場所番号:0)
		CONTINUE
	NODE_MAP3:NODE_COUNT_MAP3 = 場所番号:0
	NODE_REVERSE_MAP3:(場所番号:0) = NODE_COUNT_MAP3
	NODE_COUNT_MAP3 ++
NEXT

;ノードの距離の初期設定
;隣接しているもののみ
FOR 始点ノード, 0, NODE_COUNT_MAP3
	場所番号:0 = NODE_MAP3:始点ノード
	FOR 終点ノード, 0, NODE_COUNT_MAP3
		SIF 始点ノード == 終点ノード
			CONTINUE
		場所番号:1 = NODE_MAP3:終点ノード
		IF CAN_MOVE_3(場所番号:0, 場所番号:1) & 1
			DISTANCE_MAP3:始点ノード:終点ノード = EDGE_WEIGHT_3(場所番号:0, 場所番号:1)
			NEXT_MAP3:始点ノード:終点ノード = 終点ノード
		ENDIF
	NEXT
NEXT

CALL FLOYD_WARSHALL(NODE_COUNT_MAP3, DISTANCE_MAP3, NEXT_MAP3)

@EDGE_WEIGHT_3(場所番号:0, 場所番号:1)
#FUNCTION
#DIM 場所番号, 2

RETURNF 1
