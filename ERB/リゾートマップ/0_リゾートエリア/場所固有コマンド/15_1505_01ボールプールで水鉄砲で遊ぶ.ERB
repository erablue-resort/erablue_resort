
;-------------------------------------------------
;15_1505_01ボールプールで水鉄砲で遊ぶ
;-------------------------------------------------
@COMNAME_PLACE_380_0_1505
#FUNCTION
IF 施設改造度:15:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = 水鉄砲で遊ぶ
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_1505
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")
		
@COM380_0_1505

CALL 水鉄砲遊び(TARGET)
;成功回数が帰ってくるのでTFLAG:コマンド結果受渡し変数に保存
TFLAG:コマンド結果受渡し変数 = RESULT

SIF TFLAG:コマンド結果受渡し変数 == -1
	RETURN 0

LOCAL:0 = 100 * TFLAG:コマンド結果受渡し変数
LOCAL:1 = 70 * TFLAG:コマンド結果受渡し変数

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_1505
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_1505
SIF TFLAG:コマンド結果受渡し変数 == -1
	RETURN 0

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@と水鉄砲で遊んだ

SELECTCASE TFLAG:コマンド結果受渡し変数
	;成功数で分岐
	CASE IS <= 2
		PRINTFORMW %CALLNAME:PLAYER%は集中攻撃を受けてずぶ濡れになった…
	CASE IS <= 5
		PRINTFORMW 撃ち撃たれしながら、楽しく遊ぶ事ができた
	CASE IS <= 9
		PRINTFORMW リロードとショットの駆け引きをしながら、水鉄砲バトルを楽しんだ！
	CASE IS <= 14
		PRINTFORMW 水鉄砲とは思えない熱い激戦を繰り広げた！
	CASEELSE
		PRINTFORMW もはや常人とは思えないハイレベルな攻防に、周囲の観光客も大盛り上がりだ！
ENDSELECT


