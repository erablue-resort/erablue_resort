
;-------------------------------------------------
;罰ゲーム処理関連
;-------------------------------------------------
;代入した番号のキャラ相手に、罰ゲームを与えるか受けるかで分岐しつつ処理
;-------------------------------------------------
		
@罰ゲーム(キャラ番号, 勝敗)
#DIM キャラ番号
#DIMS 勝敗
#DIM 罰ゲーム種類
#DIMS 罰ゲーム候補, 50
#DIM 候補数


SELECTCASE 勝敗
	CASE "勝利"
		$INPUT_LOOP
		PRINTFORML %CALLNAME:キャラ番号%に何の罰ゲームを与えようか？
		PRINTBUTTON "[0]でこぴんをする", 0
		PRINTL 
		PRINTBUTTON "[1]お酒を一杯飲む", 1
		PRINTL 
		PRINTBUTTON "[2]体に触れる", 2
		PRINTL 
		PRINTL 
		CALL PRINT_えっちな罰ゲーム(キャラ番号)
		BINPUT
		
		罰ゲーム種類 = RESULT
		SELECTCASE RESULT
			CASE 0
				RFLAG:コマンド結果受渡し変数２ = 0
				PRINTFORML %CALLNAME:MASTER%は罰ゲームとして%CALLNAME:キャラ番号%に軽くデコピンした。
				PRINTFORML %CALLNAME:キャラ番号%は痛そうにおでこを手でさすっている。
			CASE 1
				RFLAG:コマンド結果受渡し変数２ = 1
				;いずれ子供キャラにはジュースにしたり、酩酊処理を挟んだりするが今は仮実装
				PRINTFORML %CALLNAME:キャラ番号%は罰ゲームとしてお酒を一杯飲んだ。
				PRINTFORML %CALLNAME:キャラ番号%の顔が少し赤くなっている。
				IF !初体験日:キャラ番号:初飲酒
					初体験日:キャラ番号:初飲酒 = DAY
					TCVAR:キャラ番号:初飲酒フラグ = 1
					CALL 履歴データベース登録(CFLAG:(飲み会TARGET:LOCAL):人物番号, @"初めてリゾートでお酒を飲んだ","日常")
				ENDIF
				EXP:キャラ番号:飲酒経験 += RAND:3 + 1
				BASE:キャラ番号:酩酊 += RAND:100 + 100
				SIF BASE:キャラ番号:酩酊 >= 1000
					CFLAG:キャラ番号:酔いすぎ = 1
			CASE 2
				RFLAG:罰ゲーム中 = 1
				CALL COM306
				IF RESULT == -1
					GOTO INPUT_LOOP
				ENDIF
				RFLAG:コマンド結果受渡し変数２ = 2
				RFLAG:コマンド結果受渡し変数４ = TOINT(REPLACE(SELECTCOM_S, "306_", ""))
				CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%
				;口上用に変数を戻す
				SELECTCOM_S = 
				RSTR:SELECTCOM_S受け渡し = 
			CASE 10 TO 18
				RFLAG:コマンド結果受渡し変数２ = RESULT
				CALL COM_えっちな罰ゲーム(キャラ番号, 罰ゲーム種類)
				CALL MESSAGE_えっちな罰ゲーム(キャラ番号, 罰ゲーム種類)
		ENDSELECT
		

	CASE "敗北"
		;出現可能な罰ゲームを登録
		VARSET 罰ゲーム候補
		罰ゲーム候補:0 = デコピン
		罰ゲーム候補:1 = 一気飲み
		候補数 = 2
		;体に触れる系はいるか？　いらない気がするぜぇ
		LOCAL = ABLE_えっちな罰ゲーム(キャラ番号)
		SIF LOCAL >= 40
			罰ゲーム候補:(候補数++) = 下着の色を言う
		SIF LOCAL >= 50
			罰ゲーム候補:(候補数++) = セクシーポーズで淫語を言う
		SIF LOCAL >= 65 && TALENT:PLAYER:Ｂ感度 > -2
			罰ゲーム候補:(候補数++) = 乳首当てゲームをする
		SIF LOCAL >= 70
			罰ゲーム候補:(候補数++) = 尻を揉む
		SIF LOCAL >= 70
			罰ゲーム候補:(候補数++) = 胸を揉む
		SIF LOCAL >= 80 && TALENT:PLAYER:尻尾あり
			罰ゲーム候補:(候補数++) = 尻尾の付け根を揉む
		SIF LOCAL >= 80 && 体格差計算(キャラ番号, PLAYER) < 2
			罰ゲーム候補:(候補数++) = 肩組み乳揉み
		SIF LOCAL >= 80 && TALENT:PLAYER:Ｂ感度 > -2
			罰ゲーム候補:(候補数++) = 乳首ひっかき
		SIF LOCAL >= 90
			罰ゲーム候補:(候補数++) = 股間を弄る

		;罰ゲーム決定
		SELECTCASE 罰ゲーム候補:(RAND:候補数)
			CASE "デコピン"
				RFLAG:コマンド結果受渡し変数２ = 0
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%に軽くデコピンした。
				PRINTL 少しおでこがひりひりする…
			CASE "一気飲み"
				RFLAG:コマンド結果受渡し変数２ = 1
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%はお酒を一杯飲ませた。
				PRINTFORML %CALLNAME:PLAYER%は顔が少し赤くなっている。
				IF !初体験日:PLAYER:初飲酒
					初体験日:PLAYER:初飲酒 = DAY
					TCVAR:PLAYER:初飲酒フラグ = 1
					CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"初めてリゾートでお酒を飲んだ","日常")
				ENDIF
				EXP:PLAYER:飲酒経験 += RAND:3 + 1
				BASE:PLAYER:酩酊 += RAND:100 + 100
				SIF BASE:PLAYER:酩酊 >= 1000
					CFLAG:PLAYER:酔いすぎ = 1
			CASE "下着の色を言う"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%に「下着の色を言う」を要求してきた。
				RFLAG:コマンド結果受渡し変数２ = 10
				PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:キャラ番号%に
				IF TEQUIP:キャラ番号:下半身下着あり == 1
					PRINTW 服をズラして下着を少し見せてあげた。
				ELSEIF TEQUIP:キャラ番号:下半身下着あり == 0
					PRINTW 履いてないとこっそり囁いた。
				ELSEIF TEQUIP:キャラ番号:下半身下着あり == 2
					PRINTW 前張りをしていると囁いた。
				ELSEIF TEQUIP:キャラ番号:下半身下着あり == 3
					PRINTW ふんどしだと教えた。
				ENDIF
				CALL SOURCE_CALC_露出(PLAYER, キャラ番号, 100)
				CALL SOURCE_CALC_好感度要素_友情度UP(PLAYER, キャラ番号, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(PLAYER, キャラ番号, 20)
			CASE "セクシーポーズで淫語を言う"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%に「セクシーポーズで淫語を言う」を要求してきた。
				RFLAG:コマンド結果受渡し変数２ = 11
				;セクシーポーズで淫語
				CALL SOURCE_CALC_露出(PLAYER, キャラ番号, 200)
				CALL SOURCE_CALC_好感度要素_友情度UP(PLAYER, キャラ番号, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(PLAYER, キャラ番号, 50)
				PRINTFORMW %CALLNAME:PLAYER%は煽るようなポーズを見せつけながら、%CALLNAME:キャラ番号%の耳元で淫靡なセリフを囁いた。
			CASE "乳首当てゲームをする"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%に「乳首当てゲーム」を要求してきた。
				PRINTFORMW %CALLNAME:PLAYER%は了承し、両手を後ろに回して%CALLNAME:キャラ番号%の指を待ち受けている。
				RFLAG:コマンド結果受渡し変数２ = 12
				;乳首当てゲーム
				CALL SOURCE_CALC_露出(PLAYER, キャラ番号, 200)
				CALL SOURCE_CALC_快Ｂ(PLAYER, キャラ番号, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(PLAYER, キャラ番号, 30)
				CALL SOURCE_CALC_好感度要素_性愛度UP(PLAYER, キャラ番号, 100)
				SELECTCASE 乳首当て結果判定(PLAYER)
					CASE 0 TO 4
						RFLAG:コマンド結果受渡し変数４ = 1
						PRINTFORMW %CALLNAME:PLAYER%の胸に、乳首めがけて指を前に出した。
						PRINTW 【的中！】
						PRINTFORMW %CALLNAME:キャラ番号%は見事に%CALLNAME:PLAYER%の乳首を突いた！
						CALL SOURCE_CALC_快Ｂ(PLAYER, キャラ番号, 100)
					CASE 5 TO 9
						RFLAG:コマンド結果受渡し変数４ = -1
						PRINTFORMW %CALLNAME:PLAYER%の胸に、乳首めがけて指を前に出した。
						PRINTW 【ハズレ…】
						PRINTFORMW %CALLNAME:キャラ番号%は乳首の位置を見誤ってしまったようだ。
				ENDSELECT
			CASE "尻を揉む"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 320
				CALL CALL_COM(キャラ番号)
				;口上用に変数を戻す
				SELECTCOM = LOCAL
				CALL MESSAGE_COM320
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 13
			CASE "胸を揉む"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 321
				CALL CALL_COM(キャラ番号)
				SELECTCOM = LOCAL
				CALL MESSAGE_COM321
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 14
			CASE "尻尾の付け根を揉む"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 322
				CALL CALL_COM(キャラ番号)
				SELECTCOM = LOCAL
				CALL MESSAGE_COM322
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 15
			CASE "肩組み乳揉み"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 323
				CALL CALL_COM(キャラ番号)
				SELECTCOM = LOCAL
				CALL MESSAGE_COM323
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 16
			CASE "乳首ひっかき"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 324
				CALL CALL_COM(キャラ番号)
				SELECTCOM = LOCAL
				CALL MESSAGE_COM324
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 17
			CASE "股間を弄る"
				PRINTFORMW %CALLNAME:キャラ番号%は罰ゲームとして%CALLNAME:PLAYER%の体に触れることを選んだようだ。
				;CALL_COMを経由させる
				SWAP TARGET, PLAYER
				LOCAL = SELECTCOM
				RSTR:SELECTCOM_S受け渡し = 
				SELECTCOM_S = 
				SELECTCOM = 327
				CALL CALL_COM(キャラ番号)
				SELECTCOM = LOCAL
				CALL MESSAGE_COM327
				SWAP TARGET, PLAYER
				RFLAG:コマンド結果受渡し変数２ = 18
		ENDSELECT
ENDSELECT

TRYCALLFORM KOJO_EVENT_罰ゲーム_{NO:キャラ番号}(キャラ番号)


@PRINT_えっちな罰ゲーム(キャラ番号)
#DIM キャラ番号
;複数の罰ゲームに派生させるので別関数で処理する

RFLAG:罰ゲーム中 = 1
IF CFLAG:キャラ番号:好感度レベル < 50
	SETCOLOR 0x666666
	PRINTPLAIN ▼えっちな罰ゲーム（好感度レベルが足りません）
	PRINTL
	RESETCOLOR
ELSE
	PRINTL ▼えっちな罰ゲーム
	;いずれ脱衣も実装する……絶対にだ……
	IF ABLE_えっちな罰ゲーム(キャラ番号) > 40
		PRINT ├
		PRINTBUTTON "[10]下着の色を言う", 10
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[10]下着の色を言う（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	IF ABLE_えっちな罰ゲーム(キャラ番号) > 50
		PRINT ├
		PRINTBUTTON "[11]セクシーポーズで淫語を言う", 11
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[11]セクシーポーズで淫語を言う（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	IF ABLE_えっちな罰ゲーム(キャラ番号) > 65 && TALENT:キャラ番号:Ｂ感度 > -2
		PRINT ├
		PRINTBUTTON "[12]乳首当てゲームをする", 12
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[12]乳首当てゲームをする（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE320
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 70
		PRINT ├
		PRINTBUTTON "[13]尻を揉む", 13
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[13]尻を揉む（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE321
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 70
		PRINT ├
		PRINTBUTTON "[14]胸を揉む", 14
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[14]胸を揉む（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE322
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 80
		PRINT ├
		PRINTBUTTON "[15]尻尾の付け根を揉む", 15
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[15]尻尾の付け根を揉む（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE323
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 80
		PRINT ├
		PRINTBUTTON "[16]肩組み乳揉み", 16
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[16]肩組み乳揉み（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE324
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 80
		PRINT ├
		PRINTBUTTON "[17]乳首ひっかき", 17
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN ├[17]乳首ひっかき（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
	CALL COM_ABLE327
	IF RESULT && ABLE_えっちな罰ゲーム(キャラ番号) > 90
		PRINT └
		PRINTBUTTON "[18]股間を弄る", 18
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTPLAIN └[18]股間を弄る（条件が足りません）
		PRINTL 
		RESETCOLOR
	ENDIF
ENDIF




@ABLE_えっちな罰ゲーム(キャラ番号)
#FUNCTION
#DIM キャラ番号
#DIM 許容値
;どこまで罰ゲームを許してくれるかを表す数値、高いほどいろいろ許してくれる

;初期値は好感度レベル
許容値 = CFLAG:キャラ番号:好感度レベル

;素質に手を入れた後にいずれ色々追加すること

;賭博師の風格持ち時、プラス補正
SIF あなた特殊能力:ギャンブラー == 2
	許容値 += 50

;性欲なしはダメ
SIF TALENT:キャラ番号:性欲 == -2
	許容値 = 0

RETURNF 許容値



@COM_えっちな罰ゲーム(キャラ番号, ARG)
#DIM キャラ番号

SELECTCASE ARG
	CASE 10
		;下着の色を言う
		CALL SOURCE_CALC_露出(キャラ番号, PLAYER, 100)
		CALL SOURCE_CALC_好感度要素_友情度UP(キャラ番号, PLAYER, 10)
		CALL SOURCE_CALC_好感度要素_性愛度UP(キャラ番号, PLAYER, 20)
	CASE 11
		;セクシーポーズで淫語
		CALL SOURCE_CALC_露出(キャラ番号, PLAYER, 200)
		CALL SOURCE_CALC_好感度要素_友情度UP(キャラ番号, PLAYER, 10)
		CALL SOURCE_CALC_好感度要素_性愛度UP(キャラ番号, PLAYER, 50)
	CASE 12
		;乳首当てゲーム
		CALL SOURCE_CALC_露出(キャラ番号, PLAYER, 200)
		CALL SOURCE_CALC_快Ｂ(キャラ番号, PLAYER, 50)
		CALL SOURCE_CALC_好感度要素_友情度UP(キャラ番号, PLAYER, 30)
		CALL SOURCE_CALC_好感度要素_性愛度UP(キャラ番号, PLAYER, 100)
		SELECTCASE 乳首当て結果判定(キャラ番号)
			CASE 0 TO 4
				RFLAG:コマンド結果受渡し変数４ = 1
				CALL SOURCE_CALC_快Ｂ(キャラ番号, PLAYER, 100)
			CASE 5 TO 8
				RFLAG:コマンド結果受渡し変数４ = -1
			CASE 9
				RFLAG:コマンド結果受渡し変数４ = 2
				CALL SOURCE_CALC_快Ｂ(キャラ番号, PLAYER, 150)
		ENDSELECT
	CASE 13
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 320
		CALL CALL_COM(キャラ番号)
		;口上用に変数を戻す
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 13
	CASE 14
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 321
		CALL CALL_COM(キャラ番号)
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 14
	CASE 15
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 322
		CALL CALL_COM(キャラ番号)
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 15
	CASE 16
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 323
		CALL CALL_COM(キャラ番号)
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 16
	CASE 17
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 324
		CALL CALL_COM(キャラ番号)
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 17
	CASE 18
		;弱点コマンド処理の都合上、CALL_COMを経由させる
		LOCAL = SELECTCOM
		RSTR:SELECTCOM_S受け渡し = 
		SELECTCOM_S = 
		SELECTCOM = 327
		CALL CALL_COM(キャラ番号)
		SELECTCOM = LOCAL
		RFLAG:コマンド結果受渡し変数２ = 18
ENDSELECT

CALL SOURCE_補正処理(キャラ番号)


@MESSAGE_えっちな罰ゲーム(キャラ番号, ARG)
#DIM キャラ番号

IF FLAG:汎用文章表示切り替えオプション > 0
	IF TCVAR:TARGET:弱点コマンドフラグ
		DRAWLINE
		SETCOLOR カラーパレット("えっちな色")
		PRINTW <<<弱点コマンド！！>>>
		RESETCOLOR
	ELSEIF TCVAR:TARGET:弱点シチュフラグ
		SETCOLOR カラーパレット("えっちな色")
		PRINT <<<弱点シチュエーション
		PRINT_IMG "えっちハート"
		PRINTL >>>
		RESETCOLOR
	ENDIF
ENDIF

SELECTCASE ARG
	CASE 10
		;下着の色を言う
		PRINTFORM %CALLNAME:キャラ番号%は
		IF TALENT:キャラ番号:羞恥心 > 0
			PRINT 顔を真っ赤にしながら
		ELSEIF TALENT:キャラ番号:羞恥心 < 0
			PRINT なんでも無いような顔で
		ELSE
			PRINT 少し恥ずかしそうに
		ENDIF
		IF TEQUIP:キャラ番号:下半身下着あり == 1
			PRINTW 自身の下着の色を教えてくれた。
		ELSEIF TEQUIP:キャラ番号:下半身下着あり == 0
			PRINTW 履いてないことを教えてくれた。
		ELSEIF TEQUIP:キャラ番号:下半身下着あり == 2
			PRINTW 前張りだと教えてくれた。
		ELSEIF TEQUIP:キャラ番号:下半身下着あり == 3
			PRINTW ふんどしだと教えてくれた。
		ENDIF
	CASE 11
		;セクシーポーズで淫語
		PRINTFORM %CALLNAME:キャラ番号%は
		IF TALENT:キャラ番号:羞恥心 > 0
			PRINTL 心底恥ずかしそうに
		ELSEIF TALENT:キャラ番号:羞恥心 < 0
			PRINTL 少し乗り気な様子で
		ELSE
			PRINTL 困ったように笑いながら
		ENDIF
		IF 知識素質:キャラ番号:性知識 < 1
			PRINTW とんちんかんなポーズと言葉を披露した。
		ELSEIF 知識素質:キャラ番号:性知識 < 3
			PRINTW ぎこちないセクシーポーズを取り、精一杯のえっちな言葉を並べた。
		ELSEIF 知識素質:キャラ番号:性知識 < 5
			PRINTW 魅惑的なポーズを取り、性欲を煽るような過激な言葉を語った。
		ELSE
			PRINTFORMW とても子供には見せられないエグいポーズを見せつけ、%CALLNAME:MASTER%の耳元で淫靡なセリフを囁いた。
		ENDIF
	CASE 12
		;乳首当てゲーム
		IF TALENT:キャラ番号:羞恥心 > 0
			PRINT 頬を赤く染めて目を瞑って待つ
		ELSEIF TALENT:キャラ番号:羞恥心 < 0
			PRINT 当てられるかな？と笑う
		ELSE
			PRINT 
		ENDIF
		PRINTFORMW %CALLNAME:キャラ番号%の胸に、乳首めがけて指を前に出した。
		CALL 乳首当てゲーム結果メッセージ(キャラ番号)
	CASE 13
		CALL MESSAGE_COM320
	CASE 14
		CALL MESSAGE_COM321
	CASE 15
		CALL MESSAGE_COM322
	CASE 16
		CALL MESSAGE_COM323
	CASE 17
		CALL MESSAGE_COM324
	CASE 18
		CALL MESSAGE_COM327
ENDSELECT


@乳首当て結果判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ

IF MAX(TEQUIP:対象キャラ:上半身鎧あり, TEQUIP:対象キャラ:上半身服あり, TEQUIP:対象キャラ:上タイツあり, TEQUIP:対象キャラ:上半身下着あり) == 0
	RETURNF 0
ELSE
	RETURNF RAND:10
ENDIF


