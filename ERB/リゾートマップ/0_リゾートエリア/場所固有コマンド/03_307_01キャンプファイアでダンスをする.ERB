
;-------------------------------------------------
;03_307_01キャンプファイアでダンスをする
;-------------------------------------------------
@COMNAME_PLACE_380_0_307
#FUNCTION

IF 施設改造度:3:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = ダンスに誘う
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_307
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")
		
@COM380_0_307

;成功度の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 10
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF


DOWNBASE:MASTER:気力 += 100
DOWNBASE:MASTER:体力 += 100

DOWNBASE:気力 += 100
DOWNBASE:体力 += 100


;成功以上なら好感度・恋慕度を追加上昇
LOCAL:0 = 500
LOCAL:1 = 100
IF RFLAG:コマンド結果受渡し変数 <= -1
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.30
ELSEIF RFLAG:コマンド結果受渡し変数 <= 9
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
	NOWEX:恋心度上昇格納 += 100
	;恋慕度
	SIF CFLAG:ゲージ起動分類 == 1
		NOWEX:恋慕度上昇格納 += 100
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
	NOWEX:恋心度上昇格納 += 500
	;恋慕度
	SIF CFLAG:ゲージ起動分類 == 1
		NOWEX:恋慕度上昇格納 += 500
ENDIF

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
FOR LOCAL:2, 1, CHARANUM
	SIF LOCAL:2 == TARGET
		CONTINUE
	SIF TARGET:(LOCAL:2) <= 0
		CONTINUE
	SIF CFLAG:(TARGET:(LOCAL:2)):睡眠
		CONTINUE
	CALL SOURCE_CALC_歓楽(LOCAL:2, PLAYER, LOCAL:1)
	CALL SOURCE_CALC_好感度要素_友情度UP(LOCAL:2, PLAYER, LOCAL:1 / 2)
NEXT

TIME += 30
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_307
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;結構仲良しじゃないとダメ
SIF CFLAG:TARGET:好感度レベル < 50
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_307

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と炎の周りでダンスを踊った。

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE IS < 0
		;失敗時
		PRINTW しかし、ステップを間違えて上手く踊れなかった…
	CASE IS < 10
		;成功時
		PRINTW くるくると回り、一緒に楽しく踊ることが出来た。
	CASEELSE
		;大成功時
		PRINTL 息の合った踊りで心地よいひと時を過ごせた。
		SIF CFLAG:ゲージ起動分類 == 1
			PRINTFORMW ロマンチックな雰囲気の中、%CALLNAME:TARGET%は熱っぽく%CALLNAME:PLAYER%を見つめている…
ENDSELECT


