@サイド領域描画_ダンジョン
#DIMS 表示文字列
#DIMS 追加表示文字列

サイド描画行数保存 = LINECOUNT

;基礎部分
表示文字列 = <div rect='5925, 0, 3975, 4150' padding='50,25,130' color='#101010' border='31' bcolor='#C0C0C0'><nobr>
追加表示文字列 = 

TRYCALLFORM サイド描画表示種類_%サイド描画表示種類_ダンジョン用%(表示文字列, 追加表示文字列)
表示文字列 += "</nobr></div>"
;下部分表示
LOCAL = 5925
LOCAL:10 = 4135
LOCAL:1 = 500
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:10}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_表示なし'>表示なし</button></div>"
SIF サイド描画表示種類_ダンジョン用 == "表示なし"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:10 -20}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
LOCAL += LOCAL:1 + 50
LOCAL:1 = 700
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:10}, {LOCAL:1}, 200' padding='25,0,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_イベントログ'>イベントログ</button></div>"
SIF サイド描画表示種類_ダンジョン用 == "イベントログ"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:10 - 20}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
LOCAL += LOCAL:1 + 50
LOCAL:1 = 700
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:10}, {LOCAL:1}, 200' padding='25,0,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_前回戦闘ログ'>前回戦闘ログ</button></div>"
SIF サイド描画表示種類_ダンジョン用 == "前回戦闘ログ"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:10 - 20}, {LOCAL:1 - 40}, 60' color='#101010'></div>"

SIF 追加表示文字列 != ""
	表示文字列 += @"%追加表示文字列%"
HTML_PRINT 表示文字列, 1



@サイド領域_実行処理_ダンジョン
#DIM 入力数値

IF STRCOUNT(RESULTS, "サイド領域_サイド切り替え_")
	サイド描画表示種類_ダンジョン用 = %REPLACE(RESULTS, "サイド領域_サイド切り替え_", "")%
	RETURN 1
ENDIF

SELECTCASE サイド描画表示種類_ダンジョン用
	CASE "イベントログ"
		IF RESULTS == "サイド領域_スクロール_上"
			サイド描画縦スクロール保存 = MIN(STRCOUNT(サイド描画_ダンジョンログ保存, "<br>"), サイド描画縦スクロール保存 + 35)
		ELSEIF RESULTS == "サイド領域_スクロール_下"
			サイド描画縦スクロール保存 = MAX(0, サイド描画縦スクロール保存 -35)
		ENDIF
	CASE "前回戦闘ログ"
		IF RESULTS == "サイド領域_スクロール_上"
			サイド描画縦スクロール保存 = MIN(STRCOUNT(サイド描画_戦闘ログ保存, "<br>"), サイド描画縦スクロール保存 + 35)
		ELSEIF RESULTS == "サイド領域_スクロール_下"
			サイド描画縦スクロール保存 = MAX(0, サイド描画縦スクロール保存 -35)
		ENDIF
ENDSELECT

RETURN 0


@サイド描画表示種類_イベントログ(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列
#DIM 繰り返し回数
#DIMS ログ表示用文字列
#DIMS ツールチップ保存文字列, 100
#DIM 退避件数

VARSET RESULTS
VARSET ツールチップ保存文字列
ログ表示用文字列 = %サイド描画_ダンジョンログ保存%

;ツールチップを判定行数から排除するため、中身を配列に突っ込む
REGEXPMATCH ログ表示用文字列, "title='.*?'", 1
退避件数 = RESULT
FOR 繰り返し回数, 0, 退避件数
	ツールチップ保存文字列:繰り返し回数 '= RESULTS:繰り返し回数
	ログ表示用文字列 '= REPLACE(ログ表示用文字列, RESULTS:繰り返し回数, @"___ツールチップ退避{繰り返し回数}___")
NEXT

IF STRCOUNT(ログ表示用文字列, "<br>") > 35
	追加表示文字列 += "<div rect='6000, 4000, 3825, 100'>　　　　　"
	IF STRCOUNT(ログ表示用文字列, "<br>") > 35 + サイド描画縦スクロール保存
		追加表示文字列 += "<button value='サイド領域_スクロール_上'>[上へスクロール]</button>"
	ELSE
		追加表示文字列 += "　　　　　　　　"
	ENDIF
	追加表示文字列 += "　　　　　　　　　　　　"
	SIF サイド描画縦スクロール保存 > 0
		追加表示文字列 += "<button value='サイド領域_スクロール_下'>[下へスクロール] </button>"
	追加表示文字列 += "</div>"
ENDIF

;<br>の数で表示行数を判定
;36行まで表示可能
IF STRCOUNT(ログ表示用文字列, "<br>") > 35 + サイド描画縦スクロール保存
	FOR 繰り返し回数, 0, STRCOUNT(ログ表示用文字列, "<br>") - 35 - サイド描画縦スクロール保存
		ログ表示用文字列 = %REPLACE(ログ表示用文字列, "^.*?<br>", "")%
	NEXT
ENDIF

IF 退避件数 > 0
	FOR 繰り返し回数, 0, 退避件数
		ログ表示用文字列 '= REPLACE(ログ表示用文字列, @"___ツールチップ退避{繰り返し回数}___",ツールチップ保存文字列:繰り返し回数 )
	NEXT
ENDIF

表示文字列 += @"%ログ表示用文字列%"



@サイド描画表示種類_前回戦闘ログ(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列
#DIM 繰り返し回数
#DIMS ログ表示用文字列
#DIMS ツールチップ保存文字列, 100
#DIM 退避件数

VARSET RESULTS
VARSET ツールチップ保存文字列
ログ表示用文字列 = %サイド描画_戦闘ログ保存%

;ツールチップを判定行数から排除するため、中身を配列に突っ込む
REGEXPMATCH ログ表示用文字列, "title='.*?'", 1
退避件数 = RESULT
FOR 繰り返し回数, 0, 退避件数
	ツールチップ保存文字列:繰り返し回数 '= RESULTS:繰り返し回数
	ログ表示用文字列 '= REPLACE(ログ表示用文字列, RESULTS:繰り返し回数, @"___ツールチップ退避{繰り返し回数}___")
NEXT

IF STRCOUNT(ログ表示用文字列, "<br>") > 35
	追加表示文字列 += "<div rect='6000, 4000, 3825, 100'>　　　　　"
	IF STRCOUNT(ログ表示用文字列, "<br>") > 35 + サイド描画縦スクロール保存
		追加表示文字列 += "<button value='サイド領域_スクロール_上'>[上へスクロール]</button>"
	ELSE
		追加表示文字列 += "　　　　　　　　"
	ENDIF
	追加表示文字列 += "　　　　　　　　　　　　"
	SIF サイド描画縦スクロール保存 > 0
		追加表示文字列 += "<button value='サイド領域_スクロール_下'>[下へスクロール] </button>"
	追加表示文字列 += "</div>"
ENDIF

;<br>の数で表示行数を判定
;36行まで表示可能
IF STRCOUNT(ログ表示用文字列, "<br>") > 35 + サイド描画縦スクロール保存
	FOR 繰り返し回数, 0, STRCOUNT(ログ表示用文字列, "<br>") - 35 - サイド描画縦スクロール保存
		ログ表示用文字列 = %REPLACE(ログ表示用文字列, "^.*?<br>", "")%
	NEXT
ENDIF

IF 退避件数 > 0
	FOR 繰り返し回数, 0, 退避件数
		ログ表示用文字列 '= REPLACE(ログ表示用文字列, @"___ツールチップ退避{繰り返し回数}___",ツールチップ保存文字列:繰り返し回数 )
	NEXT
ENDIF

表示文字列 += @"%ログ表示用文字列%"
