
@固有アビ_83_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM Lv閾値 =  0, 15, 30, 50

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = マナ・ブラスト＋＋
	消費ＭＰ = 80
	効果数値 = 70
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
			アビ名 = 星華のマナ・ブラスト
			消費ＭＰ = 65
			効果数値 = 90
		ENDIF	
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = マナ・ブラスト＋
	消費ＭＰ = 80
	効果数値 = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = マナ・ブラスト
	消費ＭＰ = 80
	効果数値 = 30
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ターゲットに関わらず3回水属性［攻撃力×80％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>敵全体に氷結効果［3ターン］"
		詳細文文字列受け渡し変数 += @"<br>味方全体のＨＰを［回復力✕{効果数値}％］回復させる"
		詳細文文字列受け渡し変数 += @"<br>味方全体のデバフを一つ削除する"
		詳細文文字列受け渡し変数 += @"<br>◆敵が氷結状態の時、攻撃回数2倍"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, 3
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 80
				CALL アビテンプレート_ダメージ処理_水属性
			NEXT
		IF デバフ番号取得_枠("氷結") > -1
			FOR LOCAL, 0, 3
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 80
				CALL アビテンプレート_ダメージ処理_水属性
			NEXT	
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 氷結
		アビ汎用文字列:バフ・デバフ対象能力 = 氷結
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値:0 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理_MAXでも回復
		
		CALL アビテンプレート_有利効果_デバフ消去

ENDSELECT

@固有アビ_83_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM Lv閾値 =  0, 20, 35, 55

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = テュロス・アジリス＋＋
	消費ＭＰ = 180
	効果数値 = 3000
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
			アビ名 = 星域のテュロス・アジリス
			消費ＭＰ = 180
			効果数値 = 5000
		ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = テュロス・アジリス＋
	消費ＭＰ = 200
	効果数値 = 1000
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = テュロス・アジリス
	消費ＭＰ = 200
	効果数値 = 500
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体の攻撃力を15％上昇させる［2ターン］
		詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを50％軽減［2ターン］"
		詳細文文字列受け渡し変数 += @"<br>味方全体にディスペルガードを付与［2ターン］"
		詳細文文字列受け渡し変数 += @"<br>味方全体にバリアを[{効果数値}]展開"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 星域_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 15
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
		アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:効果量 = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット

ENDSELECT

@固有アビ_83_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = プレアデス
	消費ＭＰ = 70
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×700％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>自身のトリプルアタック率を［100％］UP［3ターン］"
		詳細文文字列受け渡し変数 += @"<br>自身に水属性追撃効果［3ターン］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 700
		CALL アビテンプレート_ダメージ処理_水属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = TA率
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 70
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

ENDSELECT

@固有アビ_83_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM バフ・デバフ行数
#DIM 星華
#DIM 隊列番号
隊列番号 = キャラ隊列検索(GETCHARA(83))

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = エウクラートン・フォス
	消費ＭＰ = 100
ELSE
	アビ名 = 
ENDIF
IF 隊列番号 > -1
	星華 = 刻印現在数確認("星華", 隊列番号)
	SIF 戦闘中カウント:隊列番号:0 >= 2 || 星華 <= 4
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方1人に慈愛の星芒効果[消去不可]
		詳細文文字列受け渡し変数 += @"<br>味方1人に再行動効果[1ターン/消去不可]"
		詳細文文字列受け渡し変数 += @"<br>味方1人に自動復活効果[100％回復/消去不可]"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは星華が5つスタックした時に、それを3つ消費して使用できる"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1度の戦闘で2回まで使用できる"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 慈愛の星芒
		アビ汎用文字列:バフ・デバフ対象能力 = 慈愛の星芒
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 慈愛の星芒_TA率
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 100
		CALL アビテンプレート_有利効果_付随バフ効果セット("慈愛の星芒")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 慈愛の星芒_通常攻撃ダメージ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_通常攻撃ダメージ
		アビ汎用変数:効果量 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("慈愛の星芒")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 慈愛の星芒_アビリティダメージ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果量 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("慈愛の星芒")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 自動復活
		アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 星華
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_刻印減少(3)

		戦闘中カウント:キャラ隊列検索(GETCHARA(83)):0 += 1

ENDSELECT


@固有戦闘開始時処理_キャラ_83
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 聖泉の星華
アビ汎用文字列:バフ・デバフ対象能力 = 聖泉の星華
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビテンプレ変数リセット


@固有通常攻撃後処理_キャラ_83(全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 星華

SIF 全滅フラグ
	RETURN 0

隊列番号 = キャラ隊列検索(GETCHARA(83))
バフ・デバフ行数 = バフ番号取得_枠("聖泉の星華", 隊列番号)
星華 = 刻印現在数確認("星華", 隊列番号)

IF バフ・デバフ行数 > -1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 星華
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_刻印追加(1)
ENDIF

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 星華_攻撃力
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:付随割合 = 5
	アビ汎用変数:持続ターン = 2
	CALL アビテンプレート_有利効果_付随バフ効果セット("星華")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 星華_回復力
	アビ汎用文字列:バフ・デバフ対象能力 = 回復力
	アビ汎用変数:付随割合 = 10
	アビ汎用変数:持続ターン = 2
	CALL アビテンプレート_有利効果_付随バフ効果セット("星華")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 星華_防御力
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:付随割合 = 5
	アビ汎用変数:持続ターン = 2
	CALL アビテンプレート_有利効果_付随バフ効果セット("星華")



@バフ・デバフ特殊表示_慈愛の星芒(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 慈愛の星芒
PRINTFORML トリプルアタック率を［100％］UPし、通常攻撃とアビリティの与ダメージを［10％］UPさせる
PRINTFORML 持続ターン：永続


@バフ・デバフ特殊表示_聖泉の星華(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 聖泉の星華
PRINTFORML 通常攻撃時に「星華」を得る
PRINTFORML 持続ターン：永続


@バフ・デバフ特殊表示_星華(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 星華
PRINTFORML 「星華」のスタック数に応じて攻撃UP/回復性能UP/防御UP


@探索欄表示_キャラ_83(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "星華:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"Ū{刻印現在数確認("星華", 隊列番号)}", 200, 90
