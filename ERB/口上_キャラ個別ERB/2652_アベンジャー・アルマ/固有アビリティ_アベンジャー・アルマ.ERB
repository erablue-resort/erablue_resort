
;1アビ
;☆元にした原作スキル
;「つんつんします」
;一太刀で無数の斬撃を放ったかの如く周囲の敵を切りつける

;その解説文でその名前はいったい何？となりますが高性能な主力技でした
;行動速度上昇はこの原型技（ジリオンブレイド）が持つ「別の技に素早く繋げられる」効果をなんとなく再現したつもり

@固有アビ_2652_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM アビランク
#DIM 攻撃回数 = 3
#DIM 速度上昇率

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビランク = 2
ELSE
	アビランク = 1
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = つんつんします＋
		消費ＭＰ = 110
		威力値 = 220
		速度上昇率 = 20
	CASEELSE
		アビ名 = つんつんします
		消費ＭＰ = 110
		威力値 = 180
		速度上昇率 = 15
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>ターゲットに関わらず［攻撃力✕{威力値}％］の{攻撃回数}回自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［{速度上昇率}％］（A枠・2ターン）の行動速度上昇効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(攻撃回数)
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性

		;次ターンの行動が早くなってほしいだけなので2ターン
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 行動速度A
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 速度上昇率
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


;2アビ
;☆元にした原作スキル
;「まもります、絶対に」
;憑依解除効果スキルの解除効果をこのスキルの効果時間中無効にする
;ただし、装備解除による憑依解除は無効化されない

;「鬼さんこちらです」
;相手のターゲットを自分に変更させる

;ECO独自の憑依システムに関係するスキルとヘイトコントロール技を合わせて
;単体ディスペルガード＋かばう的なやつにしてみました（すぐ消えるガッツつき）
;ベイヤールと相性がよくない

@固有アビ_2652_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビランク = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = まもります、絶対に＋
		消費ＭＰ = 60
	CASE 1
		アビ名 = まもります、絶対に
		消費ＭＰ = 60
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に（5ターンor1回）のディスペルガード効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（通常枠・1ターン）の単体かばう効果を与える。"
		SIF アビランク >= 2
			詳細文文字列受け渡し変数 += @"<br>（1ターンor2回）のガッツ効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
		アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 単体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 単体かばう
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		IF アビランク >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ガッツ
			アビ汎用文字列:バフ・デバフ対象能力 = ガッツ
			アビ汎用変数:持続ターン = 1
			アビ汎用変数:持続被ダメ回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT



;3アビ
;☆元にした原作スキル
;「秘密の力を開放します」
;対象の武器に闇の力を与える

;「はい、がんばります」
;自身の物理最大攻撃力を上昇させる

;バフ2種をまとめてアベンジ的に自傷を付けました
;クールタイムもついて説明文が長いわりにバフが控えめかも

@固有アビ_2652_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク
#DIM ＨＰ消費率 = 20
#DIM 消費ＨＰ
#DIM 隊列番号
#DIM クールタイム

隊列番号 = キャラ隊列検索(キャラ番号)

消費ＨＰ = BATTLE_STATE:戦闘行動キャラ:最大ＨＰ * ＨＰ消費率 / 100

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 1
		アビ名 = 秘密の力を開放します
		消費ＭＰ = 30
	CASEELSE
		アビ名 = 
ENDSELECT

;ＣＴ処理
IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:1
	クールタイム = MAX(0, 3 + 戦闘中カウント:隊列番号:1 - 現在ターン数)
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
ELSE
	クールタイム = 0
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}　　消費ＨＰ：最大ＨＰの{ＨＰ消費率}％（{消費ＨＰ}）
		詳細文文字列受け渡し変数 += @"<br>味方全体に[20％]（独自枠・2ターン）の闇属性強化効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に[30％]（独自枠・3ターン）の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは［3ターン］のＣＴが発生する。"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"残り{クールタイム}ターン。"
		RETURN 消費ＭＰ
	CASE "アビ効果"

		;自殺はできないように
		IF アビテンプレート用_キャンセルフラグ
			RETURN
		ELSEIF 消費ＨＰ >= BATTLE_STATE:戦闘行動キャラ:ＨＰ
			CALL 口上変数初期化
			WSTR:(K++) = ＨＰが不足しています（ＨＰ：{消費ＨＰ + 1}以上必要）
			CALL メッセージ欄表示用関数(,,,0)
			アビテンプレート用_キャンセルフラグ = 1
			RETURN
		ENDIF

		BATTLE_STATE:戦闘行動キャラ:ＨＰ -= 消費ＨＰ

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 秘密の力_属性与ダメ
		アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_闇
		アビ汎用変数:効果量 = 20
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 秘密の力_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果量 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
		アビテンプレート用_アビ封印用フラグ = 1
		戦闘中カウント:隊列番号:1 = 現在ターン数
ENDSELECT



;サポアビ
;アベンジャーなら逆境を持っててほしいと思い……
;違うゲームのアベンジャーだこれ

@固有ターン開始時処理_キャラ_2652
#DIM キャラ番号
#DIM 隊列番号

キャラ番号 = GETCHARA(2652)
隊列番号 = キャラ隊列検索(キャラ番号)

;毎ターン発動されると表示が邪魔なので逆境付いてるなら発動させない
IF バフ番号取得("逆境", 隊列番号) == -1 && BATTLE_STATE:隊列番号:ＨＰ <= (BATTLE_STATE:隊列番号:最大ＨＰ / 4) * 3
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 逆境
	アビ汎用文字列:バフ・デバフ対象能力 = 逆境
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:効果割合 = 40
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF


@口上_ステータス画面_固有特性解説_2652(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『アベンジ……しますよ？』 □---------------------------------------------------
KSTR:(K++) = 　ターン開始時にＨＰが75％以下の場合、自身に[40％]（通常枠・3ターン）の逆境効果を与える。

CALL 口上変数表示(対象キャラ, 1)
