
;☆元にした原作スキル
;「暴れるんじゃあ……です」
;対象の防御力を低下させる5回攻撃を行う

;原作においては封印推奨されるほどの低性能スキルでしたが、
;固有の攻撃技自体がこれと「つんつんします」しかなかったことと
;所有スキルの中で唯一名前がダジャレになっている点から
;これを奥義にするしかないなと思い採用しました

;しかし5回攻撃する技だったことをすっかり忘れていたので
;なんとかアベンジ的な追撃をつけてごまかした……つもり……
;防デバフはただのアーマーブレイク互換です

@固有奥義_2652(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 奥義ランク
#DIM 追加攻撃威力 = 70
#DIM 追加攻撃回数
#DIM 敵番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義ランク = 2
ELSE
	奥義ランク = 1
ENDIF

;最低1回の追撃が入るのでやや控えめに
;下げすぎた気がしなくもない
SELECTCASE 奥義ランク
	CASE IS >= 2
		奥義名 = 暴れるんじゃあ……です＋
		奥義威力 = 310
	CASEELSE
		奥義名 = 暴れるんじゃあ……です
		奥義威力 = 260
ENDSELECT

;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
		詳細文文字列受け渡し変数 += @"<br>［20％］（B枠・3ターン）の防御力減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身の残りＨＰに応じて、追加で1～4回[攻撃力✕{追加攻撃威力}％]の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>（ＨＰが25％減るごとに+1回）"
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる

		;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
		;必要な場合を除きここでリセット
		CALL アビ汎用変数文字列リセット
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載

		敵番号 = アビテンプレート用_対象保存:0

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット

		;追加攻撃
		;ここもうちょっとスマートに作れるのではとおもいます
		;76～100％
		IF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4) * 3 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ
			追加攻撃回数 = 1
		;51～75％
		ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4) * 2 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4) * 3
			追加攻撃回数 = 2
		;26～50％
		ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4) < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4) * 2
			追加攻撃回数 = 3
		;～25％
		ELSEIF BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 4
			追加攻撃回数 = 4
		ENDIF

		FOR LOCAL, 0, 追加攻撃回数
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(敵番号)
			アビ汎用変数:効果割合 = 追加攻撃威力
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT
ENDSELECT
