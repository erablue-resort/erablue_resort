;
@コマンド実行時画像名称変換処理(対象キャラ, コマンド名)
#FUNCTIONS
#DIM  対象キャラ
#DIMS コマンド名
#DIMS コマンド画像名称
#DIMS コマンド派生名称
#DIMS キャラ衣装識別

;DEBUGPRINTFORML @コマンド実行時画像名称変換処理(%CALLNAME:対象キャラ%, %コマンド名%)

;"表示グラフィックカテゴリ"だと表示が怪しい
コマンド画像名称 '= ""

キャラ衣装識別 '= ""

キャラ衣装識別 '= REPLACE(CSTR:対象キャラ:顔グラ表示切り替え用文字列, "顔_", "")
SIF キャラ衣装識別 == ""
	キャラ衣装識別 '= REPLACE(CSTR:対象キャラ:短冊グラ表示切り替え用文字列, "短冊_", "")
SIF キャラ衣装識別 == ""
	キャラ衣装識別 '= CSTR:対象キャラ:表示グラフィックカテゴリ
SIF キャラ衣装識別 == ""
	キャラ衣装識別 = デフォルト

コマンド画像名称 '= GRAP_COM_RAND(対象キャラ, "コマンド実行時_", キャラ衣装識別, コマンド名)

SIF コマンド画像名称 != ""
	RETURNF コマンド画像名称

SELECTCASE 1
	CASE GROUPMATCH(コマンド名, "Ｖ挿入する", "Ｇスポ責め", "ポルチオ責め", "種付けプレス", "子宮口挿入", "角ハンドル", "尻尾ひっぱり後背位", "首筋甘噛み交尾", "おっぱい手綱正常位") > 0
		;V性交系
		コマンド画像名称 '= コマンド実行時画像文字列算出処理(対象キャラ)
	CASE GROUPMATCH(コマンド名, "入口を責める", "子宮裏を責める", "Ｓ字結腸責め", "角ハンドルＡ", "尻尾ひっぱり後背位A") > 0
		;A性交系
		コマンド画像名称 '= コマンド実行時画像文字列算出処理(対象キャラ)
	CASEELSE
ENDSELECT

;★%名称%%体位%だと意図しない動作になる(最初の判定で別の体位の画像を呼んでしまう場合がある)ので%体位%%名称%にする
コマンド派生名称 '= ""
コマンド派生名称 '= REPLACE(コマンド画像名称, @"コマンド実行時_%キャラ衣装識別%_", "") + @"%コマンド名%"

IF コマンド画像名称 != "" && GRAP_COM_RAND(対象キャラ, "コマンド実行時_", キャラ衣装識別, @"%コマンド派生名称%") != ""
	コマンド画像名称 '= GRAP_COM_RAND(対象キャラ, "コマンド実行時_", キャラ衣装識別, @"%コマンド派生名称%")
	SIF コマンド画像名称 == ""
		コマンド画像名称 '= GRAP_COM_RAND(対象キャラ, "コマンド実行時_", キャラ衣装識別, コマンド画像名称)
ENDIF

RETURNF コマンド画像名称

@コマンド実行時画像文字列算出処理(対象キャラ)
#FUNCTIONS
#DIM  対象キャラ
#DIMS 文字列記憶
#DIMS D_NAME

VARSET LOCAL
VARSET 差分エフェクト

D_NAME '= ""

LOCALS '= REPLACE(CSTR:対象キャラ:短冊グラ表示切り替え用文字列, "短冊_", "コマンド実行時_")
SIF LOCALS == ""
	LOCALS '= REPLACE(CSTR:対象キャラ:顔グラ表示切り替え用文字列, "顔_", "コマンド実行時_")

IF LOCALS == ""
	SIF CSTR:対象キャラ:表示グラフィックカテゴリ == ""
		CSTR:対象キャラ:表示グラフィックカテゴリ = デフォルト
	;LOCALS = %TYPE + CSTR:対象キャラ:表示グラフィックカテゴリ%
	LOCALS = %CSTR:対象キャラ:表示グラフィックカテゴリ%
ENDIF

LOCALS '= LOCALS + "_"

;;DEBUGPRINTFORML LOCALS == %LOCALS%
;;DEBUGPRINTFORML CSTR:対象キャラ:表示グラフィックカテゴリ == %CSTR:対象キャラ:表示グラフィックカテゴリ%

IF MODE_存在判定_ターゲット側("Ｖ挿入系", 対象キャラ)
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("V_FRONT")
		IF MODE_存在判定_ターゲット側(V_FRONT:LOCAL, 対象キャラ)
			LOCAL:1 ++
			SIF コマンド画像差分抽出(対象キャラ, "", LOCALS, V_FRONT:LOCAL, "", "") && D_NAME == ""
				D_NAME '= RESULTS
			;;DEBUGPRINTFORML  D_NAME == %D_NAME%
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("V_BACK")
		IF MODE_存在判定_ターゲット側(V_BACK:LOCAL, 対象キャラ)
			LOCAL:2 ++
			SIF コマンド画像差分抽出(対象キャラ, "", LOCALS, V_BACK:LOCAL, "", "") && D_NAME == ""
				D_NAME '= RESULTS
			;;DEBUGPRINTFORML  D_NAME == %D_NAME%
		ENDIF
	NEXT
	;なければ正面体位は正常位を、背面体位は後背位を表示する
	IF LOCAL:1 && D_NAME == ""
		VARSET 差分エフェクト
		IF コマンド画像差分抽出(対象キャラ, "", LOCALS, "正常位", "", "") && D_NAME == ""
			D_NAME '= RESULTS
			;;DEBUGPRINTFORML  D_NAME == %D_NAME%
		ENDIF
	ELSEIF LOCAL:2 && D_NAME == ""
		VARSET 差分エフェクト
		IF コマンド画像差分抽出(対象キャラ, "", LOCALS, "後背位", "", "") && D_NAME == ""
			D_NAME '= RESULTS
			;;DEBUGPRINTFORML  D_NAME == %D_NAME%
		ENDIF
	ENDIF
ENDIF

VARSET LOCAL
IF MODE_存在判定_ターゲット側("Ａ挿入系", 対象キャラ)
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("A_FRONT")
		IF MODE_存在判定_ターゲット側(A_FRONT:LOCAL, 対象キャラ)
			LOCAL:1 ++
			SIF コマンド画像差分抽出(対象キャラ, "", LOCALS, A_FRONT:LOCAL, "", "") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("A_BACK")
		IF MODE_存在判定_ターゲット側(A_BACK:LOCAL, 対象キャラ)
			LOCAL:2 ++
			SIF コマンド画像差分抽出(対象キャラ, "", LOCALS, A_BACK:LOCAL, "", "") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	;なければ正面体位はA正常位を、背面体位はA後背位を表示する
	SIF LOCAL:1 && コマンド画像差分抽出(対象キャラ, "", LOCALS, "Ａ正常位", "") && D_NAME == ""
		D_NAME '= RESULTS
	SIF LOCAL:2 && コマンド画像差分抽出(対象キャラ, "", LOCALS, "Ａ後背位", "") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

;差分が見つかればそれを、なければ空文字
SIF D_NAME != ""
	RETURNF D_NAME

RETURNF ""


@コマンド画像差分抽出(対象キャラ, TYPE, TYPE:1, DIFF, DIFF:1="", DIFF:2="", TYPE:2="", DIFF:3="")
#FUNCTION
#DIM  対象キャラ
#DIMS TYPE, 3
#DIMS DIFF, 4

;;DEBUGPRINTFORML @コマンド画像差分抽出(対象キャラ, %TYPE%, %TYPE:1%, %DIFF%, %DIFF:1%, %DIFF:2%, %TYPE:2%, %DIFF:3%)
;;DEBUGPRINTFORML  
IF ENUMFILES(@"resources/%CSTR:対象キャラ:画像フォルダ%/", @"%TYPE:1 + TYPE:2 +DIFF%*.*")
	;現在表示してるグラの差分があるか見る
	RESULTS '= TYPE:1 + コマンド画像ランダム処理(対象キャラ, TYPE:1, TYPE:2 + DIFF)
	RETURNF 1
ENDIF

RETURNF 0


@コマンド画像ランダム処理(ARG, TYPE, PATRN, PATRN:1)
#FUNCTIONS
#DIMS FOLDER
#DIMS TYPE
#DIMS PATRN, 2
#DIMS 妊娠用PATRN保存
#DIM L_CNT
#DIMS 画像配列保存, 2000

SIF !INRANGE(ARG, 0, CHARANUM-1)
	RETURNF

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

妊娠用PATRN保存 = 
IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2
	妊娠用PATRN保存 '= PATRN
	PATRN '= "妊娠" + PATRN
	SIF PATRN:1 != ""
		PATRN:1 '= "妊娠" + PATRN:1
ENDIF

$妊娠差分用_LOOP

VARSET LOCAL
VARSET 画像配列保存
VARSET RESULTS

FOLDER '= "resources/" + CSTR:ARG:画像フォルダ
LOCAL = ENUMFILES(FOLDER, TYPE + PATRN + "*.*")
FOR L_CNT, 0, LOCAL
	IF STRCOUNT(RESULTS:L_CNT, "差分") == 0 || STRCOUNT(PATRN, "差分")
		画像配列保存:(LOCAL:10) '= RESULTS:L_CNT
		LOCAL:10 += 1
	ELSE
	ENDIF
NEXT
LOCAL:11 = LOCAL:10
IF PATRN:1 != ""
	LOCAL:1 = ENUMFILES(FOLDER, TYPE + PATRN:1 + "*.*")
	FOR L_CNT, LOCAL, LOCAL + LOCAL:1
		IF STRCOUNT(RESULTS:(L_CNT - LOCAL), "差分") == 0 || STRCOUNT(PATRN:1, "差分")
			画像配列保存:(LOCAL:11) '= RESULTS:(L_CNT - LOCAL)
			LOCAL:11 += 1
		ELSE
		ENDIF
	NEXT
ENDIF
LOCAL = LOCAL:10
LOCAL:1 = LOCAL:11 - LOCAL:10
IF LOCAL + LOCAL:1 < 1
	IF PATRN == "妊娠デフォルト"
		;妊娠デフォルトが無いならデフォルトに戻してもういっかい
		PATRN = デフォルト
		;分岐用に一時保存にも入れておく
		妊娠用PATRN保存 = デフォルト
		GOTO 妊娠差分用_LOOP
	ELSEIF STRCOUNT(PATRN, "妊娠") > 0
		;着せ替えの妊娠差分がないなら戻して最初から
		PATRN '= 妊娠用PATRN保存
		妊娠用PATRN保存 = 
		GOTO 妊娠差分用_LOOP
	ELSE
		IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2 && 妊娠用PATRN保存 == ""
			;妊娠しててPATRN保存が空っぽ = 現在衣装のグラが妊娠も通常も無いパターン
			;なので通常妊娠画像を見に行く
			PATRN = 妊娠デフォルト
			GOTO 妊娠差分用_LOOP
		ELSE
			RETURNF "デフォルト"
		ENDIF
	ENDIF
ENDIF
L_CNT = RAND(LOCAL + LOCAL:1)
RESULTS '= 画像配列保存:L_CNT

;DEBUGPRINTFORML CSTR:ARG:表示グラフィックカテゴリ:%CSTR:ARG:表示グラフィックカテゴリ%
;IF !STRCOUNT(PATRN, "差分") && !STRCOUNT(PATRN:1, "差分")
	;CSTR:ARG:表示グラフィックカテゴリ '= PATRN
	;SIF L_CNT >= LOCAL
		;CSTR:ARG:表示グラフィックカテゴリ '= PATRN:1
;ENDIF
;DEBUGPRINTFORML CSTR:ARG:表示グラフィックカテゴリ:%CSTR:ARG:表示グラフィックカテゴリ%

SUBSTRING RESULTS, 1 +STRLENS(FOLDER) + STRLENS(TYPE), -1
REPLACE RESULTS, "\.png", ""
REPLACE RESULTS, "\.webp", ""

RETURNF RESULTS

