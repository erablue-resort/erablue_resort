;奥義：公安局長の手腕：敵1人に対して攻撃力の53%分のダメージ。その後敵単体に「特殊デバフ」を与える。
;デバフ中はダメージを蓄積していき、カンナの３ターン目にデバフを解除する。解除時、蓄積したダメージを相手に与える。
;ただし原作ゲームに則ってダメージ上限を付ける。
;ダメージ上限は「基礎BATTLE_STATE:キャラ番号:攻撃力」を参照する。

;原作EXスキル再現方法
;アビ汎用文字列:バフ・デバフ枠 = 好きな名前
;アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
;として敵一体にデバフを掛ける
;
;そうすると、このデバフがかかったキャラは被ダメ時に
;@特殊反応_被ダメ時_好きな名前(バフ番号, 攻撃者隊列, 被ダメ記録順)
;の関数を読みに行くようになる
;
;そして上記の被ダメ時関数の中で
;変数 += 被ダメ時総ダメ:被ダメ記録順 * 3 / 10
;等で獲得すればよい
;格納先の変数は、被ダメ反応回数:被ダメ記録順で何回攻撃を受けたか、被ダメ時総ダメ:被ダメ記録順でその攻撃で何ダメージ受けたか、被ダメ時最大ダメ:被ダメ記録順でその攻撃で最も高いダメージは幾つだったか（多段アビなどで使用）

;奥義の使用時にデバフを掛ける処理と、蓄積をリセットする処理
;あとはデバフの解除時にダメージを与える（と一応リセットする）処理

@固有奥義_1720(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値
#DIM 成功率
#DIM 効果ターン
#DIM ダメージ上限
#DIM 蓄積割合
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	奥義名 = 公安局長の手腕+
	奥義威力 = 84
	蓄積割合 = 50
	成功率 = 1000
	効果ターン = 99
	ダメージ上限 = 基礎BATTLE_STATE:キャラ番号:攻撃力 * 269 / 10
ELSE
	奥義名 = 公安局長の手腕
	奥義威力 = 53
	蓄積割合 = 30
	成功率 = 1000
	効果ターン = 99
	ダメージ上限 = 基礎BATTLE_STATE:キャラ番号:攻撃力 * 207 / 10
ENDIF

アビ汎用文字列:実行時メッセージ１ = 「『狂犬』の名を、一度は聞いたことがあるだろう？」

SELECTCASE ARGS
	CASE "奥義名"
		;蓄積ダメージが炸裂するまで使用不可
		IF ダンジョン表示モード == "戦闘画面" && 戦闘中カウント:隊列番号:1 == 1
			RETURN -1
		ELSE
			TSTR:コマンド名受渡 = %奥義名%
		ENDIF
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>その後、対象に特殊デバフを付与する。
		RETURN 奥義威力
	CASE "奥義説明文_詳細"
		詳細文文字列受け渡し変数 += @"敵単体に自属性［{奥義威力}％］のダメージを与えた後、[公安局長の手腕]デバフを付与する。"
		詳細文文字列受け渡し変数 += @"<br>デバフ中は各キャラクターの与えたダメージの[{蓄積割合}％]を蓄積する。"
		詳細文文字列受け渡し変数 += @"<br>デバフ付与後３ターン目開始時にデバフを解除し、蓄積したダメージの合計を敵単体に与える。"
		詳細文文字列受け渡し変数 += @"<br>※ダメージ蓄積上限：{ダメージ上限}"
	CASE "奥義追加効果"
		アビ汎用文字列:バフ・デバフ枠 = 公安局長の手腕
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 成功率
		CALL アビテンプレート_不利効果_デバフ効果セット

		;3ターン判定用
		戦闘中カウント:隊列番号:0 = 4
		;奥義使用フラグ
		戦闘中カウント:隊列番号:1 = 1

ENDSELECT

;---------------------------------------------------
;敵がダメージを受けてもこれを参照しに来る
@特殊反応_被ダメ時_公安局長の手腕(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIM バフ番号
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM 隊列番号
#DIM 蓄積割合
#DIM カンナ番号
#DIM ダメージ上限
#DIM 現行ダメージ値

カンナ番号 = GETCHARA(1720)
隊列番号 = キャラ隊列検索(GETCHARA(1720))

IF 陥落チェック(カンナ番号) && 基礎BATTLE_STATE:カンナ番号:Lv >= 45
	蓄積割合 = 5
	ダメージ上限 = 基礎BATTLE_STATE:カンナ番号:攻撃力 * 269 / 10
ELSE
	蓄積割合 = 3
	ダメージ上限 = 基礎BATTLE_STATE:カンナ番号:攻撃力 * 207 / 10
ENDIF

;ダメージ蓄積処理
戦闘中カウント:隊列番号:2 += 被ダメ時総ダメ:被ダメ記録順 * 蓄積割合 / 10
現行ダメージ値 = 戦闘中カウント:隊列番号:2

;ダメージ上限判定
IF 現行ダメージ値 >= ダメージ上限
	戦闘中カウント:隊列番号:2 = ダメージ上限
ELSE
	戦闘中カウント:隊列番号:2 += 0
ENDIF

;---------------------------------------------------
;デバフ解除・ダメージ発生処理
@固有行動開始時処理_キャラ_1720
#DIM 隊列番号
#DIM 蓄積炸裂カウント
#DIM 対象一時保存
#DIM 蓄積ダメージ量

隊列番号 = キャラ隊列検索(GETCHARA(1720))
蓄積ダメージ量 = 戦闘中カウント:隊列番号:2

IF 戦闘中カウント:隊列番号:0 > 1
    WSTR:(K++) = 蓄積ダメージ炸裂まであと{戦闘中カウント:隊列番号:0 - 1}ターン！
	WSTR:(K++) = 現在の蓄積量：{蓄積ダメージ量}！
    戦闘中カウント:隊列番号:0 -= 1
ELSEIF 戦闘中カウント:隊列番号:0 == 1
    ;炸裂処理の実行

	;特殊デバフ付与敵キャラを取得
	CALLF デバフ所持者検索_枠("公安局長の手腕")
	対象一時保存 = TOINT(効果所持者検索結果:0)

	アビ汎用文字列:実行時メッセージ１ = 蓄積したダメージが炸裂する……！

	;ダメージ発生
	CALL アビ対象選択テンプレート_指定(対象一時保存)
	アビ汎用変数:効果量 = 蓄積ダメージ量
	アビ汎用文字列:ダメージ種類 = 奥義ダメージ
	CALL アビテンプレート_ダメージ処理_自属性

	;デバフ強制削除処理
	CALL デバフ削除_枠(対象一時保存, "公安局長の手腕")

	;全変数リセット処理
    戦闘中カウント:隊列番号:0 = 0
	戦闘中カウント:隊列番号:1 = 0
ENDIF
