;--------------------------------------------------
;固有奥義マギサ
;--------------------------------------------------
;メテオスウォ－ム：威力400、自攻撃10％UP
;メテオスウォ－ム＋：威力450、自攻撃10％UP
;メテオスウォ－ム＋＋：威力500、自攻撃15％UP
;メテオスウォ－ム・イメンス：陥落後、モラクス召喚時のみ。威力500、自攻撃15％UP、威力250の追撃
;メテオスウォーム・イメンス＋＋：陥落後、モラクス召喚時のみ。威力600、自攻撃30％UP、威力300の追撃
;イメンス＋＋は、ぶっちゃけ現状のLVMAXまで上げたご褒美に等しい。
;--------------------------------------------------
@固有奥義_38(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM 奥義実威力
#DIM 攻撃バフ効果値
#DIM 追撃威力
#DIM モーさんチェック
#DIMS 奥義名

	;奥技強化チェック
	;条件を満たし[サンクション]を使用中なら追撃が発生する
	;/
	;チェックのため初期化
	モーさんチェック = 0
	;LV35以上で、陥落しており、サンクションを使用済みなら、奥義が強化される
	SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35 && 陥落チェック(キャラ番号) && バフ番号取得_枠("モラクス_攻撃",キャラ隊列検索(キャラ番号)) == 1
		モーさんチェック = 1
	;特殊処理を行うので奥義威力はゼロに設定する
	奥義威力 = 0

	IF 0
	;サンクション中かつLv80ならさらに超パワーアップ
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 80 && モーさんチェック
		奥義名 = メテオスウォーム・イメンス＋＋
		奥義実威力 = 600
		攻撃バフ効果値 = 30
		追撃威力 = 300
	;サンクション中ならパワーアップ
	ELSEIF モーさんチェック
		奥義名 = メテオスウォーム・イメンス
		奥義実威力 = 500
		攻撃バフ効果値 = 20
		追撃威力 = 250
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
		奥義名 = メテオスウォーム＋＋
		奥義実威力 = 500
		攻撃バフ効果値 = 20
		追撃威力 = 0
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
		奥義名 = メテオスウォーム＋
		奥義実威力 = 450
		攻撃バフ効果値 = 15
	ELSE
		奥義名 = メテオスウォーム
		奥義実威力 = 400
		攻撃バフ効果値 = 10
	ENDIF
	
	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
			;下の４つの文字列に文章を入れると、奥義使用時の文章を変化させることが出来る。
			;これらを使わない場合は「（キャラ名）の奥義：（奥義名）！」と表示される。
			アビ汎用文字列:実行時メッセージ１ = 天から燃え立つ星が墜ちてくる……！

		CASE "奥義説明文"
			詳細文文字列受け渡し変数 '= @"奥義威力：{奥義実威力}％　自属性全体攻撃。"
			詳細文文字列受け渡し変数 += @"<br>攻撃前に自身に3ターンの攻撃力強化［{攻撃バフ効果値}％/独自枠］を付与する。"
			;追撃が発生するかどうかで３行目を変化させる
			IF モーさんチェック == 1
				詳細文文字列受け渡し変数 += @"<br>更に威力{追撃威力}％による追加攻撃が発生する。"
			;条件による奥義強化のおしらせ。サンクションを覚えているが、サンクションを使用していない場合
			ELSEIF モーさんチェック == 0 && 固有38_4_サンクション取得条件() == 1
				詳細文文字列受け渡し変数 += @"<br>[サンクション]の効果中、[メテオスウォーム・イメンス]に変化する。"
			ELSE
				;サンクションを覚えていないなら表示しない
			ENDIF
			RETURN 奥義威力
		CASE "奥義追加効果"
			;自己バフ
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 攻撃バフ効果値
			アビ汎用文字列:バフ・デバフ枠 = メテオ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

			;奥義攻撃本体
			CALL アビ汎用変数文字列リセット
			;奥義仕様先登録のため、ダメージ種類設定
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義実威力
			CALL アビテンプレート_ダメージ処理_自属性

			;追撃部分
			IF モーさんチェック
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:ダメージ種類 = 奥義ダメージ
				CALL アビ対象選択テンプレート_敵全体_生者のみ
				アビ汎用変数:効果割合 = 追撃威力
				CALL アビテンプレート_ダメージ処理_自属性
			ENDIF
	ENDSELECT
	RETURN RESULT

