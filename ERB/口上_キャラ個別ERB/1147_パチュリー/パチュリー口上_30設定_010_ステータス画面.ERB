;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――ステータス画面での一言など
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;通常画面一言口上
;--------------------------------------------------
;リゾート探索画面で、非うふふ中のとき、今対象にしているキャラクターの顔/短冊画像の下あたりに出るよ！
;--------------------------------------------------
@口上_INFO画面_一言コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 一時計算用
#DIMS 優先表示パターン
#DIMS テキストパターン

	CALL 口上変数初期化_1147

	;口上と同じ感覚で使う為に書式を統一しているが、通常時の画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨
	;また、一行のみの表示を想定。二行以上は基本的に禁止
	;※うふふ中には表示されません

	;状況によってランダム
	;★このあたり射精で使ってるシステム使いたい
	;好感度要素は必ず出る
	テキストパターン '= "好感度要素１/好感度要素２"

	;恋慕
	SIF パッチェさん_陥落段階(対象キャラ, "恋慕")
		テキストパターン += "/恋慕"

	;状況で分岐系
	;飲み会中
	SIF CFLAG:対象キャラ:飲み会
		テキストパターン += "/飲み会"
	;デート中
	;デートの相手のキャラ番号 + 100が入っている
	;あなた相手なら「CFLAG:対象キャラ:デート == MASTER + 100」
	SIF CFLAG:対象キャラ:デート
		テキストパターン += "/デート中"
	;一緒に遊ぶ中
	SIF CFLAG:対象キャラ:一緒に遊ぶ
		テキストパターン += "/一緒に遊ぶ中"
	;同行中
	SIF CFLAG:対象キャラ:同行
		テキストパターン += "/同行中"
	;一緒に隠密中
	SIF CFLAG:PLAYER:隠密 && CFLAG:対象キャラ:隠密
		テキストパターン += "/一緒に隠密中"

	;上記のランダムパターンの中から選ぶ
	テキストパターン '= TEXTR(テキストパターン)

	;ただし：特定の状況ならランダムでなく、上書き
	;/
	;仕事中の一言口上
	;仕事中なら、他のパターンを無視して、必ず仕事中になる
	SIF TALENT:対象キャラ:従業員 && TCVAR:対象キャラ:休暇中 == 0 && CFLAG:対象キャラ:現在位置 == 現在仕事:対象キャラ:1 % 10000 && CFLAG:対象キャラ:現在マップ種別 == 現在仕事:対象キャラ:1 / 10000
		テキストパターン '= "仕事中"
	;あなたが隠れていて、キャラはあなたに気づいていない
	SIF CFLAG:PLAYER:隠密 && !CFLAG:対象キャラ:隠密
		テキストパターン '= "あなた隠密中"
	;キャラの野外オナニー中
	SIF !CFLAG:PLAYER:隠密 && CFLAG:対象キャラ:隠密
		テキストパターン '= "野外オナニー中"
	;睡眠中ならファイナル上書き
	;んまあこのあたり、条件的には排他だと思うが……
	SIF CFLAG:対象キャラ:睡眠
		テキストパターン '= "睡眠中"
	;マッサージ中なら固定
	SIF STRFIND(NOW_SITUATION(対象キャラ), "マッサージ中") > -1
		テキストパターン '= "マッサージ中"


	IF 0
	;好感度要素１
	;最も多い好感度要素で分岐
	;まあ基本的にそっけないよなー……
	ELSEIF テキストパターン == "好感度要素１"
		;４つの要素から一番高い要素を（ただし複数要素が同値なら、その中からランダムで）返す
		一時計算用 = MAXRAND(CFLAG:対象キャラ:友情度, CFLAG:対象キャラ:信頼度, CFLAG:対象キャラ:恋心度, CFLAG:対象キャラ:性愛度)
		IF 0
		;1/4、またはいずれも要素が20を超えていないなら気付かず読書
		ELSEIF !RAND:4 || 要素最大値算出(対象キャラ) < 経験値テーブル:(関係閾値:2)
			KSTR:(K++) = （_呼び名_は読書している……）
		;友情度
		ELSEIF 一時計算用 == 0
			KSTR:(K++) = 「……なにか用？」
		;信頼度
		ELSEIF 一時計算用 == 1
			KSTR:(K++) = 「……どうかした？」
		;恋心度
		;一緒に本を読もうとするあたりハチャメチャにデレている
		ELSEIF 一時計算用 == 2
			KSTR:(K++) = 「……_二人称_も読みたいの？」
		;性愛度
		ELSEIF 一時計算用 == 3
			KSTR:(K++) = （_呼び名_は_主人_から目を逸らしている。少し頬が赤い……）
		ENDIF
	;好感度要素２
	;要素合計値で分岐
	ELSEIF テキストパターン == "好感度要素２"
		;ちなみに、ひとつひとつのマックスは「100500ポイント」
		;４つ合計で40万2000ポイントがマックスか
		一時計算用 = CFLAG:対象キャラ:友情度 + CFLAG:対象キャラ:信頼度 + CFLAG:対象キャラ:恋心度 + CFLAG:対象キャラ:性愛度
		IF 0
		;40万以上
		;４要素がほぼマックス
		ELSEIF 一時計算用 >= 400000
			;お茶を淹れるクールタイム中でも言うのは……まあ……許せ！
			KSTR:(K++) = 「今、時間はある？　少し、お茶でもどうかしら」
		;30万以上
		;とうとう向こうから用を聞いてくる
		ELSEIF 一時計算用 >= 300000
			KSTR:(K++) = 「なにか_一人称_に用？」
		;20万以上
		;２要素がほぼマックス……そろそろ拝み倒してセッ！　させてもらうところか……？
		ELSEIF 一時計算用 >= 200000
			KSTR:(K++) = 「……どうかした？」
		;10万以上
		;２要素が半分くらいか？
		ELSEIF 一時計算用 >= 100000
			KSTR:(K++) = 「……なにか用があるの」
		;5万以上
		ELSEIF 一時計算用 >= 50000
			KSTR:(K++) = 「……なにか用があるならあとにして」
		;3万以上
		ELSEIF 一時計算用 >= 30000
			;邪魔しないで、とまでは言わない
			KSTR:(K++) = 「……今本を読んでいるんだけど……」
		;それ以下
		;普通に気付かない
		ELSE
			KSTR:(K++) = （_呼び名_は読書している……）
		ENDIF
	;恋慕
	;デレデレにはならないぞ
	ELSEIF テキストパターン == "恋慕"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = 「……なに？　話したいの？」
		;「だっこして」である。めちゃくちゃに甘えている。
		ELSEIF !RAND:2
			KSTR:(K++) = 「椅子が少し硬いわ。座椅子になってくれない？」
		;わりとひとりの時間を要求するタイプだとは思う
		ELSE
			KSTR:(K++) = 「本を読みたいんだけど。……恋人？　だからって、常に一緒にいたがると思わないで」
		ENDIF
	;飲み会
	ELSEIF テキストパターン == "飲み会"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = 「お酒は、嫌いではないけど……うーん」
		ELSEIF !RAND:2
			KSTR:(K++) = 「騒がしいのはあまり好きじゃないんだけど……」
		ELSE
			KSTR:(K++) = 「酒飲みはほんとにもう……」
		ENDIF
	;デート
	ELSEIF テキストパターン == "デート中"
		IF 0
		;オーナー以外とのデート中
		ELSEIF CFLAG:対象キャラ:デート != MASTER + 100
			KSTR:(K++) = （_呼び名_はデートを楽しんでいるようだ）
		ELSEIF !RAND:3
			KSTR:(K++) = 「どこに連れていってくれるのかしら。……本屋とか？」
		ELSEIF !RAND:2
			KSTR:(K++) = 「デートを上手くこなす方法は学んであるわ」
		ELSE
			KSTR:(K++) = 「こっちだと出歩いてるときに弾幕ごっこを挑まれないから平和ねえ……」
		ENDIF
	;マッサージ中
	ELSEIF テキストパターン == "マッサージ中"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = 「あーそこそこ……」
		ELSEIF !RAND:2
			KSTR:(K++) = 「あああ……効くわ～……」
		ELSE
			KSTR:(K++) = 「んあ～……」
		ENDIF
	;一緒に遊ぶ中
	ELSEIF テキストパターン == "一緒に遊ぶ中"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = 「ええと、なるべく動かず勝利する方法は……」
		ELSEIF !RAND:2
			KSTR:(K++) = 「つ、疲れた……」
		ELSE
			KSTR:(K++) = 「ひとと遊ぶのは、ちょっと疲れるわ……」
		ENDIF
	;同行中
	ELSEIF テキストパターン == "同行中"
		IF 0
		;レアパターン
		ELSEIF !RAND:20
			KSTR:(K++) = 「おいてかないでー」
		ELSEIF !RAND:3 && パッチェさん_陥落段階(対象キャラ, "恋慕")
			KSTR:(K++) = 「どこへ連れて行く気？　……_二人称_の部屋？」
		ELSEIF !RAND:2
			KSTR:(K++) = 「どこへ連れて行く気？」
		ELSE
			KSTR:(K++) = 「どこに行くの？」
		ENDIF
	;一緒に隠密
	ELSEIF テキストパターン == "一緒に隠密中"
		IF 0
		ELSEIF !RAND:3 && パッチェさん_陥落段階(対象キャラ, "恋慕")
			KSTR:(K++) = 「……なにをする気？」
		ELSEIF !RAND:2
			KSTR:(K++) = 「なに、内緒話でもする気？」
		ELSE
			KSTR:(K++) = 「かくれんぼとか、子供っぽいことするのね」
		ENDIF
	;仕事中パターン
	ELSEIF テキストパターン == "仕事中"
		IF 0
		;ロビーで受付中
		ELSEIF 現在仕事:対象キャラ:1 == 2
			IF 0
			ELSEIF !RAND:3
				KSTR:(K++) = （_呼び名_はマニュアルを読んでいるようだ……）
			ELSEIF !RAND:2
				KSTR:(K++) = 「ええと……うまく接客する方法は……」
			;もう「本を」すら略している
			ELSE
				KSTR:(K++) = 「読みたいわ……」
			ENDIF
		;バーエリアでバーテンダー中
		ELSEIF 現在仕事:対象キャラ:1 == 503
			IF 0
			ELSEIF !RAND:3
				KSTR:(K++) = （_呼び名_はマニュアルを読んでいるようだ……）
			ELSEIF !RAND:2
				KSTR:(K++) = 「ええと……うまく接客する方法は……」
			;もう「本を」すら略している
			ELSE
				KSTR:(K++) = 「酒飲みは多いわね……」
			ENDIF
		;図書室で司書中
		ELSEIF 現在仕事:対象キャラ:1 == 506
			IF 0
			ELSEIF !RAND:3
				KSTR:(K++) = （_呼び名_は本の管理業務を行っているようだ……）
			;リファレンス。
			;大雑把に言うと「こういう本読みたいんだけど、ありますか？」「昔読んだ本が思い出せなくて……」とかのリクエストに応える業務。
			ELSEIF !RAND:2
				KSTR:(K++) = 「リファレンス業務はあまり得意ではないんだけど……」
			ELSE
				KSTR:(K++) = 「その本のある場所は……」
			ENDIF
		;プールで監視員中
		ELSEIF 現在仕事:対象キャラ:1 == 1503 || 現在仕事:対象キャラ:1 == 15
			IF 0
			ELSEIF !RAND:3
				KSTR:(K++) = （_呼び名_は周囲を監視している……）
			ELSEIF !RAND:2
				KSTR:(K++) = 「ええと、溺れているひとを助ける方法は……」
			ELSE
				KSTR:(K++) = 「……とける……」
			ENDIF
		;各種店舗で店番中
		ELSEIF 現在仕事:対象キャラ:1 > 10000
			CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:(現在仕事:対象キャラ:1 - 10000):0}(現在仕事:対象キャラ:1 - 10000)
			IF 0
			ELSEIF REPLACE(TSTR:場所名受渡, "『.*』", "") == "道具屋"
				IF 0
				;活字中毒者は値段すら読み出す
				ELSEIF !RAND:3
					KSTR:(K++) = （_呼び名_は値札を読んでいるようだ……）
				ELSEIF !RAND:2
					KSTR:(K++) = 「ええと……うまく接客する方法は……」
				;もう「本を」すら略している
				ELSE
					KSTR:(K++) = 「読みたいわ……」
				ENDIF
			ELSEIF REPLACE(TSTR:場所名受渡, "『.*』", "") == "土産物屋"
				IF 0
				;活字中毒者は値段すら読み出す
				ELSEIF !RAND:3
					KSTR:(K++) = （_呼び名_は値札を読んでいるようだ……）
				ELSEIF !RAND:2
					KSTR:(K++) = 「ええと……上手なお土産を選ぶ人へアドバイスする方法は……」
				;もう「本を」すら略している
				ELSE
					KSTR:(K++) = 「読みたいわ……」
				ENDIF
			ELSEIF REPLACE(TSTR:場所名受渡, "『.*』", "") == "食事処"
				IF 0
				;活字中毒者はメニューすら読み出す
				ELSEIF !RAND:3
					KSTR:(K++) = （_呼び名_は食事のメニューを読んでいるようだ……）
				ELSEIF !RAND:2
					KSTR:(K++) = 「ええと……うまく接客する方法は……」
				;もう「本を」すら略している
				ELSE
					KSTR:(K++) = 「読みたいわ……」
				ENDIF
			ELSEIF REPLACE(TSTR:場所名受渡, "『.*』", "") == "装飾品店"
				IF 0
				ELSEIF !RAND:3
					KSTR:(K++) = （_呼び名_はマニュアルを読んでいるようだ……）
				ELSEIF !RAND:2
					KSTR:(K++) = 「ええと……うまく接客する方法は……」
				;もう「本を」すら略している
				ELSE
					KSTR:(K++) = 「読みたいわ……」
				ENDIF
			ENDIF
		ENDIF
	;あなた隠密中
	ELSEIF テキストパターン == "あなた隠密中"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = （_呼び名_は本を読んでいる……）
		ELSEIF !RAND:2
			KSTR:(K++) = （_呼び名_は本を閉じて、うんうんと頷いている……）
		ELSE
			KSTR:(K++) = （_呼び名_はリゾートを_呼び名_なりに楽しんでいるようだ……）
		ENDIF
	;野外オナニー中
	ELSEIF テキストパターン == "野外オナニー中"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = （_呼び名_は服の裾を噛み、嬌声を我慢しているようだ……）
		ELSEIF !RAND:2
			KSTR:(K++) = （_呼び名_は雌汁をまき散らしている……）
		ELSE
			KSTR:(K++) = （_呼び名_はリゾートを_呼び名_なりに楽しんでいるようだ……_H_）
		ENDIF
	;睡眠中
	ELSEIF テキストパターン == "睡眠中"
		IF 0
		ELSEIF !RAND:3
			KSTR:(K++) = （_呼び名_はすやすやと寝息を立てている……）
		ELSEIF !RAND:2
			KSTR:(K++) = 「むきゅ……ぅうん……」
		ELSE
			KSTR:(K++) = 「すゃ……すや……」
		ENDIF
	ENDIF




	;この時点で口上が入ってるなら表示へ飛ばす
	;……というテンプレなのだが、この口上では別システムにしたので無意味。ここでコメントアウトしておきます。
;	SIF KSTR:0 != ""
;		GOTO 表示位置
;
;	$表示位置
	CALL PRINT_STATEコメント表示(対象キャラ, 2)



;--------------------------------------------------
;ステータス画面一言口上_友情
;--------------------------------------------------
@口上_ステータス画面_友情コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 要素Lv

	CALL 口上変数初期化_1147

	;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨

	要素Lv = CFLAG:対象キャラ:友情度
	IF 0
	;初対面
	ELSEIF 要素Lv == 0
		KSTR:(K++) = 「誰……？」
	;他人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:1)
		KSTR:(K++) = 「このリゾートのオーナーね。……それがなに？」
	;顔見知り
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:2)
		KSTR:(K++) = 「このリゾート、空気がいいわ」
	;友人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:3)
		KSTR:(K++) = 「図書室の充実のために、仲良くしておくにこしたことはないわね」
	;親友
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:4)
		KSTR:(K++) = 「レミィのことは知ってる？　仲良くなってくれると嬉しいわ」
	;無二の友
	ELSE
		KSTR:(K++) = 「_二人称_のことは、そうね……友人……と、思っているわ」
	ENDIF

	CALL コメント文頭処理(対象キャラ)
	CALL 口上変数表示(対象キャラ, 1)

;--------------------------------------------------
;ステータス画面一言口上_信頼
;--------------------------------------------------
@口上_ステータス画面_信頼コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 要素Lv

	CALL 口上変数初期化_1147

	;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨

	要素Lv = CFLAG:対象キャラ:信頼度
	IF 0
	;初対面
	ELSEIF 要素Lv == 0
		KSTR:(K++) = 「えっ誰……？」
	;他人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:1)
		KSTR:(K++) = 「このリゾートのオーナーね。……それがなに？」
	;顔見知り
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:2)
		KSTR:(K++) = 「このリゾート、蔵書がいいわね」
	;知己
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:3)
		KSTR:(K++) = 「なにかあったら、力を貸してあげる……くらいは、やぶさかじゃないわ」
	;相棒
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:4)
		KSTR:(K++) = 「こちらで、_一人称_の知識がどれだけ役に立つかは分からないけど……」
	;水魚の交わり
	;「これ面白いよ」って勧めてくれるのが悉くアタリな友人、マジで得難い。
	ELSE
		KSTR:(K++) = 「_二人称_の本を選ぶセンスは、信頼しているわ」
	ENDIF

	CALL PRINT_STATEコメント表示(対象キャラ)

;--------------------------------------------------
;ステータス画面一言口上_恋心
;--------------------------------------------------
@口上_ステータス画面_恋心コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 要素Lv

	CALL 口上変数初期化_1147

	;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨

	要素Lv = CFLAG:対象キャラ:恋心度
	IF 0
	;初対面
	ELSEIF 要素Lv == 0
		KSTR:(K++) = 「だから誰……？」
	;他人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:1)
		KSTR:(K++) = 「恋？　愛？　そういう冗談は嫌いね」
	;顔見知り
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:2)
		KSTR:(K++) = 「嫌いではないけど……そういう感情はないわ」
	;気になる人
	;「嫌」なのは、そういうことを感じる自分
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:3)
		KSTR:(K++) = 「近くにいると集中力が乱れがちで、……ちょっと嫌ね」
	;思い人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:4)
		KSTR:(K++) = 「……この感情がなんなのか、分からないのは……初めてよ」
	;最愛の人
	ELSE
		KSTR:(K++) = 「……あまり_一人称_の心臓に、負担をかけないでくれるかしら。病弱なの、これでも」
	ENDIF

	CALL PRINT_STATEコメント表示(対象キャラ)

;--------------------------------------------------
;ステータス画面一言口上_性愛
;--------------------------------------------------
@口上_ステータス画面_性愛コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 要素Lv

	CALL 口上変数初期化_1147

	;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨

	要素Lv = CFLAG:対象キャラ:性愛度
	IF 0
	;初対面
	ELSEIF 要素Lv == 0
		KSTR:(K++) = 「ほんと誰……？」
	;他人
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:1)
		KSTR:(K++) = 「性欲？　特に無いけど……そんなこと、聞く？」
	;顔見知り、またはうふふ経験なし
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:2) || !初体験日:対象キャラ:初うふふ
		KSTR:(K++) = 「性欲解消に他人の手とか……嫌だわ」
	;性癖に合う
	;全身敏感、自分でもまあ分からんよなぁ……
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:3)
		KSTR:(K++) = 「こんなに情けない身体だったなんて……_一人称_も知らなかったわ……はぁ……」
	;オナニー対象
	ELSEIF 要素Lv <= 経験値テーブル:(関係閾値:4)
		KSTR:(K++) = 「_二人称_に……ひどいことをされる妄想でないと、イけなくなったわ……はぁ……」
	;肉欲の虜
	ELSE
		IF 0
		ELSEIF パッチェさん_マゾメス判定(対象キャラ, "弱")
			KSTR:(K++) = 「_一人称_を_二人称_専用の雌にした責任は……絶対に取ってもらうわ」
		ELSE
			KSTR:(K++) = 「うう……_H_　負けないっ……_H_　セックスでバカになんてならないぃっ……_H_」
		ENDIF
	ENDIF

	CALL PRINT_STATEコメント表示(対象キャラ, 1)

;--------------------------------------------------
;ステータス画面一言口上_固有要素
;--------------------------------------------------
@口上_ステータス画面_キャラ固有好感度要素コメント_1147(対象キャラ)
#DIM 対象キャラ
	CALL 口上変数初期化_1147

	;固有好感度は「？？？/精液収集」
	;「？？？/魔力供給者/精液供給者/協力者/協力者/関係者」になる
	;/
	;まだ実装してないのでコメントアウトな。

;	SELECTCASE CFLAG:対象キャラ:キャラ固有好感度
;		CASE IS == 0
;			;？？？
;			KSTR:(K++) = 「……むきゅー」
;		CASE IS <= 経験値テーブル:(関係閾値:1)
;			;魔力供給者
;			KSTR:(K++) = 「中々の魔力が含まれているわね……」
;		CASE IS <= 経験値テーブル:(関係閾値:2)
;			;精液供給者
;			KSTR:(K++) = 「」
;		CASE IS <= 経験値テーブル:(関係閾値:3)
;			;協力者
;			KSTR:(K++) = 
;		CASE IS <= 経験値テーブル:(関係閾値:4)
;			;協力者
;			KSTR:(K++) = 
;		CASEELSE
;			;関係者（はあと）
;			KSTR:(K++) = 「こ、これからも……協力してくれると助かるわ……」
;	ENDSELECT

	CALL PRINT_STATEコメント表示(対象キャラ, 1)

;--------------------------------------------------
;ステータス画面一言口上_恋慕
;--------------------------------------------------
@口上_ステータス画面_恋慕コメント_1147(対象キャラ)
#DIM 対象キャラ
#DIM 要素Lv

	CALL 口上変数初期化_1147

	要素Lv = CFLAG:対象キャラ:恋慕レベル

	;知り合い以上ではあるはず……
	IF 0
	;脈ナシ
	ELSEIF 要素Lv == 0
		KSTR:(K++) = 「_二人称_と？　ないわね。ないわ」
	;ちょっと気になる
	ELSEIF 要素Lv <= 関係閾値:1
		KSTR:(K++) = 「う～ん……悪い人間では、ないんじゃないかしら」
	;なんかいいかも
	ELSEIF 要素Lv <= 関係閾値:2
		KSTR:(K++) = 「嫌いではないと思うけど……愛とか恋とか、よくわからないわ」
	;好意を自覚
	ELSEIF 要素Lv <= 関係閾値:3
		KSTR:(K++) = 「分からない感情を、分からないまま楽しむなんて……異常よ」
	;ラブラブ
	;急じゃなきゃ……いいんだよ！　と言っている
	ELSEIF 要素Lv <= 関係閾値:4
		KSTR:(K++) = 「急には抱き着かないで。心臓に悪いわ」
	;最愛の人
	;「いまわの際に言ってやる！」……ではないが。まあ、結婚式とかやれるならその時には言うんじゃない？
	ELSE
		KSTR:(K++) = 「『愛してる』って言ってほしい？　……言うべき時には言うわ」
	ENDIF

	CALL PRINT_STATEコメント表示(対象キャラ, 1)


;--------------------------------------------------
;キャラについての解説
;--------------------------------------------------
;EXキャラの出典を解説したり、キャラの設定・背景を解説するもの
;プロフ欄の形式はマギサ口上をお借りしました
;システムについてプロメティア口上作者さんと相談したりも。
;--------------------------------------------------
@口上_ステータス画面_キャラ解説_1147(対象キャラ)
#DIM 対象キャラ

	;tohoK系列のステ表示や、リゾートプロメティア口上を参考にしつつ増補
	;なお、「他のキャラにキスを捧げた場合」とかも、「オーナーとキスした場合」とカウントしちゃうかもしれない。
	;そのあたりの対応はさすがに厳しいので、すべてオーナーにキスしたものとしてカウントする。
	;/
	;「Ｂ感度がMAXなのに、パイズリを全然させてないのでLvが１のまま」みたいなのはちょっとイヤなので、その対策をする
	;長くなりすぎるので、キャラ履歴に書いてあることは書かないほうがいいかなー……
	;具体的には、下記の事項
	;・初めてキスをした、ディープキスをした、処女を捧げた、膣内射精された、などの経験部分
	;・弱点を暴かれたとかの部分（変更の可能性がないでもないので）
	;・恋慕を得た、信頼度がMAXになった、などの部分（履歴にあるし、信頼度とかは通常能力の画面で見られる）
	;ただし「スケベになるならその限りではない」
	;/
	;ってことでスタートしていくよーん
	;/
	;関係：恋慕周り
	;肉体：マゾっけなど
	;口　：キス、口淫/フェラ、精飲経験あたり
	;Ｃ　：C感度、クリトリス、ふたなり、あとは口上独自要素で「潮吹き」とかもカウントできると楽しそうだねえ。
	;Ｖ　：V感度、セックス関係
	;子宮：ポルチオ、出産
	;Ｂ　：B感度、パイズリ、乳房の成長（魔乳化したか）
	;乳首：没、Bに持っていかれちゃった
	;Ａ　：A感度
	;Ｓ　：S感度
	;精神：没。関係と淫蕩のほうに持ってかれちゃった
	;淫蕩：マゾ性癖、マゾメス度
	;
	;ちょっとマジで長くなりすぎたので、それぞれの項目の中身は分割し、別ファイルへ。

	;できればここも「2001」「2002」などを直で指定するのではなく、定数や変数でやりたいけど、ちょっとやり方わがんねがった。
	;順番はここで入れ替えることもできるが数値のほうは変えないこと
	NSTR:(K++) = ■
	NSTR:(K++) = --<button value = '2001'>[ プロフィール ]</button>
	NSTR:(K++) = --<button value = '2002'>[ 関係 ]</button>
	NSTR:(K++) = --<button value = '2009'>[ 肉体 ]</button>
	NSTR:(K++) = --<button value = '2010'>[ 淫蕩 ]</button>
	;ここで１行終了
	KSTR:(K++) = --

	NSTR:(K++) = --<button value = '2003'>[ 口腔 ]</button>
	NSTR:(K++) = --<button value = '2004'>[ 陰核 ]</button>
	NSTR:(K++) = --<button value = '2005'>[ 膣内 ]</button>
	NSTR:(K++) = --<button value = '2006'>[ 子宮 ]</button>
	NSTR:(K++) = --<button value = '2007'>[ 乳房 ]</button>
	NSTR:(K++) = --<button value = '2008'>[ 肛門 ]</button>

	;ここで１行終了
	KSTR:(K++) = --
	;改行を１行入れて間を開ける
	KSTR:(K++) = 　

	;★見栄えあんまりよくないのと、淫蕩の項目が妙に長くなっちゃったので、そこ改変したい

	;固有INPUTで入れたINPUT数値に応じて表示を変更する

	IF 0
		;なお、ここから出てくる「パチュリー_キャラ解説_プロフ」とかは、ERHで定義した定数。INPUT数値と対応させている。
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_プロフ
		$STATUS_1147_ぷろふ
		KSTR:(K++) = _B_【動かない大図書館】パチュリー_
		KSTR:(K++) = 　
		KSTR:(K++) = 能力　　　：魔法を操る程度の能力
		KSTR:(K++) = 趣味　　　：読書、曜日に合わせた魔法を使う
		KSTR:(K++) = 危険度　　：中
		KSTR:(K++) = 人間友好度：普通
		KSTR:(K++) = テーマ曲　：『ラクトガール ～ 少女密室』、『ヴワル魔法図書館』
		KSTR:(K++) = 　
		KSTR:(K++) = 　_I_不老長寿の、種族：魔法使い。_
		KSTR:(K++) = 　_I_少女の外見であるが、既に100歳を超えている。_
		KSTR:(K++) = 　_I_本好きの読書マニアで知識フリーク。病弱で喘息持ちで体力は皆無。_
		KSTR:(K++) = 　_I_幻想郷にある吸血鬼の館、紅魔館に居候しており、主であるレミリアたちとの関係は良好。_
	;関係性
	;以下の項目は別のファイルに飛ばすよ～ん
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_関係
		CALL パッチェさん_経験表示詳細_関係性(対象キャラ)
	;肉体
	;S感覚まわり。
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_肉体
		CALL パッチェさん_経験表示詳細_肉体(対象キャラ)
	;口腔
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_口腔
		CALL パッチェさん_経験表示詳細_口腔(対象キャラ)
	;陰核
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_陰核
		CALL パッチェさん_経験表示詳細_陰核(対象キャラ)
	;膣内
	;こちらは主にセックス関連など。
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_膣内
		CALL パッチェさん_経験表示詳細_膣内(対象キャラ)
	;子宮
	;こちらは子供関連など。
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_子宮
		CALL パッチェさん_経験表示詳細_子宮(対象キャラ)
	;乳房/乳首
	;乳首は単独でネタにするのが厳しい……乳房と合体する形で実装な。
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_乳房
		CALL パッチェさん_経験表示詳細_乳房(対象キャラ)
	;肛門
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_肛門
		CALL パッチェさん_経験表示詳細_肛門(対象キャラ)
	;淫蕩
	ELSEIF 特定状況口上用フラグ:対象キャラ:13 == パチュリー_キャラ解説_淫蕩
		CALL パッチェさん_経験表示詳細_淫蕩(対象キャラ)
	;その他
	ELSE
		;ぷろふ表示に飛ばす
		GOTO STATUS_1147_ぷろふ
	ENDIF

;--------------------------------------------------
;キャラ解説画面固有INPUT
;--------------------------------------------------
;キャラ解説画面のボタン動作を入れる関数
;なお、ボタンを押した後は画面が再表示されるよ。
;--------------------------------------------------
@能力表示固有INPUT_キャラ解説_1147(対象キャラ, INPUT数値)
#DIM 対象キャラ
#DIM INPUT数値

	;キャラ固有フラグの13番にINPUT数値を保存
	特定状況口上用フラグ:対象キャラ:13 = INPUT数値

	RETURN 0


