;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――固有アビリティ
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●スキルに関するメモ
;--------------------------------------------------
;戦闘中の表示について：
;　アビリティ名の文字数は全角で12文字くらいまで。
;　固有アビに使える行は３行
;　１行に表示するアビリティは３つ
;　ただし「じゃあ最大９アビまで表示が崩れないのか？」というとちょっと違って
;　/
;　[100] 通常攻撃
;　[110] なまえのなが～いアビリティいち～～～[111] アビリティ２
;　[112] アビリティ３
;　[120] アビリティ４　[121] アビリティ５
;　（証アビ行）
;　/
;　……と、本来は110と同じ行に表示されるべき112が改行されて表示されちゃう場合がある。
;--------------------------------------------------

;--------------------------------------------------
;●仕様
;--------------------------------------------------
;　自身の属性に応じてアビリティが変化する。
;　デフォルトは水だが、耳飾りで変化した属性に従う。
;　※各属性モードにおける同名スペルカードは同性能
;　※ただし、発展レベルはアビ番号で異なる
;　/
;　※複合をすべて「日＆月符」って形にしてるのはアビリティ名に統一感を持たせるため
;　※原作にはない「上級」もあるが、これも統一のため
;　・火水風土の場合
;　　・火の場合：ホーミング霊夢パターン
;　　　アビ１：火符「アグニシャイン/アグニシャイン上級/アグニレイディアンス」
;　　　アビ１：土符「レイジィトリリトン/レイジィトリリトン上級/トリリトンシェイク」
;　　　アビ３：火＆土符「ラーヴァクロムレク」
;　　　アビ４：金＆水符「マーキュリポイズン」
;　　　アビ５：木＆火符「フォレストブレイズ」
;　　　奥義：賢者の石
;　　　　火単体攻撃、土単体速度デバフ、火土単体防御デバフ、水全体毒付与、火風全体火傷付与
;　　　　火土水風にアクセス可能、アビ４まで全体攻撃がない
;　　・水の場合：針霊夢パターン
;　　　アビ１：水符「プリンセスウンディネ/プリンセスウンディネ上級/ベリーインレイク」
;　　　アビ２：木符「シルフィホルン/シルフィホルン上級/グリーンストーム」
;　　　アビ３：水＆木符「ウォーターエルフ」
;　　　アビ４：金＆水符「マーキュリポイズン」
;　　　アビ５：土＆金符「エメラルドメガリス」
;　　　奥義：賢者の石
;　　　　水単体攻撃バリア、風全体、水風全体、水全体毒付与、土縦列防御無視
;　　　　火にアクセスできないけどまあ問題ないやろ……
;　　・風の場合：ミサイル魔理沙
;　　　アビ１：木符「シルフィホルン/シルフィホルン上級/グリーンストーム」
;　　　アビ２：火符「アグニシャイン/アグニシャイン上級/アグニレイディアンス」
;　　　アビ３：木＆火符「フォレストブレイズ」
;　　　アビ４：土＆金符「エメラルドメガリス」
;　　　アビ５：火＆土符「ラーヴァクロムレク」
;　　　奥義：賢者の石
;　　　　風全体、火単体、火風全体火傷付与、水風全体、土縦列防御無視、火土単体防御デバフ
;　　　　初手全体攻撃が偉いと思う。
;　　・土の場合：レザマリ
;　　　アビ１：土符「レイジィトリリトン/レイジィトリリトン上級/トリリトンシェイク」
;　　　アビ２：金符「メタルファティーグ/メタルファティーグ上級/シルバードラゴン」
;　　　アビ３：土＆金符「エメラルドメガリス」
;　　　アビ４：水＆木符「ウォーターエルフ」
;　　　アビ５：金＆水符「マーキュリポイズン」
;　　　奥義：賢者の石
;　　　　土単体速度デバフ、固定ダメージ、土縦列防御無視、水風全体、水全体毒付与
;　　　　土と水にしかアクセスできない。ちょっとキツそう。
;　・光闇の場合
;　　・光の場合：
;　　　アビ１：エブリアングルショット
;　　　アビ２：ファイブシーズン
;　　　アビ３：日＆木符「フォトシンセシス」
;　　　アビ４：日＆水符「ハイドロジェナスプロミネンス」
;　　　アビ５：日＆月符「ロイヤルダイアモンドリング」
;　　　奥義：日符「ロイヤルフレア」
;　　　　多属性多段、多属性横列、単体回復＆再生＆高揚＆速度デバフ、光水攻撃＆敵バフ消去＆デバフ延長、光闇攻撃＆光闇防御デバフ
;　　　　アビ２、４、５と消費が重い。めっちゃ重い。大振り。
;　　・闇の場合：
;　　　アビ１：エブリアングルショット
;　　　アビ２：グリモワール
;　　　アビ３：月＆木符「サテライトヒマワリ」
;　　　アビ４：月＆金符「サンシャインリフレクター」
;　　　アビ５：日＆月符「ロイヤルダイアモンドリング」
;　　　奥義：月符「サイレントセレナ」
;　　　　多属性多段、自己強化、全体回復、全体ダメカ＋カウンター付与、光闇攻撃＆光闇防御デバフ
;　　　　自己強化と光闇防御デバフ、さらに単体高威力奥技で、瞬間火力は６属性の中で一番高い。
;　/
;　原作での基本イメージは下記の通り。
;　　生命と目覚めの「木」、変化と動きの「火」、基礎と不動の「土」、実りと豊かさの「金」、静寂と浄化の「水」、
;　　能動と攻撃の「日」、受動と防御の「月」
;　アビリティはこのイメージに従う。
;　アビリティの基本性能は以下の通り。
;　　火：火属性　単体攻撃
;　　土：土属性　デバフ付与
;　　金：土属性　固定ダメージ、防御無視
;　　水：水属性　バフ付与（バリアなど）
;　　木：風属性　範囲攻撃
;　　日：光属性　消費が重く効果が高い　純威力型
;　　月：闇属性　消費が重く効果が高い　追加効果型
;--------------------------------------------------

;--------------------------------------------------
;〇アビリティ/奥技性能まとめ
;--------------------------------------------------
;★性能については変更の可能性あり
;　特に、威力と消費のバランスが……ちょっと……分からん！！！　消費は重めにしたい。
;　エイヤっと決めてる部分が多いので変更の可能性高し。
;★将来的に、アビリティを自由に選択できるようにする可能性がある
;　キコキコことエレメンタルハーベスターとか使えるようにしたいし……
;　今は間違っても使いやすいキャラじゃないからね！
;--------------------------------------------------
;文字数：１２３４５６７８９101112
;属性　　名称　　　　　　　　　　分類　　対象　攻撃回数　威力　消費　特殊効果
;　火　　アグニシャイン　　　　　攻撃　　単体　　　　１　 220　　20　なし
;　火　　アグニシャイン上級　　　攻撃　　単体　　　　１　 280　　25　なし
;　火　　アグニレイディアンス　　攻撃　　単体　　　　１　 340　　30　なし
;　水　　プリンセスウンディネ　　攻撃　　単体　　　　１　 140　　20　バリア付与（回復力 50％）
;　水　　プリンセスウンディネ上級攻撃　　単体　　　　１　 180　　30　バリア付与（回復力100％）
;　水　　ベリーインレイク　　　　攻撃　　単体　　　　１　 220　　50　バリア付与（回復力150％）
;　風　　シルフィホルン　　　　　攻撃　　全体　　　　１　 140　　20　なし
;　風　　シルフィホルン上級　　　攻撃　　全体　　　　１　 190　　30　なし
;　風　　グリーンストーム　　　　攻撃　　全体　　　　１　 250　　38　なし
;　土　　レイジィトリリトン　　　攻撃　　乱２　　　　２　 120　　15　速度デバフ20％
;　土　　レイジィトリリトン上級　攻撃　　乱２　　　　３　 120　　30　速度デバフ20％
;　土　　トリリトンシェイク　　　攻撃　　乱２　　　　４　 120　　40　速度デバフ20％
;　無　　メタルファティーグ　　　固定Ｄ　乱１　　１～７　 100　　15　なし
;　無　　メタルファティーグ上級　固定Ｄ　乱１　　３～７　 200　　30　なし
;　無　　シルバードラゴン　　　　固定Ｄ　乱１　　５～７　 300　　40　なし
;　全　　ｴﾌﾞﾘｱﾝｸﾞﾙｼｮｯﾄ　　　　　 攻撃　　乱１　　３～５　  60　　20　属性は全属性からランダム（自属性多め）
;　全　　ｴﾌﾞﾘｱﾝｸﾞﾙｼｮｯﾄ-Hard-　　 攻撃　　乱１　　３～５　  90　　30　属性は全属性からランダム（自属性多め）
;　全　　ｴﾌﾞﾘｱﾝｸﾞﾙｼｮｯﾄ-Luna-　　 攻撃　　乱１　　３～５　 120　　40　属性は全属性からランダム（自属性多め）
;　多　　ファイブシーズン　　　　攻撃　　横列　　　　５　  70　　70　属性は火水風土無、自身にバリア付与（回復力 80％）
;　多　　ファイブシーズン-Hard-　攻撃　　横列　　　　５　  90　　90　属性は火水風土無、自身にバリア付与（回復力100％）
;　多　　ファイブシーズン-Luna-　攻撃　　横列　　　　５　 110　 130　属性は火水風土無、自身にバリア付与（回復力120％）
;バフ　　グリモワール　　　　　　バフ　　自己　　　　--　----　　 5　行動消費なし　速度バフ（20％）
;バフ　　グリモワール上級　　　　バフ　　自己　　　　--　----　　20　行動消費なし　速度+防御バフ（20％）
;バフ　　大ヴワル魔導目録　　　　バフ　　自己　　　　--　----　　30　行動消費なし　速度+防御+攻撃バフ（20％）
;火土　　ラーヴァクロムレク　　　攻撃　　単体　　　　２　 125　　30　防御デバフ20％
;火土　　ラーヴァクロムレク-Hard-攻撃　　単体　　　　２　 150　　50　防御デバフ25％
;火土　　ラーヴァクロムレク-Luna-攻撃　　単体　　　　２　 200　　60　防御デバフ30％
;土無　　エメラルドメガリス　　　攻撃　　縦列　　　　１　 250　　30　防御無視 50％
;土無　　エメラルドメガリス-Hard-攻撃　　縦列　　　　１　 250　　40　防御無視 75％
;土無　　エメラルドメガリス-Luna-攻撃　　縦列　　　　１　 250　　50　防御無視100％
;無水　　マーキュリポイズン　　　攻撃　　横列　　　　１　 200　　45　毒付与、防御無視50％
;無水　　マーキュリポイズン-Hard-攻撃　　横列　　　　１　 240　　50　毒付与、防御無視50％
;無水　　マーキュリポイズン-Luna-攻撃　　横列　　　　１　 280　　60　毒付与、防御無視50％
;水風　　ウォーターエルフ　　　　攻撃　　全体　　　　２　 120　　30　味方に弱体無効１回
;水風　　ウォーターエルフ-Hard-　攻撃　　全体　　　　２　 150　　40　味方に弱体無効１回
;水風　　ウォーターエルフ-Luna-　攻撃　　全体　　　　２　 180　　50　味方に弱体無効１回
;風火　　ﾌｫﾚｽﾄﾌﾞﾚｲｽﾞ　　　　　　 攻撃　　乱３　　　　２　 120　　30　火傷付与
;風火　　ﾌｫﾚｽﾄﾌﾞﾚｲｽﾞ-Hard-　　　 攻撃　　乱３　　　　２　 150　　40　火傷付与
;風火　　ﾌｫﾚｽﾄﾌﾞﾚｲｽﾞ-Luna-　　　 攻撃　　乱３　　　　２　 180　　50　火傷付与
;回復　　フォトシンセシス　　　　回復　　味１　　　　１　  80　　20　回復力（ 80％）回復、再生、高揚付与、独自枠の速度デバフ（30％）強制付与
;回復　　フォトシンセシス-Hard-　回復　　味１　　　　１　 100　　25　回復力（100％）回復、再生、高揚付与、独自枠の速度デバフ（30％）強制付与
;回復　　フォトシンセシス-Luna-　回復　　味１　　　　１　 120　　30　回復力（120％）回復、再生、高揚付与、独自枠の速度デバフ（30％）強制付与
;光水　　ﾊｲﾄﾞﾛｼﾞｪﾅｽﾌﾟﾛﾐﾈﾝｽ　　　 攻撃　　全体　　　　２　 100　　80　敵バフ１つ解除、デバフ効果１ターン延長
;光水　　ﾊｲﾄﾞﾛｼﾞｪﾅｽﾌﾟﾛﾐﾈﾝｽ-Hard- 攻撃　　全体　　　　２　 130　 100　敵バフ２つ解除、デバフ効果１ターン延長
;光水　　ﾊｲﾄﾞﾛｼﾞｪﾅｽﾌﾟﾛﾐﾈﾝｽ-Luna- 攻撃　　全体　　　　２　 170　 130　敵バフ３つ解除、デバフ効果１ターン延長
;回復　　サテライトヒマワリ　　　バフ　　味全　　　　--　 ---　　50　全体回復（回復力25％）、弱体１つ解除
;回復　　サテライトヒマワリ-Hard-バフ　　味全　　　　--　 ---　　60　全体回復（回復力40％）、弱体２つ解除
;回復　　サテライトヒマワリ-Luna-バフ　　味全　　　　--　 ---　　70　全体回復（回復力55％）、弱体３つ解除
;バフ　　ｻﾝｼｬｲﾝﾘﾌﾚｸﾀｰ　　　　　　バフ　　味全　　　　--　 ---　　50　ダメカ20％＋カウンター付与（３回）
;バフ　　ｻﾝｼｬｲﾝﾘﾌﾚｸﾀｰ-Hard-　　　バフ　　味全　　　　--　 ---　　60　ダメカ20％＋カウンター付与（４回）
;バフ　　ｻﾝｼｬｲﾝﾘﾌﾚｸﾀｰ-Luna-　　　バフ　　味全　　　　--　 ---　　70　ダメカ20％＋カウンター付与（５回）
;光闇　　ﾛｲﾔﾙﾀﾞｲﾔﾓﾝﾄﾞﾘﾝｸﾞ　　　　攻撃　　全体　　　　２　 150　　80　味方に光闇属性攻撃強化　光闇耐性弱体付与
;光闇　　ﾛｲﾔﾙﾀﾞｲﾔﾓﾝﾄﾞﾘﾝｸﾞ-Hard-　攻撃　　全体　　　　２　 180　 100　味方に光闇属性攻撃強化　光闇耐性弱体付与
;光闇　　ﾛｲﾔﾙﾀﾞｲﾔﾓﾝﾄﾞﾘﾝｸﾞ-Luna-　攻撃　　全体　　　　２　 210　 130　味方に光闇属性攻撃強化　光闇耐性弱体付与
;バフ　　賢者の石　　　　　　　　攻撃　　自己　　　　--　----　奥技　自分に賢者の石バフを付与（賢者の石は追撃）
;　光　　ロイヤルフレア　　　　　攻撃　　全体　　　　１　 450　奥技　シンプル特大火力
;　闇　　サイレントセレナ　　　　攻撃　　単体　　　　１　 550　奥技　シンプル特大火力
;--------------------------------------------------

;--------------------------------------------------
;参考資料：組み合わせ一覧
;--------------------------------------------------
;　火土：ラーヴァクロムレク
;　　金：セントエルモピラー
;　　水：フロギスティックレイン、フロギスティックピラー
;　　木：フォレストブレイズ
;　土金：エメラルドメガリス、ジンジャガスト、エメラルドメガロポリス
;　　水：ノエキアンデリュージュ
;　　木：（この組み合わせだけ存在しない）
;　金水：マーキュリポイズン
;　　木：エレメンタルハーベスター
;　水木：ウォーターエルフ
;--------------------------------------------------

;--------------------------------------------------
;固有アビリティ１、２
;--------------------------------------------------
@固有アビ_1147_アビ１(呼び出し引数, キャラ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIMS 現在属性
#DIM アビ番号
#DIMS アビ名

	;アビリティのなにを取得しようとしているかを保存しておく
	;名称や効果など。
	;引数として受け取るときに変えたので不要な
;	呼び出し引数 = ARGS

	;現在属性取得
	;属性については関数へレッツゴー
	現在属性 = %パッチェさん固有_現在属性取得(キャラ番号)%

	;何番のアビリティかを持たせる
	アビ番号 = 1

	;おまじない：空にしておく
	;アビ名を空白にすると、未習得になる
	アビ名 = 

	IF 0
	ELSEIF 現在属性 == "火"
		CALL パッチェさん固有_アグニシャイン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "水"
		CALL パッチェさん固有_プリンセスウンディネ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "風"
		CALL パッチェさん固有_シルフィホルン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "土"
		CALL パッチェさん固有_レイジィトリリトン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_エブリアングルショット(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_エブリアングルショット(呼び出し引数, キャラ番号, アビ番号)
	ELSE
		;ここにはこないはずだよ
	ENDIF

	;通常はゼロが返る
	;ただし、呼び出し先で何らかの値（たとえば消費MP）を返してきた場合、それを返す
	RETURN RESULT


@固有アビ_1147_アビ２(呼び出し引数, キャラ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIMS 現在属性
#DIM アビ番号
#DIMS アビ名


	;現在属性取得
	現在属性 = %パッチェさん固有_現在属性取得(キャラ番号)%

	;何番のアビリティかを持たせる
	;アビ１とは違うのだよ、アビ１とは。
	アビ番号 = 2

	;おまじない：空にしておく
	;アビ名を空白にすると、未習得になる
	アビ名 = 

	IF 0
	ELSEIF 現在属性 == "火"
		CALL パッチェさん固有_レイジィトリリトン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "水"
		CALL パッチェさん固有_シルフィホルン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "風"
		CALL パッチェさん固有_アグニシャイン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "土"
		CALL パッチェさん固有_メタルファティーグ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_ファイブシーズン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_グリモワール(呼び出し引数, キャラ番号, アビ番号)
	ELSE
		;ここにはこないはずだよ
	ENDIF

	;通常はゼロが返る
	;ただし、呼び出し先で何らかの値（たとえば消費MP）を返してきた場合、それを返す
	RETURN RESULT


;--------------------------------------------------
;・アグニシャイン
;　火属性
;　単体対象　単発攻撃
;　汎用アビ、ファイアよりちょっと強い
;　ただし消費が若干重く、発展が遅い（ファイアはファイア＋＋になるのはLv20、こっちは書いてる時点ではLv30想定）
;--------------------------------------------------
@パッチェさん固有_アグニシャイン(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値

	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = アグニレイディアンス
		消費ＭＰ = 30
		威力値 = 340
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = アグニシャイン上級
		消費ＭＰ = 25
		威力値 = 280
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = アグニシャイン
		消費ＭＰ = 20
		威力値 = 220
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	;火符「アグニシャイン」……などと、宣言してくれる場合がある。
	CALL パッチェさん固有_スペカ宣言("火符", アビ名)

	;文字数に応じてオート変換する関数を通す
	;表示が全角12文字程度までで、それ以上に長い場合は半角にして返す
	;このタイミングで返すのは、宣言で「火符『ｱｸﾞﾆﾚｲﾃﾞｨｱﾝｽ』」って半角になるとカッコ悪いから
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に、火属性の［攻撃力✕{威力値}％］ダメージを与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_火属性
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・プリンセスウンディネ
;　水属性
;　単体対象　単発攻撃
;　攻撃後、味方全体にバリア付与
;　バリアっぽいのは「ジェリーインフィッシュ」だが。
;　汎用アビにバリアアビリティはない。う～ん……
;　威力は低いが、バリア側が発展する、とする。バリア値については調整……？
;--------------------------------------------------
@パッチェさん固有_プリンセスウンディネ(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM バリア値
#DIM 算出回復力

	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = ベリーインレイク
		消費ＭＰ = 50
		威力値 = 220
		バリア値 = 150
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = プリンセスウンディネ上級
		消費ＭＰ = 30
		威力値 = 180
		バリア値 = 100
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = プリンセスウンディネ
		消費ＭＰ = 20
		威力値 = 140
		バリア値 = 50
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("水符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に、水属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>また、味方全体にバリア効果[回復力✕{バリア値}％/破壊されるまで永続]"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;まず攻撃
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_水属性

			;バリア数値は回復力に依存
			CALL バフ込み数値算出("回復力")
			算出回復力 = RESULT

			;攻撃後、バリア発生
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = バリア
			アビ汎用文字列:バフ・デバフ対象能力 = バリア
			アビ汎用変数:効果量 = 算出回復力 * バリア値 / 100
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・シルフィホルン
;　風属性
;　全体対象　単発攻撃
;　単純な全体攻撃。
;　汎用スキル：ブロウより、消費が重くて効果が高い……というシンプル調整
;--------------------------------------------------
@パッチェさん固有_シルフィホルン(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値

	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = グリーンストーム
		消費ＭＰ = 38
		威力値 = 250
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = シルフィホルン上級
		消費ＭＰ = 30
		威力値 = 190
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = シルフィホルン
		消費ＭＰ = 20
		威力値 = 140
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("風符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵全体に、風属性の［攻撃力✕{威力値}％］ダメージを与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_風属性
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・レイジィトリリトン
;　土属性
;　ランダムターゲット（2体）　多段攻撃（4回）　デバフ付与
;　汎用アビ、アクアシュートが130*（２、３、４回）攻撃＝260.390.520
;　単発威力はちょっと低いが速度デバフ付き
;--------------------------------------------------
@パッチェさん固有_レイジィトリリトン(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 攻撃回数
#DIM デバフ威力
#DIM デバフ付与率

	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = トリリトンシェイク
		消費ＭＰ = 40
		攻撃回数 = 4
		威力値 = 120
		;攻撃回数が増えて付与率も上がるのはあまりよくないが……
		デバフ威力 = 30
		デバフ付与率 = 80
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = レイジィトリリトン上級
		消費ＭＰ = 30
		攻撃回数 = 3
		威力値 = 120
		デバフ威力 = 30
		デバフ付与率 = 60
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = レイジィトリリトン
		消費ＭＰ = 15
		攻撃回数 = 2
		威力値 = 120
		デバフ威力 = 20
		デバフ付与率 = 60
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("土符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>ランダムな敵2体に、土属性の［攻撃力✕{威力値}％］ダメージを{攻撃回数}回与える。"
			詳細文文字列受け渡し変数 += @"<br>対象の行動速度を3ターン［{デバフ威力}％/B枠］減少させる。基礎成功率{デバフ付与率}％。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			FOR LOCAL, 0, 攻撃回数
				;敵対心は無視「しない」と明示（ヘイトに影響される）
				アビ汎用変数:敵対心無視オプション = 0
				CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(2)

				;攻撃部分
				アビ汎用変数:効果割合 = 威力値
				CALL アビテンプレート_ダメージ処理_土属性

				;速度デバフ
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 行動速度B
				アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
				アビ汎用変数:効果割合 = デバフ威力
				アビ汎用変数:持続ターン = 3
				アビ汎用変数:基礎成功確率 = デバフ付与率
				CALL アビテンプレート_不利効果_デバフ効果セット
			NEXT
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・メタルファティーグ
;　無属性固定ダメージ
;　ランダムターゲット　多段攻撃（X-7回）
;　ガレヲンの降砂岩よりだいぶ弱いが、まあこんなもんだよな感。安いし。
;--------------------------------------------------
@パッチェさん固有_メタルファティーグ(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 効果回数
#DIM 効果回数_最小
#DIM 効果回数_最大

	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = シルバードラゴン
		消費ＭＰ = 40
		威力値 = 300
		効果回数_最小 = 5
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = メタルファティーグ上級
		消費ＭＰ = 30
		威力値 = 200
		効果回数_最小 = 3
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = メタルファティーグ
		消費ＭＰ = 15
		威力値 = 100
		効果回数_最小 = 1
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	効果回数_最大 = 7

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("金符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>ランダムな敵に、無属性［{威力値}］ダメージを{効果回数_最小}～{効果回数_最大}回与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;「効果回数」回、「威力値分の無属性固定ダメージを与える」
			効果回数 = RAND(効果回数_最小, 効果回数_最大 + 1)

			FOR LOCAL, 0, 効果回数
				;ヘイトは無視しない
				アビ汎用変数:敵対心無視オプション = 0
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				;固定ダメージを与える場合はこう
				アビ汎用変数:効果量 = 威力値
				CALL アビテンプレート_ダメージ処理_無属性
			NEXT
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・エブリアングルショット
;　ランダム属性（攻撃ごとに属性が変化する）
;　ランダムターゲット　多段攻撃（3～5回）
;　汎用アビ、アクアシュートが130*（２、３、４回）攻撃＝260.390.520
;　平均攻撃回数は４回として、割り算して、65、97.5、130……から、ちょびっと弱くする
;　60.90.120*4＝240.360.480
;　……まあまあクソスキルでは……？
;--------------------------------------------------
@パッチェさん固有_エブリアングルショット(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 効果回数
#DIM 効果回数_最小
#DIM 効果回数_最大

	;名称、他に習うなら「上級」だが……「特級」的な名称が原作にないんですよね
	;パワー段階を表して「P1,P2……」でもいいかなと思ったけど、P4まであるんだよねぇ……
	;なので「ハードシューター」「ルナシューター」って意味でHard,Lunaを付けてみんとす
	;文字数は全角12文字程度までなので、半角で表示。
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 3
		アビ名 = エブリアングルショット-Luna-
		消費ＭＰ = 40
		威力値 = 120
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 2
		アビ名 = エブリアングルショット-Hard-
		消費ＭＰ = 30
		威力値 = 90
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "壱符") >= 1
		アビ名 = エブリアングルショット
		消費ＭＰ = 20
		威力値 = 60
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	効果回数_最小 = 3
	効果回数_最大 = 5

	;選択時/実行時メッセージ
	;エブリアングルショットはスペカではないので符名がない。
	;なので「支援『エブリアングルショット』」になる。
	CALL パッチェさん固有_スペカ宣言("支援", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>ランダムな敵に、ランダムな属性の［攻撃力✕{威力値}％］ダメージを{効果回数_最小}～{効果回数_最大}回与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;「効果回数」回、「敵対心を無視して選択した敵ランダム１体に」「威力値のランダム属性ダメージを与える」
			効果回数 = RAND(効果回数_最小, 効果回数_最大 + 1)
			FOR LOCAL, 0, 効果回数
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 威力値
				;自属性は割合高め（まあつまり光か闇のどっちかだが）
				IF 0
				ELSEIF !RAND:7
					CALL アビテンプレート_ダメージ処理_自属性
				ELSEIF !RAND:6
					CALL アビテンプレート_ダメージ処理_火属性
				ELSEIF !RAND:5
					CALL アビテンプレート_ダメージ処理_水属性
				ELSEIF !RAND:4
					CALL アビテンプレート_ダメージ処理_土属性
				ELSEIF !RAND:3
					CALL アビテンプレート_ダメージ処理_風属性
				ELSEIF !RAND:2
					CALL アビテンプレート_ダメージ処理_光属性
				ELSE
					CALL アビテンプレート_ダメージ処理_闇属性
				ENDIF
			NEXT
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・ファイブシーズン
;　火水風土無属性攻撃
;　列対象　５回攻撃
;　地霊殿におけるボムのひとつ。原作では「自分の周囲に弾消し＋ダメージフィールド×５」という設定
;　霊撃なので消費が重い。
;　アニラのアビ１のLv80状態より弱いが、あれはアニラが強すぎる案件だろ……
;--------------------------------------------------
@パッチェさん固有_ファイブシーズン(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 算出回復力
#DIM バリア値

	;名称、他に習うなら「上級」だが……「特級」的な名称が原作にないんですよね
	;エブリアングルショットと同じ扱いにする。
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = ファイブシーズン-Luna-
		消費ＭＰ = 130
		威力値 = 110
		バリア値 = 120
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = ファイブシーズン-Hard-
		消費ＭＰ = 100
		威力値 = 90
		バリア値 = 100
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = ファイブシーズン
		消費ＭＰ = 80
		威力値 = 70
		バリア値 = 80
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	;ファイブシーズンはスペカではないので符名がない。
	;ただしおそらくはスペカなので、ここはオリジナルで「季符」とする。
	CALL パッチェさん固有_スペカ宣言("季符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵横列に、火水風土無属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>また、自身にバリア効果[回復力✕{バリア値}％/破壊されるまで永続]"

			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵横列
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビテンプレート_ダメージ処理_無属性

			;バリア数値は回復力に依存
			CALL バフ込み数値算出("回復力")
			算出回復力 = RESULT

			;攻撃後、バリア発生
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = バリア
			アビ汎用文字列:バフ・デバフ対象能力 = バリア
			アビ汎用変数:効果量 = 算出回復力 * バリア値 / 100
			CALL アビテンプレート_有利効果_バフ効果セット

	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・グリモワール
;　自己バフ
;　自己対象　行動を消費しない
;　　グリモワール　　　：消費5、速度強化
;　　グリモワール上級　：消費20、速度強化＋防御強化
;　　大ヴワル魔導目録　：消費30、速度強化＋防御強化＋攻撃強化
;　自己強化。緋想天・天則における自己強化カード。
;　原作での効果は「霊力（弾幕攻撃、ガード、移動用リソース）の自然回復速度が上昇」。
;　MP回復……はリゾートだと強すぎるため、ステータス強化。
;　速度強化＋防御強化＋攻撃強化とする。効果量は変わらず。
;　ただ、パッチェさんは速度と防御がビックリするほど低いので、速度防御強化はたぶん焼け石に水。
;　攻撃強化まで行かないと微妙。行ってもまあ微妙な感はある。
;　★調整案：４回まで累積可能な自己強化？
;--------------------------------------------------
@パッチェさん固有_グリモワール(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 速度効果値
#DIM 攻撃効果値
#DIM 防御効果値

	;ヴワル魔法図書館要素が欲しいので、３段階目はロストワードより「大ヴワル魔導目録」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 3
		アビ名 = 大ヴワル魔導目録
		消費ＭＰ = 30
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 2
		アビ名 = グリモワール上級
		消費ＭＰ = 20
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 1
		アビ名 = グリモワール
		消費ＭＰ = 5
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	;グリモワールはスペカではないので符名がない。
	;……「読書『グリモワール上級』」！　まあネタだな！
	CALL パッチェさん固有_スペカ宣言("読書", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	速度効果値 = 20
	防御効果値 = 20
	攻撃効果値 = 20

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>自身の速度を３ターン[{速度効果値}％/A枠]上昇させる。"
			SIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 2
				詳細文文字列受け渡し変数 += @"<br>自身の防御力を３ターン[{防御効果値}％/A枠]上昇させる。"
			SIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 3
				詳細文文字列受け渡し変数 += @"<br>自身の攻撃力を３ターン[{攻撃効果値}％/A枠]上昇させる。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティは行動回数を消費しない。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;行動を消費しない。
			アビテンプレート用_行動消費無しフラグ = 1
			;対象は自分だけ
			CALL アビ対象選択テンプレート_自己のみ

			;速度強化
			アビ汎用文字列:バフ・デバフ枠 = 行動速度A
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 速度効果値
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

			;防御強化
			IF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 2
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 防御力A
				アビ汎用文字列:バフ・デバフ対象能力 = 防御力
				アビ汎用変数:効果割合 = 防御効果値
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF

			;攻撃強化
			IF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "システムカード") >= 3
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果割合 = 攻撃効果値
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;固有アビリティ３、４、５
;--------------------------------------------------
@固有アビ_1147_アビ３(呼び出し引数, キャラ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIMS 現在属性
#DIM アビ番号
#DIMS アビ名

	;現在属性取得
	現在属性 = %パッチェさん固有_現在属性取得(キャラ番号)%

	;何番のアビリティかを持たせる
	アビ番号 = 3

	;おまじない：空にしておく
	;アビ名を空白にすると、未習得になる
	アビ名 = 

	IF 0
	ELSEIF 現在属性 == "火"
		CALL パッチェさん固有_ラーヴァクロムレク(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "水"
		CALL パッチェさん固有_ウォーターエルフ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "風"
		CALL パッチェさん固有_フォレストブレイズ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "土"
		CALL パッチェさん固有_エメラルドメガリス(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_フォトシンセシス(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_サテライトヒマワリ(呼び出し引数, キャラ番号, アビ番号)
	ELSE
		;ここにはこないはずだよ
	ENDIF

	;通常はゼロが返る
	;ただし、呼び出し先で何らかの値（たとえば消費MP）を返してきた場合、それを返す
	RETURN RESULT

@固有アビ_1147_アビ４(呼び出し引数, キャラ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIMS 現在属性
#DIM アビ番号
#DIMS アビ名

	;現在属性取得
	現在属性 = %パッチェさん固有_現在属性取得(キャラ番号)%

	;何番のアビリティかを持たせる
	アビ番号 = 4

	;おまじない：空にしておく
	;アビ名を空白にすると、未習得になる
	アビ名 = 

	IF 0
	ELSEIF 現在属性 == "火"
		CALL パッチェさん固有_マーキュリポイズン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "水"
		CALL パッチェさん固有_マーキュリポイズン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "風"
		CALL パッチェさん固有_エメラルドメガリス(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "土"
		CALL パッチェさん固有_ウォーターエルフ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_ハイドロジェナスプロミネンス(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_サンシャインリフレクター(呼び出し引数, キャラ番号, アビ番号)
	ELSE
		;ここにはこないはずだよ
	ENDIF

	;通常はゼロが返る
	;ただし、呼び出し先で何らかの値（たとえば消費MP）を返してきた場合、それを返す
	RETURN RESULT

@固有アビ_1147_アビ５(呼び出し引数, キャラ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIMS 現在属性
#DIM アビ番号
#DIMS アビ名

	;現在属性取得
	現在属性 = %パッチェさん固有_現在属性取得(キャラ番号)%

	;何番のアビリティかを持たせる
	アビ番号 = 5

	;おまじない：空にしておく
	;アビ名を空白にすると、未習得になる
	アビ名 = 

	IF 0
	ELSEIF 現在属性 == "火"
		CALL パッチェさん固有_フォレストブレイズ(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "水"
		CALL パッチェさん固有_エメラルドメガリス(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "風"
		CALL パッチェさん固有_ラーヴァクロムレク(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "土"
		CALL パッチェさん固有_マーキュリポイズン(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_ロイヤルダイヤモンドリング(呼び出し引数, キャラ番号, アビ番号)
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_ロイヤルダイヤモンドリング(呼び出し引数, キャラ番号, アビ番号)
	ELSE
		;ここにはこないはずだよ
	ENDIF

	;通常はゼロが返る
	;ただし、呼び出し先で何らかの値（たとえば消費MP）を返してきた場合、それを返す
	RETURN RESULT


;--------------------------------------------------
;・ラーヴァクロムレク
;　火+土属性攻撃
;　単体対象　デバフ付与。
;　防御デバフをかけてから殴るのが特徴。防御デバフなのは「溶岩や泥のように溶かしてしまう」イメージから。
;--------------------------------------------------
@パッチェさん固有_ラーヴァクロムレク(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM デバフ威力
#DIM デバフ付与率

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = ラーヴァクロムレク-Luna-
		消費ＭＰ = 60
		威力値 = 200
		デバフ威力 = 30
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = ラーヴァクロムレク-Hard-
		消費ＭＰ = 50
		威力値 = 150
		デバフ威力 = 25
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = ラーヴァクロムレク
		消費ＭＰ = 30
		威力値 = 125
		デバフ威力 = 20
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("火＆土符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	デバフ付与率 = 80

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体の防御力を3ターン［{デバフ威力}％/B枠］減少させる。基礎成功率{デバフ付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>その後、対象に、火属性と土属性の［攻撃力✕{威力値}％］ダメージを与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;対象を取り、防御デバフ
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用文字列:バフ・デバフ枠 = 防御力B
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = デバフ威力
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = デバフ付与率
			CALL アビテンプレート_不利効果_デバフ効果セット

			;攻撃部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビテンプレート_ダメージ処理_土属性
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・マーキュリポイズン
;　水属性攻撃
;　横列対象　毒付与。防御力を50％無視する。
;　防御無視は金の属性部分。
;--------------------------------------------------
@パッチェさん固有_マーキュリポイズン(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 防御力無視割合
#DIM 毒威力
#DIM 毒付与率
#DIM 効果ターン

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = マーキュリポイズン-Luna-
		消費ＭＰ = 60
		威力値 = 280
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = マーキュリポイズン-Hard-
		消費ＭＰ = 50
		威力値 = 240
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = マーキュリポイズン
		消費ＭＰ = 45
		威力値 = 200
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("金＆水符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	防御力無視割合 = 50
	毒威力 = 80
	毒付与率 = 50
	効果ターン = 3

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵横列に水属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティは相手の防御力を{防御力無視割合}％無視する。"
			詳細文文字列受け渡し変数 += @"<br>その後、{効果ターン}ターン毒［攻撃力✕{毒威力}％］効果を与える。基礎成功率{毒付与率}％。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"

			;対象を取り、攻撃
			CALL アビ対象選択テンプレート_敵横列
			アビ汎用変数:防御無視オプション_割合 = 防御力無視割合
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_水属性

			;毒付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 毒
			アビ汎用文字列:バフ・デバフ対象能力 = 毒
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 毒威力 / 100
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 毒付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
	
	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・フォレストブレイズ
;　火＆風属性攻撃
;　ランダム３体対象　２回攻撃。
;　火傷付与。
;--------------------------------------------------
@パッチェさん固有_フォレストブレイズ(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 防御力無視割合
#DIM 火傷威力
#DIM 火傷付与率
#DIM 効果ターン

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = フォレストブレイズ-Luna-
		消費ＭＰ = 50
		威力値 = 180
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = フォレストブレイズ-Hard-
		消費ＭＰ = 40
		威力値 = 150
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = フォレストブレイズ
		消費ＭＰ = 30
		威力値 = 120
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("木＆火符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	火傷威力 = 100
	火傷付与率 = 80
	効果ターン = 3

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵横列に風属性と火属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>その後、{効果ターン}ターン火傷［回復力✕{火傷威力}％］効果を与える。基礎成功率{火傷付与率}％。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;対象を取り、攻撃
			アビ汎用変数:効果割合 = 威力値
			;２回ともランダムなので、対象は２回取る
			;原作の弾幕演出から、風（木の葉部分）が先
			CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(3)
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(3)
			CALL アビテンプレート_ダメージ処理_火属性

			;火傷付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 火傷威力 / 100
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 火傷付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・ウォーターエルフ
;　水＆風属性攻撃
;　全体対象　２回攻撃。
;　味方全体に弱体無効１回付与。エルフの守り的な……なにか……
;--------------------------------------------------
@パッチェさん固有_ウォーターエルフ(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 弱体無効回数

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = ウォーターエルフ-Luna-
		消費ＭＰ = 50
		威力値 = 180
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = ウォーターエルフ-Hard-
		消費ＭＰ = 40
		威力値 = 150
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = ウォーターエルフ
		消費ＭＰ = 30
		威力値 = 120
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("水＆木符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	弱体無効回数 = 1

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵全体に水属性と風属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>その後、味方全体に{弱体無効回数}回の弱体無効効果を与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;対象を取り、攻撃
			アビ汎用変数:効果割合 = 威力値
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビテンプレート_ダメージ処理_風属性

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 弱体無効
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
			アビ汎用変数:持続回数 = 弱体無効回数
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・エメラルドメガリス
;　土属性攻撃
;　縦列対象　防御力無視。
;--------------------------------------------------
@パッチェさん固有_エメラルドメガリス(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 防御力無視割合

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = エメラルドメガリス-Luna-
		消費ＭＰ = 50
		威力値 = 250
		防御力無視割合 = 100
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = エメラルドメガリス-Hard-
		消費ＭＰ = 40
		威力値 = 250
		防御力無視割合 = 75
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = エメラルドメガリス
		消費ＭＰ = 30
		威力値 = 250
		防御力無視割合 = 50
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("土＆金符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵縦列に土属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティは相手の防御力を{防御力無視割合}％無視する。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;対象を取り、攻撃
			CALL アビ対象選択テンプレート_敵縦列
			アビ汎用変数:効果割合 = 威力値
			アビ汎用変数:防御無視オプション_割合 = 防御力無視割合
			CALL アビテンプレート_ダメージ処理_土属性

	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・フォトシンセシス
;　回復
;　単体対象　再生、高揚付与　ついでに、1000％の付与率で速度デバフも付与。
;　消去不可なのは、非想天則でも発生保障あって消えないから。
;--------------------------------------------------
@パッチェさん固有_フォトシンセシス(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM 再生威力
#DIM 高揚威力
#DIM 速度デバフ威力
#DIM 速度デバフ付与率
#DIM バフターン数

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = フォトシンセシス-Luna-
		消費ＭＰ = 30
		威力値 = 120
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = フォトシンセシス-Hard-
		消費ＭＰ = 25
		威力値 = 100
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = フォトシンセシス
		消費ＭＰ = 20
		威力値 = 80
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	;本当は『日木符』だけど、統一性のため『日＆木符』。
	CALL パッチェさん固有_スペカ宣言("日＆木符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	再生威力 = 30
	高揚威力 = 15
	速度デバフ威力 = 30
	速度デバフ付与率 = 1000
	バフターン数 = 3

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>味方単体を対象とし、ＨＰを［回復力✕{威力値}％］回復させる。"
			詳細文文字列受け渡し変数 += @"<br>また、下記効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>・再生［回復力✕{再生威力}％/{バフターン数}ﾀｰﾝ/独自枠/消去不可］"
			詳細文文字列受け渡し変数 += @"<br>・高揚［+{高揚威力}/{バフターン数}ﾀｰﾝ/独自枠/消去不可］"
			詳細文文字列受け渡し変数 += @"<br>・行動速度＊減少＊[{速度デバフ威力}％/{バフターン数}ﾀｰﾝ/独自枠/消去不可]"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;対象は味方単体
			CALL アビ対象選択テンプレート_味方単体_生者のみ

			;回復部分
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_回復処理_MAXでも回復
			CALL アビ汎用変数文字列リセット

			;再生部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			アビ汎用文字列:バフ・デバフ枠 = 再生_Fｼﾝｾｼｽ
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 再生威力 / 100
			アビ汎用変数:持続ターン = バフターン数
			CALL アビテンプレート_有利効果_バフ効果セット

			;高揚部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ対象能力 = 高揚
			アビ汎用文字列:バフ・デバフ枠 = 高揚_Fｼﾝｾｼｽ
			アビ汎用変数:効果量 = 高揚威力
			アビ汎用変数:持続ターン = バフターン数
			CALL アビテンプレート_有利効果_バフ効果セット

			;速度デバフ部分
			;「弱体耐性貫通オプション」で弾けないようにする。（おまじない：デバフ命中率は1000にしておくわね）
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:弱体耐性貫通オプション = 1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用文字列:バフ・デバフ枠 = 行動速度_Fｼﾝｾｼｽ
			アビ汎用変数:効果割合 = 速度デバフ威力
			アビ汎用変数:基礎成功確率 = 速度デバフ付与率
			アビ汎用変数:持続ターン = バフターン数
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・ハイドロジェナスプロミネンス
;　光水属性攻撃
;　全体対象　敵のバフを消去　敵のデバフを１ターン延長
;　敵全体のデバフを延長するので、消費が非常に重い
;　原作では「無意味に力を打ち消しあっている感じ」と評されているが、こっちでは無意味でない感じで
;　力を打ち消し＝バフを解除し、さらにデバフを延長する
;　さすがに敵全員のデバフを確定で延長するとクソ強いので、延長するかは確率。
;--------------------------------------------------
@パッチェさん固有_ハイドロジェナスプロミネンス(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM バフ解除数
#DIM デバフ延長ターン数
#DIM デバフ延長率
#DIM 延長前持続
#DIM デバフ行数

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	;文字数は、全角12文字程度までを想定しているので、半角。
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = ハイドロジェナスプロミネンス-Luna-
		消費ＭＰ = 130
		威力値 = 170
		バフ解除数 = 3
		デバフ延長率 = 50
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = ハイドロジェナスプロミネンス-Hard-
		消費ＭＰ = 100
		威力値 = 130
		バフ解除数 = 2
		デバフ延長率 = 40
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = ハイドロジェナスプロミネンス
		消費ＭＰ = 80
		威力値 = 100
		バフ解除数 = 1
		デバフ延長率 = 30
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("日＆水符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	デバフ延長ターン数 = 1

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵全体に光属性と水属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>バフ効果を{バフ解除数}つ消去する。"
			詳細文文字列受け渡し変数 += @"<br>また、弱体効果をそれぞれ{デバフ延長率}％で［{デバフ延長ターン数}ターン］延長することがある。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵全体_生者のみ

			;攻撃部分
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビテンプレート_ダメージ処理_水属性

			;バフ消去
			CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, バフ解除数
				CALL アビテンプレート_不利効果_バフ消去
			NEXT

			;デバフ延長
			アビ汎用文字列:バフ・デバフ枠 = ALL
			アビ汎用変数:基礎成功確率 = デバフ延長率
			アビ汎用変数:効果量 = 1
			CALL アビテンプレート_不利効果_デバフ効果延長_持続ターン
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・サテライトヒマワリ
;　回復
;　味方全体対象　味方弱体解除
;--------------------------------------------------
@パッチェさん固有_サテライトヒマワリ(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM デバフ解除数

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = サテライトヒマワリ-Luna-
		消費ＭＰ = 70
		威力値 = 55
		デバフ解除数 = 3
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = サテライトヒマワリ-Hard-
		消費ＭＰ = 60
		威力値 = 40
		デバフ解除数 = 2
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = サテライトヒマワリ
		消費ＭＰ = 50
		威力値 = 25
		デバフ解除数 = 1
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("月＆木符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>味方全体のＨＰを［回復力✕{威力値}％］回復させる。"
			詳細文文字列受け渡し変数 += @"<br>また、弱体効果を{デバフ解除数}つ消去する。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;回復部分
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_回復処理_MAXでも回復

			;デバフ解除部分
			CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, デバフ解除数
				CALL アビテンプレート_有利効果_デバフ消去
			NEXT
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・サンシャインリフレクター
;　バフ
;　味方全体対象　ダメージカット＋カウンター状態付与
;　これも文字数問題あり。
;　原作では「周囲にパネルを召喚し、パネルを破壊すると弾が飛んでくる」という感じ
;　パネルは当然当たり判定があるので本体に攻撃が届きにくい……のを、防御バフとする
;　撃ち返しはもちろんカウンター
;--------------------------------------------------
@パッチェさん固有_サンシャインリフレクター(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM カウンター威力
#DIM バフ威力
#DIM カウンター回数

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = サンシャインリフレクター-Luna-
		消費ＭＰ = 70
		カウンター回数 = 5
		カウンター威力 = 140
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = サンシャインリフレクター-Hard-
		消費ＭＰ = 60
		カウンター回数 = 4
		カウンター威力 = 120
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = サンシャインリフレクター
		消費ＭＰ = 50
		カウンター回数 = 3
		カウンター威力 = 100
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("月＆金符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	;バフ数値はちょっと微妙な感じに
	バフ威力 = 20

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>味方全体に{カウンター回数}回のダメージカット［{バフ威力}％］効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>また、{カウンター回数}回の特殊カウンター［能力値✕{カウンター威力}％］効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>このカウンターは攻撃力・防御力・回復力のうち最も高い能力でダメージを与える。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			;防御バフ部分
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = ダメージカット
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = バフ威力
			アビ汎用変数:持続回数 = カウンター回数
			CALL アビテンプレート_有利効果_バフ効果セット

			;カウンターバフ付与
			アビ汎用文字列:バフ・デバフ枠 = Sリフレクター
			アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
			アビ汎用変数:効果割合 = カウンター威力
			アビ汎用変数:持続回数 = カウンター回数
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;・サンシャインリフレクター（カウンター部分）
;　汎用アビ、リベンジウォールからコピペ
;--------------------------------------------------
@特殊反応_被ダメ時_Sリフレクター(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIMS CONST 能力候補 = "攻撃力", "防御力", "回復力"
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM バフ番号

	CALL アビ対象選択テンプレート_指定(攻撃者隊列)
	アビテンプレート用_アビ名 = 撃ち返し

	アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
	アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")

	LOCAL:1 = 0
	FOR LOCAL, 0, 3
		CALL バフ込み数値算出(能力候補:LOCAL)
		IF LOCAL:1 < RESULT
			LOCAL:1 = RESULT
			アビ汎用文字列:攻撃側使用能力値 '= 能力候補:LOCAL
		ENDIF
	NEXT
	CALL アビテンプレート_ダメージ処理_自属性


;--------------------------------------------------
;・ロイヤルダイヤモンドリング
;　光闇属性攻撃
;　全体対象　光闇防御デバフ。これぶっぱしてから奥義をぶっぱしよう！
;　非想天則だと微妙なんだよねぇこれ……これで必殺ゲージ全部使うのはちょっと……
;　原作では持続がめちゃくちゃ長かったので、こちらも持続ターンが長い感じで。
;--------------------------------------------------
@パッチェさん固有_ロイヤルダイヤモンドリング(呼び出し引数, キャラ番号, アビ番号)
#DIMS 呼び出し引数
#DIM キャラ番号
#DIM アビ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 威力値
#DIM デバフ威力
#DIM デバフ持続
#DIM デバフ付与率

	;弐符はいずれも「上級」などがないので、ファイブシーズンと同様「Hard,Luna」とする
	;文字数は、全角12文字程度までを想定しているので、半角。
	IF 0
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 3
		アビ名 = ロイヤルダイヤモンドリング-Luna-
		消費ＭＰ = 130
		威力値 = 210
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 2
		アビ名 = ロイヤルダイヤモンドリング-Hard-
		消費ＭＰ = 100
		威力値 = 180
	ELSEIF パッチェさん固有_段階レベル(キャラ番号, アビ番号, "弐符") >= 1
		アビ名 = ロイヤルダイヤモンドリング
		消費ＭＰ = 80
		威力値 = 150
	ELSE
		;空っぽ！　にすると、初期状態で習得しない。
		アビ名 = 
	ENDIF

	;選択時/実行時メッセージ
	CALL パッチェさん固有_スペカ宣言("日＆月符", アビ名)

	;文字数に応じてオート変換する関数を通す
	アビ名 = %パッチェさん固有_アビ名半角変換(アビ名)%

	;持続は長いが効果値は弱め
	デバフ威力 = 15
	デバフ持続 = 5
	デバフ付与率 = 80

	SELECTCASE 呼び出し引数
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵全体に光属性と闇属性の［攻撃力✕{威力値}％］ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>また、光属性、闇属性の耐性低下［{デバフ威力}/{デバフ持続}ターン］効果を与える。基礎成功率{デバフ付与率}％。"
			;説明文では消費ＭＰを返すよ
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵全体_生者のみ

			;攻撃部分
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビテンプレート_ダメージ処理_闇属性

			;デバフ部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = デバフ威力
			アビ汎用変数:持続ターン = デバフ持続
			アビ汎用変数:基礎成功確率 = デバフ付与率

			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_光
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_光
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_闇
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_闇
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT

	;通常はゼロを返すよ
	RETURN RESULT


;--------------------------------------------------
;●戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1147(対象キャラ)
#DIM 対象キャラ
	CALL 口上変数初期化

	;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
	;基本的にWAIT系列は使わないことを推奨

	KSTR:(K++) = □ 特性:『七曜の魔法使い』 □------------------------------------------------------------
	KSTR:(K++) = 自分の属性に応じてアビリティ・奥義が変化する。
	KSTR:(K++) = （属性は耳飾りで変更できる）
	KSTR:(K++) = 　
	;KSTR:(K++) = □ 特性:『紫もやし』 □------------------------------------------------------------------
	KSTR:(K++) = □ 特性:『喘息＆病弱＆引きこもり』 □----------------------------------------------------
	KSTR:(K++) = 低耐久で、＊きわめて＊鈍足。
	KSTR:(K++) = 　

	CALL 口上変数表示(対象キャラ, 1)


;--------------------------------------------------
;●固有アビリティの習得・強化段階を返す関数
;--------------------------------------------------
@パッチェさん固有_段階レベル(キャラ番号, アビ番号, 符選択)
#FUNCTION
#DIM キャラ番号
#DIM アビ番号
#DIMS 符選択
#DIM パチェレベル
#DIM 発展度

	;アビ番号はそれが何番目のアビリティであるか
	;アビ１の「アグニシャイン」はLv40で「アグニレイディアンス」になるが、
	;アビ２の「アグニシャイン」はLv50で「アグニレイディアンス」になるイメージ

	;符選択は、「壱符」「弐符」「ラストワード」
	;「壱符」はアグニシャインなど、単独属性のアビリティ
	;「弐符」はエメラルドメガリスなど、多属性のアビリティ（まあ中には攻撃は１属性とかあるけど、設定上多属性のやつ）
	;「ラストワード」は賢者の石やロイヤルフレア、奥技枠
	;（ちなみに原作にはラストワードが設定されていない）
	;（ロストワードではシステム上ラストワードがあり、「最後の賢者の石」「プロトエリクサー・ルビー」などが設定されている）

	;発展度は、最終的に返す値
	;未習得は０、アグニシャインは１、アグニシャイン上級は２、アグニレイディアンスは３……という具合。
	;初期値は０（未習得）を返すようにする
	発展度 = 0

	パチェレベル = 基礎BATTLE_STATE:キャラ番号:Lv

	IF 0
	ELSEIF 符選択 == "壱符"
		IF 0
		ELSEIF アビ番号 == 1
			IF 0
			ELSEIF パチェレベル > 30
				発展度 = 3
			ELSEIF パチェレベル > 15
				発展度 = 2
			;アビ番号１だけは初期習得
			ELSEIF パチェレベル > 0
				発展度 = 1
			ENDIF
		ELSEIF アビ番号 == 2
			IF 0
			ELSEIF パチェレベル > 40
				発展度 = 3
			ELSEIF パチェレベル > 25
				発展度 = 2
			ELSEIF パチェレベル > 15
				発展度 = 1
			ENDIF
		ELSE
			;ここにはこないはずだよ……が、おまじないは入れておく
			IF 0
			ELSEIF パチェレベル > 50
				発展度 = 3
			ELSEIF パチェレベル > 30
				発展度 = 2
			ELSEIF パチェレベル > 20
				発展度 = 1
			ENDIF
		ENDIF
	ELSEIF 符選択 == "弐符"
		IF 0
		ELSEIF アビ番号 == 2
			IF 0
			ELSEIF パチェレベル > 50
				発展度 = 3
			ELSEIF パチェレベル > 30
				発展度 = 2
			ELSEIF パチェレベル > 20
				発展度 = 1
			ENDIF
		ELSEIF アビ番号 == 3
			IF 0
			ELSEIF パチェレベル > 50
				発展度 = 3
			ELSEIF パチェレベル > 30
				発展度 = 2
			ELSEIF パチェレベル > 20
				発展度 = 1
			ENDIF
		ELSEIF アビ番号 == 4
			IF 0
			ELSEIF パチェレベル > 55
				発展度 = 3
			ELSEIF パチェレベル > 35
				発展度 = 2
			ELSEIF パチェレベル > 25
				発展度 = 1
			ENDIF
		ELSEIF アビ番号 == 5
			IF 0
			ELSEIF パチェレベル > 65
				発展度 = 3
			ELSEIF パチェレベル > 45
				発展度 = 2
			ELSEIF パチェレベル > 30
				発展度 = 1
			ENDIF
		ELSE
			;おまじない
			IF 0
			ELSEIF パチェレベル > 65
				発展度 = 3
			ELSEIF パチェレベル > 35
				発展度 = 2
			ELSEIF パチェレベル > 25
				発展度 = 1
			ENDIF
		ENDIF
	ELSEIF 符選択 == "ラストワード"
		IF 0
		ELSEIF パチェレベル > 75
			発展度 = 3
		ELSEIF パチェレベル > 40
			発展度 = 2
		;奥義枠なのでLv1から習得
		ELSEIF パチェレベル > 0
			発展度 = 1
		ENDIF
	ELSEIF 符選択 == "システムカード"
		;現状ではグリモワールだけ。増やすことは多分ない
		IF 0
		ELSEIF パチェレベル > 30
			発展度 = 3
		ELSEIF パチェレベル > 15
			発展度 = 2
		ELSEIF パチェレベル > 10
			発展度 = 1
		ENDIF
	ELSE
		;おまじない
		IF 0
		ELSEIF パチェレベル > 30
			発展度 = 3
		ELSEIF パチェレベル > 20
			発展度 = 2
		ELSEIF パチェレベル > 10
			発展度 = 1
		ENDIF
	ENDIF

	RETURNF 発展度


;--------------------------------------------------
;●事前・事後
;--------------------------------------------------
@パッチェさん固有_スペカ宣言(符宣言, アビ名, 宣言必須フラグ = "宣言不要")
#DIMS 符宣言
#DIMS アビ名
#DIMS 宣言必須フラグ

	;ターゲット選択中
	;原作のローディングメッセージをモデルにするので、三点リーダ（…）でなく「...」
	アビ汎用文字列:選択時メッセージ１ = 少女宣言中...

	;アビリティ発動時
	;スペルカード宣言をする場合がある
	IF 0
	;必須であれば必ずなんか言う
	;奥義のはずなのでちょっと力を入れる
	;とはいえ設定上「宣言」なので、別に静かに言ってもいいんだよね
	;魔理沙の「マスタースパーク」とかは「マスタァアーッ！　スパァアークッ！！！」ってなってるケースが多いが。
	ELSEIF 宣言必須フラグ == "宣言必須な"
		IF 0
		;これは激レアのネタセリフ。
		ELSEIF !RAND:100 && アビ名 == "ロイヤルフレア"
			アビ汎用文字列:実行時メッセージ１ = 「――そこまでよ！！！」
		;原作Exステージセリフモデルな
		ELSEIF !RAND:3
			アビ汎用文字列:実行時メッセージ１ = 「今日は、調子が良いから――とっておきの魔法を見せてあげるわ！」
		ELSEIF !RAND:2
			アビ汎用文字列:実行時メッセージ１ = 「――%符宣言%、『%アビ名%』――」
		ELSE
			アビ汎用文字列:実行時メッセージ１ = 「%符宣言%――『%アビ名%』……！」
		ENDIF
	;33％で何らかのセリフを言う
	ELSEIF !RAND:3
		IF 0
		;こちらは「火＆土符、『ラーヴァクロムレク-Luna-』」という感じ
		ELSEIF !RAND:3
			アビ汎用文字列:実行時メッセージ１ = 「……%符宣言%、『%アビ名%』」
		ELSEIF !RAND:2
			アビ汎用文字列:実行時メッセージ１ = 「スペルカードを使うわ」
		;「火符『アグニシャイン』」とかそんな感じで言うはず。
		ELSE
			アビ汎用文字列:実行時メッセージ１ = 「%符宣言%『%アビ名%』」
		ENDIF
	ELSE
		;必ず宣言するわけではない
	ENDIF


@パッチェさん固有_現在属性取得(キャラ番号)
#FUNCTIONS
#DIM キャラ番号
#DIM 現在属性

	;現在属性の値
	;　0：火　1：水
	;　2：風　3：土
	;　4：光　5：闇
	;　無はいったん考慮外（ほぼありえないので）

	;現在属性を取得（デフォルトは水属性＝１になるはず）
	現在属性 = 基礎BATTLE_STATE:キャラ番号:属性
	;装備補正:属性がある場合（≒耳飾りを装備している場合）、そちらの属性で上書き
	SIF 装備ステータス補正保存:キャラ番号:属性
		現在属性 = 装備ステータス補正保存:キャラ番号:属性 - 10
	;0-5の値に収まるようにしておく
	現在属性 =  LIMIT(現在属性, 0, 5)

	;で、文字列に変換してお返しな
	RETURNF 属性数値文字列変換(現在属性)


@パッチェさん固有_アビ名半角変換(アビ名)
#FUNCTIONS
#DIMS アビ名

	;受け取ったアビ名が半角24文字以上＝全角13文字以上だったら、半角にする
	SIF STRLENS(アビ名) > 24
		アビ名 = %TOHALF(アビ名)%

	;変換したかもしれないアビ名を返す
	RETURNF アビ名



