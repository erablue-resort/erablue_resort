;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――その他オブその他
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;その他関数にあった「固有困りごと」はちょっとコレ肥大化したので分離しました。
;--------------------------------------------------


;--------------------------------------------------
;口上変数初期化_1147
;--------------------------------------------------
;通常の「口上変数初期化」のあと、絶対にやりたいことを通す処理
;現状は口上色の統一変更だけ
;--------------------------------------------------
@口上変数初期化_1147(口上色変更 = 1)
#DIM 口上色変更
#DIM 対象キャラ
	;まず初期化な
	;通常の「口上変数初期化」はここだけ。
	CALL 口上変数初期化

;★まだ設定部分作ってないのでいったんカット
;	;引数で持ってきてないのでここで検索な
;	対象キャラ = キャラ検索("[動かない大図書館]パチュリー・ノーレッジ")
;
;	;口上色変更フラグが立っていたら（デフォルトではオン）、口上色をパッチェさん色に変更
;	IF 口上色変更 == 1
;		;色設定
;		;フラグに応じて、変更先の色が変動
;		IF 0
;		;１の場合は白（か、システム設定のデフォ色）のまま
;		ELSEIF 特定状況口上用フラグ:対象キャラ:0 == 1
;			CALL 口上色変更_1147_カラーデフォルト
;		;２の場合は、青緑色にする
;		ELSEIF 特定状況口上用フラグ:対象キャラ:0 == 2
;			CALL 口上色変更_1147_カラー薄紫
;		;３の場合は、カラーパレット関数の「紫」にする
;		ELSEIF 特定状況口上用フラグ:対象キャラ:0 == 3
;			CALL 口上色変更_1147_カラー紫
;		ELSE
;			CALL 口上色変更_1147_カラーデフォルト
;		ENDIF
;	ELSE
;		;おまじない：色設定が無ければカラーデフォルトにする
;		CALL 口上色変更_1147_カラーデフォルト
;	ENDIF

;--------------------------------------------------
;口上色変更_1147_カラーほげほげ
;--------------------------------------------------
;口上変数初期化以外でも色を変えるケースがあるので、ちょっと切り分けておく
;--------------------------------------------------
@口上色変更_1147_カラーデフォルト
	;ダミーにするかな、どうするかな……
	;ともあれこれでデフォルト色になるはず。
	KCOLOR:K = -1
@口上色変更_1147_カラー薄紫
	;薄紫……と言いつつ、青緑。
	KCOLOR:K = カラーパレット("青緑")
@口上色変更_1147_カラー紫
	;ここは素直に紫。
	KCOLOR:K = カラーパレット("紫")


;--------------------------------------------------
;パッチェさん_マゾメス判定
;--------------------------------------------------
;めちゃくちゃされてドスケベにされた、目覚めた、元々そうだったって思い知った状態判定
;様々なアレコレを判定し数値を返すよ！
;それぞれの判定は結構厳しい感じで。
;/
;最終的にどの程度の数字を返すかはちょっと迷うところ。調整必須な。
;とりあえず「５以上になってたら」の反応多めにするかなあ……
;--------------------------------------------------
@パッチェさん_マゾメス判定(対象キャラ, 判定強度 = "弱")
#FUNCTION
#DIM 対象キャラ
#DIMS 判定強度
#DIM マゾメス度
#DIM マゾメス度_ABL部分
#DIM マゾメス度_EXP部分
#DIM マゾメス度_TALENT部分
#DIM マゾメス度_性癖部分
#DIM マゾメス度_その他部分


	;事前確認
	;うふふ済みでなければ強制的にゼロ
	SIF !パッチェさん_陥落段階(対象キャラ, "肉体関係")
		RETURNF 0
	;セフレにせずに下記達成するのは無理だと思うけど、一応
	SIF !パッチェさん_陥落段階(対象キャラ, "性的陥落")
		RETURNF 0
	;マゾ性感が低いと強制的にゼロ。まあマゾメス判定だしなあ……
	SIF ABL:対象キャラ:マゾ性感 < 3
		RETURNF 0

	;指定された強度が無・弱・中・強・最強でなければ帰る
	SIF !GROUPMATCH(判定強度, "無", "弱", "中", "強", "最強")
		RETURNF 0


	;ABL部分
	マゾメス度_ABL部分 = 0
	;感度部分
	;感度Lv４以上が３か所以上で+1
	SIF 2 < (ABL:対象キャラ:Ｃ感覚 >= 4) + (ABL:対象キャラ:Ｖ感覚 >= 4) + (ABL:対象キャラ:Ａ感覚 >= 4) + (ABL:対象キャラ:Ｂ感覚 >= 4) + (ABL:対象キャラ:Ｓ感覚 >= 4)
		マゾメス度_ABL部分 += 1
	SIF ABL:対象キャラ:欲望 >= 5
		マゾメス度_ABL部分 += 1
	SIF ABL:対象キャラ:マゾ性感 >= 5
		マゾメス度_ABL部分 += 1
	SIF ABL:対象キャラ:奉仕精神 >= 5
		マゾメス度_ABL部分 += 1
	;中毒は３から
	SIF ABL:対象キャラ:自慰中毒 >= 3
		マゾメス度_ABL部分 += 1
	SIF ABL:対象キャラ:精液中毒 >= 3
		マゾメス度_ABL部分 += 1
	;その他技巧部分の合計
	;８か所あるので、Lv3を超えてる技巧が４つあるかを判定
	;鍛えられているか（仕込まれているか）という感じ
	SIF 3 < ((ABL:対象キャラ:技巧 >= 3) + (ABL:対象キャラ:指テク >= 3) + (ABL:対象キャラ:舌技 >= 3) + (ABL:対象キャラ:胸 >= 3) + (ABL:対象キャラ:腰使い >= 3) + (ABL:対象キャラ:膣 >= 3) + (ABL:対象キャラ:アナル >= 3) + (ABL:対象キャラ:道具扱い >= 3))
		マゾメス度_ABL部分 += 1

	;経験部分
	マゾメス度_EXP部分 = 0
	SIF EXP:対象キャラ:愛情経験 >= 100
		マゾメス度_EXP部分 += 1
	;これだけイかされてたら観念しろマジで
	SIF EXP:対象キャラ:絶頂経験 >= 500
		マゾメス度_EXP部分 += 1
	;３回以上産まされてたら観念する
	SIF EXP:対象キャラ:出産経験 >= 3
		マゾメス度_EXP部分 += 1
	SIF EXP:対象キャラ:肉体熟知 >= 300
		マゾメス度_EXP部分 += 1
	;この辺りは性癖が中毒になってるかで判定しようかな
;	SIF EXP:対象キャラ:首絞め絶頂経験 >= 50
;		マゾメス度_EXP部分 += 1
;	SIF EXP:対象キャラ:子宮姦経験 >= 50
;		マゾメス度_EXP部分 += 1
;	;A性交経験、１でもあればアブノーマルではあるが、ここでは屈服してそうかどうかなので、ある程度の経験は必要とする。
;	SIF EXP:対象キャラ:Ａ性交経験 >= 50
;		マゾメス度_EXP部分 += 1

	;TALENT部分
	マゾメス度_TALENT部分 = 0
	;畜生胎になってたら＋１
	;出産回数３以上が↑にあるが、つまりめちゃくちゃ産ませるだけで＋２になるぞ、という話。
	SIF TALENT:対象キャラ:受胎体質 >= 3
		マゾメス度_TALENT部分 += 1
	;即イキにされてたら＋１
	SIF TALENT:対象キャラ:イキやすさ >= 2
		マゾメス度_TALENT部分 += 1
	;発情期アリ、かつ、発情期素質が４つ以上追加されてたら＋１
	SIF TALENT:対象キャラ:発情期あり >= 1 && (3 < (GETBIT(TALENT:対象キャラ:発情期あり, 0) + GETBIT(TALENT:対象キャラ:発情期あり, 1) + GETBIT(TALENT:対象キャラ:発情期あり, 2) + GETBIT(TALENT:対象キャラ:発情期あり, 3) + GETBIT(TALENT:対象キャラ:発情期あり, 4) + GETBIT(TALENT:対象キャラ:発情期あり, 5) + GETBIT(TALENT:対象キャラ:発情期あり, 6) + GETBIT(TALENT:対象キャラ:発情期あり, 7)))
		マゾメス度_TALENT部分 += 1

	;性癖部分
	;逆アナルなど、「これ当人がドスケベボディを思い知るって感じか……？」のやつはカット
	マゾメス度_性癖部分 = 0
	SIF 性癖素質:対象キャラ:野外オナニー >= 3
		マゾメス度_性癖部分 += 1
;	SIF 性癖素質:対象キャラ:逆アナル >= 3
;		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:搾乳プレイ >= 3
		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:触手プレイ >= 3
		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:精飲 >= 3
		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:膣内射精 >= 3
		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:腸内射精 >= 3
		マゾメス度_性癖部分 += 1
;	SIF 性癖素質:対象キャラ:同性愛 >= 3
;		マゾメス度_性癖部分 += 1
;	SIF 性癖素質:対象キャラ:寝取られ >= 3
;		マゾメス度_性癖部分 += 1
	SIF 性癖素質:対象キャラ:首絞め >= 3
		マゾメス度_性癖部分 += 1

	;その他要素
	マゾメス度_その他部分 = 0
	;恋慕なら＋１
	SIF パッチェさん_陥落段階(対象キャラ, "恋慕")
		マゾメス度_その他部分 += 1
	;発情とかムラムラとかはど～かな～……「思い知らされた」とは別かなー……
;	;性欲が溜まっているか、発情期
;	SIF BASE:対象キャラ:性欲 >= 1500 || CFLAG:対象キャラ:発情期フラグ < 0
;		マゾメス度_その他部分 += 1
;	;（女性で、Vがあり）超危険日
;	SIF GETBIT(TALENT:対象キャラ:性別, 0) && TALENT:対象キャラ:Ｖ感度 > -2 && RISKY_DAY(対象キャラ) == 2
;		マゾメス度_その他部分 += 1
	;あーでも元々発情期が存在しないキャラだから「発情期あり」にされてたらアリか？
	;ってことで↑に追加な。


	;合計。
	;まあここで合体させる必要もないんだが……
	;ともあれここまで色々あるけど、ABL３＋EXP４＋TALENT２＋性癖２＋その他２＝最大13。
	マゾメス度  = LIMIT(マゾメス度_ABL部分, 0, 3)
	マゾメス度 += LIMIT(マゾメス度_EXP部分, 0, 4)
	マゾメス度 += LIMIT(マゾメス度_TALENT部分, 0, 2)
	マゾメス度 += LIMIT(マゾメス度_性癖部分, 0, 2)
	マゾメス度 += LIMIT(マゾメス度_その他部分, 0, 2)
	;まあマックス10くらいかな……
	マゾメス度 = LIMIT(マゾメス度, 0, 10)

	;指定された判定強度によって分岐
	IF 0
	;「まったく堕ちてない」のを返す
	ELSEIF 判定強度 == "無" && マゾメス度 == 0
		マゾメス度 = 1
	ELSEIF マゾメス度 >= パッチェさん_マゾメス判定難度(対象キャラ, 判定強度)
		;対象とする判定強度以上ならマゾメス度をそのまま返す

	;判定強度：無　　　かつ　マゾメスである
	;判定強度が弱以上　かつ　マゾメス条件に満たない
	;……なら、ゼロを返す
	ELSE
		マゾメス度 = 0
	ENDIF

	RETURNF マゾメス度

;--------------------------------------------------
;パッチェさん_マゾメス判定難度
;--------------------------------------------------
;↑で書いたけどちょっと判定に迷いがある。
;なので……
;/
;IF 0
;ELSEIF パッチェさん_マゾメス判定(対象キャラ, "最強")
;	KSTR:(K++) = 「もうあなたには絶対に逆らえないわ……服従する…………もちろん、性的なことに関しては、だけど」
;ELSEIF パッチェさん_マゾメス判定(対象キャラ, "強")
;	KSTR:(K++) = 「ええ。あなたの、マゾメスよ。……満足した？」
;ELSEIF パッチェさん_マゾメス判定(対象キャラ, "中")
;	KSTR:(K++) = 「……結構思い知らされているわ。押されたら押し返せないかも」
;ELSEIF パッチェさん_マゾメス判定(対象キャラ, "弱")
;	KSTR:(K++) = 「ちょっと認めているわ。１以上返ってくる時点でまあまあマゾメスよ」
;ELSEIF パッチェさん_マゾメス判定(対象キャラ, "無")
;	KSTR:(K++) = 「逆にまったく認めてないわ」
;ELSE
;	KSTR:(K++) = 「『マゾを思い知らせる』とか言っていたけどこの程度？」
;ENDIF
;--------------------------------------------------
@パッチェさん_マゾメス判定難度(対象キャラ, お認め難度)
#FUNCTION
#DIM 対象キャラ
#DIMS お認め難度
#DIM 難度一時保存

	;なんかあとでやる可能性があるので一時保存に突っ込む
	;判定値出したところでは発情期とかの部分をカットしたが、「発情期なら難度が下がる」をやる可能性がないでもない
	;逆に、怒り状態なら必ず10を返す、とかやる可能性もある
	;まあ判定値算出のほうに足し引きしたほうがストレートだが……
	IF 0
	ELSEIF お認め難度 == "最強"
		難度一時保存 = 10
	ELSEIF お認め難度 == "強"
		難度一時保存 = 5
	ELSEIF お認め難度 == "中"
		難度一時保存 = 3
	ELSEIF お認め難度 == "弱"
		難度一時保存 = 1
	ELSEIF お認め難度 == "無"
		難度一時保存 = 0
	ELSE
		難度一時保存 = 0
	ENDIF

	RETURNF 難度一時保存


;--------------------------------------------------
;パッチェさん_GET_CUPSIZE
;--------------------------------------------------
;「GET_CUPSIZE」から、カッコを外し、全角で返すようにしたやつ（あと、自分で見やすいようにコメントやDIM追加したり）
;まー「GET_CUPSIZE」で帰ってきた「(J)」とかを加工してやればいいんだけど……
;--------------------------------------------------
@パッチェさん_GET_CUPSIZE(対象キャラ, TALENT補正フラグ=1)
#FUNCTIONS
#DIM 対象キャラ
#DIM TALENT補正フラグ
#DIMS CONST 表記 = "無","ＡＡＡ","Ａ","Ａ","Ｂ","Ｃ","Ｄ","Ｅ","Ｆ","Ｇ","Ｈ","Ｉ","Ｊ","Ｋ","Ｌ","Ｍ","Ｎ","Ｏ","Ｐ","Ｑ","Ｒ","Ｓ","Ｔ","Ｕ","Ｖ","Ｗ","Ｘ","Ｙ","Ｚ","Ｚ＋"
#DIM CONST バスト素質補正値 = 1, 2, 4, 6, 8, 11, 29
#DIM カップ数
#DIMS カップ数アルファベット


	カップ数 = (補正込みバストサイズ(対象キャラ) - CALC_BODYSIZE_UNDERBUST(対象キャラ) + 10) / 25 - 1

	;文字列変数が若干不自由なので無理やり作成
	カップ数 = MAX(カップ数 , 1)
	;TALENTの表記に無理やり追従させる
	;……とのコメント。
	;おそらく「爆乳（G～I）」とかの表記のこと。爆乳サイズだと「G～I」になるようにしている
	IF TALENT補正フラグ
		;TALENT:バストサイズは下記の数値範囲を取る
		;-2=絶壁(AAA～AA) -1=貧乳(A～B) 0=並乳(C～D) 1=巨乳(E～F) 2=爆乳(G～I) 3=魔乳(J↑)
		;/
		;普乳の場合
		;　バスト素質補正値:(0+3）→バスト素質補正値:3＝6＜計算したカップ数
		;　　→カップ数を6にする
		;　バスト素質補正値:(0+2）+1→バスト素質補正値:2＝4+1＝5＞計算したカップ数
		;　　→カップ数を5にする
		;　「普乳設定なのに、計算したカップ数が10（＝Gカップ）とかだったら、バスト素質補正値:3＝６（Dカップ）になるように補正」
		;　「普乳設定なのに、計算したカップ数が2（＝AAカップ）とかだったら、バスト素質補正値:2+1＝５（Cカップ）になるように補正」
		;　……する処理、ということ。
		SIF バスト素質補正値:(TALENT:対象キャラ:バストサイズ + 3) < カップ数
			カップ数 = バスト素質補正値:(TALENT:対象キャラ:バストサイズ + 3)
		SIF バスト素質補正値:(TALENT:対象キャラ:バストサイズ + 2) + 1 > カップ数
			カップ数 = バスト素質補正値:(TALENT:対象キャラ:バストサイズ + 2) + 1
	ENDIF

	カップ数 = MIN(カップ数, 29)
	カップ数アルファベット = %表記:(カップ数)%

	;男なら「雄っぱい」を返す。まああり得ない想定だが……
	IF !HAS_VAGINA(対象キャラ)
		RETURNF "雄っぱい"
	ELSE
		RETURNF カップ数アルファベット
	ENDIF


;--------------------------------------------------
;＠SET_1147_EXP_COSPLAY
;--------------------------------------------------
;その衣装を着用したことがあるか＆うふふしたことがあるかを記憶
;　特定状況口上用フラグ:対象キャラ:XX　衣装を着たことがあるか
;　特定状況口上用フラグ:対象キャラ:XX+1　衣装を着て、うふふしたことがあるか
;……というのを独自で保存しようとしていたのだが、本体側で保存してくれるようになった。
;SET/保存部分に関しては、なごりだけ残しておく
;--------------------------------------------------
@SET_1147_EXP_COSPLAY(対象キャラ, 衣装名, うふふフラグ = 1)
#DIM 対象キャラ
#DIMS 衣装名
#DIM うふふフラグ

	;明示的にゼロを返しておくね
	RETURN 0

;--------------------------------------------------
;＠GET_1147_EXP_COSPLAY
;--------------------------------------------------
;基本はスケベな時に見るだろうと思われるので、基本は「衣装を着てセックスしたことがあるか」を返す
;/
;＞CALL GET_1147_EXP_COSPLAY(対象キャラ, "パチュリーのパジャマワンピース")
;……といった形での呼び出しを行う。
;まあ、STRFINDで直でやってもいいんですが……。
;--------------------------------------------------
@GET_1147_EXP_COSPLAY(対象キャラ, 衣装名, うふふフラグ = 1)
#FUNCTION
#DIM 対象キャラ
#DIMS 衣装名
#DIM うふふフラグ
#DIMS 着用衣装文字列


	;その衣装でセックスしたことがあるか、を見たい場合
	IF うふふフラグ
		着用衣装文字列 = CSTR:対象キャラ:衣装初回_着用うふふ
	ELSE
		着用衣装文字列 = CSTR:対象キャラ:衣装初回_着用
	ENDIF
	
	;拾った文字列に、指定された衣装名が含まれるかを見る
	SIF STRFIND(着用衣装文字列, 衣装名) > -1
		RETURNF 1

	;着用経験が拾えなかったら、ゼロを返す
	RETURNF 0


;--------------------------------------------------
;＠GET_1147_EXP_COSPLAY_COUNT
;--------------------------------------------------
;着用したことがあるスケベ衣装の数を返す
;--------------------------------------------------
@GET_1147_EXP_COSPLAY_COUNT(対象キャラ, うふふフラグ = 1)
#FUNCTION
#DIM 対象キャラ
#DIM うふふフラグ
#DIMS 着用衣装文字列
#DIM 着用衣装数
#DIMS DYNAMIC 着用衣装分割配列, 100



	;経験衣装
	IF うふふフラグ
		着用衣装文字列 = CSTR:対象キャラ:衣装初回_着用うふふ
	ELSE
		着用衣装文字列 = CSTR:対象キャラ:衣装初回_着用
	ENDIF

	;着用衣装分割配列に分割しつつ……
	SPLIT 着用衣装文字列, "_", 着用衣装分割配列

	;分割した数を変数にシュート
	着用衣装数 = RESULT

	RETURNF 着用衣装数




;--------------------------------------------------
;＠パッチェさん_精液量保存
;--------------------------------------------------
;パッチェさんがオーナーから搾り取った精液量を部位ごとに保存
;--------------------------------------------------
@パッチェさん_精液量保存(対象キャラ, 射精者, フィニッシュ場所, 精液量ミリリットル, XX内射精フラグ = 1)
#DIM 対象キャラ
#DIM 射精者
#DIM フィニッシュ場所
#DIM 精液量ミリリットル
#DIM XX内射精フラグ
#DIM 射精先番号

	;相手キャラがオーナーでないなら帰る
	SIF 射精者 != MASTER
		RETURN 0

	;XX内射精フラグは、下記用途で使用
	;どちらも１以上で「フィニッシュ場所内に射精された」になる
	;　1.将来的にコンドームが実装された場合、搾り取った精液量に加算しないようにする判定
	;　2.口内射精で飲んだかどうかの判定
	;……この１のほう、やるかなあ……？　まあ、持ってこれるようにしておいた、ということで。

	IF 0
	;膣内射精
	ELSEIF フィニッシュ場所 == 0
		射精先番号 = 23
		;コンドーム内射精とみる（現状、入らないはずだよ）
		SIF XX内射精フラグ == 0
			特定状況口上用フラグ:対象キャラ:27 = LIMIT((特定状況口上用フラグ:対象キャラ:27 + 精液量ミリリットル), 0, 99999999)
	;腸内
	ELSEIF フィニッシュ場所 == 1
		射精先番号 = 24
		;コンドーム内射精とみる（現状、入らないはずだよ）
		SIF XX内射精フラグ == 0
			特定状況口上用フラグ:対象キャラ:27 = LIMIT((特定状況口上用フラグ:対象キャラ:27 + 精液量ミリリットル), 0, 99999999)
	;口内
	;ここだけふたつのフラグでカウント
	;口で絞った量は20、そのうち飲み込んだものは21
	ELSEIF フィニッシュ場所 == 2
		;ティッシュに吐き出すか、吐き出してむせる場合は口内ではなくその他判定
		射精先番号 = 20

		;精飲フラグが立ってたらここで追加
		;喜んで飲むは２、なんとか飲み込むは１
		SIF XX内射精フラグ > 0
			特定状況口上用フラグ:対象キャラ:21 = LIMIT((特定状況口上用フラグ:対象キャラ:21 + 精液量ミリリットル), 0, 99999999)

	;乳内
	ELSEIF フィニッシュ場所 == 3
		射精先番号 = 22
	;その他
	;（顔・腹・背中、その他など）
	ELSE
		射精先番号 = 25
	ENDIF


	;（射精先番号）の場所で精液を吐き出させた……と記録
	;20：口内累積精液量１（全量）
	;21：口内累積精液量２（飲み干し）
	;22：乳内累積精液量
	;23：膣内累積精液量（子宮で飲んだ量）
	;24：肛内累積精液量
	;25：その他累積精液量（顔、腹、背中、その他の合計）
	;26：パチュリー射精量（パチュリーがふたなりチンポから出した量）
	;27：コンドーム内射精（場所に関わらず、コンドーム内に射精）

	特定状況口上用フラグ:対象キャラ:射精先番号 = LIMIT((特定状況口上用フラグ:対象キャラ:射精先番号 + 精液量ミリリットル), 0, 99999999)

	;コンドーム内射精
;	特定状況口上用フラグ:対象キャラ:27 = LIMIT((特定状況口上用フラグ:対象キャラ:27 + 精液量ミリリットル), 0, 99999999)



;--------------------------------------------------
;パッチェさん_画像表示
;--------------------------------------------------
;ありがたいことに複数種類画像を用意していただいている……
;で、それを複数個所で表示するようにすると面倒なことになるので、ここでまとめて表示できるようにしておく
;「知識を教える」など、系列として用意した名称を指定すると、「口上表示_知識を教える_01.png」「口上表示_知識を教える_02.png」などがでてくる
;指定系列以外の画像を表示する際は、「表示画像系列」に画像の正式名称を入れて、直接指定フラグをオンにする必要がある
;直接表示の場合、きちんと「顔_全裸_差分_背面座位膣内射精1」と指定してやること
;（たぶん「えっ、普通にIMAGEPATH指定でよくない……？」って思ったと思う。いちおう、将来的に、ランダム表示とかする場合があるので……）
;--------------------------------------------------
@パッチェさん_画像表示(対象キャラ, 表示画像系列, 直接指定フラグ = 0, 枝番指定 = -1)
#DIM 対象キャラ
#DIMS 表示画像系列
#DIM 直接指定フラグ
#DIM 枝番指定
#DIMS 表示画像名
#DIM 画像枝番

	;初期化
	表示画像名 = ""
	;-1のままであれば、枝番を付けない
	画像枝番 = -1

	IF 0
	ELSEIF 直接指定フラグ
		;画像名をキッチリ指定してるならここで処理
		;画像名を入れてやる（そのうえで加工する可能性があります）
		表示画像名 = 表示画像系列
;		;枝番指定……は、今はカット
;		;将来的には「顔_ウエディングドレス」「顔_ウエディングドレス_差分_表情0」「顔_ウエディングドレス_差分_表情1」
;		;……をランダムで表示させられるようにしたいが……これ、ないケースもあるからなぁー……
;		IF 枝番指定 != -1
;			枝番指定 = RAND:枝番指定
;			表示画像名 = 表示画像名 + TOSTR(枝番指定)
;		ENDIF
		;画像が見つかったらKSTRに突っ込む
		SIF EXISTFILE(@"resources/%CSTR:対象キャラ:画像フォルダ%/%表示画像名%.png")
			KSTR:(K++) = IMAGEPATH_%CSTR:対象キャラ:画像フォルダ%/%表示画像名%.png
		;で、帰る
		RETURN 0
	ELSEIF 表示画像系列 == "知識を教える"
		画像枝番 = 1 + RAND:4
	;「読書系全部からランダムで表示」の場合は……どうしようね！
	ELSEIF 表示画像系列 == "読書中_視線"
		画像枝番 = 1 + RAND:4
	ELSEIF 表示画像系列 == "読書中_集中"
		画像枝番 = 1 + RAND:5
	;授乳手コキ
	;乳首が勃起しているかしていないかで分岐
	ELSEIF 表示画像系列 == "授乳手コキ"
		;勃起していたら「勃起乳首」、
		;そうでなければ「陥没乳首」を画像系列名にする
		IF TCVAR:対象キャラ:陥没乳首露出フラグ || !TALENT:対象キャラ:陥没乳首
			表示画像系列 = 授乳手コキ_勃起乳首
		ELSE
			表示画像系列 = 授乳手コキ_陥没乳首
		ENDIF
	ELSEIF 表示画像系列 == "スカートたくしあげ"
		;今日のぱんつによって分岐
		IF 0
		;そもそも履いていない場合
		ELSEIF TEQUIP:対象キャラ:下半身下着あり == 0
			表示画像系列 = スカートたくしあげ_ノーパン
		ELSEIF CSTR:対象キャラ:着せ替え服 == "セクシーランジェリー"
			表示画像系列 = スカートたくしあげ_セクシーランジェリー
		ELSEIF 今日の下着文字列生成(対象キャラ) == "穴あきドロワーズ" || 今日の下着文字列生成(対象キャラ) == "股布のないドロワーズ"
			表示画像系列 = スカートたくしあげ_穴開きドロワ
		ELSEIF 今日の下着文字列生成(対象キャラ) == "ドロワーズ"
			表示画像系列 = スカートたくしあげ_ドロワ
		ELSEIF 今日の下着文字列生成(対象キャラ) == "白パンツ"
			表示画像系列 = スカートたくしあげ_白パン
		;その他は基本「スケベ下着」。
		ELSE
			IF 0
			ELSEIF !RAND:2
				表示画像系列 = スカートたくしあげ_超紐パン
			ELSE
				表示画像系列 = スカートたくしあげ_紐パン
			ENDIF
		ENDIF
	;直接指定フラグが立ってなくて、表示画像名の系列がない
	ELSE
		;もうここで帰る
		RETURN 0
	ENDIF

	;枝番指定があるならそれを番号にする
	SIF 枝番指定 != -1
		画像枝番 = 枝番指定
	;画像枝番が-1でなく、10以下の場合、枝番を「01」のような形に加工
	IF 画像枝番 != -1 
		IF 画像枝番 < 10
			表示画像名 += "_0" + TOSTR(画像枝番)
		ELSE
			表示画像名 += "_" + TOSTR(画像枝番)
		ENDIF
	ENDIF

	;「口上表示_知識を教える_01」のような形をつくりま～す
	;（これがファイルの正式名称のはずだよ）
	表示画像名 '= "口上表示_" + 表示画像系列 + 表示画像名

	;そのファイルが見つからないと表示はしない
	SIF EXISTFILE(@"resources/%CSTR:対象キャラ:画像フォルダ%/%表示画像名%.png")
		KSTR:(K++) = IMAGEPATH_%CSTR:対象キャラ:画像フォルダ%/%表示画像名%.png

;--------------------------------------------------
;パッチェさん_汎用喘ぎ声
;--------------------------------------------------
;省力……に……なってるのかなぁ……
;これ個人的な喘ぎ声の奥義なんだけど「お」の口の形になってたら「お」しか出てこないってのがあって
;……なので、『「あ」「ぇ」「お」「ハートマーク」あたりをランダムで複数個』みたいな処理はできねぇんだよなぁ……
;/
;なお、ハートマークの後ろには必ず全角スペースを入れているが、返すテキストのラストにハートマークはない。注意。
;ハートマークの次の行頭には全角スペースを入れること。注意。
;--------------------------------------------------
@パッチェさん_汎用喘ぎ声(対象キャラ, テキスト種類, ハート強度 = 1, テキスト繰り返し回数 = 1)
#DIM 対象キャラ
#DIMS テキスト種類
#DIM ハート強度
#DIMS テキスト内容
#DIM テキスト繰り返し回数
#DIM LCOUNT

	;ハート強度が偶数だったらプラス１。１、３、５個にしておきたい。
	SIF ハート強度 == 2 || ハート強度 == 4
		ハート強度 = ハート強度 + 1
	;ハートは１～５個まで！
	ハート強度 = LIMIT(ハート強度, 1, 5)


	;テキスト繰り返し回数
	;まあ長すぎるとよくないしこのくらいで制限
	テキスト繰り返し回数 = LIMIT(テキスト繰り返し回数, 1, 3)


	;初期化
	テキスト内容 '= ""

	FOR LCOUNT, 0, テキスト繰り返し回数
		;２回目のループではスペースをイッパツ挟む
		;（ハートマークの後ろには必ずスペースを挟みたいため）
		;「あっあｖ　ひぁああ……っｖ　ああぁああ゛っｖ」……というカタチにするためである。
		;都合上、以下の汎用テキストのラストは全部記号でやる
		SIF テキスト繰り返し回数 != 1
			テキスト内容 += "　"

		;★以下、いくら増補してもよい


		IF 0
		;首を絞められていて呼吸困難
		;喘息持ちにリアルでやってはならない（戒め）
		ELSEIF テキスト種類 == "クビシメロマンチスト"
			IF 0
			ELSEIF !RAND:17
				テキスト内容 += "は……はなぢてェ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:16
				テキスト内容 += "く" + ("_H_" * ハート強度)
				テキスト内容 += "　ぎ" + ("_H_" * ハート強度)
				テキスト内容 += "　ほぎッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:15
				テキスト内容 += "かひ、か、ハ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:14
				テキスト内容 += "うぎゅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:13
				テキスト内容 += "……たしゅ、けてぇえ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:12
				テキスト内容 += "……たひゅけッ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:11
				テキスト内容 += "ふぎゅぅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:10
				テキスト内容 += "ふぎ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:9
				テキスト内容 += "……ア……ア゛……" + ("_H_" * ハート強度)
			ELSEIF !RAND:8
				テキスト内容 += "……しんッ、じゃうッ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "……し……しにゅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:6
				テキスト内容 += "お゛ひ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "う゛……ァ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "むぎゅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "あっ……カハッ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "うっぎゅッ……" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "ぐ、ぎゅ……" + ("_H_" * ハート強度)
			ENDIF
		;完全に失神して昇天している状態
		;「……あへ……_H_」くらいのやつ
		ELSEIF テキスト種類 == "失神"
			IF 0
			ELSEIF !RAND:17
				テキスト内容 += "おほぇ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:16
				テキスト内容 += "むきゅー……" + ("_H_" * ハート強度)
			ELSEIF !RAND:15
				テキスト内容 += "へ……ほへぇ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:14
				テキスト内容 += "ほへ……ほ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:13
				テキスト内容 += "……はひぃ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:12
				テキスト内容 += "……ぅ゛お゛……" + ("_H_" * ハート強度)
			ELSEIF !RAND:11
				テキスト内容 += "ふぎゅぅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:10
				テキスト内容 += "ふぎ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:9
				テキスト内容 += "……あへ……" + ("_H_" * ハート強度)
			;これはギャグパターンだけど……まあいいか！
			ELSEIF !RAND:8
				テキスト内容 += "……（死ーん）……" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "……ぷぇ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:6
				テキスト内容 += "お゛へ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "う゛……" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "むぎゅ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "……う゛ぉ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "ぁ……へ……" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "ぽへ……" + ("_H_" * ハート強度)
			ENDIF
		;アヘっている
		;「あへっ_H_　へひぃい_H_な感じ。
		;あ、ど～しても長いので、以下、２行以上に分けてたりです。
		ELSEIF テキスト種類 == "アヘ"
			IF 0
			ELSEIF !RAND:13
				テキスト内容 += "お゛ッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ほオ゛ッ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛ッイぐゥッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:12
				テキスト内容 += "お゛ッ" + ("_H_" * ハート強度)
				テキスト内容 += "　オ゛～～～～ッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ぐッほォオオオッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:11
				テキスト内容 += "ぷぎゅッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ふんごぉおッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ほぉおーッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:10
				テキスト内容 += "おふーっ" + ("_H_" * ハート強度)
				テキスト内容 += "　おふ～っ" + ("_H_" * ハート強度)
				テキスト内容 += "　……ふんッぐ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:9
				テキスト内容 += "お……ほぉお～～～～っ……" + ("_H_" * ハート強度)
				テキスト内容 += "　ほぉおお～……" + ("_H_" * ハート強度)
			ELSEIF !RAND:8
				テキスト内容 += "ふぎッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ぱひぃイイイ～～っ" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "くあぁああっ" + ("_H_" * ハート強度)
				テキスト内容 += "　っ、あァああああっ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "んっん" + ("_H_" * ハート強度)
				テキスト内容 += "　んぉおおおおおおおおお" + ("_H_" * ハート強度)
			ELSEIF !RAND:6
				テキスト内容 += "は、ばッ" + ("_H_" * ハート強度)
				テキスト内容 += "　はぁあ～っ" + ("_H_" * ハート強度)
				テキスト内容 += "　あぁあ……っ" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "はへ" + ("_H_" * ハート強度)
				テキスト内容 += "　ほぇえッ" + ("_H_" * ハート強度)
				テキスト内容 += "　あぇああ" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "へエ゛ぇっ" + ("_H_" * ハート強度)
				テキスト内容 += "　はぎゅッ" + ("_H_" * ハート強度)
				テキスト内容 += "　う゛お゛～" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "ほぎょッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ほへぇエ゛" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "ぐッお゛" + ("_H_" * ハート強度)
				テキスト内容 += "　おお゛～ッ" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "ふがっ" + ("_H_" * ハート強度)
				テキスト内容 += "　あ゛～" + ("_H_" * ハート強度)
			ENDIF
		;オホっている
		;「お゛ほ_H_　ほぉおお゛_H_な感じ。
		ELSEIF テキスト種類 == "オホ"
			IF 0
			ELSEIF !RAND:12
				テキスト内容 += "ほッ……おほッ……" + ("_H_" * ハート強度)
				テキスト内容 += "　ほぉお…………" + ("_H_" * ハート強度)
			ELSEIF !RAND:11
				テキスト内容 += "ふごッ" + ("_H_" * ハート強度)
				テキスト内容 += "　ォオオオオオオオォオ～～っ" + ("_H_" * ハート強度)
			ELSEIF !RAND:10
				テキスト内容 += "おッォオオオオオォオっ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛お゛お゛お゛お゛ッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:9
				テキスト内容 += "たぢけてッ" + ("_H_" * ハート強度)
				テキスト内容 += "　たぢけてェッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:8
				テキスト内容 += "たじゅけッ" + ("_H_" * ハート強度)
				テキスト内容 += "　助げでぇえええええええ" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "ふんッぎゅ" + ("_H_" * ハート強度)
				テキスト内容 += "　ふぎひぃいいい……" + ("_H_" * ハート強度)
			ELSEIF !RAND:6
				テキスト内容 += "ふぎゅッ、ふぎぃいいい……" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "う゛ッぉ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛お゛お゛" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "ほお゛ぉ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛ぉおお゛っ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "お゛……ぎゅ……っ" + ("_H_" * ハート強度)
				テキスト内容 += "　んお゛っ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "おッお゛" + ("_H_" * ハート強度)
				テキスト内容 += "　おお゛ぉおおおお" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "おッ、ほ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛～" + ("_H_" * ハート強度)
			ENDIF
		;マジイキ絶叫
		;「うっぎッ_H_　ふぎぃいいいいいいい_H_」な感じ。
		ELSEIF テキスト種類 == "マジイキ"
			;★増補重点な
			IF 0
			ELSEIF !RAND:10
				テキスト内容 += "ふごッ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛ッほぉお゛おおッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:9
				テキスト内容 += "イんぎゅッ" + ("_H_" * ハート強度)
				テキスト内容 += "　イぐイぐイっぐッッッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:8
				テキスト内容 += "へひーッ" + ("_H_" * ハート強度)
				テキスト内容 += "　んひぃいーっ" + ("_H_" * ハート強度)
			ELSEIF !RAND:7
				テキスト内容 += "へひーッ" + ("_H_" * ハート強度)
				テキスト内容 += "　へひーッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:6
				テキスト内容 += "ぐッぉおお゛お゛おッ" + ("_H_" * ハート強度)
				テキスト内容 += "　イぐイぐイぐイぐッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "う゛ッぉ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛お゛お゛" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "ほお゛ぉ" + ("_H_" * ハート強度)
				テキスト内容 += "　お゛ぉおお゛っ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "お゛……ぎゅ……っ" + ("_H_" * ハート強度)
				テキスト内容 += "　んお゛っ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "おッお゛" + ("_H_" * ハート強度)
				テキスト内容 += "　おお゛ぉおおおお" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "うッぎ" + ("_H_" * ハート強度)
				テキスト内容 += "　ぃイ゛いいいい" + ("_H_" * ハート強度)
			ENDIF
		;絶息
		;「か……は……っ_H_」な感じ。
		;失神はしてないが、声を絞り出しきって半死半生。
		ELSEIF テキスト種類 == "絶息"
			IF 0
			ELSEIF !RAND:6
				テキスト内容 += "……むきゅー……" + ("_H_" * ハート強度)
			ELSEIF !RAND:5
				テキスト内容 += "はっ……ァ……ッ" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "か、はっ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "はっ……" + ("_H_" * ハート強度)
				テキスト内容 += "　ァ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "…………ッ" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "ァ……ァ……ッ" + ("_H_" * ハート強度)
			ENDIF
		;軽イキ
		;「イ、っく_H_　うぅ_H_」くらいな感じ。耐えられている。
		ELSEIF テキスト種類 == "軽イキ"
			IF 0
			ELSEIF !RAND:5
				テキスト内容 += "ひぃん……んひぃいいん……" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "ふぐ" + ("_H_" * ハート強度)
				テキスト内容 += "　う、んっ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "イ、くっ" + ("_H_" * ハート強度)
				テキスト内容 += "　ううっ" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "ああっ" + ("_H_" * ハート強度)
				テキスト内容 += "　うっあ" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "ひぃん" + ("_H_" * ハート強度)
				テキスト内容 += "　んんんっ……" + ("_H_" * ハート強度)
			ENDIF
		;困惑イキ
		;開発済みのところにいきなりチンポぶち込まれて快楽を理解できていない、みたいなシチュ
		;「お゛？　ほぉ～……_H_」くらいな感じ。
		ELSEIF テキスト種類 == "困惑イキ"
			IF 0
			ELSEIF !RAND:5
				テキスト内容 += "イ゛……っぐッ？　お゛ッ？　ひッぃいい……" + ("_H_" * ハート強度)
			ELSEIF !RAND:4
				テキスト内容 += "お゛？　おッ？　ほ……オ゛……" + ("_H_" * ハート強度)
			ELSEIF !RAND:3
				テキスト内容 += "お゛……ぷっぴょ……" + ("_H_" * ハート強度)
			ELSEIF !RAND:2
				テキスト内容 += "お゛？　おッ？　ほ……オ……" + ("_H_" * ハート強度)
			ELSE
				テキスト内容 += "んぉ゛っ？　お゛？　ふぐっ、ォ……オ……ッ" + ("_H_" * ハート強度)
			ENDIF

		;イってない
		;ただの喘ぎ。
		ELSEIF テキスト種類 == "非絶頂"
			IF 0
			ELSEIF !RAND:5
				テキスト内容 += "っく、ぅんっ……ふぅっ……"
			ELSEIF !RAND:4
				テキスト内容 += "あっ、あぁっ……"
			ELSEIF !RAND:3
				テキスト内容 += "はぁっ、あ、っ、ぅんっ……"
			ELSEIF !RAND:2
				テキスト内容 += "はぁーっ、あっ、あっ……"
			ELSE
				テキスト内容 += "んくぅっ、ふぅ、あぁあ……っ"
			ENDIF
		ELSE
			;なんもしないけど想定してないので「出たらクソ恥ずかしい文言」を設定しておく。必死でチェックしろよ……！
			テキスト内容 '= "これが出たらバグです。報告してネ_H_"
		ENDIF
	NEXT

	パチュリー_汎用ランダムテキスト用変数 '= テキスト内容





;--------------------------------------------------
;パッチェさん_汎用エロワード
;--------------------------------------------------
;主に地の文で使うためのスケベなワード
;基本的に「精液/精子/子種汁」などの１語にし、「雄臭い精液」などにはしない
;一般的なワードは出す率高め
;一応パッチェさんの状況を拾えるように「対象キャラ」は引数で持っておく
;/
;地の文を連続で呼び出そうとすることはないだろうと思われるため、呼び出しは基本１回でよい
;（あんまり呼び出すと誤差程度とはいえ処理速度落ちるしね）
;ただし２行に渡るテキストの場合は注意を要する
;＞1.0：「%精液%をぶちまけた」
;＞2.0：「%精液%をぶちこんだ」
;＞3.0：「%精液%を注ぎ込んだ」
;これは問題ないが（１～３のどれかが出るので）
;＞4.0：「%精液%を注ぎ込むのはホント気持ちいい……」
;＞4.1：「悶えるパッチェさんを見ると無限に%精液%が増産されて行く……」
;これだと「%精液%」が同じになっちゃうので多少問題。精液TEXTRを使わず素直に書くこと。
;--------------------------------------------------
@パッチェさん_汎用エロワードセット(対象キャラ)
;--------------------------------------------------
#DIM 対象キャラ

	パチュリー_子袋TXTR '= TEXTR("子宮/子宮/子宮/子袋/孕み袋")
	パチュリー_雌穴TXTR '= TEXTR("膣内/膣内/膣内/メス穴/雌穴/コキ穴")
	パチュリー_珍宝TXTR '= TEXTR("男根/男根/男根/ちんぽ/チンポ")
	パチュリー_精液TXTR '= TEXTR("精液/精液/精子/精子/孕ませ汁/子種汁")
	パチュリー_尻穴TXTR '= TEXTR("アナル/アナル/アナル/尻穴/ケツ穴")




;--------------------------------------------------
;パッチェさん_ちんぽネーム
;--------------------------------------------------
;ちんちんの呼び方。
;　学術的なテンションなら「ペニス」
;　うぶなら「おちんちん」
;　屈服状態なら「おちんぽ様」
;……とかを返す。
;上の汎用スケベワードと合体予定
;--------------------------------------------------
@パッチェさん_ちんぽネーム(対象キャラ)
#FUNCTIONS
#DIM 対象キャラ
#DIMS ちんぽネーム

	;デフォはこれ。
	ちんぽネーム = ペニス
	;これは「オーナーの」とか「あなたの」とか。
	;ただこれ、「あなたの（ちんぽネーム）、すごく硬い」が「あなたのあなたの、すごく硬い」とかになっちゃうので、没。
	;ちんぽネーム += "/%CSTR:対象キャラ:二人称%の"

;	ちんぽネーム += "/"

;	IF 0
;	;屈服済み
;	ELSEIF パッチェさん_マゾメス判定(対象キャラ, "中")
;		;パッチェさんもここまでくると色ボケしてるので、普段は多分しない「ヤバい」という表現を使う。かわいいね。
;		ちんぽネーム += "/おちんぽ様/おちんぽ/雌殺し/ヤバいの/オスチンポ/つよつよおちんぽ/絶倫おチンポ"
;	;恥ずかしさが残っている
;	ELSE
;		ちんぽネーム += "/おちん……/おちんちん"
;	ENDIF
;
;	;オーナーのちんぽがデカい（巨根以上）
;	SIF 初体験日取得("初うふふ", 対象キャラ, MASTER) && TALENT:MASTER:男性器サイズ >= 1
;		ちんぽネーム += "/おおきいの"
;	;何回も射精でイかされている
;	SIF EXP:対象キャラ:膣内射精絶頂経験 >= 50 || EXP:対象キャラ:腸内射精絶頂経験 >= 50
;		ちんぽネーム += "/すごいの"
;	;奥責め系やられまくってる
;	SIF TALENT:MASTER:男性器サイズ >= 1 && (EXP:対象キャラ:子宮姦経験 >= 20 || EXP:対象キャラ:ポルチオ開発 >= 50 || EXP:対象キャラ:Ｓ字結腸開発 >= 50)
;		ちんぽネーム += "/長いの"
;	;奥責め系やられまくってる
;	SIF だうんけいけんたくさん
;		ちんぽネーム += "/つよいの"
;
;	;候補の中から選ぶ……のはRETURNFのところでいいんだけど、まあ。はい。
;	ちんぽネーム '= TEXTR(ちんぽネーム)
;
	;……ってここまで書いて思ったけど、いったん素直に返すかぁ……？
	;仮処理。
	ちんぽネーム = おちんちん
	;+屈服済み
	SIF パッチェさん_マゾメス判定(対象キャラ, "中")
		ちんぽネーム += "/おちんぽ様/おちんぽ/雌殺し/ヤバいの/オスチンポ/つよつよおちんぽ"

	ちんぽネーム '= TEXTR(ちんぽネーム)
	;/仮処理ここまで

	RETURNF ちんぽネーム

;--------------------------------------------------
;@パッチェさん_ハート
;--------------------------------------------------
;没。
;↓ではランダム個数返すようにしてるけどフツーに個数固定のほうがいいというのもあるが……
;--------------------------------------------------
;@パッチェさん_ハート(ハート強度, 個数固定フラグ = 0)
;#FUNCTIONS
;#DIM ハート強度
;#DIM 個数固定フラグ
;#DIMS 返しテキスト
;
;	;ハート強度が３なら、１～３個を返す
;
;	;２個以上（かつ、個数固定フラグがオフ）なら、ランダム個数
;	;ハート強度の指定が３なら、0～2 + 1＝1～3個な
;	SIF ハート強度 >= 2 && !個数固定フラグ
;		ハート強度 = RAND:ハート強度 + 1
;	;「_H_」みたいな文章を*3すると「_H__H__H_」ってなるよ！
;	返しテキスト = "_H_" * ハート強度
;
;	RETURNF 返しテキスト
;


;--------------------------------------------------
;@パッチェさん_HEIZENモード
;--------------------------------------------------
;クソザコナメクジのパッチェさんも感覚遮断したらHEIZENしてくれるよね！
;返り値：
;　ゼロ：HEIZENモードオフ
;　１　：HEIZENモードON
;　２　：たぶんやらんけど「なるべく我慢してもらうモード」とか実装するならここ
;--------------------------------------------------
@パッチェさん_HEIZENモード(対象キャラ)
#FUNCTION
#DIM 対象キャラ
	;なお未完成なので必ずゼロを返すものとする。
	RETURNF 0

	;HEIZENモードフラグを返す
	RETURNF 特定状況口上用フラグ:対象キャラ:1




;--------------------------------------------------
;＠ADD_1147_ANNUNCIATION
;--------------------------------------------------
;「妊娠した」って伝えた回数を加算
;--------------------------------------------------
@ADD_1147_ANNUNCIATION(対象キャラ)
#DIM 対象キャラ
	特定状況口上用フラグ:対象キャラ:37++

;--------------------------------------------------
;＠GET_1147_ANNUNCIATION
;--------------------------------------------------
;「妊娠した」と伝えた回数を返す
;--------------------------------------------------
@GET_1147_ANNUNCIATION(対象キャラ)
#FUNCTION
#DIM 対象キャラ
	RETURNF 特定状況口上用フラグ:対象キャラ:37


;--------------------------------------------------
;陥落チェック！
;--------------------------------------------------
;書き方を統一できるようにちょっと一段噛ますやつ
;恋慕やセフレなどの陥落のみならず、既成事実あるかどうかも確認できるようにする
;「口説くコマンドでトランス入ったら１段上の反応を返す」とかできるようにするためでもあります
;--------------------------------------------------
;	SIF パッチェさん_陥落段階(対象キャラ, "恋慕")
;		PRINTFORML 恋慕だね
;	SIF パッチェさん_陥落段階(対象キャラ, "セフレんぼ")
;		PRINTFORML セフレかつ恋慕だね
;	SIF パッチェさん_陥落段階(対象キャラ, "性的陥落")
;		PRINTFORML 性的に陥落しているね（今バージョンだと、つまりセフレまたは恋慕だね）
;	SIF パッチェさん_陥落段階(対象キャラ, "性的陥落") && !パッチェさん_陥落段階(対象キャラ, "ファーストキス")
;		PRINTFORML 性的に陥落しているけどファーストキスはまだ、……だね！
;……って感じで使う
;--------------------------------------------------
@パッチェさん_陥落段階(対象キャラ, チェック項目)
#FUNCTION
#DIM 対象キャラ
#DIMS チェック項目

	;結婚
	SIF チェック項目 == "結婚" && TALENT:対象キャラ:恋慕 == 2
		RETURNF 1
	SIF チェック項目 == "恋慕" && GETBIT(陥落チェック(対象キャラ), 0)
		RETURNF 1
	SIF チェック項目 == "セフレ" && GETBIT(陥落チェック(対象キャラ), 1)
		RETURNF 1
	SIF チェック項目 == "非エロ陥落" && GETBIT(陥落チェック(対象キャラ), 2)
		RETURNF 1
	;セフレかつ恋慕。体から落とした状態
	SIF チェック項目 == "セフレんぼ" && GETBIT(陥落チェック(対象キャラ), 0) && GETBIT(陥落チェック(対象キャラ), 1)
		RETURNF 1
	;これは現在のところ「セフレまたは恋慕」
	SIF チェック項目 == "性的陥落" && 陥落チェック_性的(対象キャラ)
		RETURNF 1
	;コクれば恋人状態になれる状態に至っているかどうか
	SIF チェック項目 == "恋慕自覚" && 初体験日:対象キャラ:恋慕自覚
		RETURNF 1
	;押し倒しているかどうか
	;SIF (チェック項目 == "既成事実" || チェック項目 == "肉体関係") && 初体験日:対象キャラ:初うふふ
	SIF (チェック項目 == "既成事実" || チェック項目 == "肉体関係") && 初体験日取得("初うふふ", 対象キャラ, MASTER)
		RETURNF 1
	;キス経験
	SIF (チェック項目 == "初キス" || チェック項目 == "ファーストキス") && 初体験済みチェック("ファーストキス", PLAYER)
		RETURNF 1
	;陥落してなくて恋慕自覚ですらない、厳密な未陥落
	SIF チェック項目 == "未陥落" && !初体験日:対象キャラ:恋慕自覚 && !陥落チェック(対象キャラ)
		RETURNF 1



;--------------------------------------------------
;●たくさん差分くん
;--------------------------------------------------
;大量のセリフ候補をだいたい等確率のランダムでなんかなんとかするための処理群
;--------------------------------------------------

;--------------------------------------------------
;INITIALIZE_パッチェさん_たくさん差分くん
;--------------------------------------------------
;ERHで宣言した関連変数を初期化する
;--------------------------------------------------
@パッチェさん_たくさん差分くん_INITIALIZE()
	VARSET パチュリー_セリフ配列
	VARSET パチュリー_地の文配列
	パチュリー_前半候補NO = 0
	パチュリー_後半候補NO = 0


;--------------------------------------------------
;パッチェさん_たくさん差分くん_ARRAY
;--------------------------------------------------
;セリフまたは地の文の候補を配列に突っ込んでいく
;/
;下記のように使用。
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 0, "候補１個目の本文１行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 1, "候補１個目の本文２行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 0, "候補２個目の本文１行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 0, "候補３個目の本文１行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 0, "候補４個目の本文１行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 0, "候補５個目の本文１行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 1, "候補５個目の本文２行目")
;＞CALL パッチェさん_たくさん差分くん_ARRAY("セリフ", 2, "候補５個目の本文３行目。『２』を入れなくてもいいけど（ゼロ以外ならいいんだけど）行数はカウントしたほうがいいっすよ")
;--------------------------------------------------
@パッチェさん_たくさん差分くん_ARRAY(どっち配列, ザ行数, 本文ヌ)
#DIMS どっち配列
#DIM ザ行数
#DIMS 本文ヌ
#DIM 初期化

	IF 本文ヌ == ""
		;入れようとしてるのが空行ならなんもしない
		;全角スペースでも入れろ
	;セリフを入れる場合
	ELSEIF どっち配列 == "セリフ" || どっち配列 == "前半"
		;送られてきた行数がゼロなら（＝次のセリフ/地の文候補になったのなら）候補ナンバーをプラス
		;コレの都合でKSTRに入れるところで選択するRANDは「(0～候補NO)+1」になる。
		SIF ザ行数 == 0
			パチュリー_前半候補NO++
		;空行かをチェックし、もし空行でなければSPLIT用の分割文字「/」を入れる
		SIF パチュリー_セリフ配列:パチュリー_前半候補NO != ""
			パチュリー_セリフ配列:パチュリー_前半候補NO += "/"
		;本文を突っ込む
		パチュリー_セリフ配列:パチュリー_前半候補NO += 本文ヌ
	;地の文を入れる場合
	ELSEIF どっち配列 == "地の文" || どっち配列 == "後半"
		;送られてきた行数がゼロなら（＝次のセリフ/地の文候補になったのなら）候補ナンバーをプラス
		;コレの都合でKSTRに入れるところで選択するRANDは「(0～候補NO)+1」になる。
		SIF ザ行数 == 0
			パチュリー_後半候補NO++
		;空行かをチェックし、もし空行でなければSPLIT用の分割文字「/」を入れる
		SIF パチュリー_地の文配列:パチュリー_後半候補NO != ""
			パチュリー_地の文配列:パチュリー_後半候補NO += "/"
		;本文を突っ込む
		パチュリー_地の文配列:パチュリー_後半候補NO += 本文ヌ
	ENDIF



;--------------------------------------------------
;パッチェさん_たくさん差分くん_TO_KSTR
;--------------------------------------------------
;KSTRにセリフ・空行・地の文を入れる
;このKSTRを出力するのは戻ってから
;--------------------------------------------------
@パッチェさん_たくさん差分くん_TO_KSTR(出力限定 = "どちらも出す")
#DIM ザ行数
#DIMS 出力限定
#DIMS すぷりっつ, 10

	;候補が１以上無ければ入らない（候補数ゼロは論外）
	;（あとそもそも候補にないパターンとかもあるしね）
	;ここに入らないパターンもあり得る
	IF パチュリー_前半候補NO > 0 && (出力限定 == "どちらも出す" || 出力限定 == "セリフのみ" || 出力限定 == "前半のみ")
		;候補を選択
		;入れかたの都合で＋１する
		パチュリー_前半候補NO = RAND(パチュリー_前半候補NO) + 1

		;RANDった候補番目の配列の文言を、スラッシュ区切りで分割（おまじないでVARSETしておくね）
		VARSET すぷりっつ
		SPLIT パチュリー_セリフ配列:パチュリー_前半候補NO, "/", すぷりっつ

		;おまじない：分割数が10を超えた場合中止して警告
		SIF RESULT >= 10
			GOTO END_パッチェさん_たくさん差分くん_TO_KSTR

		;KSTRに「配列：すぷりっつ」の、「ザ行数」番目の文言を突っ込んでいく
		;※RESULTにはSPLITで分割した数が入ってるよ※
		FOR ザ行数, 0, RESULT
			KSTR:(K++) = %すぷりっつ:ザ行数%
		NEXT
	ENDIF

	;後半部分、まあだいたい同上だよ
	IF パチュリー_後半候補NO > 0 && (出力限定 == "どちらも出す" || 出力限定 == "地の文のみ" || 出力限定 == "後半のみ")
		;セリフと地の文を分かつ空行を入れる
		;前半候補があり（候補NOが１以上で）、前半も出力していた場合
		SIF パチュリー_前半候補NO > 0 && 出力限定 == "どちらも出す"
			KSTR:(K++) = 　

		;候補を選択
		;入れかたの都合で＋１する
		パチュリー_後半候補NO = RAND(パチュリー_後半候補NO) + 1

		;RANDった候補番目の配列の文言を、スラッシュ区切りで分割（おまじないでVARSETしておくね）
		VARSET すぷりっつ
		SPLIT パチュリー_地の文配列:パチュリー_後半候補NO, "/", すぷりっつ

		;おまじない：分割数が10を超えた場合中止して警告
		SIF RESULT >= 10
			GOTO END_パッチェさん_たくさん差分くん_TO_KSTR

		;KSTRに「配列：すぷりっつ」の、「ザ行数」番目の文言を突っ込んでいく
		;※RESULTにはSPLITで分割した数が入ってるよ※
		FOR ザ行数, 0, RESULT
			KSTR:(K++) = %すぷりっつ:ザ行数%
		NEXT
	ENDIF

	IF 0
		$END_パッチェさん_たくさん差分くん_TO_KSTR
		PRINTFORML 関数「パッチェさん_たくさん差分くん_TO_KSTR」にて10行以上突っ込まれた問題が発生
		RETURN 0
	ENDIF

