;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――固有チェインバースト処理
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●固有チェインバースト処理
;--------------------------------------------------
;チェインバーストとは：
;　同一ターンに2回目以降の奥義を放つと追撃で敵全体にダメージを与えるシステム
;この処理では、特定の組み合わせ（など）で、特殊な名称や効果の奥技追撃を定義できますよ～、と言っているわけだな。
;--------------------------------------------------
;チェインバーストに参加しているキャラを取る時について：
;　「チェンバ基準キャラ保存:0～4」に隊列番号が入っている
;　この処理はチェンバに参加したキャラすべてを見るが、「ターン内で奥義を撃った順」に見ていき、
;　固有チェンバが見つかった時点で終わる。そのため早く奥義を撃ったキャラで固有チェンバを満たすと後ろは発動しないことに注意すること
;--------------------------------------------------
@固有チェインバースト処理_1147(対象キャラ, 隊列番号)
#DIM 対象キャラ
#DIM 隊列番号
#DIM チェンバ威力
#DIM 紅魔館チェンバカウント
#DIM チェンバ分岐
#DIMS 奥義名


	;●発生条件チェック
	;「チェンバ組み合わせ確認」は、５人までチェック可能
	;指定したキャラ全員が含まれていれば１、そうでなければ０を返してくる
	;（あれ？　パーティメンバーって４人までじゃない？　ままええわ）
	;……条件を満たしていない場合、RETURN 0で通常のチェンバ処理へ戻る

	;ってことで分岐チェック。初期化
	チェンバ分岐 = 0

	;紅魔館メンバーの数を数える
	紅魔館チェンバカウント = 0
	;めーりん
	SIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[華人小娘]紅美鈴")
		紅魔館チェンバカウント += 1
	;こあ
	SIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[名もなき小さな悪魔]小悪魔")
		紅魔館チェンバカウント += 1
	;咲夜さん
	SIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[完全で瀟洒な従者]十六夜咲夜")
		紅魔館チェンバカウント += 1
	;レミリア
	SIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[永遠に紅い幼き月]レミリア")
		紅魔館チェンバカウント += 1
	;フランちゃん
	SIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[悪魔の妹]フランドール")
		紅魔館チェンバカウント += 1

	;チェンバ分岐１、紅魔館バースト
	;レミリアまたはフランの参加必須で、他に最低でも１名の参加は必須
	IF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[永遠に紅い幼き月]レミリア") && 紅魔館チェンバカウント >= 2
		チェンバ分岐 = 1
	;フランちゃん
	ELSEIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[悪魔の妹]フランドール") && 紅魔館チェンバカウント >= 2
		チェンバ分岐 = 1
	;チェンバ分岐２、図書館バースト
	;パッチェさんと小悪魔のタッグ技
	ELSEIF チェンバ組み合わせ確認("[動かない大図書館]パチュリー・ノーレッジ", "[名もなき小さな悪魔]小悪魔")
		チェンバ分岐 = 2
	;上記分岐に引っかからなかったら：
	ELSE
		;上述の通り、りたーんゼロで通常処理に戻す
		RETURN 0
	ENDIF


	;●チェインバースト処理部分
	;以下、通常のチェインバーストの処理をテンプレートとして記載
	;必要部分を適宜変更のこと

	;チェインバースト時の口上
	TRYCALLFORM KOJO_DUNGEON_チェンバ発動時_{NO:(BATTLE_STATE:隊列番号:キャラ登録番号)}(BATTLE_STATE:隊列番号:キャラ登録番号)
	CALL アビテンプレ変数リセット

	;処理のために戦闘行動キャラへ代入
	戦闘行動キャラ = 隊列番号

	;ここでダメージに使うステータス数値を算出
	;攻撃、防御、回復のうち一番高いステータスが採用される
	CALL バフ込み数値算出("攻撃力")
	LOCAL = RESULT
	LOCALS = 攻撃力
	CALL バフ込み数値算出("防御力")
	IF LOCAL < RESULT
		LOCAL = RESULT
		LOCALS = 防御力
	ENDIF
	CALL バフ込み数値算出("回復力")
	IF LOCAL < RESULT
		LOCAL = RESULT
		LOCALS = 回復力
	ENDIF

	;ダメージ処理
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:ダメージ種類 = チェンバダメージ
	アビ汎用文字列:攻撃側使用能力値 = %LOCALS%
	;威力
	アビ汎用変数:効果割合 = 200 + ターン中奥義回数保存 * 10


	;発動時メッセージ
	;……おそらく、弄るとしたらこのメッセージ部分についてだけのほうがいい気はする。

	;紅魔館メンバーのスリープラトン
	;紅魔郷のサブタイは『ジ・エンバディメント・オブ・スカーレット・デビル』。まあ、「紅の悪魔の具現」くらいの意味。
	IF チェンバ分岐 == 1
		アビ汎用文字列:実行時メッセージ１ = 紅の霧が周囲を満たす……昼も夜もなく……吸血鬼の、都合のいいように！
		アビ汎用文字列:実行時メッセージ２ = チェインバースト！　ジ・エンバディメント・オブ・スカーレット・デビル！！
	;小悪魔とのタッグ技
	;花曇の魔女は、パチュリーの異名の一つ。
	;「賢者の惑星(ほし)」は、ロストワードのパチュリーのラストワードのひとつ。
	ELSEIF チェンバ分岐 == 2
		アビ汎用文字列:実行時メッセージ１ = 使い魔のバックアップを受けた花曇の魔女が、気質を操る！
		アビ汎用文字列:実行時メッセージ２ = チェインバースト！　賢者の惑星！！
	ENDIF

	;その他攻撃オプション
	SETBIT アビ汎用変数:かばう無視オプション, 0
	SETBIT アビ汎用変数:かばう無視オプション, 1
	アビ汎用変数:命中率補正値 = 200
	CALL アビテンプレート_ダメージ処理_自属性

	;ここでカットイン処理、特殊画像を出さないなら触らなくてヨシ
	CALL カットイン呼出("チェインバースト")
	IF RESULT != -1
		WAIT
		CALL カットイン終了
	ENDIF

	RETURN 1

