;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――固有奥技
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●仕様
;--------------------------------------------------
;　自身の属性に応じて奥技が変化する
;　デフォルトは水だが、耳飾りで変化した属性に従う。
;　※各属性モードにおける同名スペルカードは同性能
;　・火水風土の場合
;　　火水木金土符「賢者の石」
;　　　自身に２ターンの間、賢者の石（火）・賢者の石（水）・賢者の石（木）・賢者の石（金）・賢者の石（土）状態を付与。
;　　　賢者の石（XX）の効果は下記の通り。
;　　　　賢者の石（火）：パチュリーの通常攻撃後、火属性の攻撃*130％ダメージ。
;　　　　賢者の石（水）：パチュリーの通常攻撃後、水属性の攻撃*80％ダメージ。敵バフ消去１。
;　　　　賢者の石（木）：パチュリーの通常攻撃後、風属性の攻撃*60％ダメージ。全体攻撃。
;　　　　賢者の石（金）：パチュリーの通常攻撃後、Lv*（10～20）の固定ダメージ。
;　　　　賢者の石（土）：パチュリーの通常攻撃後、土属性の攻撃*50％ダメージ。速度デバフ20％。
;　　・日符「ロイヤルフレア」
;　　　敵全体に光属性の強力な攻撃。特に追加効果はないが、威力は高め。
;　　・月符「サイレントセレナ」
;　　　敵単体に闇属性の強力な攻撃。特に追加効果はないが、威力は高め。
;--------------------------------------------------

@固有奥義_1147(呼び出し引数, 対象キャラ)
#DIM 対象キャラ
#DIMS 呼び出し引数
#DIMS 現在属性

	;アビリティのなにを取得しようとしているかを保存しておく
	;名称や効果など。
	;引数として受け取るときに変えたので不要な
;	呼び出し引数 = ARGS

	;現在属性取得
	;属性については、固有アビリティ.ERBの下の方の関数へレッツゴー
	現在属性 = パッチェさん固有_現在属性取得(対象キャラ)

	IF 0
	;スタイルによる分岐
	ELSEIF CSTR:対象キャラ:スタイル管理文字列 == "[花曇の魔女]パチュリー・ノーレッジ"
		CALL パッチェさん固有_賢者の惑星(呼び出し引数, 対象キャラ)
	ELSEIF CSTR:対象キャラ:スタイル管理文字列 == "[無救]マチョリー・ノーレッジ"
		CALL パッチェさん固有_ザ・ラクトガール(呼び出し引数, 対象キャラ)
	;光：ロイヤルフレア
	ELSEIF 現在属性 == "光"
		CALL パッチェさん固有_ロイヤルフレア(呼び出し引数, 対象キャラ)
	;闇：サイレントセレナ
	ELSEIF 現在属性 == "闇"
		CALL パッチェさん固有_サイレントセレナ(呼び出し引数, 対象キャラ)
	;火水風土：賢者の石
	ELSE
		CALL パッチェさん固有_賢者の石(呼び出し引数, 対象キャラ)
	ENDIF



;--------------------------------------------------
;・火水木金土符「賢者の石」
;　属性なし　威力なし
;　自己対象　自身に、通常攻撃後特殊攻撃を行うバフを付与する
;　バフについては下記。
;--------------------------------------------------
@パッチェさん固有_賢者の石(呼び出し引数, 対象キャラ)
#DIMS 呼び出し引数
#DIM 対象キャラ
#DIM パチェレベル
#DIMS 奥義名
#DIM 奥義威力
#DIM 石持続ターン

	;レベルで分岐するかなぁ……？
	パチェレベル = 基礎BATTLE_STATE:対象キャラ:Lv

	;レベルやイベント完了などで強化される場合、奥義名や奥義威力などを分岐させることができる
	;＞イベント完了フラグ確認("固有イベント名") > 0

	;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
	;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと
	;/
	;特殊処理を行うので奥技威力はゼロ
	奥義名 = 賢者の石
	奥義威力 = 0

	;選択時/実行時メッセージ
	;/
	;下の４つの文字列に文章を入れると、対象選択時の文章を変化させることが出来る。
	;これらを使わない場合は「誰を攻撃しますか？」と表示される。
	;アビ汎用文字列:選択時メッセージ１ = 
	;アビ汎用文字列:選択時メッセージ２ = 
	;アビ汎用文字列:選択時メッセージ３ = 
	;アビ汎用文字列:選択時メッセージ４ = 
	;/
	;下の４つの文字列に文章を入れると、奥義使用時の文章を変化させることが出来る。
	;これらを使わない場合は「（キャラ名）の奥義：（奥義名）！」と表示される。
	;アビ汎用文字列:実行時メッセージ１ = 
	;アビ汎用文字列:実行時メッセージ２ = 
	;アビ汎用文字列:実行時メッセージ３ = 
	;アビ汎用文字列:実行時メッセージ４ = 
	;/
	;……で、パッチェさんはこの辺ちょっと独自処理を行うよ、と。
	;これは宣言必須。
	CALL パッチェさん固有_スペカ宣言("火水木金土符", 奥義名)

	石持続ターン = 3

	SELECTCASE 呼び出し引数
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			;性能調整時、ここの数字をチェックするの忘れないようにね～
;			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
			詳細文文字列受け渡し変数 += @"<br>自身に{石持続ターン}ターンの間、通常攻撃後に追加攻撃を行う効果を５つ付与する。"
			詳細文文字列受け渡し変数 += @"<br>　賢者の石／火：敵ランダム１体を対象に火属性の追加攻撃［攻撃力✕130％］"
			詳細文文字列受け渡し変数 += @"<br>　賢者の石／水：敵ランダム１体を対象に水属性の追加攻撃［攻撃力✕80％/バフ消去１］"
			詳細文文字列受け渡し変数 += @"<br>　賢者の石／木：敵全体を対象に風属性の追加攻撃［攻撃力✕60％］"
			詳細文文字列受け渡し変数 += @"<br>　賢者の石／金：敵ランダム１体を対象に無属性の追加攻撃［Lv✕10～20］の固定ダメージ"
			詳細文文字列受け渡し変数 += @"<br>　賢者の石／土：敵ランダム３体を対象に土属性の追加攻撃［攻撃力✕50％/速度デバフ20％］"

			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥義威力が0だとここの部分もスキップされる

			;～この辺に事前処理を書く～

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;基本的にアビテンプレートの関数を使用すること
			;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載

			;ってことで独自枠追撃を付与な
			;いずれも自分のみ
			CALL アビ対象選択テンプレート_自己のみ

			;別に「賢者の石セット」で１枠にしてもいいんだけど、５枠のほうが「っぽい」かな～
			;火
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 賢者の石／火
			アビ汎用文字列:バフ・デバフ対象能力 = 賢者の石／火
			アビ汎用変数:持続ターン = 石持続ターン
			CALL アビテンプレート_有利効果_バフ効果セット
			CALL アビ汎用変数文字列リセット

			;水
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 賢者の石／水
			アビ汎用文字列:バフ・デバフ対象能力 = 賢者の石／水
			アビ汎用変数:持続ターン = 石持続ターン
			CALL アビテンプレート_有利効果_バフ効果セット
			CALL アビ汎用変数文字列リセット

			;木
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 賢者の石／木
			アビ汎用文字列:バフ・デバフ対象能力 = 賢者の石／木
			アビ汎用変数:持続ターン = 石持続ターン
			CALL アビテンプレート_有利効果_バフ効果セット
			CALL アビ汎用変数文字列リセット

			;金
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 賢者の石／金
			アビ汎用文字列:バフ・デバフ対象能力 = 賢者の石／金
			アビ汎用変数:持続ターン = 石持続ターン
			CALL アビテンプレート_有利効果_バフ効果セット
			CALL アビ汎用変数文字列リセット

			;土
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 賢者の石／土
			アビ汎用文字列:バフ・デバフ対象能力 = 賢者の石／土
			アビ汎用変数:持続ターン = 石持続ターン
			CALL アビテンプレート_有利効果_バフ効果セット
			CALL アビ汎用変数文字列リセット

	ENDSELECT


;--------------------------------------------------
;・賢者の石：追撃
;　通常攻撃後に発生　下記追加攻撃を行う
;　　火：火属性　威力：130　対象：乱１　高威力
;　　水：水属性　威力： 80　対象：乱１　敵バフ消去
;　　木：木属性　威力： 60　対象：全体　全体攻撃
;　　金：固定Ｄ　威力：---　対象：乱１　固定ダメージは「パチュリーのレベル✕10～20」
;　　土：土属性　威力： 50　対象：乱３　速度デバフ20％
;　単体への合計火力が320*2＝640+固定ダメージ、ターゲットが散るのがまあまあ弱点だが……ダメージ高いねぇ～……
;--------------------------------------------------
@固有通常攻撃後処理_バフ_賢者の石／火(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 威力値
	SIF 全滅フラグ
		RETURN 0

	;威力値高め
	威力値 = 130

	アビテンプレート用_アビ名 = 追撃：賢者の石（火）
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
	アビ汎用変数:効果割合 = 威力値
	CALL アビテンプレート_ダメージ処理_火属性


@固有通常攻撃後処理_バフ_賢者の石／水(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 威力値

	SIF 全滅フラグ
		RETURN 0

	威力値 = 80

	アビテンプレート用_アビ名 = 追撃：賢者の石（水）

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
	アビ汎用変数:効果割合 = 威力値
	CALL アビテンプレート_ダメージ処理_水属性

	;対象はそのまま、バフ消去１
	CALL アビ汎用変数文字列リセット
	CALL アビテンプレート_不利効果_バフ消去


@固有通常攻撃後処理_バフ_賢者の石／風(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 威力値

	SIF 全滅フラグ
		RETURN 0

	威力値 = 60

	アビテンプレート用_アビ名 = 追撃：賢者の石（風）
	CALL アビ汎用変数文字列リセット
	;全体攻撃。
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果割合 = 威力値
	CALL アビテンプレート_ダメージ処理_風属性


@固有通常攻撃後処理_バフ_賢者の石／金(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 威力値
#DIM パチェレベル

	SIF 全滅フラグ
		RETURN 0

	;ダメージは固定、パチュリーのレベルの10～20倍
	パチェレベル = 基礎BATTLE_STATE:(キャラ検索("[動かない大図書館]パチュリー・ノーレッジ")):Lv
	;RAND（100～200）/10、をしてるので、10～20倍だわよ
	威力値 = RAND(100, 200) * パチェレベル / 10

	アビテンプレート用_アビ名 = 追撃：賢者の石（金）
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
	;固定ダメージ。
	アビ汎用変数:効果量 = 威力値
	CALL アビテンプレート_ダメージ処理_無属性


@固有通常攻撃後処理_バフ_賢者の石／土(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 威力値
#DIM デバフ威力
#DIM 持続ターン
#DIM デバフ付与率

	SIF 全滅フラグ
		RETURN 0

	威力値 = 50
	;さすがに追加デバフはあまり強くない
	デバフ威力 = 20
	持続ターン = 3
	デバフ付与率 = 50

	アビテンプレート用_アビ名 = 追撃：賢者の石（土）
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
	アビ汎用変数:効果割合 = 威力値
	CALL アビテンプレート_ダメージ処理_土属性

	;速度デバフ追加。
	;さすがにここの速度デバフは独自枠ではない
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 行動速度B
	アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
	アビ汎用変数:効果割合 = デバフ威力
	アビ汎用変数:持続ターン = 持続ターン
	アビ汎用変数:基礎成功確率 = デバフ付与率
	CALL アビテンプレート_不利効果_デバフ効果セット


;--------------------------------------------------
;・日符「ロイヤルフレア」
;　光属性
;　全体対象　単発攻撃
;　ごく単純な全体攻撃。シンプル高火力。
;　命中率が高いのは、全画面攻撃だから。
;--------------------------------------------------
@パッチェさん固有_ロイヤルフレア(呼び出し引数, 対象キャラ)
#DIMS 呼び出し引数
#DIM 対象キャラ
#DIMS 奥義名
#DIM 奥義威力
#DIM ロイフレ威力
#DIM 命中補正

	奥義名 = ロイヤルフレア
	;全体攻撃なので、いったん奥技威力はゼロ
	奥義威力 = 0
	;真の威力はこっち
	ロイフレ威力 = 450
	命中補正 = 30

	;宣言必須。
	CALL パッチェさん固有_スペカ宣言("日符", 奥義名)

	SELECTCASE 呼び出し引数
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{ロイフレ威力}％
			詳細文文字列受け渡し変数 += @"<br>敵全体に光属性の［攻撃力✕{ロイフレ威力}％］ダメージを与える。命中率が高い。"
			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥義威力が0だとここの部分もスキップされる

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;基本的にアビテンプレートの関数を使用すること
			;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載

			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:命中率補正値 = 命中補正
			アビ汎用変数:効果割合 = ロイフレ威力
			CALL アビテンプレート_ダメージ処理_光属性
	ENDSELECT


;--------------------------------------------------
;・月符「サイレントセレナ」
;　闇属性
;　単体対象　単発攻撃
;　ごく単純な単体攻撃。シンプル高火力。
;　調整ネタメモ：パーティ内の紅魔館メンバーの数に応じて威力上昇、小悪魔がパーティにいると再攻撃発動、など
;--------------------------------------------------
@パッチェさん固有_サイレントセレナ(呼び出し引数, 対象キャラ)
#DIMS 呼び出し引数
#DIM 対象キャラ
#DIMS 奥義名
#DIM 奥義威力

	奥義名 = サイレントセレナ
	奥義威力 = 550

	;宣言必須。
	CALL パッチェさん固有_スペカ宣言("月符", 奥義名)

	SELECTCASE 呼び出し引数
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
			詳細文文字列受け渡し変数 += @"<br>敵単体に闇属性の［攻撃力✕{奥義威力}％］ダメージを与える。"
			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥義威力が0だとここの部分もスキップされる

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;基本的にアビテンプレートの関数を使用すること
			;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載
			
			;奥義は「自属性単体攻撃」が基本
			;サイレントセレナは自分が闇属性の場合に使用可能で、
			;「闇属性の単体攻撃を行う」だけものなので、ここではな～～～んもしなくていい
	ENDSELECT



;--------------------------------------------------
;・「賢者の惑星」
;　自属性
;　全体対象　単発攻撃
;　花曇スタイルの奥技。
;　全体への多属性多段攻撃。
;　「賢者の惑星」は、東方ロストワードより。
;--------------------------------------------------
@パッチェさん固有_賢者の惑星(呼び出し引数, 対象キャラ)
#DIMS 呼び出し引数
#DIM 対象キャラ
#DIMS 奥義名
#DIM 奥義威力
#DIM 単発威力
#DIM 命中補正

	奥義名 = 賢者の惑星
	;全体攻撃なので、いったん奥技威力はゼロ
	奥義威力 = 0
	;真の威力はこっち
	単発威力 = 80

	;宣言必須。
	CALL パッチェさん固有_スペカ宣言("ラストワード", 奥義名)


	SELECTCASE 呼び出し引数
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{単発威力}％✕５
			詳細文文字列受け渡し変数 += @"<br>敵全体に、火、水、風、土に加えて自属性の[攻撃力✕{単発威力}％]ダメージを与える。"
			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥義威力が0だとここの部分もスキップされる

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;基本的にアビテンプレートの関数を使用すること
			;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載

			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 単発威力
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビテンプレート_ダメージ処理_自属性
	ENDSELECT



@パッチェさん固有_ザ・ラクトガール(呼び出し引数, 対象キャラ)
#DIMS 呼び出し引数
#DIM 対象キャラ
#DIMS 奥義名
#DIM 奥義威力
#DIM 単発威力

	奥義名 = ザ・ラクトガール
	;ただの単体奥技
	奥義威力 = 550

	;宣言必須。
	CALL パッチェさん固有_スペカ宣言("ラストワード", 奥義名)


	SELECTCASE 呼び出し引数
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
			詳細文文字列受け渡し変数 += @"<br>敵単体に、自属性の[攻撃力✕{奥義威力}％]ダメージを与える。"
			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥義威力が0だとここの部分もスキップされる

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;基本的にアビテンプレートの関数を使用すること
			;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載

			;マジでただの単発・単体奥技ならこれだけでオッケーって言うサンプルみたいなもんです
			;独自要素は「スペカ宣言」くらいかコレ

	ENDSELECT






