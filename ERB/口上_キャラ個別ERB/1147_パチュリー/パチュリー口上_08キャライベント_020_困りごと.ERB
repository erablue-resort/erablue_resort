;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――ランダムダンジョン：困りごと
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;固有困りごと依頼
;--------------------------------------------------
;ランダムダンジョン「困りごと解決」の内部に専用イベを挿入するもの
;発生条件は下記の通り。
;・キャラが島にいる
;・ある程度仲がいい
;・PTメンバーに含まれていない（リゾート歩いてたら偶然会って、困りごとがある……という具合なので）
;最後に「_PAGE」を付ける場合は、そこはKSTRにするべし？
;--------------------------------------------------
@イベント_困りごと解決_キャラ依頼_1147
#DIM 対象キャラ

	;引数で持ってきてないのでここで検索な
	対象キャラ = キャラ検索("[動かない大図書館]パチュリー")

	CALL 口上変数初期化_1147

	;既に訪れていた場合
	IF 初来訪マス判定用フラグ == 0
		KSTR:(K++) = この辺りに困っている人はいないようだ。
		CALL メッセージ欄表示用関数(,,,0)
	;最初に訪れた時
	ELSE
		WSTR:(K++) = 「ああ、ちょうどいいところに」
		WSTR:(K++) = _呼び名_の声が、大量の本の壁の奥から聞こえる……
		WSTR:(K++) = 「本をたくさん買ったはいいけれど、持ち帰りサービスをお願いするお金が足りなくて。
		KSTR:(K++) = 　すぐ返すから、ちょっとお金を貸してもらえないかしら」_PAGE

		WSTR:(K++) = ……いくらか返品して、返金してもらった分で宅配を頼めばいいのでは……？
		WSTR:(K++) = そう思ったが、なにか譲れないものがあるのだろう……たぶん。
		KSTR:(K++) = %CALLNAME:PLAYER%は多少のルピを渡し、……念のため、このルピで本を買わないように、と言い含めた。_PAGE

		;33％で古ぼけた指輪
		IF !RAND:3
			WSTR:(K++) = _呼び名_は目を逸らし、袖口から出した小箱を押し付けてきた。
			WSTR:(K++) = 「こっちで買ったエンチャント品なんだけど、研究も終わったしあげるわ」
			;40％で銀、60％で銅
			;（そもそも1/3でしかこっちに来ないのでちょっと銀率が高め）
			LOCAL = RAND:100
			IF 0
			ELSEIF LOCAL < 40
				LOCALS = 古ぼけた指輪・銀
			ELSE
				LOCALS = 古ぼけた指輪・銅
			ENDIF
		;その他の場合、本系アイテムを入手可能
		ELSE
			WSTR:(K++) = _呼び名_は目を逸らし、袖口から出した本を押し付けてきた。
			WSTR:(K++) = 「これは布教用の本だから。返さなくてもいいわ」
			;本系アイテム３種からランダム
			;名著は10％、小説は30％、娯楽本は60％
			LOCAL = RAND:100
			IF 0
			ELSEIF LOCAL < 10
				LOCALS = 入手困難な名著
			ELSEIF LOCAL < 40
				LOCALS = 流行りの小説本
			ELSE
				LOCALS = ありふれた娯楽本
			ENDIF
		ENDIF

		CALL 素材入手処理(LOCALS, 1)

		KSTR:(K++) = %CALLNAME:PLAYER%は_呼び名_からお礼に「%LOCALS%」を貰った。_PAGE
		WSTR:(K++) = フワフワと飛行しながら本屋に戻っていく_呼び名_を見送り、他の困りごとを探しに行く。
		;報酬設定、贈答アイテム系の素材が望ましい
		;CALL 素材入手処理(任意の素材の名前, 1)

		;ここから下は弄らない
		;--------------------------------------------------
		LOCAL = GETCHARA(1147)
		ダンジョン表示モード = 
		CALLF 顔グラ表示用文字列関数(LOCAL)
		ダンジョン表示モード = マップ画面
		CALL メッセージ欄表示用関数(立ち絵リソース記録, CALLNAME:LOCAL)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
		CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
		CALL 口上変数初期化
		;--------------------------------------------------
		;ここまで弄らない
	ENDIF
	CALL 画面再描画


