@固有アビ_45_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 有利属性

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = ファントムペイン＋
	消費ＭＰ = 125
ELSE
	アビ名 = ファントムペイン
	消費ＭＰ = 150
ENDIF

有利属性 = 有利属性取得(キャラ番号)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ターゲットに関わらず［攻撃力✕100％］の闇属性ダメージを6回与える。
		詳細文文字列受け渡し変数 += @"<br>味方全体に（5ターン・消去不可）の死の祝福効果を与える。"
		;リミットボーナスアビリティ（陥落報酬）
		IF 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを"
			詳細文文字列受け渡し変数 += @"<font color='#%属性数値文字色変換_HTML(有利属性)%'>%属性数値文字列変換(有利属性)%属性</font>"
			詳細文文字列受け渡し変数 += @"に変換する状態を与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを<font color='#%カラーパレット_HTML("光属性")%'>光属性</font>に変換する状態を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ターゲットに関わらず［攻撃力✕100％］の闇属性ダメージを6回与える。
		詳細文文字列受け渡し変数 += @"<br>味方全体に（5ターン・消去不可）の死の祝福効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞毒･灼熱･腐敗などターンダメージを回復に変換する状態。"
		;リミットボーナスアビリティ（陥落報酬）
		IF 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを"
			詳細文文字列受け渡し変数 += @"<font color='#%属性数値文字色変換_HTML(有利属性)%'>%属性数値文字列変換(有利属性)%属性</font>"
			詳細文文字列受け渡し変数 += @"に変換する状態を与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを<font color='#%カラーパレット_HTML("光属性")%'>光属性</font>に変換する状態を与える。"
		ENDIF
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(6)
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 攻撃力
		CALL アビテンプレート_ダメージ処理_闇属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 死の祝福
		アビ汎用文字列:バフ・デバフ対象能力 = 死の祝福
		;アビ汎用文字列:バフ・デバフ名 = 死の祝福
		アビ汎用文字列:バフ・デバフアイコン = バフ_死の祝福
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		;アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 変転
		アビ汎用文字列:バフ・デバフ対象能力 = 変転

		アビ汎用変数:持続ターン = 5
		IF 陥落チェック(キャラ番号)
			アビ汎用文字列:バフ・デバフ名 = %属性数値文字列変換(有利属性)%変転
			アビ汎用文字列:バフ・デバフアイコン = バフ_%属性数値文字列変換(有利属性)%変転
			アビ汎用変数:効果量 = 有利属性
		ELSE
			アビ汎用文字列:バフ・デバフ名 = 光変転
			アビ汎用文字列:バフ・デバフアイコン = バフ_光変転
			アビ汎用変数:効果量 = 属性文字列数値変換("光"); 
		ENDIF
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@固有アビ_45_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	アビ名 = コラプション＋
	消費ＭＰ = 200
ELSE
	アビ名 = コラプション
	消費ＭＰ = 250
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵味方全体に光属性攻撃DOWN/闇属性防御DOWN/毒効果/腐敗効果。
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［25％］（通常枠・5ターン）の光属性攻撃減少効果を与える。基礎成功率200％。
		詳細文文字列受け渡し変数 += "<br>［25％］（通常枠・5ターン）の闇属性防御減少効果を与える。基礎成功率200％。"
		詳細文文字列受け渡し変数 += "<br>［222］（通常枠・5ターン）の毒効果を与える。基礎成功率200％。"
		詳細文文字列受け渡し変数 += "<br>［222］（通常枠・5ターン）の腐敗効果を与える。基礎成功率200％。"
		詳細文文字列受け渡し変数 += "<br>味方全体に［10％］（通常枠・5ターン）の光属性攻撃減少効果を与える。"
		詳細文文字列受け渡し変数 += "<br>［10％］（通常枠・5ターン）の闇属性防御減少効果を与える。"
		詳細文文字列受け渡し変数 += "<br>［5％］（通常枠・5ターン）の毒効果を与える。"
		詳細文文字列受け渡し変数 += "<br>［5％］（通常枠・5ターン）の腐敗効果を与える。"
	
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_光
		アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_光
		アビ汎用文字列:バフ・デバフ名 = 光属性攻撃DOWN
		アビ汎用変数:効果割合 = 25
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 200
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_闇
		アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_闇
		アビ汎用文字列:バフ・デバフ名 = 闇属性防御DOWN
		アビ汎用変数:効果割合 = 25
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 200
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:効果量 = 222
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 200
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 腐敗
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用文字列:バフ・デバフ名 = 腐敗
		アビ汎用文字列:バフ・デバフアイコン = デバフ_腐敗
		アビ汎用変数:効果量 = 222
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 200
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_光
		アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_光
		アビ汎用文字列:バフ・デバフ名 = 光属性攻撃DOWN
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 999
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_闇
		アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_闇
		アビ汎用文字列:バフ・デバフ名 = 闇属性防御DOWN
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 999
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用文字列:バフ・デバフ名 = 毒
		アビ汎用変数:効果割合 = 5
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 999
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 腐敗
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用文字列:バフ・デバフ名 = 腐敗
		アビ汎用文字列:バフ・デバフアイコン = デバフ_腐敗
		アビ汎用変数:効果割合 = 5
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 999
		アビ汎用変数:特殊表示オプション = 1
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@固有アビ_45_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = スピリット･オブ･カース
	消費ＭＰ = 200
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（永続・消去不可）の不死王の再臨効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティは再使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（永続・消去不可）の不死王の再臨効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞戦闘不能になった場合に一度だけ復活し、自身に（永続・消去不可）の不浄効果を付与する状態。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは再使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 不死王の再臨
		アビ汎用文字列:バフ・デバフ対象能力 = 不死王の再臨
		アビ汎用文字列:バフ・デバフ名 = 不死王の再臨
		アビ汎用文字列:バフ・デバフアイコン = デバフ_アンデッド;ホントは独自アイコンにしたいけど一旦
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 不死王の再臨_自動復活
		アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
		アビ汎用文字列:バフ・デバフ名 = 不死王の再臨
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_付随バフ効果セット("不死王の再臨")

		IF 陥落チェック(キャラ番号)
			;リミットボーナスアビリティ（陥落報酬）
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 不死王の再臨_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 30
			CALL アビテンプレート_有利効果_付随バフ効果セット("不死王の再臨")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 不死王の再臨_敵対心
			アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
			アビ汎用変数:効果割合 = 40
			CALL アビテンプレート_有利効果_付随バフ効果セット("不死王の再臨")
		ENDIF
		戦闘中カウント:戦闘行動キャラ:0 = 1
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有戦闘開始時処理_キャラ_45
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(45))

SIF BATTLE_STATE:隊列番号:ＨＰ < 1
	RETURN 0

アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声
アビ汎用文字列:バフ・デバフ対象能力 = 死の祝福
アビ汎用文字列:バフ・デバフ名 = 不死の呼び声
アビ汎用変数:効果割合 = 0
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声_ターンダメージ
アビ汎用文字列:バフ・デバフ対象能力 = 毒
アビ汎用文字列:バフ・デバフ名 = 不死の呼び声
アビ汎用変数:効果量 = MIN(BATTLE_STATE:LOCAL:最大ＨＰ / 10, 1000)
CALL アビテンプレート_不利効果_付随デバフ効果セット("不死の呼び声")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声_強圧
アビ汎用文字列:バフ・デバフ対象能力 = 強圧
CALL アビテンプレート_不利効果_付随デバフ効果セット("不死の呼び声")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 30
CALL アビテンプレート_有利効果_付随バフ効果セット("不死の呼び声")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:効果割合 = 100
CALL アビテンプレート_有利効果_付随バフ効果セット("不死の呼び声")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死の呼び声_奥義与ダメ
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
アビ汎用変数:効果割合 = 50
CALL アビテンプレート_有利効果_付随バフ効果セット("不死の呼び声")

@固有ターン終了時処理_キャラ_45
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 弱体数合計

弱体数合計 = 0

隊列番号 = キャラ隊列検索(GETCHARA(38))

FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	弱体数合計 += 弱体数算出(LOCAL)
NEXT

IF 弱体数合計 > 0
	SIF 弱体数合計 > 10
		弱体数合計 = 10
	
	CALL アビ汎用変数文字列リセット
	アビテンプレート用_アビ名 = ゴーストマーチ

	アビ汎用文字列:実行時メッセージ１ = 「逃がさない」
	アビ汎用文字列:実行時メッセージ２ = りっちょのゴーストマーチ！

	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果割合 = 100
	FOR LOCAL, 0, 弱体数合計
		CALL アビテンプレート_ダメージ処理_闇属性
	NEXT

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 5 * 弱体数合計
	CALL アビテンプレート_有利効果_奥義ゲージUP
ENDIF

バフ・デバフ行数 = デバフ番号取得_枠("不浄", 隊列番号)
IF バフ・デバフ行数 > -1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 10
	CALL アビテンプレート_不利効果_奥義ゲージDOWN
ENDIF

@固有戦闘不能時処理_キャラ_45
#DIM 隊列番号
#DIM 不死王の再臨

隊列番号 = キャラ隊列検索(GETCHARA(45))

不死王の再臨 = バフ番号取得("不死王の再臨", 隊列番号)
IF 不死王の再臨 > -1
	CALL 戦闘効果削除(不死王の再臨)

	IF 戦闘中カウント:隊列番号:1 == 0
		WSTR:(K++) = 「アハッ！アハハッ！！」

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 不浄
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用文字列:バフ・デバフ名 = 不浄
		アビ汎用文字列:バフ・デバフアイコン = デバフ_消耗
		アビ汎用変数:効果量 = MIN(BATTLE_STATE:LOCAL:最大ＨＰ / 10, 3000)
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		戦闘中カウント:隊列番号:1 = 1
	ENDIF
ENDIF

; @バフ・デバフ特殊表示_死の祝福(隊列番号, バフ・デバフ行数)
; #DIM 隊列番号
; #DIM バフ・デバフ行数
; VARSET LOCALS

; PRINTFORML 死の祝福
; PRINTFORML 毒･灼熱･腐敗などのターンダメージを回復に変換する状態。［消去不可］
; PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

@バフ・デバフ特殊表示_不死王の再臨(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 不死王の再臨
PRINTFORML 戦闘不能になった場合に一度だけ復活し、自身に（永続・消去不可）の不浄効果を付与する状態。［消去不可］
PRINTFORML 持続ターン：永続

@バフ・デバフ特殊表示_腐敗(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 腐敗
IF DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合") != 0
	PRINTFORML 行動終了時、最大ＨＰに応じた割合のダメージを受けます。
	PRINTFORM 効果枠：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果枠"), 20, LEFT%　　
	PRINTFORML 効果割合：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合"), 4}％
ELSE
	PRINTFORML 行動終了時、ダメージを受けます。
	PRINTFORM 効果枠：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果枠"), 20, LEFT%　　
	PRINTFORML 効果量：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"), 4}
ENDIF
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

@バフ・デバフ特殊表示_不浄(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 不浄
PRINTFORML 毎ターンHPと奥義ゲージが減少する状態。［消去不可］
PRINTFORML 持続ターン：永続

@有利属性取得(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 自属性

自属性 = 基礎BATTLE_STATE:対象キャラ:属性

IF ダンジョン表示モード != "" && INRANGE(キャラ隊列検索(対象キャラ), 0, 3)
	自属性 = BATTLE_STATE:キャラ隊列検索(対象キャラ):属性
ELSEIF 装備ステータス補正保存:対象キャラ:属性
	自属性 = 装備ステータス補正保存:対象キャラ:属性 - 10
ENDIF
RETURNF 有利属性変換(自属性)

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_45(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『不死の呼び声』 □---------------------------------------------------------
KSTR:(K++) = 　HPが回復せず毎ターンダメージを受けるが、攻防UP/必ず連続攻撃/奥義性能UP/一部のターンダメージを回復に変換
KSTR:(K++) = 
KSTR:(K++) = □ 特性：『ゴーストマーチ』 □-------------------------------------------------------
KSTR:(K++) = 　ターン終了時、味方に弱体効果が付与されている場合、敵全体に［攻撃力✕100％］の闇属性ダメージ/自分の奥義ゲージUP（5％）
KSTR:(K++) = 　◆弱体効果の数に応じて攻撃回数UP/性能強化（最大10回）
KSTR:(K++) = 
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 45
	KSTR:(K++) = □ リミットボーナスアビリティ □-----------------------------------------------------
	KSTR:(K++) = 　不死王の再臨効果中防御UP/敵対心UP
	KSTR:(K++) =
ENDIF

CALL 口上変数表示(対象キャラ, 1)
