@固有奥義_1187(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM 隊列番号
#DIMS 奥義名
#DIM 闇属性攻撃倍率
#DIM イリヤ数
#DIM 追加充能量
#DIM 消費段階
#DIM 乳海判定
#DIM 朔月判定
#DIM Stacks

隊列番号 = キャラ隊列検索(キャラ番号)
乳海判定 = バフ番号取得_枠("乳海", 隊列番号)

;--------------------------------------------
; ★奥義ゲージの最大値を 300 に固定
;--------------------------------------------
IF 隊列番号 > -1
	BATTLE_STATE:隊列番号:ゲージ最大値 = 300
ENDIF

;--------------------------------------------
;乳海バフの有無によって奥義を切り替える
;--------------------------------------------
IF 乳海判定 > -1
	; 乳海バフあり → 乳海形態 → 「貫穿日輪の星河朔月」を使用
	;--------------------------------------------
	; 初期値（消費ゲージ 100）
	;--------------------------------------------
	消費段階 = 100
	奥義名 = 日輪が穿つ星河の朔月
	奥義威力 = 300
	闇属性攻撃倍率 = 30

	;--------------------------------------------
	; 現在の奥義ゲージ量に応じて消費段階を決定
	;--------------------------------------------
	IF 隊列番号 > -1
		IF BATTLE_STATE:隊列番号:奥義ゲージ >= 300
			消費段階 = 300
			奥義威力 = 750
			闇属性攻撃倍率 = 80
		ELSEIF BATTLE_STATE:隊列番号:奥義ゲージ >= 200
			消費段階 = 200
			奥義威力 = 500
			闇属性攻撃倍率 = 50
		ENDIF
	ENDIF

	;--------------------------------------------
	; 必要／実際に消費する奥義ゲージ量
	;--------------------------------------------
	アビ汎用変数:必要奥義ゲージ量 = 消費段階
	アビ汎用変数:消費奥義ゲージ量 = 消費段階

	;--------------------------------------------
	; フィールド上の「イリヤ」特性の数をカウント
	;--------------------------------------------
	イリヤ数 = 0
	FOR LOCAL, 0, 20
		IF LOCAL < 10
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 <= 0 || BATTLE_STATE:LOCAL:ＨＰ <= 0
				CONTINUE
			SIF バフ番号取得_枠("イリヤ", LOCAL) > -1
				イリヤ数 ++
		ELSE
			SIF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ <= 0
				CONTINUE
			SIF デバフ番号取得_枠("イリヤ", LOCAL) > -1
				イリヤ数 ++
		ENDIF
	NEXT

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
			IF 隊列番号 > -1
				SIF BATTLE_STATE:隊列番号:奥義ゲージ < 消費段階
					RETURN 1
			ENDIF

		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 消耗奥义槽：100／200／300
			詳細文文字列受け渡し変数 += @"<br>当前消耗奥义槽：{消費段階}"
			詳細文文字列受け渡し変数 += @"<br>自身の［{闇属性攻撃倍率}％］闇属性攻撃を上昇。敵全体に［攻撃力✕{奥義威力}％］の自身属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージを10上昇。イリヤ特性を持つ味方全体の奥義ゲージを10上昇。"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35 && 陥落チェック(キャラ番号)
				詳細文文字列受け渡し変数 += @"<br>イリヤ特性対象が存在する場合、自身の［30％］クリティカル威力が上昇"
			ENDIF
			RETURN 奥義威力

		CASE "奥義説明文_詳細"
			詳細文文字列受け渡し変数 = 消耗奥义槽：100／200／300
			詳細文文字列受け渡し変数 += @"<br>当前消耗奥义槽：{消費段階}"
			詳細文文字列受け渡し変数 += @"<br>自身の［{闇属性攻撃倍率}％］闇属性攻撃を上昇。敵全体に［攻撃力✕{奥義威力}％］の自身属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージを10上昇。イリヤ特性を持つ味方全体の奥義ゲージを10上昇。"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35 && 陥落チェック(キャラ番号)
				詳細文文字列受け渡し変数 += @"<br>イリヤ特性対象が存在する場合、自身の［30％］クリティカル威力が上昇"

			ENDIF
			
		CASE "奥義前効果"
			; 処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			; 必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
			
		CASE "奥義追加効果"
			;自身暗属性攻击提升
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 日輪が穿つ星河の朔月_暗属性攻撃
			アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_対闇属性攻撃
			アビ汎用変数:効果割合 = 闇属性攻撃倍率
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

			;对敌全体造成伤害
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_自属性

			;我方全体奥义槽充能
			FOR LOCAL, 0, 5
				SIF !BATTLE_STATE:LOCAL:キャラ登録番号 || BATTLE_STATE:LOCAL:ＨＰ <= 0
					CONTINUE
				追加充能量 = 10
				SIF バフ番号取得_枠("イリヤ", LOCAL) > -1
					追加充能量 = 20
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_指定(LOCAL)
				アビ汎用変数:効果量 = 追加充能量
				CALL アビテンプレート_有利効果_奥義ゲージUP
			NEXT

			;Lv35＋陥落追加效果
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35 && 陥落チェック(キャラ番号) && イリヤ数 > 0
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = 日輪が穿つ星河の朔月_暴击威力
				アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ
				アビ汎用変数:効果割合 = 30
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
	
ELSE
	; 无乳海buff → 朔月形态 → 使用"星天を照らせ地の朔月"
	Stacks = MAX(0,刻印現在数確認("朔月",隊列番号))
	奥義名 = 星天を照らせ地の朔月（{Stacks}スタック）
	奥義威力 = 0
	
	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
			
			; 随机选择奥义台词
			SELECTCASE RAND:2
			CASE 0
				アビ汎用文字列:実行時メッセージ１ = 「地に瞬く願いの光……落ちた月は、無垢なる輝きを束ね、天を望む。『星天を照らせ地の朔月』──」
			CASEELSE
				アビ汎用文字列:実行時メッセージ１ = 「百億の小さな灯り……その一瞬の輝きを忘れないで。『星天を照らせ地の朔月』──どうか、あなたの想いが届きますように」
			ENDSELECT
			
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 自身に朔月の加護状態を付与(3ターン,最大5スタック)。
			RETURN 奥義威力
			
		CASE "奥義説明文_詳細"
			詳細文文字列受け渡し変数 += @"<br>朔月の加護"
			詳細文文字列受け渡し変数 += @"<br>毎ターン味方全体の［10✕スタック］の奥義ゲージUP"
			詳細文文字列受け渡し変数 += @"<br>毎ターン味方全体に［回復力✕50％✕スタック］のＨＰ回復を与える。"
			詳細文文字列受け渡し変数 += @"<br>毎ターン味方全体に［20％✕スタック］（独自枠・1ターン）のクリティカル率上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>味方全体に攻撃力アップ効果［10％✕スタック］(3ターン,最大20スタック)"
			
		CASE "奥義前効果"
			; 処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			; 必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
			
		CASE "奥義追加効果"
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:累積上限_固定値 = 5
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ対象能力 = 朔月
			CALL アビテンプレート_刻印追加(1)
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 朔月の加護
			アビ汎用文字列:バフ・デバフ対象能力 = 朔月の加護
			CALL アビテンプレート_有利効果_パッシブバフ効果セット
	ENDSELECT
ENDIF