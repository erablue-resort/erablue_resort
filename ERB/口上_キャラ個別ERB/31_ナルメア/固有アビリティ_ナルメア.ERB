@固有アビ_31_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果数値３
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
	IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
		アビ名 = 胡蝶刃・神楽
	ELSE
		アビ名 = 胡蝶刃・源氏
	ENDIF
	消費ＭＰ = 20
	効果数値 = 10
	効果数値２ = 70
	効果数値３ = 20
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 源氏
;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 神楽

IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
	IF RAND:2
		アビ汎用文字列:実行時メッセージ１ = 「舞えよ胡蝶。刃は踊り、神楽の如く…」
	ELSE
		アビ汎用文字列:実行時メッセージ１ = 「刃は吟じ、神楽の如く…」
	ENDIF
ELSE
	IF RAND:2
		アビ汎用文字列:実行時メッセージ１ = 「舞えよ胡蝶。刃は翻り、源氏の如く…」
	ELSE
		アビ汎用文字列:実行時メッセージ２ = 「刃は語り、源氏の如く…」
	ENDIF
ENDIF

IF キャラ隊列検索(キャラ番号) >= 0
	SIF 現在ターン数 == 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}　　　「神楽の構え」に変更する。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}　　　「源氏の構え」に変更する。
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>自身に[累積{効果数値}％上限50％]（独自枠・4ターン）の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>[攻撃力✕{効果数値２}％]（独自枠・4ターン）の追撃効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>[{効果数値３}％]（独自枠・3ターン）の防御力減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に胡蝶双舞効果が存在する時、自身に［10％］の奥義ゲージ増加効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			戦闘中カウント:戦闘行動キャラ:0 = 1
		ELSE
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
		CALL アビ対象選択テンプレート_自己のみ
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
			CALL アビ汎用変数文字列リセット
		ENDIF
		;追撃{効果数値２}4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		;攻撃累積{効果数値}上限50%4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:累積上限_割合 = 50
		CALL アビテンプレート_有利効果_累積バフ効果セット
		;自分の防御ダウン{効果数値３}3T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値３
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		CALL アビ汎用変数文字列リセット
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 = 現在ターン数
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_31_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIMS アビ名
#DIM 攻撃対象一時退避

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = 泡沫夢幻＋＋
	効果数値 = 400
	消費ＭＰ = 65
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 泡沫夢幻＋
	効果数値 = 400
	消費ＭＰ = 55
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 泡沫夢幻
	効果数値 = 300
	消費ＭＰ = 45
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に[攻撃力✕{効果数値}％]の自属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>（4ターン）の胡蝶相剋効果を与える。"
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>構えに応じた追加効果を発動する。"
		ELSE
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>自身に［200％］（独自枠・3ターン）のTA率上昇効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>自身に［200％］（独自枠・3ターン）のDA率上昇効果を与える。"
					詳細文文字列受け渡し変数 += @"<br>［50％］（独自枠・3ターン）のTA率上昇効果を与える。"
				ENDIF
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>［25％］（Ｂ枠・3ターン）の防御力減少効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>［15％］（Ｂ枠・3ターン）の防御力減少効果を与える。"
				ENDIF
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に[攻撃力✕{効果数値}％]の自属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>（4ターン）の胡蝶相剋効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（胡蝶相剋："
		詳細文文字列受け渡し変数 += @"<br>　単独では効果のないデバフ効果。）"
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>構えに応じた追加効果を発動する。"
		ELSE
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>自身に［200％］（独自枠・3ターン）のTA率上昇効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>自身に［200％］（独自枠・3ターン）のDA率上昇効果を与える。"
					詳細文文字列受け渡し変数 += @"<br>［50％］（独自枠・3ターン）のTA率上昇効果を与える。"
				ENDIF
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>［25％］（Ｂ枠・3ターン）の防御力減少効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>［15％］（Ｂ枠・3ターン）の防御力減少効果を与える。"
				ENDIF
			ENDIF
		ENDIF
	CASE "アビ効果"
		;自属性ダメージ
		CALL アビ対象選択テンプレート_敵単体
		攻撃対象一時退避 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		;胡蝶相剋付与
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶相剋
		アビ汎用文字列:バフ・デバフ対象能力 = 胡蝶相剋
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
		;胡蝶双舞があれば両方、なければ構えに応じた効果
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア2アビ源氏処理(キャラ番号)
			CALL ナルメア2アビ神楽処理(キャラ番号,攻撃対象一時退避)
			CALL 戦闘効果削除(戦闘行動キャラ)
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			;源氏ならTA率上昇
			CALL ナルメア2アビ源氏処理(キャラ番号)
		ELSE
			;神楽なら防御ダウン
			CALL ナルメア2アビ神楽処理(キャラ番号,攻撃対象一時退避)
		ENDIF
ENDSELECT

@ナルメア2アビ源氏処理(ARG)
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			IF 基礎BATTLE_STATE:ARG:Lv >= 28
				アビ汎用文字列:バフ・デバフ枠 = 泡沫夢幻_TA率
				アビ汎用文字列:バフ・デバフ対象能力 = TA率
				アビ汎用変数:効果量 = 200
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ELSE
				アビ汎用文字列:バフ・デバフ枠 = 泡沫夢幻_DA率
				アビ汎用文字列:バフ・デバフ対象能力 = DA率
				アビ汎用変数:効果量 = 200
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
				アビ汎用文字列:バフ・デバフ枠 = 泡沫夢幻_TA率
				アビ汎用文字列:バフ・デバフ対象能力 = TA率
				アビ汎用変数:効果量 = 50
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF

@ナルメア2アビ神楽処理(ARG,デバフ対象)
#DIM デバフ対象
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(デバフ対象)
			アビ汎用文字列:バフ・デバフ枠 = 防御力B
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			IF 基礎BATTLE_STATE:ARG:Lv >= 28
				アビ汎用変数:効果割合 = 25
			ELSE
				アビ汎用変数:効果割合 = 15
			ENDIF
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット

@固有アビ_31_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 鏡花水月
	消費ＭＰ = 35
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>構えに応じた効果を発動する。"
		ELSE
			IF	戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>敵単体に［1］のバフ消去を与える。"
				詳細文文字列受け渡し変数 += @"<br>［1］のスロウ効果を与える。成功率80％"
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>胡蝶相剋デバフを持つ敵が存在する時、自身に［280％］（独自枠・1回）の攻撃力上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［50％］（Ａ枠・3ターン）のクリティカル確率上昇効果を与える。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア3アビ源氏処理
			CALL ナルメア3アビ神楽処理
			CALL 戦闘効果削除(戦闘行動キャラ)
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			CALL ナルメア3アビ源氏処理
		ELSE
			CALL ナルメア3アビ神楽処理
		ENDIF
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 反射
		アビ汎用文字列:バフ・デバフ対象能力 = 反射
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@ナルメア3アビ源氏処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵単体
	CALL アビテンプレート_不利効果_バフ消去()
	アビ汎用変数:効果量 = 1
	アビ汎用変数:基礎成功確率 = 80
	CALL アビテンプレート_不利効果_スロウ

@ナルメア3アビ神楽処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	;アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	;アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	;アビ汎用変数:効果割合 = 20
	;アビ汎用変数:持続回数 = 1
	;CALL アビテンプレート_有利効果_バフ効果セット
	;CALL アビ汎用変数文字列リセット
	FOR LOCAL, 10, 20
		IF デバフ番号取得_枠("胡蝶相剋", LOCAL) != -1
			アビ汎用文字列:バフ・デバフ枠 = 鏡花水月_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 280
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			BREAK
		ENDIF
	NEXT
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
	アビ汎用変数:効果量 = 50
	アビ汎用変数:持続ターン = 3
	CALL アビテンプレート_有利効果_バフ効果セット

@固有アビ_31_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM アビリティ使用フラグ
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 胡蝶双舞
	効果数値 = 60
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

アビ汎用文字列:実行時メッセージ１ = 「大知は閑閑たり、小知は間間たり」

SIF 現在ターン数 == アビリティ使用フラグ && アビテンプレート用_アビ封印用フラグ != 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に胡蝶双舞効果を与える。
		詳細文文字列受け渡し変数 += @"<br>（胡蝶双舞："
		詳細文文字列受け渡し変数 += @"<br>　単独では効果のないバフ効果。）"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値}％］（独自枠・1回）の奥義ダメージ上昇効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶双舞
		アビ汎用文字列:バフ・デバフ対象能力 = 胡蝶双舞
		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶双舞_奥義
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		アビリティ使用フラグ = 現在ターン数
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有戦闘開始時処理_キャラ_31
;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:GETCHARA(31):スタイル管理文字列 == "[揺らぎの想姫]ナルメア"
	TRYCALL 揺らぎの想姫固有戦闘開始時処理_キャラ()
	RETURN
ENDIF

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット


@固有戦闘不能時処理_キャラ_31
;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:GETCHARA(31):スタイル管理文字列 == "[揺らぎの想姫]ナルメア"
	TRYCALL 揺らぎの想姫固有戦闘不能時処理_キャラ()
	RETURN
ENDIF


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_31(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:対象キャラ:スタイル管理文字列 == "[揺らぎの想姫]ナルメア"
	TRYCALL 揺らぎの想姫口上_ステータス画面_固有特性解説()
	RETURN
ENDIF

KSTR:(K++) = □ 特性：『質実剛健』 □---------------------------------------------------------------
KSTR:(K++) = 　強化効果が無効化されない
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『鍔迫合』 □-----------------------------------------------------------------
KSTR:(K++) = 　鏡花水月使用時、自身に1回の反射効果

CALL 口上変数表示(対象キャラ, 1)

