
;1アビ
;☆元にした原作スキル
;「五月雨撃ち」
;指定範囲内に一定時間矢の雨を降らす

;派手に強いタイプの主力技その1
;つまりアローレインなのでは……？と思いつつも攻デバフとかは無く原作通りの7回攻撃
;別のスキル「刹那」と合わせたいネタは当時からありました

@固有アビ_2653_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM アビランク

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビランク = 2
ELSE
	アビランク = 1
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = 五月雨撃ち＋
		消費ＭＰ = 120
		威力値 = 100
	CASEELSE
		アビ名 = 五月雨撃ち
		消費ＭＰ = 120
		威力値 = 85
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>ターゲットに関わらず［攻撃力✕{威力値}％］の7回自属性ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(7)
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT



;2アビ
;☆元にした原作スキル
;「重ね撃ち」
;２本連続で矢を射る

;地味に強いタイプの主力技その2
;通常攻撃の代わりに使っていける技でしたが、そのまま再現するとただの2HIT攻撃になってしまうのと
;いくら燃費が良くとも毎ターンポチるのはめんどくさいのでバフ付き攻撃に変更

@固有アビ_2653_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク
#DIM 威力値
#DIM 追撃割合

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビランク = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = 重ね撃ち＋
		消費ＭＰ = 40
		威力値 = 180
		追撃割合 = 25
	CASE 1
		アビ名 = 重ね撃ち
		消費ＭＰ = 40
		威力値 = 150
		追撃割合 = 20
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に[{追撃割合}％]（A枠・4ターン）の追撃効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 追撃割合
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



;3アビ
;☆元にした原作スキル
;「分析」
;範囲内の対象のクリティカルと命中率を上昇させる

;敵に固有デバフを与えそうな技名ですがほぼ原作通りの範囲バフ
;エンチャントではなく「敵の分析情報を共有」的なイメージのためやや被りにくい枠にしました
;命中率バフは現状あまり見ないので加減がわからず効果量控えめすぎたかも

@固有アビ_2653_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 1
		アビ名 = 分析
		消費ＭＰ = 50
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>味方全体に[15％]（B枠・3ターン）の命中率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>[25％]（B枠・3ターン）のクリティカル率上昇効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 15
		アビ汎用文字列:バフ・デバフ枠 = 命中率B
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 25
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率B
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


;サポアビ
;とりあえず命中率を上げたかっただけですが、
;成長率から弄ると調整が難しそうなので非表示のバフが入るようにしました
;効果がリトルすぎたかも

@固有ターン開始時処理_キャラ_2653
#DIM キャラ番号
#DIM 隊列番号

キャラ番号 = GETCHARA(2653)
隊列番号 = キャラ隊列検索(キャラ番号)

IF BATTLE_STATE:隊列番号:ＨＰ >= (BATTLE_STATE:隊列番号:最大ＨＰ / 2)
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = リトルシューター_命中率
	アビ汎用文字列:バフ・デバフ対象能力 = 命中率
	アビ汎用変数:持続ターン = 1
	アビ汎用変数:効果割合 = 10
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF


@口上_ステータス画面_固有特性解説_2653(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『リトルシューター』 □---------------------------------------------------------
KSTR:(K++) = 　ＨＰが50％以上の場合、命中率が上昇する。
KSTR:(K++) = 　（ターン開始時、自身に[10％]（独自枠・1ターン）の命中率上昇効果）
CALL 口上変数表示(対象キャラ, 1)
