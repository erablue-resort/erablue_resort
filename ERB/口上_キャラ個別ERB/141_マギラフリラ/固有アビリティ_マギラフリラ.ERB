
@固有アビ_141_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = ソードオペラ+
	消費ＭＰ = 150
ELSE
	アビ名 = ソードオペラ
	消費ＭＰ = 150
ENDIF

隊列番号 = キャラ隊列検索(GETCHARA(141))

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕100％］の光属性ダメージを5回与える。
			詳細文文字列受け渡し変数 += @"<br>自身に［500］（通常枠）のバリア効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>自身に魔剣が存在する時、このアビリティの攻撃回数は［魔剣の数÷2］（切り捨て）回増加する。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕100％］の光属性ダメージを5回与える。
			詳細文文字列受け渡し変数 += @"<br>自身に［500］（通常枠）のバリア効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			FOR LOCAL, 0, (5 + (戦闘中カウント:隊列番号:0 / 2))
				CALL アビテンプレート_ダメージ処理_光属性
			NEXT
		ELSE
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			FOR LOCAL, 0, 5
				CALL アビテンプレート_ダメージ処理_光属性
			NEXT
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 500
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@固有アビ_141_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = シルヴァーワルツ+
	消費ＭＰ = 125
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	アビ名 = シルヴァーワルツ
	消費ＭＰ = 125
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕75％］の光属性ダメージを10回与える。
			詳細文文字列受け渡し変数 += @"［累積10％/最大30％］（累積枠・5ターン）の攻撃力減少効果を与える。<br>"
			詳細文文字列受け渡し変数 += @"［累積10％/最大30％］（累積枠・5ターン）の防御力減少効果を与える。<br>"
			詳細文文字列受け渡し変数 += @"［1］（通常枠・5ターン）の最大CT増加効果を与える。<br>"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕75％］の光属性ダメージを10回与える。
			詳細文文字列受け渡し変数 += @"［累積10％/最大30％］（累積枠・5ターン）の攻撃力減少効果を与える。<br>"
			詳細文文字列受け渡し変数 += @"［累積10％/最大30％］（累積枠・5ターン）の防御力減少効果を与える。<br>"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 75
		FOR LOCAL, 0, 10
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力累積
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用文字列:バフ・デバフ枠 = 防御力累積
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 1
			アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
			アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_141_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ名 = 刃重の意地+
	消費ＭＰ = 200
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 刃重の意地
	消費ＭＰ = 200
ENDIF

SIF 現在ターン数 < 10
	アビテンプレート用_アビ封印用フラグ = 1

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:2 == 1
	アビテンプレート用_アビ封印用フラグ = 1
	

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>◆我が刃重の全てを賭して…貴方がたを…倒す！！
		詳細文文字列受け渡し変数 += @"<br>このアビリティは10ターン目以降に使用可能になる。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に魔剣を最大にする効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>ウェポンバースト効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 += @"<br>［50％］（独自枠・消去不可）の奥義ゲージ上昇量上昇効果を与える。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>このアビリティは10ターン目以降に使用可能になる。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		戦闘中カウント:隊列番号:0 = 10
		
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 刃重の意地
			アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
			アビ汎用変数:持続ターン = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		戦闘中カウント:隊列番号:2 = 1
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有通常攻撃後処理_バフ_スペリオルシンフォニー(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数

SIF 全滅フラグ
	RETURN 0

SELECTCASE RAND:4
	CASE 0
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「迅雷の大槌…ミョルニル！」
		CASE 1
			WSTR:(K++) = 「神なる迅雷を宿す巨大な金槌よ！悉くを叩き潰せ！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 大雷の槌
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
		アビ汎用変数:効果割合 = 700
		CALL アビテンプレート_ダメージ処理_光属性
	CASE 1
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「必殺の魔銃…フライクーゲル！」
		CASE 1
			WSTR:(K++) = 「古の邪法が生みし必殺の魔銃よ！万象を穿て！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 命奪う弾丸
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加_フライクーゲル
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
	CASE 2
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「奇跡の権杖…カドゥケウス！」
		CASE 1
			WSTR:(K++) = 「福音をもたらす奇跡の権杖よ！彼の者どもを討ち祓え！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 救いの杖
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = RAND(100, 300)
		CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
	CASE 3
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「神威の秘槍…ゲイボルグ！」
		CASE 1
			WSTR:(K++) = 「影の后より下賜されし秘槍よ！あまねく敵を滅ぼせ！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 破裂する槍
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
		アビ汎用変数:効果割合 = 300
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率_ゲイボルグ
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = クリティカルダメージ率_ゲイボルグ
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@バフ・デバフ特殊表示_魔剣(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

隊列番号 = キャラ隊列検索(GETCHARA(141))

PRINTFORM 魔剣バフ
PRINTFORML ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 魔剣の本数：{戦闘中カウント:隊列番号:0}
PRINTFORML 持続ターン：永続

@探索欄表示_キャラ_141(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "魔剣:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
IF 戦闘中カウント:隊列番号:0 > 0
	FOR LOCAL, 0, 戦闘中カウント:隊列番号:0
		GDRAWTEXT レイヤー番号, "†", 165 + LOCAL * 11, 90
	NEXT
ENDIF

@固有戦闘開始時処理_キャラ_141
;戦闘開始時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

戦闘中カウント:隊列番号:0 = 10

バフ・デバフ行数 = バフ番号取得_枠("魔剣", 隊列番号)
IF バフ・デバフ行数 < 0
	WSTR:(K++) = 「刃重衆の神髄をお見せしましょう」
	アビテンプレート用_アビ名 = 刃重の神髄
	
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 魔剣
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 追撃_魔剣
	アビ汎用文字列:バフ・デバフ対象能力 = 追撃
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 25
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加_魔剣
	アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
	アビ汎用変数:持続ターン = 1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 10
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 被ダメ減少_魔剣
	アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
	アビ汎用変数:持続ターン = 1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF


@固有行動開始時処理_キャラ_141
;行動開始時処理関数から呼び出し
#DIM 魔剣粉砕
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

魔剣粉砕 = デバフ番号取得_枠("魔剣粉砕", 隊列番号)
IF 魔剣粉砕 < 0 && 戦闘中カウント:隊列番号:0 > 0
	戦闘中カウント:隊列番号:0 = MIN(10, 戦闘中カウント:隊列番号:0 + 1)
	
	バフ・デバフ行数 = バフ番号取得_枠("魔剣", 隊列番号)
	IF バフ・デバフ行数 > -1
		WSTR:(K++) = 「もっと上手に踊ってくださいまし」
		
		アビテンプレート用_アビ名 = 刃重の神髄
	
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 25
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加_魔剣
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
	
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 10
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 被ダメ減少_魔剣
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
	ENDIF
	
	WSTR:(K++) = 魔剣を1つ手繰り寄せた！
ELSEIF 魔剣粉砕 > -1
	IF 戦闘中カウント:隊列番号:0 == 0
		WSTR:(K++) = マギラフリラは失った魔剣を手繰り寄せている…
		行動不能フラグ = 1
	ENDIF
ENDIF



@固有ターン終了時処理_キャラ_141
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

IF 戦闘中カウント:隊列番号:0 == 0
	バフ・デバフ行数 = バフ番号取得_枠("魔剣粉砕", 隊列番号)
	IF バフ・デバフ行数 < 0
		WSTR:(K++) = マギラフリラの元に魔剣が戻った！
		
		戦闘中カウント:隊列番号:0 = 5
	ENDIF
ENDIF



@特殊反応_被ダメ時_魔剣(バフ番号, 隊列番号, 被ダメ記録順)
#DIM 被ダメ回数
#DIM 被ダメ記録順
#DIM スペリオルシンフォニー行数
#DIM 隊列番号
#DIM バフ番号

被ダメ回数 = 被ダメ反応回数:被ダメ記録順
隊列番号 = キャラ隊列検索(GETCHARA(141))

スペリオルシンフォニー行数 = バフ番号取得_枠("スペリオルシンフォニー", 隊列番号)
IF スペリオルシンフォニー行数 > -1
	SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「その武器達はスペリオルシリーズと呼ばれる名品ですのよ！？」
	CASE 1
		WSTR:(K++) = 「私の大事なコレクションに傷を…！？」
	ENDSELECT
ELSE
	WSTR:(K++) = 「私の魔剣をよくも…！」
ENDIF

アビテンプレート用_アビ名 = 魔剣の盾

WSTR:(K++) = 魔剣を{MIN(戦闘中カウント:隊列番号:0, 被ダメ回数)}つ失った…
戦闘中カウント:隊列番号:0 = MAX(0, 戦闘中カウント:隊列番号:0 - 被ダメ回数)

IF 戦闘中カウント:隊列番号:0 == 0
	SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「うぅっ…防ぎきれませんわ…！」
	CASE 1
		WSTR:(K++) = 「くっ…！？ テンポが乱れて…」
	ENDSELECT
	
	WSTR:(K++) = 魔剣を失い行動できなくなった…
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 魔剣粉砕
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:持続ターン = 4
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_141(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『刃重の神髄』 □--------------------------------------------------------------
KSTR:(K++) = 　最大HPが低く奥義ゲージが溜まりにくいが、必ずTA/追撃効果/戦闘開始時に魔剣を10本得る
KSTR:(K++) = 　魔剣は本数に応じた与ダメ上昇・被ダメ減少効果を持つ
KSTR:(K++) = 　魔剣は行動開始時に1本/奥義発動時に3本回復する
KSTR:(K++) = 　魔剣は攻撃を受けた時、被ダメ回数だけ減少/0になった場合、3ターン行動不能となる

CALL 口上変数表示(対象キャラ, 1)


