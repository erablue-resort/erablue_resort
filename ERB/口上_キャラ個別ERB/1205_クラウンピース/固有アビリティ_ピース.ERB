@固有アビ_1205_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2
#DIM 敵番号
#DIM 対象数

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = ストライプドアビス
	消費ＭＰ = 35
	効果数値:0 = 300
	効果数値:1 = 200

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 地獄「ストライプドアビス」！
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = スターアンドストライプ
	消費ＭＰ = 20
	効果数値:0 = 300

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄符「スターアンドストライプ」！
ELSE
	アビ名 = スターアンドストライプ
	消費ＭＰ = 15
	効果数値:0 = 200

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄符「スターアンドストライプ」！
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>１・３・５番目の横列の敵に光属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += @"<br>２・４番目の横列の敵に闇属性の［攻撃力✕{効果数値:1}％］ダメージを与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		対象数 = 0
		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			SIF (敵番号 % 4) / 2 == 0 && !敵BATTLE_STATE:(敵番号 - 10):ボスフラグ
				CONTINUE
			CALL アビ対象選択テンプレート_指定(敵番号, 対象数)
			対象数 ++
		NEXT
		IF 対象数 > 0
			アビ汎用変数:効果割合 = 効果数値:0
			CALL アビテンプレート_ダメージ処理_光属性
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 20
			CALL 口上変数初期化
			WSTR:(K++) = 対象が存在しません
			CALL メッセージ欄表示用関数(,,,0)
			アビテンプレート用_キャンセルフラグ = 1
			RETURN
		ENDIF

		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			対象数 = 0
			;無駄に凝って逆順
			FOR 敵番号, 19, 9, -1
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				SIF (敵番号 % 4) / 2 == 1 && !敵BATTLE_STATE:(敵番号 - 10):ボスフラグ
					CONTINUE
				CALL アビ対象選択テンプレート_指定(敵番号, 対象数)
				対象数 ++
			NEXT
			IF 対象数 > 0
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果割合 = 効果数値:1
				CALL アビテンプレート_ダメージ処理_闇属性
			ENDIF
		ENDIF
ENDSELECT


@固有アビ_1205_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2
#DIM 敵番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = かすりの獄意
	消費ＭＰ = 40
	効果数値:0 = 250
	効果数値:1 = 220

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄炎「かすりの獄意」！
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = グレイズインフェルノ
	消費ＭＰ = 25
	効果数値:0 = 160
	効果数値:1 = 140

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄炎「グレイズインフェルノ」！
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = グレイズインフェルノ
	消費ＭＰ = 20
	効果数値:0 = 120
	効果数値:1 = 100

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄炎「グレイズインフェルノ」！
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に火属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>ダメージを与えられなかった場合、対象に3ターン火傷［攻撃力✕{効果数値:1}％］効果を与える。基礎成功率100％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			SIF アビテンプレート用_アビ名表示済フラグ
				CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(敵番号)
			アビ汎用変数:効果割合 = 効果数値:0
			CALL アビテンプレート_ダメージ処理_火属性
			;RESULTに与えたダメージ量が入っている
			IF RESULT <= 0
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 火傷
				アビ汎用文字列:バフ・デバフ対象能力 = 火傷
				CALL バフ込み数値算出("攻撃力")
				アビ汎用変数:効果量 = RESULT * 効果数値:1 / 100
				アビ汎用変数:持続ターン = 3
				アビ汎用変数:基礎成功確率 = 100
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF
		NEXT
ENDSELECT


@固有アビ_1205_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM 与ダメージ量

;効果が強くなっても名前は同じ
アビ名 = ルナティックトーチリレー
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	消費ＭＰ = 50
	効果数値:0 = 150
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	消費ＭＰ = 40
	効果数値:0 = 120
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 「ルナティックトーチリレー」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>ランダムな敵に自属性の［攻撃力✕{効果数値:0}％］ダメージを8回与える。"
		詳細文文字列受け渡し変数 += "<br>自身のＨＰを［与ダメージ✕15％］回復する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用変数:敵対心無視オプション = 1
		CALL アビ対象選択テンプレート_敵ランダムＸ体(8)
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性
		;RESULTに与えたダメージ量が入っている
		与ダメージ量 = RESULT
		IF 与ダメージ量 > 0
			;ダメージが発生した場合のみ処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 与ダメージ量 * 3 / 20
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@固有アビ_1205_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1

;効果が強くなっても消費ＭＰは同じ
消費ＭＰ = 10
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = ルナティックタイム＋
	効果数値:0 = 3
ELSE
	アビ名 = ルナティックタイム
	効果数値:0 = 5
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は人を狂わす程度の能力を使った！
アビ汎用文字列:実行時メッセージ２ = 「イッツ、ルナティックターイム！」

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>味方単体に{効果数値:0}ターン狂化効果を与える。"
		詳細文文字列受け渡し変数 += "<br>この効果を受けた対象は防御力が攻撃力に転換され、行動がランダムな敵１体への通常攻撃のみになる。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
			詳細文文字列受け渡し変数 += "<br>このアビリティは行動を消費しない。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 戦闘中カウント:戦闘行動キャラ:0 < 現在ターン数
			戦闘中カウント:戦闘行動キャラ:0 = 現在ターン数
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
			アビテンプレート用_アビ名表示済フラグ = 1
		ENDIF
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 狂化
		アビ汎用文字列:バフ・デバフ対象能力 = 狂化
		アビ汎用変数:持続ターン = 効果数値:0
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
			アビテンプレート用_行動消費無しフラグ = 1
		ENDIF
ENDSELECT

@固有バフ・デバフ処理_デバフ_狂化_攻撃力(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN 補正前数値 + BATTLE_STATE:隊列番号:防御力
ELSE
	RETURN 補正前数値 + 敵BATTLE_STATE:(隊列番号 - 10):防御力
ENDIF

@固有バフ・デバフ処理_デバフ_狂化_防御力(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN MAX(0, 補正前数値 - BATTLE_STATE:隊列番号:防御力)
ELSE
	RETURN MAX(0, 補正前数値 - 敵BATTLE_STATE:(隊列番号 - 10):防御力)
ENDIF

@バフ・デバフ特殊表示_狂化(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 狂化デバフ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 効果枠：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果枠"), 20, LEFT%
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}　　

@固有行動開始時処理_デバフ_狂化(バフ・デバフ行数)
#DIM バフ・デバフ行数

IF 戦闘行動キャラ < 10
	SIF キャラ一致チェック(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号, "[狂気なる地獄の妖精]クラウンピース")
		RETURN
	CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
ELSE
	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
ENDIF
アビテンプレート用_アビ名 = 攻撃
アビ汎用変数:効果割合 = 100
CALL アビテンプレート_通常攻撃処理

行動不能フラグ = 1


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1205(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨
;
KSTR:(K++) = □ 特性：『命を焚く松明』 □-------------------------------------------------------------------------------
KSTR:(K++) = 　狂化デバフによる行動制限を受けない（ステータス変化は受ける）。
;
CALL 口上変数表示(対象キャラ, 1)
