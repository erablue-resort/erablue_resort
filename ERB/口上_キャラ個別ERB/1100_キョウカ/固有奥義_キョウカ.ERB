;1100.キョウカ 固有奥義
;--------------------------------------------------
;奥義「コスモブルーフラッシュ」「コスモブループラネット」
;--------------------------------------------------
@固有奥義_1100(ARGS, キャラ番号)
#DIM  キャラ番号
#DIM  奥義威力
#DIMS 奥義名
#DIM  クリダメ補正値

; 陥落済 ＋ Lv40以上で強化
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	; 奥義威力:400％ → 550％
	; 追加：クリティカルダメージ率アップ(+25％,1.5倍→1.75倍)
	奥義名         = コスモブループラネット
	奥義威力       = 550
	クリダメ補正値 = 25
; 通常版
ELSE
	; 奥義威力:400％
	奥義名         = コスモブルーフラッシュ
	奥義威力       = 400
	クリダメ補正値 = 0
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％"
		SIF クリダメ補正値
			詳細文文字列受け渡し変数 += @"<br>◆この奥義のクリティカルダメージは＋{クリダメ補正値}％される"
		RETURN 奥義威力
	CASE "奥義前効果"
		; クリティカルダメージ補正値が0の場合は×
		SIF !クリダメ補正値
			RETURN
		; クリティカルダメージ補正値を増加
		アビ汎用変数:クリティカルダメージ補正値 = クリダメ補正値
	CASE "奥義追加効果"
		; 処理なし
ENDSELECT
