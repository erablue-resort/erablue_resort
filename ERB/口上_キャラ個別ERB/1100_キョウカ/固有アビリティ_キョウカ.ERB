;1100.キョウカ 固有アビ
;--------------------------------------------------
;1アビ「アイスランス」
;--------------------------------------------------
@固有アビ_1100_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビダメ倍率
#DIM クリダメ補正値

; 参考:ファイア
; MP20 威力200
; MP22 威力250
; MP25 威力300 横列
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 20
		アビ名 = アイスランス＋＋
		; 消費MP30 → 40
		; 単体に水属性300％ダメージ → 350％
		; 追加：自身のクリティカルダメージ率アップ(+50％,1.5倍→2.0倍)
		消費ＭＰ = 35
		アビダメ倍率   = 350
		; 追加効果:クリティカルダメージ率
		クリダメ補正値 = 50
	CASE IS >= 8
		アビ名 = アイスランス＋
		; 消費MP25 → 30
		; 単体に水属性250％ダメージ → 300％
		消費ＭＰ = 30
		アビダメ倍率   = 300
		; 追加効果:クリティカルダメージ率
		クリダメ補正値 = 0
	CASE IS >= 1
		アビ名 = アイスランス
		; 消費MP25
		; 単体に水属性250％ダメージ
		消費ＭＰ = 25
		アビダメ倍率   = 250
		; 追加効果:クリティカルダメージ率
		クリダメ補正値 = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ倍率   = 0
		クリダメ補正値 = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{アビダメ倍率}％］の水属性ダメージを与える。"
		SIF クリダメ補正値
			詳細文文字列受け渡し変数 += @"<br>このアビリティのクリティカルダメージ率は［＋{クリダメ補正値}％］として計算される。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		; 敵単体に水属性ダメージ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		; クリティカルダメージ補正値を増加
		SIF クリダメ補正値 != 0
			アビ汎用変数:クリティカルダメージ補正値 = クリダメ補正値
		アビ汎用変数:効果割合 = アビダメ倍率
		CALL アビテンプレート_ダメージ処理_水属性
ENDSELECT

;--------------------------------------------------
;2アビ「アクアウィング」
;--------------------------------------------------
@固有アビ_1100_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM アビダメ倍率
#DIM アビダメ回数
#DIM L_CNT

; 参考:クエイク
; MP20 威力100 2回
; MP25 威力140 2回
; MP35 威力150 3回
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 28
		アビ名 = アクアウィング＋＋
		; 消費MP40 → 55
		; ランダム対象に水属性140％ダメージ✕3回 → 150％✕4回
		消費ＭＰ = 55
		アビダメ倍率 = 150
		アビダメ回数 = 4
	CASE IS >= 16
		アビ名 = アクアウィング＋
		; 消費MP30 → 40
		; ランダム対象に水属性110％ダメージ✕3回 → 140％
		消費ＭＰ = 40
		アビダメ倍率 = 140
		アビダメ回数 = 3
	CASE IS >= 8
		アビ名 = アクアウィング
		; 消費30
		; ランダム対象に水属性110％ダメージ✕3回
		消費ＭＰ = 30
		アビダメ倍率 = 110
		アビダメ回数 = 3
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ倍率 = 0
		アビダメ回数 = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>ランダムな敵単体に［攻撃力✕{アビダメ倍率}％］の水属性ダメージを{アビダメ回数}回与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		; ターゲットに関わらずＸ回攻撃
		アビ汎用変数:効果割合 = アビダメ倍率
		; 死んだ対象を攻撃しないように1回づつ対象を再抽選
		; 途中で敵が全滅した場合については特に追記がなくても対応してくれる模様
		FOR L_CNT, 0, アビダメ回数
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_水属性
		NEXT
ENDSELECT

;--------------------------------------------------
;3アビ「マジックブースト」
;--------------------------------------------------
@固有アビ_1100_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビ効果1_効果割合
#DIM アビ効果1_効果ターン
#DIM アビ効果2_効果量
#DIM アビ効果2_効果ターン

SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 32
		アビ名 = マジックブースト＋＋
		; 消費MP30 → 40
		; 追加：自身にクリティカル率アップ効果(50％/3T)
		消費ＭＰ = 40
		; 効果1:攻撃力アップ
		アビ効果1_効果割合   = 30
		アビ効果1_効果ターン = 3
		; 効果2:クリティカル率アップ
		アビ効果2_効果量     = 50
		アビ効果2_効果ターン = 3
	CASE IS >= 24
		アビ名 = マジックブースト＋
		; 消費MP20 → 30
		; 自身に攻撃力アップ効果(20％/3T) → 30％
		消費ＭＰ = 30
		; 効果1:攻撃力アップ
		アビ効果1_効果割合   = 30
		アビ効果1_効果ターン = 3
		; 効果2:クリティカル率アップ
		アビ効果2_効果量     = 0
		アビ効果2_効果ターン = 0
	CASE IS >= 12
		アビ名 = マジックブースト
		; 消費MP20
		; 自身に攻撃力アップ効果(20％/3T)
		消費ＭＰ = 20
		; 効果1:攻撃力アップ
		アビ効果1_効果割合   = 20
		アビ効果1_効果ターン = 3
		; 効果2:クリティカル率アップ
		アビ効果2_効果量     = 0
		アビ効果2_効果ターン = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		; 効果1:攻撃力アップ
		アビ効果1_効果割合   = 0
		アビ効果1_効果ターン = 0
		; 効果2:クリティカル率アップ
		アビ効果2_効果量     = 0
		アビ効果2_効果ターン = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>自身に［{アビ効果1_効果割合}％］（独自枠・{アビ効果1_効果ターン}ターン）の攻撃力上昇効果を与える。"
		IF アビ効果2_効果量
			詳細文文字列受け渡し変数 += @"<br>［{アビ効果2_効果量}％］（独自枠・{アビ効果2_効果ターン}ターン）のクリティカル率上昇効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象設定
		CALL アビ対象選択テンプレート_自己のみ
		
		;攻撃力アップバフ
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合   = アビ効果1_効果割合
		アビ汎用変数:持続ターン = アビ効果1_効果ターン
		アビ汎用文字列:バフ・デバフ枠       = マジックブースト_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_バフ効果セット
		
		;クリティカル率アップバフ
		IF アビ効果2_効果量
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量     = アビ効果2_効果量
			アビ汎用変数:持続ターン = アビ効果2_効果ターン
			アビ汎用文字列:バフ・デバフ枠       = マジックブースト_クリティカル率
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
