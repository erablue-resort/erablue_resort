@固有アビ_3097_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 弱体成功率 = 90
#DIM 弱体効果量 = 5
#DIM 追加弱体フラグ = 0
#DIM 強化段階 = 0
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	強化段階 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	強化段階 = 1
ELSE
	強化段階 = 0
ENDIF

;レベル状況を変更するのは手間なので、アビの外に影響でない範囲でデバッグ用処理追加
[IF_DEBUG]
	強化段階 = 0
[ENDIF]

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 強化段階 >= 2
	アビ名 = マークス・オブ・キス＋＋
	追加弱体フラグ = 1
	弱体効果量 = 10
	弱体成功率 = 100
	消費ＭＰ = 180
ELSEIF 強化段階 == 1
	アビ名 = マークス・オブ・キス＋
	追加弱体フラグ = 1
	弱体効果量 = 5
	弱体成功率 = 100
	消費ＭＰ = 110
ELSE
	アビ名 = マークス・オブ・キス
	追加弱体フラグ = 0
	弱体効果量 = 5
	弱体成功率 = 90
	消費ＭＰ = 50
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵単体に［{弱体効果量}％］（独自枠・5ターン・消去不可）の<br>弱体耐性・攻撃力・防御力減少効果を与える。成功率{弱体成功率}％。"
		IF 追加弱体フラグ
			詳細文文字列受け渡し変数 += @"<br>［{弱体効果量}％］（独自枠・5ターン・消去不可）の回避率・行動速度減少効果を与える。成功率{弱体成功率}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = キスマーク（頬）
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:効果割合 = 弱体効果量
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 弱体成功率
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = キスマーク（臍）
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 弱体効果量
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 弱体成功率
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = キスマーク（腕）
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 弱体効果量
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:基礎成功確率 = 弱体成功率
		CALL アビテンプレート_不利効果_デバフ効果セット

		IF 追加弱体フラグ
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = キスマーク（首）
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 弱体効果量
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 弱体成功率
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = キスマーク（足）
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 弱体効果量
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 弱体成功率
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_3097_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 強化段階 = 0
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	強化段階 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	強化段階 = 1
ELSE
	強化段階 = 0
ENDIF

;レベル状況を変更するのは手間なので、アビの外に影響でない範囲でデバッグ用処理追加
[IF_DEBUG]
	強化段階 = 0
[ENDIF]

IF 強化段階 >= 2 
	アビ名 = クイックスイープ＋＋
	消費ＭＰ = 44
ELSEIF 強化段階 == 1 
	アビ名 = クイックスイープ＋
	消費ＭＰ = 36
ELSE
	アビ名 = クイックスイープ
	消費ＭＰ = 22
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 強化段階 >= 2 
			詳細文文字列受け渡し変数 = 消費ＭＰ：44
			詳細文文字列受け渡し変数 += "<br>敵ランダム3体に［攻撃力✕150％］の自属性ダメージを与える。"
		ELSEIF 強化段階 == 1 
			詳細文文字列受け渡し変数 = 消費ＭＰ：36
			詳細文文字列受け渡し変数 += "<br>敵ランダム3体に［攻撃力✕140％］の自属性ダメージを与える。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：22
			詳細文文字列受け渡し変数 += "<br>敵ランダム3体に［攻撃力✕120％］の自属性ダメージを与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 強化段階 >= 2 
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
			アビ汎用変数:効果割合 = 150
			アビ汎用文字列:攻撃側使用能力値 = 攻撃力
			CALL アビテンプレート_ダメージ処理_自属性
		ELSEIF 強化段階 == 1 
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
			アビ汎用変数:効果割合 = 140
			アビ汎用文字列:攻撃側使用能力値 = 攻撃力
			CALL アビテンプレート_ダメージ処理_自属性
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
			アビ汎用変数:効果割合 = 120
			アビ汎用文字列:攻撃側使用能力値 = 攻撃力
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF
ENDSELECT

@固有アビ_3097_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 強化段階 = 0
#DIM 効果成功率 = 80
#DIM ディスペル回数 = 1
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	強化段階 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	強化段階 = 1
ELSE
	強化段階 = 0
ENDIF

;レベル状況を変更するのは手間なので、アビの外に影響でない範囲でデバッグ用処理追加
[IF_DEBUG]
	強化段階 = 0
[ENDIF]

IF 強化段階 >= 2 
	アビ名 = フラートウィンク＋＋
	消費ＭＰ = 150
	効果成功率 = 100
	ディスペル回数 = 2
ELSEIF 強化段階 == 1 
	アビ名 = フラートウィンク＋
	消費ＭＰ = 100
	効果成功率 = 100
	ディスペル回数 = 1
ELSE
	アビ名 = フラートウィンク
	消費ＭＰ = 60
	効果成功率 = 80
	ディスペル回数 = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵全体に［1］のスロウ効果を与える。成功率{効果成功率}％。"
		詳細文文字列受け渡し変数 += @"<br>［{ディスペル回数}］のバフ消去を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:基礎成功確率 = 効果成功率
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
		
		CALL アビ汎用変数文字列リセット
		FOR LOCAL, 0, ディスペル回数
			CALL アビテンプレート_不利効果_バフ消去
		NEXT

ENDSELECT
 

@固有戦闘開始時処理_キャラ_3097
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 半淫魔の情欲
アビ汎用文字列:バフ・デバフ対象能力 = 半淫魔の情欲
アビ汎用変数:効果割合 = 0
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 半淫魔の情欲_弱体成功率
アビ汎用文字列:バフ・デバフ対象能力 = 弱体成功率
アビ汎用変数:効果割合 = 0
CALL アビテンプレート_有利効果_付随バフ効果セット("半淫魔の情欲")

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 死と瘴気の魔女
アビ汎用文字列:バフ・デバフ対象能力 = 死と瘴気の魔女
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 死と瘴気の魔女_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:効果割合 = 20
アビ汎用文字列:付随先_隊列設定 = {戦闘行動キャラ}
CALL アビテンプレート_有利効果_付随バフ効果セット("死と瘴気の魔女")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 死と瘴気の魔女_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 0
アビ汎用文字列:付随先_隊列設定 = {戦闘行動キャラ}
CALL アビテンプレート_有利効果_付随バフ効果セット("死と瘴気の魔女")

CALL アビテンプレ変数リセット

@固有行動開始時処理_バフ_死と瘴気の魔女(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM カワイイスパイス番号
#DIM キスマーク付与済みフラグ
#DIM キスマーク付与済みバフ効果量
#DIM 対象番号
#DIM 弱体存在 = 0
#DIM 攻撃力
#DIM 防御力

カワイイスパイス番号 = GETCHARA(3097)

;死と瘴気の魔女
FOR 対象番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
		CONTINUE
	IF 弱体存在判定(対象番号)
		弱体存在 = 1
	ENDIF
NEXT

;力技。今後キスマーク増えた時にどうするの問題ある
;特殊表示するのが丸そうだけど一旦これで
キスマーク付与済みフラグ = デバフ所持者検索_枠("キスマーク（頬）") || デバフ所持者検索_枠("キスマーク（臍）") || デバフ所持者検索_枠("キスマーク（腕）") 
キスマーク付与済みフラグ = キスマーク付与済みフラグ || デバフ所持者検索_枠("キスマーク（首）") || デバフ所持者検索_枠("キスマーク（足）") 

IF 戦闘行動キャラ == カワイイスパイス番号
	;半淫魔の情欲
	;サブメンバーでもいい
	;ここに来たらカワイイスパイスが戦闘行動キャラだと確定するけどもっと良い書き方あるかも
	IF キスマーク付与済みフラグ
		キスマーク付与済みバフ効果量 = 5
	ELSE
		キスマーク付与済みバフ効果量 = 0
	ENDIF
	DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "効果量_固定値", キスマーク付与済みバフ効果量
	;死と瘴気の魔女
	;ピュリファイアは味方バフ。こっちは自己バフ
	防御力 = バフ番号取得_枠("死と瘴気の魔女_防御力", 戦闘行動キャラ)
	攻撃力 = バフ番号取得_枠("死と瘴気の魔女_攻撃力", 戦闘行動キャラ)

	IF 弱体存在
		DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 0
		DT_CELL_SET "戦闘効果データベース", 攻撃力, "効果量_割合", 20
	ELSE
		DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 20
		DT_CELL_SET "戦闘効果データベース", 攻撃力, "効果量_割合", 0
	ENDIF
ENDIF


@バフ・デバフ特殊表示_死と瘴気の魔女(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 死と瘴気の魔女
PRINTFORML すべての敵が弱体効果を受けていない時、自身の防御力アップ/いずれかの敵が弱体効果を受けている時、自身の攻撃力アップ
PRINTFORML 持続ターン：永続



@バフ・デバフ特殊表示_半淫魔の情欲(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 半淫魔の情欲
PRINTFORML いずれかの敵にキスマークが付いている場合、自身の弱体成功率アップ
PRINTFORML 持続ターン：永続

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_3097(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『死と瘴気の魔女』 □---------------------------------------------------------------
KSTR:(K++) = 　すべての敵が弱体効果を受けていない時、自身の防御力アップ
KSTR:(K++) = 　いずれかの敵が弱体効果を受けている時、自身の攻撃力アップ
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『半淫魔の情欲』 □-----------------------------------------------------------------
KSTR:(K++) = 　いずれかの敵にキスマークが付いている場合、自身の弱体成功率アップ

CALL 口上変数表示(対象キャラ, 3097)