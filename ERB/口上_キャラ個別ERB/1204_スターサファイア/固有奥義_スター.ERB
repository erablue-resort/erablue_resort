@固有奥義_1204(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値
#DIM 三妖精奥義フラグ, 3
#DIM 三妖精隊列, 3
#DIM 攻撃数値
#DIM 敵番号
#DIM ループ用
#DIM 一時保存用_回避フラグ
#DIMS 消去枠

VARSET 三妖精奥義フラグ, 0
SIF キャラ隊列検索(キャラ番号) >= 0
	CALL 三妖精奥義発動確認(三妖精奥義フラグ)

;他の三妖精が先に奥義を使用している場合
IF 三妖精奥義フラグ:0 && 三妖精奥義フラグ:1
	CALL 三妖精隊列検索(三妖精隊列)

	;効果が強くなっても名前は同じ
	奥義名 = スリーフェアリーズ
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号) && 三妖精奥義フラグ:0 >= 2 && 三妖精奥義フラグ:1 >= 2
		奥義威力 = 400
		効果数値 = 100
	ELSE
		奥義威力 = 300
		効果数値 = 0
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%たちのスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 「スリーフェアリーズ」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵全体に三妖精の合計攻撃力による自属性のダメージを与える。
			詳細文文字列受け渡し変数 += "<br>対象のバフ効果を全て消去する。"
			IF 効果数値 > 0
				詳細文文字列受け渡し変数 += @"<br>味方全体の命中率を永続的に［{効果数値}％/独自枠］増加させる。"
			ENDIF
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			;奥義使用先登録だけ先にしておく
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			CALL アビテンプレート_何もしない

			攻撃数値 = 0
			IF INRANGE(三妖精隊列:0, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:0)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:0):攻撃力
			ENDIF
			IF INRANGE(三妖精隊列:1, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:1)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:1):攻撃力
			ENDIF
			FOR 敵番号, 10, 20
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE

				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = 攻撃力_スリーフェアリーズ
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用文字列:バフ・デバフ名 = スリーフェアリーズ
				アビ汎用変数:効果量 = 攻撃数値
				アビ汎用変数:持続行動回数 = 1
				アビ汎用変数:特殊表示オプション = -1
				CALL アビテンプレート_有利効果_バフ効果セット

				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_指定(敵番号)
				アビ汎用変数:効果割合 = 奥義威力
				アビ汎用文字列:ダメージ種類 = 奥義ダメージ
				CALL アビテンプレート_ダメージ処理_自属性

				CALL アビ汎用変数文字列リセット
				CALL アビテンプレート_不利効果_バフ全て消去
			NEXT
			IF 効果数値 > 0
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_味方全体_生者のみ
				アビ汎用文字列:バフ・デバフ枠 = 命中率_スリーフェアリーズ
				アビ汎用文字列:バフ・デバフ対象能力 = 命中率
				アビ汎用変数:効果量 = 効果数値
				アビ汎用変数:持続ターン = -1
				アビ汎用変数:消去不可オプション = 1
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
ELSE
	;効果が強くなっても名前は同じ
	奥義名 = スターストーム
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 14
		奥義威力 = 130
		効果数値 = 4
	ELSE
		奥義威力 = 130
		効果数値 = 2
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 星光「スターストーム」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵単体に自属性のダメージを{効果数値 + 1}回与える。
			詳細文文字列受け渡し変数 += @"<br>対象のバフ効果を命中回数ぶん消去する。"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				詳細文文字列受け渡し変数 += "<br>味方全体の命中率を5ターン［100％/独自枠］増加させる。"
			ENDIF
			RETURN 奥義威力
		CASE "奥義前効果"
		CASE "奥義追加効果"
			一時保存用_回避フラグ = アビテンプレート用_回避フラグ
			FOR ループ用, 0, 効果数値
				IF アビテンプレート用_回避フラグ
					アビテンプレート用_回避フラグ = 0
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビテンプレート_不利効果_バフ消去
				ENDIF
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果割合 = 奥義威力
				アビ汎用文字列:ダメージ種類 = 奥義ダメージ
				CALL アビテンプレート_ダメージ処理_自属性
				一時保存用_回避フラグ = (一時保存用_回避フラグ || アビテンプレート用_回避フラグ)
			NEXT
			IF !アビテンプレート用_回避フラグ
				CALL アビ汎用変数文字列リセット
				CALL アビテンプレート_不利効果_バフ消去
			ENDIF
			アビテンプレート用_回避フラグ = 一時保存用_回避フラグ

			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_味方全体_生者のみ
				アビ汎用文字列:バフ・デバフ枠 = 命中率_スターストーム
				アビ汎用文字列:バフ・デバフ対象能力 = 命中率
				アビ汎用変数:効果量 = 100
				アビ汎用変数:持続ターン = 5
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
ENDIF
