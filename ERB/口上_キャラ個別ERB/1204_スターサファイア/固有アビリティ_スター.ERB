@固有アビ_1204_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 3
#DIM 効果回数
#DIM 味方番号

;効果が強くなっても名前は同じ
アビ名 = スターライトレイン
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	消費ＭＰ = 25
	効果数値:0 = 8
	効果数値:1 = 80
	効果数値:2 = 90
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	消費ＭＰ = 15
	効果数値:0 = 6
	効果数値:1 = 60
	効果数値:2 = 80
ELSE
	消費ＭＰ = 10
	効果数値:0 = 4
	効果数値:1 = 50
	効果数値:2 = 75
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 星符「スターライトレイン」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>ランダムな敵味方{効果数値:0}体を選び、味方ならＨＰを［回復力✕{効果数値:1}％］回復させる。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
			詳細文文字列受け渡し変数 += "<br>さらにデバフ効果を1つ消去する。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>敵なら光属性の［回復力✕{効果数値:2}％］ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += "<br>さらに連続攻撃率を3ターン［-30％/B枠］する。基礎成功率70％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		効果回数 = 0
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
				CONTINUE
			効果回数 ++
		NEXT
		効果回数 = MIN(効果回数, RAND(効果数値:0))
		IF 効果回数 >= 0
			CALL アビ対象選択テンプレート_味方ランダムＸ体_重複無し(効果回数)
			アビ汎用変数:効果割合 = 効果数値:1
			CALL アビテンプレート_回復処理_MAXでも回復
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
				CALL アビ汎用変数文字列リセット
				CALL アビテンプレート_有利効果_デバフ消去
			ENDIF
			CALL アビ汎用変数文字列リセット
		ENDIF
		効果回数 = 効果数値:0 - 効果回数
		IF 効果回数 >= 0
			CALL アビ対象選択テンプレート_敵ランダムＸ体(効果回数)
			アビ汎用変数:効果割合 = 効果数値:2
			アビ汎用文字列:攻撃側使用能力値 = 回復力
			CALL アビテンプレート_ダメージ処理_光属性
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果量 = 30
				アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率B
				アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
				アビ汎用変数:持続ターン = 3
				アビ汎用変数:基礎成功確率 = 70
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF
		ENDIF
ENDSELECT


@固有アビ_1204_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = スプリンクルピース
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	消費ＭＰ = 30
	効果数値:0 = 110
	効果数値:1 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	消費ＭＰ = 20
	効果数値:0 = 90
	効果数値:1 = 1
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	消費ＭＰ = 15
	効果数値:0 = 70
	効果数値:1 = 1
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 星粒「スプリンクルピース」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
			詳細文文字列受け渡し変数 += "<br>対象の被ダメージを3ターン［15％］増加させる。基礎成功率80％。"
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			詳細文文字列受け渡し変数 += @"<br>味方全体に{効果数値:1}回の弱体無効効果と1回のディスペルガード効果を与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>味方全体に{効果数値:1}回の弱体無効効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 被ダメージ上昇
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = 15
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続回数 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
			アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1204_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = シューティングサファイア
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	消費ＭＰ = 40
	効果数値:0 = 200
	効果数値:1 = 35
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	消費ＭＰ = 30
	効果数値:0 = 150
	効果数値:1 = 20
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	消費ＭＰ = 20
	効果数値:0 = 100
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 星符「シューティングサファイア」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵単体に土と光属性の［攻撃力✕{効果数値:0}％］ダメージを1回ずつ与える。"
		詳細文文字列受け渡し変数 += "<br>対象のバフ効果を1つ消去する。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			詳細文文字列受け渡し変数 += @"<br>対象の攻撃力を5ターン［{効果数値:1}％/B枠］減少させる。基礎成功率90％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_土属性
		CALL アビテンプレート_ダメージ処理_光属性

		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_不利効果_バフ消去
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 効果数値:1
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1204_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM バフ番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
	アビ名 = 静かなる輝き＋
	消費ＭＰ = 10
	効果数値:0 = 50
ELSE
	アビ名 = 静かなる輝き
	消費ＭＰ = 5
	効果数値:0 = 30
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は動く物の気配を探る程度の能力を使った！
アビ汎用文字列:実行時メッセージ２ = %アビ名%！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に次の行動時まで回避率が［-{効果数値:0}％］される効果を与える。"
		詳細文文字列受け渡し変数 += "<br>この効果は対象の弱体耐性を無視し、他の回避バフ・デバフの影響を無効化する。"
		詳細文文字列受け渡し変数 += "<br>自身の次の行動がターンの最後になる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 静かなる輝き
		アビ汎用文字列:バフ・デバフ対象能力 = 静かなる輝き
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続行動回数 = 1
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		バフ番号 = バフ番号取得_枠("少女待機中", 戦闘行動キャラ)
		IF バフ番号 >= 0
			DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", 2
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 少女待機中
			アビ汎用文字列:バフ・デバフ対象能力 = 少女待機中
			アビ汎用文字列:バフ・デバフ名 = 少女待機中
			アビ汎用変数:持続行動回数 = 2
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@固有バフ・デバフ処理_デバフ_静かなる輝き_回避率(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN MAX(0, BATTLE_STATE:隊列番号:回避率 - DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"))
ELSE
	RETURN MAX(0, 敵BATTLE_STATE:(隊列番号 - 10):回避率 - DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"))
ENDIF

@バフ・デバフ特殊表示_静かなる輝き(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 回避率デバフ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 効果量：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"), 4}　　
PRINTFORML 持続行動回数：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続行動回数")}　　


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1204(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『光の三妖精』 □-------------------------------------------------------------------------------
KSTR:(K++) = 　サニーおよびルナが同一ターンに先に奥義を発動していると、特殊な奥義が使用可能

CALL 口上変数表示(対象キャラ, 1)
