
@固有アビ_92_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 紅陣＋＋
	消費ＭＰ = 50
	効果数値 = 250
	効果数値２ = 40
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = 紅陣＋
	消費ＭＰ = 25
	効果数値 = 180
	効果数値２ = 30
ELSE
	アビ名 = 紅陣
	消費ＭＰ = 15
	効果数値 = 150
	効果数値２ = 20
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕{効果数値}％］の自属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>自身に［{効果数値２}％］の奥義ゲージ増加を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 効果数値２
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT
RETURN RESULT


@固有アビ_92_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果回数
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = 陽炎＋＋
	効果数値 = 200
	効果回数 = 5
	消費ＭＰ = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 陽炎＋
	効果数値 = 150
	効果回数 = 4
	消費ＭＰ = 35
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 陽炎
	効果数値 = 100
	効果回数 = 3
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（{効果回数}回）の炎鳴流・陽炎効果を与える。
			詳細文文字列受け渡し変数 += @"<br>（炎鳴流・陽炎："
			詳細文文字列受け渡し変数 += @"<br>　攻撃を受けた時、自身の攻撃力を［累積10％/最大100％］（累積枠）増加させる。）"
			詳細文文字列受け渡し変数 += @"<br>　攻撃してきた敵に［攻撃力✕{効果数値}％］ダメージを３回与える。）"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［攻撃力✕{効果数値}％］（{効果回数}回）のカウンター効果を与える。
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 炎鳴流・陽炎
			アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用変数:持続回数 = 効果回数
			CALL アビテンプレート_有利効果_バフ効果セット
		ELSE
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = カウンター
			アビ汎用文字列:バフ・デバフ対象能力 = カウンター
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用変数:持続回数 = 効果回数
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
RETURN RESULT

@特殊反応_被ダメ時_炎鳴流・陽炎(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM バフ番号
#DIM ダメージ回数
#DIM レベル記録

アビテンプレート用_アビ名 = 炎鳴流・陽炎

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 攻撃力累積
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:累積上限_割合 = 100
CALL アビテンプレート_有利効果_累積バフ効果セット

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(攻撃者隊列)

レベル記録 = 基礎BATTLE_STATE:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):Lv

IF レベル記録 >= 28
	アビ汎用変数:効果割合 = 200
ELSE
	アビ汎用変数:効果割合 = 150
ENDIF
IF 戦闘中カウント:戦闘行動キャラ:自由枠１ >= 2
	IF レベル記録 >= 32
		アビ汎用変数:効果割合 += 100
	ELSEIF レベル記録 >= 24
		アビ汎用変数:効果割合 += 70
	ELSE
		アビ汎用変数:効果割合 += 50
	ENDIF
ENDIF
ダメージ回数 = 3
IF 戦闘中カウント:戦闘行動キャラ:自由枠１ >= 4
	IF レベル記録 >= 32
		ダメージ回数 += 3
	ELSEIF レベル記録 >= 24
		ダメージ回数 += 2
	ELSE
		ダメージ回数 += 1
	ENDIF
ENDIF
FOR LOCAL, 0, ダメージ回数
	CALL アビテンプレート_ダメージ処理_自属性
NEXT
戦闘中カウント:戦闘行動キャラ:自由枠１ = MIN(戦闘中カウント:戦闘行動キャラ:自由枠１ + 1, 5)
CALL 絶えない闘志効果更新

@固有アビ_92_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

消費ＭＰ = 0
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 華焔＋＋
	消費ＭＰ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 華焔＋
	消費ＭＰ = 70
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 華焔
	消費ＭＰ = 50
ELSE
	アビ名 = 
ENDIF

アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［100％］（独自枠・1ターン）の攻撃力上昇効果を与える。
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			詳細文文字列受け渡し変数 += @"<br>（通常枠・1ターン）の単体かばう効果を与える。"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
				詳細文文字列受け渡し変数 += @"<br>［100％］（独自枠・1ターン）の回復力上昇効果を与える。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 華焔_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 単体かばう
			アビ汎用文字列:バフ・デバフ対象能力 = 単体かばう
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット


			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 華焔_回復力
				アビ汎用文字列:バフ・デバフ対象能力 = 回復力
				アビ汎用変数:効果割合 = 100
				アビ汎用変数:持続ターン = 1
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
		ENDIF
ENDSELECT
RETURN RESULT

@絶えない闘志効果更新
#DIM レベル記録
#DIM 効果数値
#DIM 効果数値２
#DIM 効果数値３
レベル記録 = 基礎BATTLE_STATE:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号):Lv

IF レベル記録 >= 32
	効果数値 = 50
	効果数値２ = 100
	効果数値３ = 100
ELSEIF レベル記録 >= 24
	効果数値 = 40
	効果数値２ = 70
	効果数値３ = 70
ELSEIF レベル記録 >= 16
	効果数値 = 30
	効果数値２ = 50
	効果数値３ = 50
ENDIF

;防御バフ
IF 戦闘中カウント:戦闘行動キャラ:自由枠１ >= 1
	IF バフ番号取得_枠("炎鳴流・陽炎_防御力", 戦闘行動キャラ) < 0
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 炎鳴流・陽炎_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
	ENDIF
ENDIF

;ダメ倍率は炎鳴流・陽炎処理の内部に記載

;発動時回復
IF 戦闘中カウント:戦闘行動キャラ:自由枠１ >= 3
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果割合 = 効果数値２
	CALL アビテンプレート_回復処理_MAXでも回復
ENDIF

;ダメ回数は炎鳴流・陽炎処理の内部に記載

;弱体耐性バフ
IF 戦闘中カウント:戦闘行動キャラ:自由枠１ == 5
	IF バフ番号取得_枠("炎鳴流・陽炎_弱体耐性", 戦闘行動キャラ) < 0
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 炎鳴流・陽炎_弱体耐性
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:効果割合 = 効果数値３
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
	ENDIF
ENDIF


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_92(対象キャラ)
#DIM 対象キャラ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果数値３
#DIM 効果回数
CALL 口上変数初期化

SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
	CASE IS >= 32
		効果数値 = 50
		効果数値２ = 100
		効果数値３ = 100
		効果回数 = 3
	CASE IS >= 24
		効果数値 = 40
		効果数値２ = 70
		効果数値３ = 70
		効果回数 = 2
	CASE IS >= 16
		効果数値 = 30
		効果数値２ = 50
		効果数値３ = 50
		効果回数 = 1
	CASEELSE
		RETURN 0
ENDSELECT

KSTR:(K++) = □ 特性：『絶えない闘志』 □---------------------------------------------------------------
KSTR:(K++) = 　炎鳴流・陽炎発動時、絶えない闘志Lvが１増加する。初期値は０。
KSTR:(K++) = 　Lv1：防御力{効果数値}％UP
KSTR:(K++) = 　Lv2：炎鳴流・陽炎のダメージ倍率+{効果数値３}％
KSTR:(K++) = 　Lv3：炎鳴流・陽炎の発動時ＨＰ回復［回復力✕{効果数値２}％］"
KSTR:(K++) = 　Lv4：炎鳴流・陽炎のダメージ回数＋{効果回数}
KSTR:(K++) = 　Lv5：弱体耐性{効果数値３}％UP）

CALL 口上変数表示(対象キャラ, 1)
