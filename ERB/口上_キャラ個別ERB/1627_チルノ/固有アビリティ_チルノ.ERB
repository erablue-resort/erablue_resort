@固有アビ_1627_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM EASYフラグ
#DIM 攻撃対象一時退避
#DIM 敵番号
#DIM 対象数

;効果が強くなっても名前は同じ
アビ名 = アイシクルフォール
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	消費ＭＰ = 25
	効果数値:0 = 320
	EASYフラグ = 0
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	消費ＭＰ = 15
	効果数値:0 = 280
	EASYフラグ = 0
ELSE
	消費ＭＰ = 10
	効果数値:0 = 160
	EASYフラグ = 1
ENDIF

IF 装備ステータス補正保存:キャラ番号:属性 == 11
	;装備属性が水なら、威力が上昇する
	効果数値:0 += 効果数値:0 / 100 * 10
ELSEIF 装備ステータス補正保存:キャラ番号:属性 != 0
	;それ以外の属性装備をしているなら、正面安地になる
	EASYフラグ = 1
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 氷符「アイシクルフォール」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF EASYフラグ
			詳細文文字列受け渡し変数 += @"<br>敵１体以外全体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF EASYフラグ
			CALL アビ対象選択テンプレート_敵単体
			SIF アビテンプレート用_キャンセルフラグ
				RETURN -1
			攻撃対象一時退避 = アビテンプレート用_対象保存:0
		ELSE
			攻撃対象一時退避 = -1
		ENDIF

		対象数 = 0
		FOR 敵番号, 10, 20
			SIF 敵番号 == 攻撃対象一時退避
				CONTINUE
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			CALL アビ対象選択テンプレート_指定(敵番号, 対象数)
			対象数 ++
		NEXT
		アビテンプレート用_全体フラグ = 1

		IF 対象数 > 0
			アビ汎用変数:効果割合 = 効果数値:0
			CALL アビテンプレート_ダメージ処理_自属性
		ELSE
			アビ汎用文字列:実行時メッセージ３ = しかし、%CALLNAME:キャラ番号%の攻撃は誰にも当たらなかった……
			CALL アビテンプレート_何もしない
		ENDIF
ENDSELECT


@固有アビ_1627_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 4
#DIM ループ用

;効果が強くなっても名前は同じ
アビ名 = コールドディヴィニティー
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	消費ＭＰ = 30
	効果数値:0 = 140
	効果数値:1 = 7
	効果数値:2 = 10
	効果数値:3 = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	消費ＭＰ = 20
	効果数値:0 = 120
	効果数値:1 = 4
	効果数値:2 = 10
	効果数値:3 = 40
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	消費ＭＰ = 15
	効果数値:0 = 80
	効果数値:1 = 4
	効果数値:2 = 5
	効果数値:3 = 30
ELSE
	アビ名 = 
ENDIF

IF 装備ステータス補正保存:キャラ番号:属性 == 11
	;装備属性が水なら、威力が上昇する
	効果数値:0 += 10
ELSEIF 装備ステータス補正保存:キャラ番号:属性 != 0
	;それ以外の属性装備をしているなら、行動速度デバフの累積上限が少なくなる
	効果数値:3 -= 20
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 凍符「コールドディヴィニティー」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>ランダムな敵に自属性の［攻撃力✕{効果数値:0}％］ダメージを{効果数値:1}回与える。"
		詳細文文字列受け渡し変数 += @"<br>対象の行動速度を3ターン［{効果数値:2}％/独自枠］減少させる。基礎成功率100％。"
		詳細文文字列受け渡し変数 += @"<br>この行動速度減少は［{効果数値:3}％］まで累積できる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR ループ用, 0, 効果数値:1
			SIF ループ用 > 0
				CALL アビ汎用変数文字列リセット

			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 効果数値:0
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 凍符_行動速度
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 効果数値:2
			アビ汎用変数:累積上限_割合 = 効果数値:3
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		NEXT
ENDSELECT


@固有アビ_1627_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = マイナスＫ
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	消費ＭＰ = 50
	効果数値:0 = 200
	効果数値:1 = 70
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	消費ＭＰ = 45
	効果数値:0 = 160
	効果数値:1 = 70
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	消費ＭＰ = 40
	効果数値:0 = 120
	効果数値:1 = 50
ELSE
	アビ名 = 
ENDIF

IF 装備ステータス補正保存:キャラ番号:属性 == 11
	;装備属性が水なら、基礎成功率が上昇する
	効果数値:1 += 10
ELSEIF 装備ステータス補正保存:キャラ番号:属性 != 0
	;それ以外の属性装備をしているなら、氷結威力が3/4になる
	効果数値:0 = 効果数値:0 * 3 / 4
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 凍符「マイナスＫ」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に5ターン氷結［攻撃力✕{効果数値:0}％］効果を与える。基礎成功率{効果数値:1}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 氷結
		アビ汎用文字列:バフ・デバフ対象能力 = 氷結
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値:0 / 100
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 効果数値:1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@固有アビ_1627_4A(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2
#DIM 攻撃対象一時退避

IF 装備ステータス補正保存:キャラ番号:属性 == 10
	;火属性になっている場合、このアビリティは使用できない
	アビ名 = 
ELSEIF GROUPMATCH(装備ステータス補正保存:キャラ番号:属性, 0, 11) && 基礎BATTLE_STATE:キャラ番号:Lv >= 50 && 陥落チェック(キャラ番号)
	;水属性のときのみレベルと陥落で強化される
	アビ名 = ソードフリーザー
	消費ＭＰ = 99
	効果数値:0 = 425
	効果数値:1 = 999

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は冷気を操る程度の能力を使い、氷の剣を作り出した！
	アビ汎用文字列:実行時メッセージ２ = 氷符「ソードフリーザー」！
ELSE
	アビ名 = アイシクルソード
	消費ＭＰ = 9
	効果数値:0 = 200
	効果数値:1 = 99

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は冷気を操る程度の能力を使い、氷の剣を作り出した！
	アビ汎用文字列:実行時メッセージ２ = アイシクルソード！
ENDIF

IF 装備ステータス補正保存:キャラ番号:属性 == 11
	;装備属性が水なら、威力が上昇する
	効果数値:0 += 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵単体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>この攻撃のクリティカルダメージ率は［+{効果数値:1}％］される。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:クリティカルダメージ補正値 = 効果数値:1
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT


@固有アビ_1627_4B(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM クールタイム
#DIM 攻撃対象一時退避

IF 装備ステータス補正保存:キャラ番号:属性 == 10 && 基礎BATTLE_STATE:キャラ番号:Lv >= 50 && 陥落チェック(キャラ番号)
	;火属性になっているとき限定のアビリティ
	アビ名 = クールサンフラワー
	消費ＭＰ = 99
	効果数値:0 = 350
ELSE
	アビ名 = 
ENDIF

;クールタイム確認
IF ダンジョン表示モード == "戦闘画面" && INRANGE(戦闘行動キャラ, 0, 3) && 戦闘中カウント:戦闘行動キャラ:0 > 0
	クールタイム = MAX(0, 3 + 戦闘中カウント:戦闘行動キャラ:0 - 現在ターン数)
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
ELSE
	;戦闘中以外、または一度も使用していない場合はクールタイムは0
	クールタイム = 0
ENDIF

アビ汎用文字列:実行時メッセージ１ = 夏の魔力を受け、%CALLNAME:キャラ番号%の力が強まる！
アビ汎用文字列:実行時メッセージ２ = 日焼けした%CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ３ = 氷符「クールサンフラワー」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += "<br>自身に2ターンのダメージカット［9999］効果を与える。"
		詳細文文字列受け渡し変数 += "<br>発動後3ターン、このアビリティは再度使用できない。"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"（残り{クールタイム}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		戦闘中カウント:戦闘行動キャラ:0 = 現在ターン数

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果量 = 9999
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1627(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨
;
KSTR:(K++) = □ 特性：『雪ん娘』 □-------------------------------------------------------------------------------
KSTR:(K++) = 　水属性の耳飾りを装備すると、アビリティ・奥義の性能が上昇する。
KSTR:(K++) = 　水属性以外の耳飾りを装備すると、アビリティ・奥義の性能が低下する。
;
CALL 口上変数表示(対象キャラ, 1)
