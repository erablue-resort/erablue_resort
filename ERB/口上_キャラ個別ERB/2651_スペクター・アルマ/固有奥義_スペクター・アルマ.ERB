
;☆元にした原作スキル
;「八卦・六道輪廻」
;指定範囲内にいる者を対象とし
;ＨＰ大回復効果とＨＰ、ＭＰ、ＳＰ回復力上昇効果を付与させ、
;さらには行動不能状態から蘇らせることができる

;奥義にふさわしい大技、のように見えて割と気軽に連発できるものでした
;こちらで範囲蘇生をばら撒くわけにはいかない、けど全体回復＋蘇生ではあってほしいので
;「続けて発動させると自分だけ死ぬ」感じにしました
;（回復力上昇はとりあえずオミット）
;でもサポアビの起点にもなるから適度に使ってね！


@固有奥義_2651(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力 = 0
#DIMS 奥義名
#DIM 奥義ランク
#DIM 回復威力
#DIM 味方番号
#DIM 蘇生

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義ランク = 2
ELSE
	奥義ランク = 1
ENDIF

SELECTCASE 奥義ランク
	CASE IS >= 2
		奥義名 = 八卦・六道輪廻＋
		回復威力 = 100
	CASEELSE
		奥義名 = 八卦・六道輪廻
		回復威力 = 80
ENDSELECT

;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：なし
		詳細文文字列受け渡し変数 += @"<br>自身のＨＰを[100％]回復させる。"
		詳細文文字列受け渡し変数 += @"<br>味方全体のＨＰを［回復力✕{回復威力}％］回復させ、戦闘不能の味方をＨＰ[5％]で蘇生させる。"
		詳細文文字列受け渡し変数 += @"<br>自身に[100％]（独自枠・4ターン・消去不可）のアンデッド効果を与える。"
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる

		;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
		;必要な場合を除きここでリセット
		CALL アビ汎用変数文字列リセット
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載
		
		;自己回復 奥義によるアンデッド状態で使うと死にます
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:回復側使用能力値 = 最大ＨＰ
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_回復処理_MAXでも回復

		;全体回復
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 回復威力
		CALL アビテンプレート_回復処理_MAXでも回復

		;蘇生 対象がいないとエラーになるので確認
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
			;一人でもＨＰ0なら蘇生を行う
				蘇生 = 1
		NEXT
		IF 蘇生 == 1
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_死者のみ
			アビ汎用変数:蘇生可能オプション = 1
			アビ汎用文字列:回復側使用能力値 = 回復対象_最大ＨＰ
			アビ汎用変数:数値乱数幅 = 0
			アビ汎用変数:効果割合 = 5
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 八卦・六道輪廻
		アビ汎用文字列:バフ・デバフ対象能力 = アンデッド
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT
