
;1アビ
;☆元にした原作スキル
;「ぐるぐるどーん！」
;自分の周囲に重力場を発生させながら武器を振り回し、自分の周囲を攻撃する
;確率により受けた対象は混乱状態になる

;混乱効果は3アビに集中させオミット
;混乱状態の敵にダメージ上昇とかあってもよかったかも
;とりあえず消費はやや軽くしたので適当にぐるぐるさせてください

@固有アビ_2651_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 対象数 = 2
#DIM 最大追加数
#DIM アビランク

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビランク = 2
ELSE
	アビランク = 1
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = ぐるぐるどーん！＋
		消費ＭＰ = 40
		威力値 = 220
		最大追加数 = 3
	CASEELSE
		アビ名 = ぐるぐるどーん！
		消費ＭＰ = 50
		威力値 = 200
		最大追加数 = 2
ENDSELECT


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>ランダムな敵2～{2 + 最大追加数}体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象数の増加判定
		;単純に50％判定を2～3回
		FOR LOCAL, 0, 最大追加数
			SIF RAND:2 < 1
				対象数 ++
		NEXT

		CALL アビ汎用変数文字列リセット
		;たぶん適当に振り回してるので敵対心無視
		アビ汎用変数:敵対心無視オプション = 1
		CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(対象数)
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT


;2アビ
;☆元にした原作スキル
;「とりこにしてあげる！」
;対象の集中力を乱し魔法攻撃力を低下させる

;本来やりたかったであろう魅了効果に変更（※ECOの『魅了』は違う用途でした）
;単体指定なので強度と成功率は若干上げたつもり

;「ふへへっ逃がさないよ？」
;対象の敵を「混乱」させ、一定時間敵の防御力を低下させる

;＋で付く防御デバフがこれのつもり（混乱はオミット）

@固有アビ_2651_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビランク = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = とりこにしてあげる！＋
		消費ＭＰ = 35
	CASE 1
		アビ名 = とりこにしてあげる！
		消費ＭＰ = 40
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵単体に［40％］（通常枠・2ターン）の魅了効果を与える。成功率70％。"
		SIF アビランク >= 2
			詳細文文字列受け渡し変数 += @"<br>［累積10％最大30％・5ターン］の防御力減少効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 40
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:基礎成功確率 = 70
		CALL アビテンプレート_不利効果_デバフ効果セット

		IF アビランク >= 2
		;追加防デバフ  ふつうの累積
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:累積上限_割合 = 30
			アビ汎用文字列:バフ・デバフ枠 = 防御力累積
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		ENDIF
ENDSELECT


;3アビ
;☆元にした原作スキル
;「みんなおかしくなーれ♪」
;広範囲に全ての敵にダメージと共に「混乱」状態にさせる

;様々な技に混乱効果が付くキャラでしたがこのアビリティに集約させました
;成功率も威力も適当（要調整かも）

@固有アビ_2651_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM アビランク
#DIM 混乱成功率

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 1
		アビ名 = みんなおかしくなーれ♪
		消費ＭＰ = 100
		威力値 = 180
		混乱成功率 = 45
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>（1ターン）の混乱効果を与える。成功率{混乱成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 混乱
		アビ汎用文字列:バフ・デバフ対象能力 = 混乱
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:基礎成功確率 = 混乱成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


;サポアビ
;☆元にした原作スキル
;「闇の咆吼」
;対象の範囲に闇の力を持つ魔法攻撃を行う

;原作ではひたすら連発できる主力技でした
;能動的に連発させるのはなんか違うかなと思ったので発動機会を限定

;闇の咆吼発動処理
@固有ターン終了時処理_キャラ_2651
#DIM キャラ番号
#DIM 隊列番号
#DIM 威力値

キャラ番号 = GETCHARA(2651)
隊列番号 = キャラ隊列検索(キャラ番号)

;呪い/アンデッド効果割合に応じて威力上昇も考えましたが難しそうなので自前のデバフか否かだけを判断
IF デバフ番号取得("呪い", 隊列番号) > -1 || デバフ番号取得("アンデッド", 隊列番号) > -1
	IF デバフ番号取得_枠("八卦・六道輪廻", 隊列番号) > -1
		威力値 = 240
	ELSE
		威力値 = 120
	ENDIF

	KSTR:(K++) = %CALLNAME:キャラ番号%から闇の力が溢れ出す……
	SELECTCASE RAND:3
		CASE 0
			KSTR:(K++) = 「ふへへっ♪　たべちゃうぞぉ～♪」
		CASE 1
			KSTR:(K++) = 「ふわわー！　なにか出てくるよー！」
		CASE 2
			KSTR:(K++) = 「みょえ～ん……誰が呼んでるのかにゃー……」
	ENDSELECT
	KSTR:(K++) = 　

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:敵対心無視オプション = 1
	CALL アビ対象選択テンプレート_敵ランダムＸ体(4)
	アビ汎用変数:効果割合 = 威力値
	CALL アビテンプレート_ダメージ処理_闇属性
ENDIF


@口上_ステータス画面_固有特性解説_2651(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『闇の咆吼』 □-----------------------------------------------------------------
KSTR:(K++) = 　呪い状態もしくはアンデッド状態の時、ターン終了時に
KSTR:(K++) = 　ターゲットに関わらず［攻撃力✕120％］の4回闇属性ダメージを与える。
KSTR:(K++) = 　自身の奥義によるアンデッド状態の場合、ダメージが［攻撃力✕240％］に上昇する。

CALL 口上変数表示(対象キャラ, 1)
