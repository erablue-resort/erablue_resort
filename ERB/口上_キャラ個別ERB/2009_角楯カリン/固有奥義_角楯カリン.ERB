;奥義：ターゲット、排除する：敵1人に対して、攻撃力の687%分のダメージ/ボスフラグエネミーには481%分の追加ダメージ

@固有奥義_2009(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 追加ダメージ
#DIM 効果数値
#DIM 効果ターン

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	奥義名 = ターゲット、排除する+
	奥義威力 = 1099
	追加ダメージ = 625
ELSE
	奥義名 = ターゲット、排除する
	奥義威力 = 687
	追加ダメージ = 481
ENDIF

;サポアビ「精密に、慎重に」用変数
IF 陥落チェック(キャラ番号)
	効果数値 = 24
	効果ターン = 3
ELSE
	効果数値 = 12
	効果ターン = 3
ENDIF

SELECTCASE RAND:3
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「ターゲット確認」
CASE 1
	アビ汎用文字列:実行時メッセージ１ = 「目標を捉えた」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「私に任せて」
ENDSELECT

SELECTCASE RAND:3
CASE 0
	アビ汎用文字列:実行時メッセージ２ = 「オーナーの敵は逃がさない」
CASE 1
	アビ汎用文字列:実行時メッセージ２ = 「任務……開始」
CASEELSE
	アビ汎用文字列:実行時メッセージ２ = 「一発で吹っ飛ばす」
ENDSELECT

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>
		詳細文文字列受け渡し変数 += "敵単体にダメージを与える。対象がボスだった場合、更に追加ダメージを与える。"
		RETURN 奥義威力
	CASE "奥義説明文_詳細"
		詳細文文字列受け渡し変数 += @"敵単体に自属性の［{奥義威力}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>対象がボスだった場合、さらに追加で自属性の［攻撃力✕{追加ダメージ}％］ダメージを与える。"
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる

		;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
		;必要な場合を除きここでリセット
		CALL アビ汎用変数文字列リセット
	CASE "奥義追加効果"
		;攻撃対象がボスだった場合
		IF 戦闘行動対象 >= 10 && 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
			;追加ダメージ発生
			アビ汎用変数:効果割合 = 追加ダメージ
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF

		;サポアビ「精密に、慎重に」効果
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 精密に、慎重に
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
