
@固有アビ_197_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
	アビ名 = 金棘吸気++
	IF 戦闘中カウント:戦闘行動キャラ:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 金棘吸気+
	IF 戦闘中カウント:戦闘行動キャラ:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSE
	アビ名 = 金棘吸気
	IF 戦闘中カウント:戦闘行動キャラ:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ENDIF

アビ汎用文字列:選択時メッセージ１ = だーれだ？
アビ汎用文字列:実行時メッセージ１ = 反動で体力を消費した…

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に土属性［攻撃力×250％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象にスロウ効果［1回］"
		詳細文文字列受け渡し変数 += @"<br>自身の奥義ゲージ［20％］アップ"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			詳細文文字列受け渡し変数 += @"<br>自身に［耐久300］バリア効果［破壊されるまで永続］"
			詳細文文字列受け渡し変数 += @"<br>◆自身のHPが少ない程バリア効果の性能アップ"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 250
		CALL アビテンプレート_ダメージ処理_土属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_不利効果_スロウ
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 20
		CALL アビテンプレート_有利効果_奥義ゲージUP
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = バリア
			アビ汎用文字列:バフ・デバフ対象能力 = バリア
			IF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 4 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ
				アビ汎用変数:効果量 = 300
			ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 3 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 4
				アビ汎用変数:効果量 = 400
			ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 2 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 3
				アビ汎用変数:効果量 = 500
			ELSEIF BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 2
				アビ汎用変数:効果量 = 600
			ELSEIF BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5
				アビ汎用変数:効果量 = 700
			ENDIF
			アビ汎用変数:持続ターン = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:1 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:1 = 0
		ENDIF
ENDSELECT



@固有アビ_197_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
	アビ名 = 尖刃衝波++
	IF 戦闘中カウント:戦闘行動キャラ:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 尖刃衝波+
	IF 戦闘中カウント:戦闘行動キャラ:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSE
	アビ名 = 尖刃衝波
	IF 戦闘中カウント:戦闘行動キャラ:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に4回土属性［攻撃力×100％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>自身に高揚効果［5ターン］"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			詳細文文字列受け渡し変数 += @"<br>◆自身のHPが50％以上の時、奥義与ダメージ上昇効果［2ターン］/HPが50％未満の時、逆境効果［3ターン］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 4
			CALL アビテンプレート_ダメージ処理_土属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 高揚
		アビ汎用文字列:バフ・デバフ対象能力 = 高揚
		アビ汎用変数:効果量 = 10
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			IF BATTLE_STATE:戦闘行動キャラ:ＨＰ >= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 2
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = 奥義与ダメージ
				アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
				アビ汎用変数:効果量 = 1500
				アビ汎用変数:持続ターン = 2
				CALL アビテンプレート_有利効果_バフ効果セット
			ELSE
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = 逆境
				アビ汎用文字列:バフ・デバフ対象能力 = 逆境
				アビ汎用変数:持続ターン = 3
				アビ汎用変数:特殊表示オプション = 1
				CALL アビテンプレート_有利効果_バフ効果セット
				
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = 逆境_与ダメージ
				アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
				アビ汎用変数:効果割合 = 5
				CALL アビテンプレート_有利効果_付随バフ効果セット("逆境")
			ENDIF
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:2 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:2 = 0
		ENDIF
ENDSELECT



@固有アビ_197_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM シュシュク隊列
#DIM クールタイム

シュシュク隊列 = キャラ隊列検索(GETCHARA(197))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 刹那揚々+
	消費ＭＰ = 300
ELSE
	アビ名 = 刹那揚々
	消費ＭＰ = 300
ENDIF

IF INRANGE(シュシュク隊列, 0, 3) && 戦闘中カウント:シュシュク隊列:0
	クールタイム = MAX(0, 10 + 戦闘中カウント:シュシュク隊列:0 - 現在ターン数)
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
ELSE
	クールタイム = 0
ENDIF

アビ汎用文字列:実行時メッセージ１ = 反動で体力を消費した…

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身のHPを現在HPの［80％］消費し、敵単体に土属性［攻撃力×500％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象に防御力［25％］ダウン効果［5ターン］"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 += @"<br>自身が即座に奥義発動可能/次の金棘吸気・尖刃衝波がMP・行動回数を消費しない"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>自身が即座に奥義発動可能"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>発動時、10ターンのクールタイムが発生"
		詳細文文字列受け渡し変数 += @"<br>◆このアビリティは行動回数を消費しない"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"（残り{クールタイム}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		
		BATTLE_STATE:シュシュク隊列:ＨＰ -= (BATTLE_STATE:シュシュク隊列:ＨＰ / 5) * 4
		
		アビ汎用変数:効果割合 = 500
		CALL アビテンプレート_ダメージ処理_土属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 25
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			戦闘中カウント:シュシュク隊列:1 = 1
			戦闘中カウント:シュシュク隊列:2 = 1
			アビテンプレート用_表示メッセージ格納:シュシュク隊列:(A_M_NUM:シュシュク隊列++) = 次の金棘吸気と尖刃衝波がMPと行動回数を消費しなくなった！
		ENDIF
		
		アビテンプレート用_行動消費無しフラグ = 1
		戦闘中カウント:シュシュク隊列:0 = 現在ターン数
ENDSELECT



@固有戦闘開始時処理_キャラ_197
;戦闘開始時処理関数から呼び出し
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(197))

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 千麿百錬
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
アビ汎用変数:効果割合 = 0
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビテンプレ変数リセット



@固有行動開始時処理_バフ_逆境(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 効果量

効果量 = ((BATTLE_STATE:戦闘行動キャラ:最大ＨＰ - BATTLE_STATE:戦闘行動キャラ:ＨＰ) / BATTLE_STATE:戦闘行動キャラ:最大ＨＰ) * 10

DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "効果量_割合", 効果量 + 5



@固有行動開始時処理_バフ_千麿百錬(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 効果量

IF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 4 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ
	効果量 = 0
ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 3 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 4
	効果量 = 20
ELSEIF (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 2 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 3
	効果量 = 20
ELSEIF BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5 < BATTLE_STATE:戦闘行動キャラ:ＨＰ && BATTLE_STATE:戦闘行動キャラ:ＨＰ <= (BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5) * 2
	効果量 = 35
ELSEIF BATTLE_STATE:戦闘行動キャラ:ＨＰ <= BATTLE_STATE:戦闘行動キャラ:最大ＨＰ / 5
	効果量 = 50
ENDIF

DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "効果量_割合", 効果量



@バフ・デバフ特殊表示_逆境(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 逆境
PRINTFORML 現在HPが少ない程与えるダメージが上昇する状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_197(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『千麿百錬』 □------------------------------------------------------------
KSTR:(K++) = 　自身のHPが少ない程奥義性能アップ
KSTR:(K++) = 　◆自身が瀕死の時、奥義2回発動

CALL 口上変数表示(対象キャラ, 1)



