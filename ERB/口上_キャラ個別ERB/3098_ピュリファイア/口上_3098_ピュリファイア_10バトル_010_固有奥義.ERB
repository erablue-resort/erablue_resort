;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもピュリファイア口上_readme.txt記載に従う
;--------------------------------------------------
@固有奥義_3098(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM クリアダメージ効果割合
#DIM 攻撃対象一時保存
#DIM 追加攻撃弱体反映数
#DIM 与ダメージ量
#DIMS 奥義名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	奥義名 = ケオスフィクサー＋＋
	奥義威力 = 300
	クリアダメージ効果割合 = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義名 = ケオスフィクサー＋
	奥義威力 = 300
	クリアダメージ効果割合 = 70
ELSE
	奥義名 = ケオスフィクサー
	奥義威力 = 300
	クリアダメージ効果割合 = 60
ENDIF

	アビ汎用文字列:実行時メッセージ１ = 「浄化の魔女、マナの使徒。傷を拡げて差し上げましょう――！」
	アビ汎用文字列:実行時メッセージ２ = 「ケオスフィクサー！！」

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = <br>自属性による奥義威力：{奥義威力}％のダメージを与える。[敵の弱体個数(最大10)✕回復力✕{クリアダメージ効果割合}]％の追加ダメージを与え、利用した弱体個数分デバフ消去効果を与える。<br>
		RETURN 奥義威力
	CASE "奥義追加効果"
		;弱体が盛り盛りされていたらそれだけ追加ダメージが出る。
        ;カワイイスパイスとのシナジー目的

		攻撃対象一時保存 = アビテンプレート用_対象保存:0
		追加攻撃弱体反映数 = 弱体数算出(攻撃対象一時保存)

		IF 追加攻撃弱体反映数 == 0
			;構文上の疑問。早期リターンは効く？
			RETURN ;弱体が無ければ追加攻撃無し
		ELSEIF 追加攻撃弱体反映数 > 10
			追加攻撃弱体反映数 = 10
		ENDIF

		CALL アビ対象選択テンプレート_指定(攻撃対象一時保存)
		アビ汎用変数:効果割合 = クリアダメージ効果割合 * 追加攻撃弱体反映数
		アビ汎用文字列:攻撃側使用能力値 = 回復力
		
		CALL アビテンプレート_ダメージ処理_自属性
		与ダメージ量 = RESULT
		IF 与ダメージ量 > 0
			FOR LOCAL, 0, 追加攻撃弱体反映数
				;ダメージが発生したらデバフ消去
				CALL アビテンプレート_有利効果_デバフ消去()
			NEXT
		ENDIF
ENDSELECT
