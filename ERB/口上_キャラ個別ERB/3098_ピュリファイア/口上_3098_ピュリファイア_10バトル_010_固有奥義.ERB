;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもピュリファイア口上_readme.txt記載に従う
;--------------------------------------------------
@固有奥義_3098(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力 = 300
#DIM 恋慕フラグ = 0
#DIM 敵クリアフラグ = 1
#DIMS 恋慕説明 ;説明側で判定したくないので空文字にしておく。
#DIM セフレフラグ = 0
#DIM ディスペルフラグ= 0
#DIMS セフレ説明 ;説明側で判定したくないので空文字にしておく。
#DIM 弱体追加ダメージ効果割合 = 60
#DIM 攻撃対象一時保存
#DIM 追加攻撃弱体反映数
#DIM 与ダメージ量
#DIMS 基礎奥義名
#DIMS 奥義名 ;最終的な奥義名の出力が入る
#DIMS 陥落奥義追加文字列

基礎奥義名 = ケオスフィクサー

IF TALENT:キャラ番号:恋慕
	;恋慕でも結婚でも同じ、当面は。
	恋慕フラグ = 1
ELSEIF TALENT:キャラ番号:セフレ
	セフレフラグ = 1
ENDIF

;陥落状況を変更するのは手間なので、奥義の外に影響でない範囲でデバッグ用処理追加
;[IF_DEBUG]
;	恋慕フラグ = 0
;	セフレフラグ = 1
;[ENDIF]

IF 恋慕フラグ
	陥落奥義追加文字列 = EX
	敵クリアフラグ = 0
	恋慕説明 = 【陥落特典】恋慕陥落により、敵弱体回復を行わないように。<br>
ELSEIF セフレフラグ
	陥落奥義追加文字列 = ALT
	ディスペルフラグ = 1
	セフレ説明 = 【陥落特典】セフレ陥落により、弱体個数に応じた敵強化解除を与えるように。<br>
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	奥義名 = %基礎奥義名%%陥落奥義追加文字列%＋＋
	奥義威力 = 300
	弱体追加ダメージ効果割合 = 70
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義名 = %基礎奥義名%%陥落奥義追加文字列%＋
	奥義威力 = 300
	弱体追加ダメージ効果割合 = 65
ELSE
	奥義名 = %基礎奥義名%%陥落奥義追加文字列%
	奥義威力 = 300
	弱体追加ダメージ効果割合 = 60
ENDIF

	アビ汎用文字列:実行時メッセージ１ = 「浄化の魔女、マナの使徒。傷を拡げて差し上げましょう――！」
	アビ汎用文字列:実行時メッセージ２ = 「ケオスフィクサー！！」

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = <br>自属性による奥義威力：{奥義威力}％のダメージを与える。[敵の弱体個数(最大10)✕回復力✕{弱体追加ダメージ効果割合}]％の追加ダメージを与え、利用した弱体個数分デバフ消去効果を与える。<br>%恋慕説明%%セフレ説明%
		RETURN 奥義威力
	CASE "奥義追加効果"
		;弱体が盛り盛りされていたらそれだけ追加ダメージが出る。
        ;カワイイスパイスとのシナジー目的

		攻撃対象一時保存 = アビテンプレート用_対象保存:0
		追加攻撃弱体反映数 = 弱体数算出(攻撃対象一時保存)

		IF 追加攻撃弱体反映数 == 0
			;構文上の疑問。早期リターンは効く
			RETURN ;弱体が無ければ追加攻撃無し
		ELSEIF 追加攻撃弱体反映数 > 10
			追加攻撃弱体反映数 = 10
		ENDIF

		CALL アビ対象選択テンプレート_指定(攻撃対象一時保存)
		アビ汎用変数:効果割合 = 弱体追加ダメージ効果割合 * 追加攻撃弱体反映数
		アビ汎用文字列:攻撃側使用能力値 = 回復力
		
		CALL アビテンプレート_ダメージ処理_自属性
		与ダメージ量 = RESULT
		IF 与ダメージ量 > 0
			IF 敵クリアフラグ == 1
				FOR LOCAL, 0, 追加攻撃弱体反映数
					;恋慕陥落以外時、ダメージが発生したらデバフ消去
					CALL アビテンプレート_有利効果_デバフ消去()
				NEXT
			ENDIF
			IF ディスペルフラグ == 1
				FOR LOCAL, 0, 追加攻撃弱体反映数
					;セフレ陥落時、ダメージが発生したらデバフ消去
					CALL アビテンプレート_不利効果_バフ消去()
				NEXT
			ENDIF
		ENDIF
ENDSELECT
