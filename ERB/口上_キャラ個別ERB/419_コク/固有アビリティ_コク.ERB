
@固有アビ_419_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM コク隊列

コク隊列 = キャラ隊列検索(GETCHARA(419))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
	アビ名 = 吉兆之呪++
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 吉兆之呪+
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSE
	アビ名 = 吉兆之呪
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に闇属性［攻撃力×200％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>味方全体の攻撃力累積［10％］アップ効果［最大40％/永続］"
		詳細文文字列受け渡し変数 += @"<br>味方全体の防御力累積［10％］アップ効果［最大40％/永続］"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			詳細文文字列受け渡し変数 += @"<br>味方全体にディスペルガード効果［1回］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体
		アビ汎用変数:効果割合 = 200
		CALL アビテンプレート_ダメージ処理_闇属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_別枠累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
			アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		IF 戦闘中カウント:コク隊列:1 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:コク隊列:1 = 0
		ENDIF
ENDSELECT



@固有アビ_419_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM コク隊列

コク隊列 = キャラ隊列検索(GETCHARA(419))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
	アビ名 = 凶兆之呪++
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 凶兆之呪+
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSE
	アビ名 = 凶兆之呪
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:2 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に4回闇属性［攻撃力×100％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>敵全体の攻撃力累積［10％］ダウン効果［最大40％/5ターン］"
		詳細文文字列受け渡し変数 += @"<br>敵全体の防御力累積［10％］ダウン効果［最大40％/5ターン］"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			詳細文文字列受け渡し変数 += @"<br>敵全体に弱体耐性累積［10％］ダウン効果［最大30％/5ターン］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 4
			CALL アビテンプレート_ダメージ処理_闇属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 弱体耐性_累積枠
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:累積上限_割合 = 30
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		ENDIF
		
		IF 戦闘中カウント:コク隊列:2 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:コク隊列:2 = 0
		ENDIF
ENDSELECT



@固有アビ_419_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM コク隊列

コク隊列 = キャラ隊列検索(GETCHARA(419))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 殄滅紫柱+
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:3 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ELSE
	アビ名 = 殄滅紫柱
	IF INRANGE(コク隊列, 0, 3) && 戦闘中カウント:コク隊列:3 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［対象の現在HPの5％分］無属性ダメージ［最大444］
		詳細文文字列受け渡し変数 += @"<br>対象の強化効果を消去［1回］"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 += @"<br>◆使用回数に応じて発動回数アップ/ターン終了時に強化効果数が5個以上の敵がいる時、次のこのアビリティがMPと行動回数を消費しない"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>◆ターン終了時に強化効果数が5個以上の敵がいる時、次のこのアビリティがMPと行動回数を消費しない"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果量 = MIN(444, 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):ＨＰ / 20)
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			FOR LOCAL, 0, 戦闘中カウント:コク隊列:0 + 1
				CALL アビテンプレート_ダメージ処理_無属性
			NEXT
			
			CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, 戦闘中カウント:コク隊列:0 + 1
				CALL アビテンプレート_不利効果_バフ消去
			NEXT
			
			SIF 戦闘中カウント:コク隊列:0 < 2
				戦闘中カウント:コク隊列:0 += 1
		ELSE
			CALL アビテンプレート_ダメージ処理_無属性
			
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		
		IF 戦闘中カウント:コク隊列:3 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:3 = 0
		ENDIF
ENDSELECT



@固有戦闘開始時処理_キャラ_419
#DIM 隊列番号

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体_生者のみ_自己以外
アビ汎用文字列:バフ・デバフ枠 = 誰が為に黒曜石は輝く
アビ汎用文字列:バフ・デバフ対象能力 = 誰が為に黒曜石は輝く
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 誰が為に黒曜石は輝く_奥義与ダメ
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_不利効果_デバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 誰が為に黒曜石は輝く_アビ与ダメ
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビテンプレ変数リセット



@固有奥義後処理_バフ_誰が為に黒曜石は輝く(バフ・デバフ行数, 全滅フラグ)
#DIM バフ・デバフ行数
#DIM 全滅フラグ
#DIM コク隊列

SIF 全滅フラグ
	RETURN 0

コク隊列 = キャラ隊列検索(GETCHARA(419))

IF INRANGE(コク隊列, 0, 3)
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(コク隊列)
	アビ汎用変数:効果量 = 30
	CALL アビテンプレート_有利効果_奥義ゲージUP
ENDIF



@固有ターン終了時処理_キャラ_419
#DIM コク隊列

コク隊列 = キャラ隊列検索(GETCHARA(419))

FOR LOCAL, 10, 20
	SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ < 1
		CONTINUE
	
	IF 強化数算出(LOCAL) > 4
		戦闘中カウント:コク隊列:3 = 1
		アビテンプレート用_表示メッセージ格納:戦闘行動キャラ:(A_M_NUM:戦闘行動キャラ++) = 次の殄滅紫柱がMPと行動回数を消費しなくなった！
		BREAK
	ENDIF
NEXT



;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_419(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『誰が為に黒曜石は輝く』 □------------------------------------------------------------
KSTR:(K++) = 　奥義ゲージが溜まりにくく、奥義ダメージが低いがアビリティダメージアップ
KSTR:(K++) = 　◆味方が奥義を発動時、自身の奥義ゲージアップ

CALL 口上変数表示(対象キャラ, 1)



