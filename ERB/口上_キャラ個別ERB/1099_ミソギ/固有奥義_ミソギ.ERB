;1099.ミソギ 固有奥義
;--------------------------------------------------
;奥義「トリックボマー」「トリックハート」
;--------------------------------------------------
@固有奥義_1099(ARGS, キャラ番号)
#DIM  キャラ番号
#DIM  奥義威力
#DIMS 奥義名
#DIM  追加効果量
#DIM  追加効果ターン
#DIMS 追加ダメ属性
#DIM  追加ダメ倍率
#DIM  対象

; 陥落済 ＋ Lv40以上で強化
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	; 奥義威力:400% → 550%
	; 追加効果の対象:単体 → 縦列
	; 攻撃力ダウン効果量:10% → 20%
	; 追加ダメージ:なし →150%(火属性)
	奥義名   = トリックハート
	奥義威力 = 550
	; 追加効果:攻撃力/防御力ダウン
	追加効果量     = 20
	追加効果ターン = 3
	; 追加ダメージ
	追加ダメ属性 = 火属性
	追加ダメ倍率 = 150
; 通常版
ELSE
	; 奥義威力:400％
	奥義名   = トリックボマー
	奥義威力 = 400
	; 追加効果:攻撃力/防御力ダウン
	追加効果量     = 10
	追加効果ターン = 3
	; 追加ダメージ
	追加ダメ属性 = 
	追加ダメ倍率 = 0
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％"
		SELECTCASE 奥義名
			CASE "トリックボマー"
				詳細文文字列受け渡し変数 += @"<br>対象に攻撃ダウン効果［{追加効果量}％/{追加効果ターン}ターン］"
			CASE "トリックハート"
				詳細文文字列受け渡し変数 += @"<br>対象と同じ縦列の敵に%追加ダメ属性%［攻撃力✕{追加ダメ倍率}％］の追加ダメージ"
				詳細文文字列受け渡し変数 += @"<br>対象と同じ縦列の敵に攻撃力/防御力ダウン効果［{追加効果量}％/{追加効果ターン}ターン］"
		ENDSELECT
		RETURN 奥義威力
	CASE "奥義追加効果"
		対象 = アビテンプレート用_対象保存:0 - 10
		;対象と同じ縦列の敵に追加ダメージ
		IF 奥義名 == "トリックハート"
			CALL アビ汎用変数文字列リセット
			CALL 固有奥義処理_1099_対象拡張_縦列(対象)
			アビ汎用変数:効果割合 = 追加ダメ倍率
			; ダメージ種類を奥義に設定(耳飾り等の奥義ダメージ補正が乗るようにする)
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALLFORM アビテンプレート_ダメージ処理_%追加ダメ属性%
		ENDIF
		;対象 or 対象と同じ縦列の敵に攻撃力ダウン効果
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 追加効果量
		アビ汎用文字列:バフ・デバフ枠 = %奥義名%_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 追加効果ターン
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
		アビ汎用変数:効果量 = 追加効果量
		アビ汎用文字列:バフ・デバフ枠 = %奥義名%_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 追加効果ターン
		アビ汎用変数:基礎成功確率 = 100
		;対象 or 対象と同じ縦列の敵に防御力ダウン効果
ENDSELECT

@固有奥義処理_1099_対象拡張_縦列(対象)
#DIM 対象
#DIM 敵番号
#DIM 開始番号
#DIM 終了番号
#DIM 対象保存位置

IF 対象 % 2 == 0
	; 偶数 = 左列
	開始番号 = 0
	終了番号 = 8
ELSE
	; 奇数 = 右列
	開始番号 = 1
	終了番号 = 9
ENDIF

対象保存位置 = 0
VARSET アビテンプレート用_対象保存, -1
FOR 敵番号, 開始番号, 終了番号 + 1, 2
	;敵が居ないマスはスキップ
	SIF 敵BATTLE_STATE_STR:敵番号:エネミー名 == ""
		CONTINUE
	;敵が死亡済の場合はスキップ
	SIF 敵BATTLE_STATE:敵番号:ＨＰ < 1
		CONTINUE
	;敵なので番号に+10して保存
	アビテンプレート用_対象保存:対象保存位置 = 敵番号 + 10
	対象保存位置 ++
NEXT
