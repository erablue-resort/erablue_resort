;1099.ミソギ固有アビ
;--------------------------------------------------
;1アビ「インクシューター」
;--------------------------------------------------
@固有アビ_1099_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIMS アビダメ属性
#DIM アビダメ倍率
#DIM アビ効果割合
#DIM アビ効果成功率
#DIM アビ効果ターン

; 参考:フラッシュバン
; MP15 敵縦列 暗闇(50/成功率60％/3T)
; MP20 敵縦列 暗闇(50/成功率80％/3T)
; MP30 敵縦列 暗闇(80/成功率80％/3T)
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 20
		アビ名 = インクシューター＋＋
		; 消費MP25 → 35
		; 敵単体に土属性110％ダメージ → 150％
		; 暗闇効果(50％/成功率80％/3T) → 80％
		消費ＭＰ = 35
		アビダメ属性   = 土属性
		アビダメ倍率   = 150
		; 追加効果:暗闇
		アビ効果割合   = 80
		アビ効果成功率 = 80
		アビ効果ターン = 3
	CASE IS >= 8
		アビ名 = インクシューター＋
		; 消費MP20 → 25
		; 敵単体に土属性100％ダメージ → 110％
		; 暗闇効果(50％/成功率60％/3T) → 成功率80％
		消費ＭＰ = 25
		アビダメ属性   = 土属性
		アビダメ倍率   = 110
		; 追加効果:暗闇
		アビ効果割合   = 50
		アビ効果成功率 = 80
		アビ効果ターン = 3
	CASE IS >= 1
		アビ名 = インクシューター
		; 消費MP20
		; 敵単体に土属性100％ダメージ
		; 暗闇効果(50％/成功率60％/3T)
		消費ＭＰ = 20
		アビダメ属性   = 土属性
		アビダメ倍率   = 100
		; 追加効果:暗闇
		アビ効果割合   = 50
		アビ効果成功率 = 60
		アビ効果ターン = 3
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ属性   = 
		アビダメ倍率   = 0
		; 追加効果:暗闇
		アビ効果割合   = 0
		アビ効果成功率 = 0
		アビ効果ターン = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"<br>敵単体に%アビダメ属性%の［攻撃力✕{アビダメ倍率}％］ダメージを与える"
		詳細文文字列受け渡し変数 += @"<br>対象に暗闇効果［{アビ効果割合}％/{アビ効果ターン}ターン/基礎成功率{アビ効果成功率}％］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象選択
		CALL アビ対象選択テンプレート_敵単体
		;敵単体に土属性ダメージ
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = アビダメ倍率
		CALLFORM アビテンプレート_ダメージ処理_%アビダメ属性%
		;敵単体に暗闇効果
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠       = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:効果割合     = アビ効果割合
		アビ汎用変数:基礎成功確率 = アビ効果成功率
		アビ汎用変数:持続ターン   = アビ効果ターン
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

;--------------------------------------------------
;2アビ「ウォーターバルーン」
;--------------------------------------------------
@固有アビ_1099_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIMS アビダメ属性
#DIM アビダメ倍率
#DIM アビ効果1_効果割合
#DIM アビ効果1_成功率
#DIM アビ効果1_効果ターン
#DIM アビ効果2_効果量
#DIM アビ効果2_成功率

; 参考:ブレイズ
; MP15 威力100 攻撃デバフ(10%/成功率80%/3T)
; MP30 威力100 攻撃デバフ(10%/成功率80%/3T),火傷(100%/成功率70%/3T)
; MP40 威力100 攻撃デバフ(20%/成功率80%/3T),火傷(150%/成功率70%/3T)
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 28
		アビ名 = ウォーターバルーン＋＋
		; 消費MP30 → 40
		; 敵単体に水属性100％ダメージ → 120％
		; 攻撃力ダウン効果(20％/成功率80％/3T) → 成功率90％
		; 追加：スロウ効果
		消費ＭＰ = 40
		アビダメ属性         = 水属性
		アビダメ倍率         = 120
		; 追加効果1:攻撃力ダウン
		アビ効果1_効果割合   = 20
		アビ効果1_成功率     = 90
		アビ効果1_効果ターン = 3
		; 追加効果2:スロウ
		アビ効果2_効果量     = 1
		アビ効果2_成功率     = 100
	CASE IS >= 16
		アビ名 = ウォーターバルーン＋
		; 消費MP15 → 30
		; 攻撃力ダウン効果(10％/成功率80％/3T) → 20％
		消費ＭＰ = 30
		アビダメ属性         = 水属性
		アビダメ倍率         = 100
		; 追加効果1:攻撃力ダウン
		アビ効果1_効果割合   = 20
		アビ効果1_成功率     = 80
		アビ効果1_効果ターン = 3
		; 追加効果2:スロウ
		アビ効果2_効果量     = 0
		アビ効果2_成功率     = 0
	CASE IS >= 8
		アビ名 = ウォーターバルーン
		; 消費MP15
		; 敵単体に水属性100％ダメージ
		; 攻撃力ダウン効果(10％/成功率80％/3T)
		消費ＭＰ = 15
		アビダメ属性         = 水属性
		アビダメ倍率         = 100
		; 追加効果1:攻撃力ダウン
		アビ効果1_効果割合   = 10
		アビ効果1_成功率     = 80
		アビ効果1_効果ターン = 3
		; 追加効果2:スロウ
		アビ効果2_効果量     = 0
		アビ効果2_成功率     = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ属性         = 
		アビダメ倍率         = 0
		; 追加効果1:攻撃力ダウン
		アビ効果1_効果割合   = 0
		アビ効果1_成功率     = 0
		アビ効果1_効果ターン = 0
		; 追加効果2:スロウ
		アビ効果2_効果量     = 0
		アビ効果2_成功率     = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵単体に%アビダメ属性%の［攻撃力✕{アビダメ倍率}％］ダメージを与える"
		SIF アビ効果1_効果割合
			詳細文文字列受け渡し変数 += @"<br>対象に攻撃力ダウン効果［{アビ効果1_効果割合}％/{アビ効果1_効果ターン}ターン/基礎成功率{アビ効果1_成功率}％］"
		SIF アビ効果2_効果量
			詳細文文字列受け渡し変数 += @"<br>対象にスロウ効果［{アビ効果2_効果量}回］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象選択
		CALL アビ対象選択テンプレート_敵単体
		;敵単体に水属性ダメージ
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = アビダメ倍率
		CALLFORM アビテンプレート_ダメージ処理_%アビダメ属性%
		;敵単体に攻撃力ダウン効果
		IF アビ効果1_効果割合
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = ウォーターバルーン_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合     = アビ効果1_効果割合
			アビ汎用変数:基礎成功確率 = アビ効果1_成功率
			アビ汎用変数:持続ターン   = アビ効果1_効果ターン
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		;敵単体にスロウ効果
		IF アビ効果2_効果量
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量       = アビ効果2_効果量
			アビ汎用変数:基礎成功確率 = アビ効果2_成功率
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
ENDSELECT

;--------------------------------------------------
;3アビ「ペインティングキック」
;--------------------------------------------------
@固有アビ_1099_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIMS アビダメ属性
#DIM アビダメ倍率
#DIM アビ効果割合
#DIM アビ効果成功率
#DIM アビ効果ターン

SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 32
		アビ名 = ペインティングキック＋＋
		; 消費MP35 → 50
		; 敵縦列に自属性90％ダメージ → 120％
		; 防御力ダウン効果(10％/成功率80％/3T) → 20％/成功率90％
		消費ＭＰ = 50
		アビダメ属性   = 自属性
		アビダメ倍率   = 120
		; 追加効果:防御力ダウン
		アビ効果割合   = 20
		アビ効果成功率 = 90
		アビ効果ターン = 3
	CASE IS >= 24
		アビ名 = ペインティングキック＋
		; 消費MP25 → 35
		; 追加：防御力ダウン効果(10％/成功率80％/3T)
		消費ＭＰ = 35
		アビダメ属性   = 自属性
		アビダメ倍率   = 90
		; 追加効果:防御力ダウン
		アビ効果割合   = 10
		アビ効果成功率 = 80
		アビ効果ターン = 3
	CASE IS >= 12
		アビ名 = ペインティングキック
		; 消費MP25
		; 敵縦列に自属性90％ダメージ
		消費ＭＰ = 25
		アビダメ属性   = 自属性
		アビダメ倍率   = 90
		; 追加効果:防御力ダウン
		アビ効果割合   = 0
		アビ効果成功率 = 0
		アビ効果ターン = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ属性   = 
		アビダメ倍率   = 0
		; 追加効果:防御力ダウン
		アビ効果割合   = 0
		アビ効果成功率 = 0
		アビ効果ターン = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"<br>敵縦列に%アビダメ属性%の［攻撃力✕{アビダメ倍率}％］ダメージを与える"
		IF アビ効果割合
			詳細文文字列受け渡し変数 += @"<br>対象に防御力ダウン効果［{アビ効果割合}％/{アビ効果ターン}ターン/基礎成功率{アビ効果成功率}％］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象選択
		CALL アビ対象選択テンプレート_敵縦列
		;敵縦列に自属性ダメージ
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = アビダメ倍率
		CALLFORM アビテンプレート_ダメージ処理_%アビダメ属性%
		;敵縦列に防御力ダウン効果
		IF アビ効果割合
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = ペインティングキック_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合     = アビ効果割合
			アビ汎用変数:基礎成功確率 = アビ効果成功率
			アビ汎用変数:持続ターン   = アビ効果ターン
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT
