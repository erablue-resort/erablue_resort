;1098.ミミ固有アビ
;--------------------------------------------------
;1アビ「ぴょんぴょんアタック」
;--------------------------------------------------
@固有アビ_1098_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビダメ倍率
#DIM アビ効果量

; 参考:ファイア
; MP20 威力200
; MP22 威力250
; MP25 威力300 横列
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 20
		アビ名 = ぴょんぴょんアタック＋＋
		; 消費MP25 → 30
		; 敵単体に自属性200％ダメージ → 250％
		; 自身の奥義ゲージ増加(10％) → 20％
		消費ＭＰ = 30
		アビダメ倍率 = 300
		; 追加効果:自分の奥義ゲージ増加
		アビ効果量   = 20
	CASE IS >= 8
		アビ名 = ぴょんぴょんアタック＋
		; 消費MP20 → 25
		; 追加：自身の奥義ゲージ増加(10％)
		消費ＭＰ = 25
		アビダメ倍率 = 250
		; 追加効果:自分の奥義ゲージ増加
		アビ効果量   = 10
	CASE IS >= 1
		アビ名 = ぴょんぴょんアタック
		; 消費MP20
		; 敵単体に自属性200％ダメージ
		消費ＭＰ = 20
		アビダメ倍率 = 200
		; 追加効果:自分の奥義ゲージ増加
		アビ効果量   = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ倍率 = 0
		; 追加効果:自分の奥義ゲージ増加
		アビ効果量   = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵単体に自属性の［攻撃力✕{アビダメ倍率}％］ダメージを与える"
		IF アビ効果量
			詳細文文字列受け渡し変数 += @"<br>自身の奥義ゲージ［{アビ効果量}％］アップ"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		;対象選択
		CALL アビ対象選択テンプレート_敵単体
		;敵単体に自属性ダメージ
		アビ汎用変数:効果割合 = アビダメ倍率
		CALL アビテンプレート_ダメージ処理_自属性
		IF アビ効果量
			CALL アビ汎用変数文字列リセット
			;対象設定
			CALL アビ対象選択テンプレート_自己のみ
			;自身の奥義ゲージアップ
			アビ汎用変数:効果量 = アビ効果量
			CALL アビテンプレート_有利効果_奥義ゲージUP
		ENDIF
ENDSELECT

;--------------------------------------------------
;2アビ「うさぎさんエール」
;--------------------------------------------------
@固有アビ_1098_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM アビ効果1_効果割合
#DIM アビ効果1_効果ターン
#DIM アビ効果2_効果量

; 参考:レイジ
; MP20 攻撃力＋20％ 3T
; MP30 攻撃力＋35％ 3T
; MP40 攻撃力＋50％ 3T
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 28
		アビ名 = うさぎさんエール＋＋
		; 消費MP30 → 40
		; 味方全体に攻撃力アップ効果(30％/3T) → 40％
		; 追加：味方全体の奥義ゲージ増加(10％)
		消費ＭＰ = 40
		; 効果1:味方全体の攻撃力アップ
		アビ効果1_効果割合   = 40
		アビ効果1_効果ターン = 3
		; 効果2:味方全体の奥義ゲージ増加
		アビ効果2_効果量     = 10
	CASE IS >= 16
		アビ名 = うさぎさんエール＋
		; 消費MP20 → 30
		; 味方全体に攻撃力アップ効果(20％/3T) → 30％
		消費ＭＰ = 30
		; 効果1:味方全体の攻撃力アップ
		アビ効果1_効果割合   = 30
		アビ効果1_効果ターン = 3
		; 効果2:味方全体の奥義ゲージ増加
		アビ効果2_効果量     = 0
	CASE IS >= 8
		アビ名 = うさぎさんエール
		; 消費MP20
		; 攻撃力アップ効果(味方全体/20％/3T)
		消費ＭＰ = 20
		; 効果1:味方全体の攻撃力アップ
		アビ効果1_効果割合   = 20
		アビ効果1_効果ターン = 3
		; 効果2:味方全体の奥義ゲージ増加
		アビ効果2_効果量     = 0
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		; 効果1:味方全体の攻撃力アップ
		アビ効果1_効果割合   = 0
		アビ効果1_効果ターン = 0
		; 効果2:味方全体の奥義ゲージ増加
		アビ効果2_効果量     = 0
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>味方全体に攻撃力アップ効果［{アビ効果1_効果割合}％/{アビ効果1_効果ターン}ターン］"
		IF アビ効果2_効果量
			詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージ［{アビ効果2_効果量}％］アップ"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象設定
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		;味方全体に攻撃力アップ効果
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合   = アビ効果1_効果割合
		アビ汎用変数:持続ターン = アビ効果1_効果ターン
		アビ汎用文字列:バフ・デバフ枠 = うさぎさんエール
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_バフ効果セット
		;味方全体の奥義ゲージアップ
		IF アビ効果2_効果量
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = アビ効果2_効果量
			CALL アビテンプレート_有利効果_奥義ゲージUP
		ENDIF
ENDSELECT

;--------------------------------------------------
;3アビ「ぐるぐるアタック」
;--------------------------------------------------
@固有アビ_1098_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM アビダメ倍率

; 参考
; エーテルブラスト MP35 100％ 敵全体
SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
	CASE IS >= 32
		アビ名 = ぐるぐるアタック＋＋
		; 消費MP45 → 55
		; 敵全体に自属性150％ダメージ → 250％
		消費ＭＰ = 55
		アビダメ倍率 = 250
	CASE IS >= 24
		アビ名 = ぐるぐるアタック＋
		; 消費MP40 → 45
		; 敵全体に自属性100％ダメージ → 150％
		消費ＭＰ = 45
		アビダメ倍率 = 150
	CASE IS >= 12
		アビ名 = ぐるぐるアタック
		; 消費MP40
		; 敵全体に自属性100％ダメージ
		消費ＭＰ = 40
		アビダメ倍率 = 100
	CASEELSE
		アビ名 = 
		消費ＭＰ = 0
		アビダメ倍率 = 0
ENDSELECT
SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の［攻撃力✕{アビダメ倍率}％］ダメージを与える"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;対象設定
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		;敵全体に自属性ダメージ
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = アビダメ倍率
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT
