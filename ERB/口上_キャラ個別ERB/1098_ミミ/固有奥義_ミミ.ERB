;1098.ミミ 固有奥義
;--------------------------------------------------
;奥義「うさぎさんスラッシュ」「うさぎさんレボリューション」
;--------------------------------------------------
@固有奥義_1098(ARGS, キャラ番号)
#DIM  キャラ番号
#DIM  奥義威力
#DIMS 奥義名
#DIMS 追加ダメ属性
#DIM  追加ダメ倍率
#DIM  対象

; 陥落済 ＋ Lv40以上で強化
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	; 奥義威力:150 → 250
	; ランダムな敵に風属性400％ダメージ → 自属性600％ダメージ、範囲攻撃化
	奥義名   = うさぎさんレボリューション
	奥義威力 = 250
	追加ダメ属性 = 自属性
	追加ダメ倍率 = 600
; 通常版
ELSE
	; 奥義威力:150
	; ランダムな敵に風属性400％ダメージ
	奥義名   = うさぎさんスラッシュ
	奥義威力 = 150
	追加ダメ属性 = 風属性
	追加ダメ倍率 = 400
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％"
		SELECTCASE 奥義名
			CASE "うさぎさんスラッシュ"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵1体に%追加ダメ属性%［攻撃力✕{追加ダメ倍率}％］ダメージ"
			CASE "うさぎさんレボリューション"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵1体とその周囲の敵に%追加ダメ属性%［攻撃力✕{追加ダメ倍率}％］ダメージ"
		ENDSELECT
		RETURN 奥義威力
	CASE "奥義追加効果"
		;ランダムな対象1体にダメージ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
		対象 = アビテンプレート用_対象保存:0 - 10
		IF 奥義名 == "うさぎさんレボリューション"
			CALL 固有奥義処理_1098_対象拡張_周囲(対象)
		ENDIF
		アビ汎用変数:効果割合 = 追加ダメ倍率
		; ダメージ種類を奥義に設定(耳飾り等の奥義ダメージ補正が乗るようにする)
		アビ汎用文字列:ダメージ種類 = 奥義ダメージ
		CALLFORM アビテンプレート_ダメージ処理_%追加ダメ属性%
ENDSELECT

@固有奥義処理_1098_対象拡張_周囲(対象)
#DIM 対象
#DIM 敵番号
#DIM 開始番号
#DIM 終了番号
#DIM 対象保存位置

IF 対象 % 2 == 0
	; 偶数 = 左列
	開始番号 = 対象 - 2
	終了番号 = 対象 + 3
ELSE
	; 奇数 = 右列
	開始番号 = 対象 - 3
	終了番号 = 対象 + 2
ENDIF
; 上下限処理
開始番号 = MAX(開始番号, 0)
終了番号 = MIN(終了番号, 9)

対象保存位置 = 0
VARSET アビテンプレート用_対象保存, -1
; 対象が健在なら最初に設定
DO
	;敵が居ないマスはスキップ
	SIF 敵BATTLE_STATE_STR:対象:エネミー名 == ""
		CONTINUE
	;敵が死亡済の場合はスキップ
	SIF 敵BATTLE_STATE:対象:ＨＰ < 1
		CONTINUE
	;敵なので番号に+10して保存
	アビテンプレート用_対象保存:対象保存位置 = 対象 + 10
	対象保存位置 ++
LOOP 0

FOR 敵番号, 開始番号, 終了番号 + 1
	;敵が居ないマスはスキップ
	SIF 敵BATTLE_STATE_STR:敵番号:エネミー名 == ""
		CONTINUE
	;敵が死亡済の場合はスキップ
	SIF 敵BATTLE_STATE:敵番号:ＨＰ < 1
		CONTINUE
	;対象は設定済なのでスキップ
	SIF 敵番号 == 対象
		CONTINUE
	;敵なので番号に+10して保存
	アビテンプレート用_対象保存:対象保存位置 = 敵番号 + 10
	対象保存位置 ++
NEXT
