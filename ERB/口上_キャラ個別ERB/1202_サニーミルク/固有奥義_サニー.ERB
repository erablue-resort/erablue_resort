;キャラ一致チェックの三妖精版
;0ならサニー、1ならルナ、2ならスター、-1ならいずれでもない
@三妖精チェック(対象キャラ)
#FUNCTION
#DIM 対象キャラ

SELECTCASE CSVNAME(NO:対象キャラ)
	CASE "[輝ける日の光]サニーミルク"
		RETURNF 0
	CASE "[静かなる月の光]ルナチャイルド"
		RETURNF 1
	CASE "[降り注ぐ星の光]スターサファイア"
		RETURNF 2
	CASEELSE
		RETURNF -1
ENDSELECT


;キャラ隊列検索の三妖精版
@三妖精隊列検索(三妖精隊列)
#DIM REF 三妖精隊列, 0
#DIM ループ用
#DIM 三妖精番号

VARSET 三妖精隊列, -1
FOR ループ用, 0, 14
	SIF ループ用 > 3 && ループ用 < 10
		CONTINUE
	三妖精番号 = 三妖精チェック(BATTLE_STATE:ループ用:キャラ登録番号)
	SIF 三妖精番号 < 0
		CONTINUE

	三妖精隊列:三妖精番号 = ループ用
NEXT


@三妖精奥義発動確認(三妖精奥義フラグ)
#DIM REF 三妖精奥義フラグ, 0
#DIM ループ用
#DIM キャラ番号
#DIM 三妖精番号

FOR ループ用, 0, ターン中奥義回数保存
	キャラ番号 = BATTLE_STATE:(チェンバ基準キャラ保存:ループ用):キャラ登録番号
	三妖精番号 = 三妖精チェック(キャラ番号)
	SIF 三妖精番号 < 0
		CONTINUE

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
		三妖精奥義フラグ:三妖精番号 = 2
	ELSE
		三妖精奥義フラグ:三妖精番号 = 1
	ENDIF
NEXT


@固有奥義_1202(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値
#DIM 三妖精奥義フラグ, 3
#DIM 三妖精隊列, 3
#DIM 攻撃数値

VARSET 三妖精奥義フラグ, 0
SIF キャラ隊列検索(キャラ番号) >= 0
	CALL 三妖精奥義発動確認(三妖精奥義フラグ)

;他の三妖精が先に奥義を使用している場合
IF 三妖精奥義フラグ:1 && 三妖精奥義フラグ:2
	CALL 三妖精隊列検索(三妖精隊列)

	;効果が強くなっても名前は同じ
	奥義名 = スリーフェアリーズ
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号) && 三妖精奥義フラグ:1 >= 2 && 三妖精奥義フラグ:2 >= 2
		奥義威力 = 400
		効果数値 = -1
	ELSE
		奥義威力 = 300
		効果数値 = 3
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%たちのスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 「スリーフェアリーズ」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵全体に三妖精の合計攻撃力による自属性のダメージを与える。
			IF 効果数値 <= 0
				詳細文文字列受け渡し変数 += @"<br>対象の命中率を弱体耐性を無視して永続的に［-50％/独自枠］する。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>対象の命中率を弱体耐性を無視して{効果数値}ターン［-50％/独自枠］する。"
			ENDIF
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			攻撃数値 = 0
			IF INRANGE(三妖精隊列:1, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:1)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:1):攻撃力
			ENDIF
			IF INRANGE(三妖精隊列:2, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:2)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:2):攻撃力
			ENDIF
			{
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "攻撃力_スリーフェアリーズ", "バフ対象能力", "攻撃力",
				"バフ効果量_固定値", 攻撃数値, "バフ名", "スリーフェアリーズ", "持続回数", 1, "特殊表示オプション", -1
			}

			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 命中率_スリーフェアリーズ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:効果量 = 50
			アビ汎用変数:持続ターン = 効果数値
			アビ汎用変数:弱体耐性貫通オプション = 1
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ELSE
	;効果が強くなっても名前は同じ
	奥義名 = サンシャインブラスト
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 14
		奥義威力 = 400
		効果数値 = 50
	ELSE
		奥義威力 = 300
		効果数値 = 20
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 陽光「サンシャインブラスト」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵単体に自属性のダメージを与える。
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				詳細文文字列受け渡し変数 += "<br>敵全体の"
			ELSE
				詳細文文字列受け渡し変数 += "<br>対象の"
			ENDIF
			詳細文文字列受け渡し変数 += @"命中率を3ターン［-{効果数値}％/独自枠］する。基礎成功率100％。"
			RETURN 奥義威力
		CASE "奥義前効果"
		CASE "奥義追加効果"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				CALL アビ対象選択テンプレート_敵全体_生者のみ
			ENDIF
			アビ汎用文字列:バフ・デバフ枠 = 命中率_サンシャインブラスト
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:効果量 = 効果数値
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ENDIF
