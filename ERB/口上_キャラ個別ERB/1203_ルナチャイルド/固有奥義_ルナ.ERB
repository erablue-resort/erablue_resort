@固有奥義_1203(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値
#DIM 三妖精奥義フラグ, 3
#DIM 三妖精隊列, 3
#DIM 攻撃数値

VARSET 三妖精奥義フラグ, 0
SIF キャラ隊列検索(キャラ番号) >= 0
	CALL 三妖精奥義発動確認(三妖精奥義フラグ)

;他の三妖精が先に奥義を使用している場合
IF 三妖精奥義フラグ:0 && 三妖精奥義フラグ:2
	CALL 三妖精隊列検索(三妖精隊列)

	;効果が強くなっても名前は同じ
	奥義名 = スリーフェアリーズ
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号) && 三妖精奥義フラグ:0 >= 2 && 三妖精奥義フラグ:2 >= 2
		奥義威力 = 400
		効果数値 = 100
	ELSE
		奥義威力 = 300
		効果数値 = 0
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%たちのスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 「スリーフェアリーズ」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵全体に三妖精の合計攻撃力による自属性のダメージを与える。
			詳細文文字列受け渡し変数 += @"<br>対象に弱体耐性を無視して2ターンの封印［100％］効果を与える。"
			IF 効果数値 > 0
				詳細文文字列受け渡し変数 += "<br>味方全体の防御力を9ターン［{効果数値}％/独自枠］増加させる。"
			ENDIF
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			攻撃数値 = 0
			IF INRANGE(三妖精隊列:0, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:0)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:0):攻撃力
			ENDIF
			IF INRANGE(三妖精隊列:2, 0, 3)
				CALL バフ込み数値算出("攻撃力", 三妖精隊列:2)
				攻撃数値 += RESULT
			ELSE
				攻撃数値 += BATTLE_STATE:(三妖精隊列:2):攻撃力
			ENDIF
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_スリーフェアリーズ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用文字列:バフ・デバフ名 = スリーフェアリーズ
			アビ汎用変数:効果量 = 攻撃数値
			アビ汎用変数:持続行動回数 = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 封印
			アビ汎用文字列:バフ・デバフ対象能力 = 封印
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:弱体耐性貫通オプション = 1
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
			IF 効果数値 > 0
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_味方全体_生者のみ
				アビ汎用文字列:バフ・デバフ枠 = 防御力_スリーフェアリーズ
				アビ汎用文字列:バフ・デバフ対象能力 = 防御力
				アビ汎用変数:効果割合 = 効果数値
				アビ汎用変数:持続ターン = 9
				アビ汎用変数:消去不可オプション = 1
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
ELSE
	;効果が強くなっても名前は同じ
	奥義名 = サイレントフラワー
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 14
		奥義威力 = 200
		効果数値 = 150
	ELSE
		奥義威力 = 160
		効果数値 = 70
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 月光「サイレントフラワー」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵全体に自属性のダメージを与える。
			詳細文文字列受け渡し変数 += @"<br>対象に3ターンの封印［50％］効果を与える。基礎成功率{MIN(効果数値, 100)}％。"
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				詳細文文字列受け渡し変数 += "<br>味方全体の防御力を3ターン［50％/独自枠］増加させる。"
			ENDIF
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 封印
			アビ汎用文字列:バフ・デバフ対象能力 = 封印
			アビ汎用変数:効果割合 = 50
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果数値
			CALL アビテンプレート_不利効果_デバフ効果セット
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_味方全体_生者のみ
				アビ汎用文字列:バフ・デバフ枠 = 防御力_サイレントフラワー
				アビ汎用文字列:バフ・デバフ対象能力 = 防御力
				アビ汎用変数:効果割合 = 50
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
ENDIF
