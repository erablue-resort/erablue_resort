@固有アビ_1203_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = ルナティックレイン
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	消費ＭＰ = 25
	効果数値:0 = 110
	効果数値:1 = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	消費ＭＰ = 15
	効果数値:0 = 80
	効果数値:1 = 50
ELSE
	消費ＭＰ = 10
	効果数値:0 = 70
	効果数値:1 = 30
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 月符「ルナティックレイン」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += "<br>対象の弱体耐性を3ターン［-30％/B枠］する。基礎成功率70％。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>対象の行動速度を3ターン［{効果数値:1}％/B枠］減少させる。基礎成功率100％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 弱体耐性B
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
			アビ汎用変数:効果割合 = 30
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 行動速度B
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 効果数値:1
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@固有アビ_1203_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = ムーンライトウォール
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	消費ＭＰ = 30
	効果数値:0 = 60
	効果数値:1 = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	消費ＭＰ = 20
	効果数値:0 = 40
	効果数値:1 = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	消費ＭＰ = 15
	効果数値:0 = 30
	効果数値:1 = 50
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 障光「ムーンライトウォール」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>味方全体に3ターンバリア［回復力✕{効果数値:0}％］効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>対象にバリア効果中、攻撃対象となった時に光属性の［攻撃力✕{効果数値:1}％］ダメージで反撃する効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値:0 / 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ムーンライトウォール
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被タゲ時
		アビ汎用変数:効果割合 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@特殊反応_被タゲ時_ムーンライトウォール(バフ番号, 攻撃者隊列, 被タゲ回数)
#DIM 被タゲ回数
#DIM 攻撃者隊列
#DIM バフ番号
#DIM バリア行数

バリア行数 = バフ番号取得("バリア", 戦闘行動キャラ)
IF バリア行数 >= 0
	アビテンプレート用_アビ名 = 反撃

	CALL アビ対象選択テンプレート_指定(攻撃者隊列)
	アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
	アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
	CALL アビテンプレート_ダメージ処理_光属性
ELSE
	;持続回数1をセットしてこのあとの処理ですぐ消す
	DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", 1
ENDIF

@固有ターン終了時処理_バフ_ムーンライトウォール(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM バリア行数

バリア行数 = バフ番号取得("バリア", 戦闘行動キャラ)
IF バリア行数 < 0 || DT_CELL_GET("戦闘効果データベース", バリア行数, "持続ターン") == 1
	;持続ターン1をセットしてこのあとの処理ですぐ消す
	DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "持続ターン", 1
ENDIF

@バフ固有説明文_ムーンライトウォール(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL バリア効果中に攻撃対象となったとき、光属性攻撃で反撃を行います。
RETURN 0


@固有アビ_1203_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 3

;効果が強くなっても名前は同じ
アビ名 = サイレントサイクロン
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	消費ＭＰ = 40
	効果数値:0 = 120
	効果数値:1 = 50
	効果数値:2 = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	消費ＭＰ = 30
	効果数値:0 = 100
	効果数値:1 = 35
	効果数値:2 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	消費ＭＰ = 20
	効果数値:0 = 80
	効果数値:1 = 35
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 月光「サイレントサイクロン」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に風と光属性の［攻撃力✕{効果数値:0}％］ダメージを1回ずつ与える。"
		詳細文文字列受け渡し変数 += @"<br>対象に3ターンの封印［{効果数値:1}％］効果を与える。基礎成功率60％。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			詳細文文字列受け渡し変数 += @"<br>対象に{効果数値:2}ターンの混乱効果を与える。基礎成功率20％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_風属性
		CALL アビテンプレート_ダメージ処理_光属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 封印
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		アビ汎用変数:効果割合 = 効果数値:1
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 60
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 混乱
			アビ汎用文字列:バフ・デバフ対象能力 = 混乱
			アビ汎用変数:持続ターン = 効果数値:2
			アビ汎用変数:基礎成功確率 = 20
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1203_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM バフ番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
	アビ名 = 狂おしい静寂＋
	消費ＭＰ = 10
	効果数値:0 = 7
ELSE
	アビ名 = 狂おしい静寂
	消費ＭＰ = 5
	効果数値:0 = 3
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は音を消す程度の能力を使った！
アビ汎用文字列:実行時メッセージ２ = %アビ名%！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>味方単体に{効果数値:0}ターン、敵対心が非常に小さくなる効果を与える。"
		詳細文文字列受け渡し変数 += "<br>自身の次の行動がターンの最後になる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 狂おしい静寂
		アビ汎用文字列:バフ・デバフ対象能力 = 狂おしい静寂
		アビ汎用変数:持続ターン = 効果数値:0
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		バフ番号 = バフ番号取得_枠("少女待機中", 戦闘行動キャラ)
		IF バフ番号 >= 0
			DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", 2
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 少女待機中
			アビ汎用文字列:バフ・デバフ対象能力 = 少女待機中
			アビ汎用文字列:バフ・デバフ名 = 少女待機中
			アビ汎用変数:持続行動回数 = 2
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@固有バフ・デバフ処理_バフ_狂おしい静寂_敵対心(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

IF 隊列番号 < 10
	RETURN MAX(1, 補正前数値 - BATTLE_STATE:隊列番号:敵対心 + 1)
ELSE
	RETURN MAX(1, 補正前数値 - 敵BATTLE_STATE:(隊列番号 - 10):敵対心 + 1)
ENDIF

@バフ・デバフ特殊表示_狂おしい静寂(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 敵対心バフ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
IF 隊列番号 < 10
	PRINTFORML 効果量：{(1 - BATTLE_STATE:隊列番号:敵対心), 4}　　
ELSE
	PRINTFORML 効果量：{(1 - 敵BATTLE_STATE:(隊列番号 - 10):敵対心), 4}　　
ENDIF
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}　　


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1203(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『光の三妖精』 □-------------------------------------------------------------------------------
KSTR:(K++) = 　サニーおよびスターが同一ターンに先に奥義を発動していると、特殊な奥義が使用可能

CALL 口上変数表示(対象キャラ, 1)
