;--------------------------------------------------
;●雷神シド口上＠erablue_resort用
;　――固有アビリティ
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれも雷神シド口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●スキルに関するメモ
;--------------------------------------------------
;戦闘中の表示について：
;　アビリティ名の文字数は全角で12文字くらいまで。
;　固有アビに使える行は３行
;　１行に表示するアビリティは３つ
;　ただし「じゃあ最大９アビまで表示が崩れないのか？」というとちょっと違って
;　/
;　[100] 通常攻撃
;　[110] なまえのなが～いアビリティいち～～～[111] アビリティ２
;　[112] アビリティ３
;　[120] アビリティ４　[121] アビリティ５
;　（証アビ行）
;　/
;　……と、本来は[110]と同じ行に表示されるべき[112]が改行されて表示されちゃう場合がある。
;--------------------------------------------------

;--------------------------------------------------
;●コンセプト
;--------------------------------------------------
;・FF最強のシド
;　長いFF史上でもトップクラスのバランスブレイカー。
;　なので「強いこと」が原作再現になる……が、さすがにそれはちょっとよろしくない。
;　なので、本気を出すために、「定住させて陥落させてLv40以上」という条件を付けた。
;　まあこの爺さんを本気で使いたいなら強くてもいいだろ……
;・固有アビリティ
;　状態異常を与える聖剣技
;　HPを吸収する暗黒剣
;　デバフを与え、デバフが既に付与されている場合（ブレイク済みの場合）、威力が向上される剛剣
;　……の三系統。
;　ただ、アビを９個に収める都合上、「闇の剣と暗の剣」「乱命割殺打と北斗骨砕打」は同じ枠にしている。
;・無消費＆必中＆有射程＆即時発動
;　原作でのやばい強みだが、さすがにこの辺は再現しない。
;　無消費、必中あたりは再現も簡単だが、さすがにね。
;・自属性攻撃
;　「聖剣技」「暗黒剣」と言いつつ、属性は武器の属性に依存する。
;　なお、エクスカリバーは無属性武器で、聖属性を強化する。
;　原作再現の一環として「固有アビリティはすべて自属性攻撃」「エクスカリバーは光強化」とする。
;・攻撃範囲
;　原作で単体攻撃はまあ単体
;　一直線攻撃の聖光爆裂破だけは横列攻撃
;　原作で十字範囲のものは、縦列の上下１体にも攻撃とする。
;　（横列範囲もオッケーにすると横列範囲の完全上位互換になるので、縦列拡散のみにする）
;・名前のパワーアップ
;　不動無明剣→剣聖式・不動無明剣→雷神剣・不動無明剣……ウーン。
;　「剣聖剣術」「剣聖式」「雷神剣」「全剣技」「轟」など候補を考えはしたのだが
;　まあぶっちゃけ長いし全部のスキルがそうなのはちょっとな……ということで「不動無明剣＋＋」式にしておく。
;　元々「不動無明剣」とかの時点でエグいドスの効いた技名だしね。
;--------------------------------------------------

;--------------------------------------------------
;●エクスカリバーについて
;--------------------------------------------------
;原作エクスカリバーの効果は以下の通り。
;・高い攻撃力（全武器中七位。極端に高いわけではないが最後まで余裕で通用する）
;・永久ヘイスト（装備しているだけで行動速度が1.5倍。つまり、他人が２回動く間に３回殴れる）
;・聖属性強化（ただしエクスカリバー自体は無属性であり、剣技は武器属性と同じ属性になるので、剣技にはほぼ影響しない）
;・聖属性吸収
;/
;攻撃アビリティがすべて「自属性ダメージ」なのは原作再現。
;雷神……ということで、風属性にさせていただいたので、エクスカリバーの光強化はそのままだと役に立たない。
;エクスカリバーに聖属性はついてない。原作だと聖属性魔法のホーリーを使うくらいしかなかったが、こっちだと属性変更できるはず。よかったね。
;/
;永久ヘイスト、1.5倍速なんだから偶数ターンに２回行動では？　と思ったあなたは正しい。さすがにまずいだろって思ったので……はい……
;/
;この辺「武器ブレイク」されていたら……「攻撃マイナスのデバフがついてたら」消える……みたいな処理もいいかもしれない。
;まあでもさすがにキツいのでやめときます
;--------------------------------------------------

;--------------------------------------------------
;●アビリティ・パッシブ詳細
;--------------------------------------------------
;・アビ１：不動無明剣
;　　習得Lv　：1
;　　範囲　　：拡散
;　　追加効果：麻痺（追加ストップ再現）
;　　威力　　：低
;・アビ２：乱命割殺打→北斗骨砕打→雲耀滅却突き
;　　習得Lv　：10→20→60
;　　範囲　　：単体
;　　追加効果：乱命の宣告→即死→乱命の宣告＋即死
;　　威力　　：中→中→中×２
;　　　アビ枠を９に収めたく、合体。
;　　　「雲耀滅却突き」の名称はFFRKより。「極星」部分は技種別みたいなものなのでカット。
;　　　滅却とは「消え滅びること。消し滅ぼすこと」とめちゃくちゃ強い言葉なのでまあいいかな……
;　　　乱命の宣告は独自デバフ。つまり原作で言う「死の宣告」だが、まあなんかこう……わりと一般的にありそうなデバフ名なので、多少名称を変更する。
;　　　もちろんボスや即死耐性持ちには通らない。
;　　　これは……
;　　　　1.効果なしのデバフ「乱命の宣告」を与える
;　　　　2.（シドが生きている）ターン終了時、乱命の宣告がかかった敵のカウントを進める
;　　　　3.乱命の宣告カウントが一定になったら命中率100％の即死
;　　　……処理で実現する。
;　　　乱命の宣告発動時の即死率は100％であるが、「N回、状態異常を防御する」みたいな能力で防げる。成功率が非常に高いだけで防げるのだな。
;　　　アンデッド、霊魂系の敵に宣告が発動すると全回復させてしまう。
;　　　また、原作でも一応消去可能だったので、乱命の宣告も消去可能。
;・アビ３：無双稲妻突き
;　　習得Lv　：30
;　　範囲　　：拡散
;　　追加効果：封印（追加沈黙再現）
;　　威力　　：中
;　　　威力のわりに消費が軽いという調整。
;・アビ４：聖光爆裂破
;　　習得Lv　：40
;　　範囲　　：横列
;　　追加効果：混乱（追加混乱再現）
;　　威力　　：高
;　　　原作だと直線範囲の長射程に大ダメージ……だった。わりと敵を巻き込みやすい。
;　　　こっちで再現するとまあ横列範囲になるけど、そっちの方がむしろ範囲狭くなるの隊列の都合だよなー……
;・アビ５：闇の剣→暗黒剣二刀流→暗黒剣二刀流みだれうち
;　　習得Lv　：1→20→70
;　　範囲　　：単体→単体２回→２回セットのランダム４回
;　　追加効果：HP回復→HP回復＋MP回復
;　　威力　　：低→低×２→極低×２×４
;　　　HPダメージ・HP吸収の闇の剣はともかく、
;　　　MPダメージ・MP吸収の暗の剣はちょっと単独再現しても困るよな……ということで合体。アビ枠もせめて９までには納めたかったし……
;　　　暗の剣部分はだいぶ手加減したつもりだが強かったらどうしようね。
;　　　二刀流みだれうちになると、「闇の剣＋暗の剣」を４回繰り返す。FF5の仕様。
;　　　二刀流乱れ撃ちはFFTではなく5だが「二刀流」って枕詞には「乱れ撃ち」が続くんだよ。
;・アビ６：強甲破点突き
;　　習得Lv　：1
;　　範囲　　：単体
;　　追加効果：胴体防具ブレイク（全耐性マイナス＆全耐性マイナス付与済みなら威力向上）
;　　威力　　：低/高
;　　　剛剣は装備破壊技。
;　　　破壊するためには「装備していなければならない」が、そこはさすがにチェックしない。
;　　　原作では「破壊できなかった場合ダメージが入らない」という重篤すぎる弱点があったが、
;　　　PSP版では「破壊するものがなかったならダメージ上昇」という効果に改められた。
;　　　まあ「武器防具破壊耐性」があるとダメージ与えられないのは変わらないんですけどね。
;・アビ７：星天爆撃打
;　　習得Lv　：10
;　　範囲　　：単体
;　　追加効果：頭防具ブレイク（命中率マイナス＆命中率マイナス付与済みなら威力向上）
;　　威力　　：低/高
;・アビ８：冥界恐叫打
;　　習得Lv　：20
;　　範囲　　：単体
;　　追加効果：武器ブレイク（攻撃ダウン＆攻撃ダウン付与済みなら威力向上）
;　　威力　　：低/高
;・アビ９：咬撃氷狼破
;　　習得Lv　：30
;　　範囲　　：単体
;　　追加効果：装飾品ブレイク（バフが少ないほど威力が向上。バフ消去）
;　　威力　　：高～低
;　　　バフが少ないほど大ダメージ。
;　　　「装飾品（＝特殊効果）をすでに破壊しているなら」というイメージ。
;
;パッシブ
;・聖剣エクスカリバー
;　それぞれ独自枠・消去不可・99ターンで下記効果を付与。
;　＞50％の行動速度アップ
;　＞３の倍数のターン、２回行動
;　＞50％の光属性強化
;　＞50％の光属性耐性
;・似非聖剣エクスカリパー
;　[さいきょうのけん]（独自枠・99ターン・消去不可・回避不可）で下記効果を付与。
;　＞自身の攻撃力を99％減少

;--------------------------------------------------



;--------------------------------------------------
;〇不動無明剣
;--------------------------------------------------
@固有アビ_2611_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 状態異常_効果発生率
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン
#DIM 拡散_中心
#DIM 拡散_上
#DIM 拡散_下

;	;スタイル判定関数を呼び出す
;	;スタイル変更が無い場合はここは何も触らなくて良い
;	IF 0
;	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
;		アビ名 = 
;		RETURN 0
;	ELSEIF スタイル判定関数(キャラ番号, "[スタイル名]キャラ名") == 1
;		アビ名 = 
;		RETURN 0
;	ENDIF


	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv1から習得なのでカット
;	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 1
;		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名   = 不動無明剣＋＋
		威力値   = 240
		消費ＭＰ = 25
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名   = 不動無明剣＋
		威力値   = 220
		消費ＭＰ = 20
	ELSE
		アビ名   = 不動無明剣
		威力値   = 200
		消費ＭＰ = 15
	ENDIF

	;ストップ代わりに麻痺を発動。
	;麻痺の効果発生率（行動できなくなる率）は20％とする
	;付与率は、原作では1/4＝25％なのでそこに合わせる
	状態異常_効果発生率 = 20
	状態異常_付与率     = 25
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体とその周囲の敵に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の麻痺効果を与える。成功率{状態異常_付与率}％。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の麻痺効果を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>対象の上に敵がいるなら、［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の麻痺効果を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>対象の下に敵がいるなら、［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の麻痺効果を与える。成功率{状態異常_付与率}％。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ汎用変数文字列リセット
			;ここはシンプルに対象を選択
			CALL アビ対象選択テンプレート_敵単体
			;拡散の中心を保存
			拡散_中心 = アビテンプレート用_対象保存:0
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_自属性

			;麻痺部分
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 麻痺
			アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
			アビ汎用変数:効果割合               = 状態異常_効果発生率
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット

			;対象の上への攻撃
			CALL アビ汎用変数文字列リセット
			拡散_上 = 雷神シド_攻撃拡散処理(拡散_中心, "上")
			;ゼロ以外が返ってきた（そこになんかいる）ならその先へ
			IF 拡散_上 != 0
				CALL アビ対象選択テンプレート_指定(拡散_上)
				アビ汎用変数:効果割合 = 威力値
				CALL アビテンプレート_ダメージ処理_自属性

				;麻痺部分
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠       = 麻痺
				アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
				アビ汎用変数:効果割合               = 状態異常_効果発生率
				アビ汎用変数:持続ターン             = 状態異常_持続ターン
				アビ汎用変数:基礎成功確率           = 状態異常_付与率
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF

			;対象の下への攻撃
			CALL アビ汎用変数文字列リセット
			拡散_下 = 雷神シド_攻撃拡散処理(拡散_中心, "下")
			;ゼロ以外が返ってきた（そこになんかいる）ならその先へ
			IF 拡散_下 != 0
				CALL アビ対象選択テンプレート_指定(拡散_下)
				アビ汎用変数:効果割合 = 威力値
				CALL アビテンプレート_ダメージ処理_自属性

				;麻痺部分
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠       = 麻痺
				アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
				アビ汎用変数:効果割合               = 状態異常_効果発生率
				アビ汎用変数:持続ターン             = 状態異常_持続ターン
				アビ汎用変数:基礎成功確率           = 状態異常_付与率
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF
	ENDSELECT


;--------------------------------------------------
;〇乱命割殺打→北斗骨砕打→雲耀滅却突き
;--------------------------------------------------
@固有アビ_2611_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値_骨砕打
#DIM 威力値_割殺打
#DIM 状態異常_付与率
#DIM 効果種類
#DIM 経絡が断たれる者

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv10から習得
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 10
		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
		アビ名   = 雲耀滅却突き
		消費ＭＰ = 60
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名   = 北斗骨砕打
		消費ＭＰ = 30
	ELSE
		アビ名   = 乱命割殺打
		消費ＭＰ = 25
	ENDIF
	;威力はまあまあ固定。
	;北斗骨砕打のほうが上位の技なのだが、原作ではこっちのほうが威力がやや低い。
	;計算式をクッソ大雑把に言うと「（武器攻撃力＋技威力値）×ちから」なのだが、骨砕打は威力値２、割殺打は威力値３。
	;武器攻撃力21のエクスカリバーでAT15の状態で使った場合、骨砕打は345、割殺打は360……くらいの差が出る。微妙。
	威力値_骨砕打 = 180
	威力値_割殺打 = 200

	;乱命割殺打は原作では３ターン後に死亡する状態異常「死の宣告」を付与。
	;ただ、『死の宣告』ってわりとありそうな響きのデバフ名なので、「乱命の宣告」という名前にする。
	;北斗骨砕打は原作では即死を付与。
	;付与率は、原作では1/4＝25％なのでそこに合わせる
	状態異常_付与率     = 25

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF 0
			ELSEIF アビ名 == "雲耀滅却突き"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_骨砕打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>［{状態異常_付与率}％］の即死効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_割殺打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>乱命の宣告効果（独自枠）を与える。成功率{状態異常_付与率}％。"
			ELSEIF アビ名 == "北斗骨砕打"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_骨砕打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>［{状態異常_付与率}％］の即死効果を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_割殺打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>乱命の宣告効果（独自枠）を与える。成功率{状態異常_付与率}％。"
			ENDIF
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF 0
			ELSEIF アビ名 == "雲耀滅却突き"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_骨砕打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>［{状態異常_付与率}％］の即死効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_割殺打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>乱命の宣告効果（独自枠）を与える。成功率{状態異常_付与率}％。"
				詳細文文字列受け渡し変数 += @"<br>＞雷神シドが生存した状態で累計３ターン経過したあと、即死効果を与える。成功率100％。"
			ELSEIF アビ名 == "北斗骨砕打"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_骨砕打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>［{状態異常_付与率}％］の即死効果を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_割殺打}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>乱命の宣告効果（独自枠）を与える。成功率{状態異常_付与率}％。"
				詳細文文字列受け渡し変数 += @"<br>＞雷神シドが生存した状態で累計３ターン経過したあと、即死効果を与える。成功率100％。"
			ENDIF
		CASE "アビ効果"
			;対象は単体
			CALL アビ対象選択テンプレート_敵単体
			;対象にした敵を保存しておくね
			経絡が断たれる者 = アビテンプレート用_対象保存:0 - 10

			;発動する効果を置く
			IF 0
			ELSEIF アビ名 == "雲耀滅却突き"
				SETBIT 効果種類, 1
				SETBIT 効果種類, 2
			ELSEIF アビ名 == "北斗骨砕打"
				SETBIT 効果種類, 1
			ELSE
				SETBIT 効果種類, 2
			ENDIF

			;北斗骨砕打
			IF GETBIT(効果種類, 1)
				CALL アビ汎用変数文字列リセット
				;対象への攻撃
				アビ汎用変数:効果割合 = 威力値_骨砕打
				CALL アビテンプレート_ダメージ処理_自属性

				;回避フラグが立っていない場合＝当たった場合、即死付与
				;まだ生きてる場合のみ。
				IF !アビテンプレート用_回避フラグ && 敵BATTLE_STATE:経絡が断たれる者:ＨＰ >= 1
					CALL アビ汎用変数文字列リセット
					アビ汎用変数:基礎成功確率 = 状態異常_付与率
					CALL アビテンプレート_不利効果_即死
				ENDIF
			ENDIF

			;一応リセットしておく
			アビテンプレート用_回避フラグ = 0

			;乱命割殺打
			;第２撃は生きてる相手のみ。
			IF GETBIT(効果種類, 2) && 敵BATTLE_STATE:経絡が断たれる者:ＨＰ >= 1
				CALL アビ汎用変数文字列リセット
				;対象への攻撃
				アビ汎用変数:効果割合 = 威力値_割殺打
				CALL アビテンプレート_ダメージ処理_自属性

				;回避フラグが立っていない場合＝当たった場合、宣告付与
				;まだ生きてる場合のみ。
				IF !アビテンプレート用_回避フラグ && 敵BATTLE_STATE:経絡が断たれる者:ＨＰ >= 1
					CALL アビ汎用変数文字列リセット

					;即死耐性で成功率を下げる
					;（これ以外の「弱体耐性、対弱体バリア」などはデバフ効果セットの通常効果で弾いてくれるはず……）
					CALL バフ・デバフ測定(アビテンプレート用_対象保存:0, "状態異常耐性_即死")
					状態異常_付与率 -= RESULT

					;ボスなど即死が効いてはいけない相手には効かない
					IF 敵BATTLE_STATE:経絡が断たれる者:ボスフラグ
						;ボスなら効かない
					ELSE
						;乱命の宣告そのものに効果はない
						アビ汎用文字列:バフ・デバフ枠 = 乱命の宣告
						;持続は99だがカウントがゼロになると消えるようにしている
						アビ汎用変数:持続ターン = 99
						アビ汎用変数:基礎成功確率 = 状態異常_付与率
						CALL アビテンプレート_不利効果_デバフ効果セット
					ENDIF

				ENDIF
			ENDIF
	ENDSELECT


;--------------------------------------------------
;〇無双稲妻突き
;--------------------------------------------------
@固有アビ_2611_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 状態異常_効果発生率
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン
#DIM 拡散_中心
#DIM 拡散_上
#DIM 拡散_下

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv30から習得
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 30
		アビ名 = 
	;消費のわりに威力が高い調整……としたい
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビ名   = 無双稲妻突き＋＋
		威力値   = 350
		消費ＭＰ = 25
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
		アビ名   = 無双稲妻突き＋
		威力値   = 325
		消費ＭＰ = 20
	ELSE
		アビ名   = 無双稲妻突き
		威力値   = 300
		消費ＭＰ = 10
	ENDIF

	;沈黙代わりに封印を発動。
	;封印の効果発生率（行動できなくなる率）は10％とする
	;付与率は、原作では1/4＝25％なのでそこに合わせる
	状態異常_効果発生率 = 10
	状態異常_付与率     = 25
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体とその周囲の敵に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］の封印効果を与える。成功率{状態異常_付与率}％。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の封印効果を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>対象の上に敵がいるなら、［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の封印効果を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>対象の下に敵がいるなら、［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［{状態異常_効果発生率}％］（{状態異常_持続ターン}ターン）の封印効果を与える。成功率{状態異常_付与率}％。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ汎用変数文字列リセット
			;ここはシンプルに対象を選択
			CALL アビ対象選択テンプレート_敵単体
			;拡散の中心を保存
			拡散_中心 = アビテンプレート用_対象保存:0
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_自属性

			;封印部分
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 封印
			アビ汎用文字列:バフ・デバフ対象能力 = 封印
			アビ汎用変数:効果割合               = 状態異常_効果発生率
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット

			;対象の上への攻撃
			CALL アビ汎用変数文字列リセット
			拡散_上 = 雷神シド_攻撃拡散処理(拡散_中心, "上")
			;ゼロ以外が返ってきた（そこになんかいる）ならその先へ
			IF 拡散_上 != 0
				CALL アビ対象選択テンプレート_指定(拡散_上)
				アビ汎用変数:効果割合 = 威力値
				CALL アビテンプレート_ダメージ処理_自属性

				;封印部分
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠       = 封印
				アビ汎用文字列:バフ・デバフ対象能力 = 封印
				アビ汎用変数:効果割合               = 状態異常_効果発生率
				アビ汎用変数:持続ターン             = 状態異常_持続ターン
				アビ汎用変数:基礎成功確率           = 状態異常_付与率
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF

			;対象の下への攻撃
			CALL アビ汎用変数文字列リセット
			拡散_下 = 雷神シド_攻撃拡散処理(拡散_中心, "下")
			;ゼロ以外が返ってきた（そこになんかいる）ならその先へ
			IF 拡散_下 != 0
				CALL アビ対象選択テンプレート_指定(拡散_下)
				アビ汎用変数:効果割合 = 威力値
				CALL アビテンプレート_ダメージ処理_自属性

				;封印部分
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠       = 封印
				アビ汎用文字列:バフ・デバフ対象能力 = 封印
				アビ汎用変数:効果割合               = 状態異常_効果発生率
				アビ汎用変数:持続ターン             = 状態異常_持続ターン
				アビ汎用変数:基礎成功確率           = 状態異常_付与率
				CALL アビテンプレート_不利効果_デバフ効果セット
			ENDIF
	ENDSELECT


;--------------------------------------------------
;〇聖光爆裂破
;--------------------------------------------------
@固有アビ_2611_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv40から習得
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 40
		アビ名 = 
	;原作の聖剣技の中では最も高威力
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビ名   = 聖光爆裂破＋＋
		威力値   = 400
		消費ＭＰ = 50
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
		アビ名   = 聖光爆裂破＋
		威力値   = 375
		消費ＭＰ = 45
	ELSE
		アビ名   = 聖光爆裂破
		威力値   = 350
		消費ＭＰ = 40
	ENDIF

	;混乱を発動。
	;混乱は効果量・割合に効果なしなので発生率はカット
	;付与率は、原作では1/4＝25％なのでそこに合わせる
	状態異常_付与率     = 25
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵横列に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>混乱効果（{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵横列に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>混乱効果（{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ対象選択テンプレート_敵横列
			アビ汎用変数:効果割合 = 威力値
			CALL アビテンプレート_ダメージ処理_自属性

			;混乱部分
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 混乱
			アビ汎用文字列:バフ・デバフ対象能力 = 混乱
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇闇の剣→暗黒剣二刀流→暗黒剣二刀流みだれうち
;--------------------------------------------------
@固有アビ_2611_5(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 威力値_闇の剣
#DIM 威力値_闇の剣_回復力
#DIM 威力値_暗の剣
#DIM 威力値_暗の剣_最大
#DIM 威力値_暗の剣_最小
#DIM 攻撃対象一時保存
#DIM 闇の剣_命中一時保存
#DIM 隊列番号

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv1から習得なのでカット
;	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 30
;		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビ名   = 暗黒剣二刀流みだれうち
		;乱れ撃ちなのでダメージ半減。
		威力値_闇の剣   = 50
		威力値_闇の剣_回復力 = 12
		威力値_暗の剣_最大 = 3
		威力値_暗の剣_最小 = 0
		消費ＭＰ = 50
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名   = 暗黒剣二刀流
		威力値_闇の剣   = 100
		威力値_闇の剣_回復力 = 25
		威力値_暗の剣_最大 = 5
		威力値_暗の剣_最小 = 1
		消費ＭＰ = 30
	ELSE
		アビ名   = 闇の剣
		威力値_闇の剣   = 100
		威力値_闇の剣_回復力 = 25
		威力値_暗の剣_最大 = 0
		威力値_暗の剣_最小 = 0
		消費ＭＰ = 10
	ENDIF


	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF 0
			;どー書いたもんかなこれ。
			;「ランダム対象１体を選択し、右手左手で攻撃」×４、なんだけど……この書き方しかないかなあ。
			ELSEIF アビ名 == "暗黒剣二刀流みだれうち"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に、下記攻撃を４回行う。"
				詳細文文字列受け渡し変数 += @"<br>＞敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>＞自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［対象の最大ＭＰ✕{威力値_暗の剣_最小}～{威力値_暗の剣_最大}％］のＭＰダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>＞自身に［{威力値_暗の剣_最小}～{威力値_暗の剣_最大}］のＭＰ回復を与える。"
			ELSEIF アビ名 == "暗黒剣二刀流"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
				詳細文文字列受け渡し変数 += @"<br>敵単体に、［対象の最大ＭＰ✕{威力値_暗の剣_最小}～{威力値_暗の剣_最大}％］のＭＰダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［{威力値_暗の剣_最小}～{威力値_暗の剣_最大}］のＭＰ回復を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
			ENDIF
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF 0
			ELSEIF アビ名 == "暗黒剣二刀流みだれうち"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に、下記攻撃を４回行う。"
				詳細文文字列受け渡し変数 += @"<br>＞敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>＞自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［対象の最大ＭＰ✕{威力値_暗の剣_最小}～{威力値_暗の剣_最大}％］のＭＰダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>＞自身に［{威力値_暗の剣_最小}～{威力値_暗の剣_最大}］のＭＰ回復を与える。"
			ELSEIF アビ名 == "暗黒剣二刀流"
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
				詳細文文字列受け渡し変数 += @"<br>敵単体に、［対象の最大ＭＰ✕{威力値_暗の剣_最小}～{威力値_暗の剣_最大}％］のＭＰダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［{威力値_暗の剣_最小}～{威力値_暗の剣_最大}］のＭＰ回復を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値_闇の剣}％］の自属性ダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身に［攻撃力✕{威力値_闇の剣_回復力}％］のＨＰ回復を与える。"
			ENDIF
		CASE "アビ効果"
			IF 0
			ELSEIF アビ名 == "暗黒剣二刀流みだれうち"
				;「４回」「（敵対心を無視せず）選択した敵ランダム１体に」「闇の剣と暗の剣」を繰り返す。
				;都合上、２×４＝８連撃が１体に集中することもあり得る。最大でも４体攻撃。
				FOR LOCAL, 0, 4
					;２撃目もここで決定した対象。
					;ただし、次の二刀流攻撃の際は再度ターゲットを選択する。
					CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
					攻撃対象一時保存 = アビテンプレート用_対象保存:0

					;闇の剣：単体にダメージ＋ＨＰ回復。
					;対象への攻撃
					CALL アビ汎用変数文字列リセット
					アビ汎用変数:効果割合 = 威力値_闇の剣
					CALL アビテンプレート_ダメージ処理_自属性

					;当たった場合のみ、自己回復
					IF !アビテンプレート用_回避フラグ
						CALL アビ汎用変数文字列リセット
						CALL アビ対象選択テンプレート_自己のみ
						CALL バフ込み数値算出("攻撃力")
						アビ汎用変数:効果量 = RESULT * 威力値_闇の剣_回復力
						CALL アビテンプレート_回復処理_MAXでも回復
					ENDIF

					;おまじない：命中判定リセット
					アビテンプレート用_回避フラグ = 0

					;暗の剣：単体にMPダメージ＋MP回復。
					;暗黒剣二刀流では「敵の現在値の１～５％」を削り、「自身のMPを１～５％回復する」になる。
					;雷神シドはMPの成長率を減らしているのであんまり回復しないはず。
					;/
					;暗の剣は、原作ではMPダメージを与えそのままの値MPを吸収していた……が、さすがにそれはちょっとどうかと思うので今回はナシ。
					;減らすも増やすもだいぶ手加減した値にする。
					;MPダメージの出力、大ボスでも一撃でMPが枯渇するほどだったので……

					;対象への攻撃
					CALL アビ汎用変数文字列リセット
					;闇の剣が当たった敵と同じ敵を狙う
					CALL アビ対象選択テンプレート_指定(攻撃対象一時保存)
					威力値_暗の剣 = RAND(威力値_暗の剣_最小, 威力値_暗の剣_最大 + 1)
					アビ汎用変数:効果割合 = 威力値_暗の剣
					CALL アビテンプレート_ＭＰ減少処理_現在値依存()

					;当たった場合のみ、自己回復
					IF !アビテンプレート用_回避フラグ
						CALL アビ汎用変数文字列リセット
						CALL アビ対象選択テンプレート_自己のみ
						威力値_暗の剣 = RAND(威力値_暗の剣_最小, 威力値_暗の剣_最大 + 1)
						アビ汎用変数:効果割合 = 威力値_暗の剣
						CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
					ENDIF
				NEXT
			ELSEIF アビ名 == "暗黒剣二刀流"
				;闇の剣：単体にダメージ＋ＨＰ回復。
				;対象への攻撃
				CALL アビ汎用変数文字列リセット
				;攻撃対象選択はここでのみ（２撃目も同じ対象）
				CALL アビ対象選択テンプレート_敵単体
				攻撃対象一時保存 = アビテンプレート用_対象保存:0

				アビ汎用変数:効果割合 = 威力値_闇の剣
				CALL アビテンプレート_ダメージ処理_自属性

				;当たった場合のみ、自己回復
				IF !アビテンプレート用_回避フラグ
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					CALL バフ込み数値算出("攻撃力")
					アビ汎用変数:効果量 = RESULT * 威力値_闇の剣_回復力
					CALL アビテンプレート_回復処理_MAXでも回復
				ENDIF

				;おまじない：命中判定リセット
				アビテンプレート用_回避フラグ = 0

				;暗の剣：単体にMPダメージ＋MP回復。
				;暗黒剣二刀流では「敵の現在値の１～５％」を削り、「自身のMPを１～５％回復する」になる。
				;雷神シドはMPの成長率を減らしているのであんまり回復しないはず。
				;/
				;暗の剣は、原作ではMPダメージを与えそのままの値MPを吸収していた……が、さすがにそれはちょっとどうかと思うので今回はナシ。
				;減らすも増やすもだいぶ手加減した値にする。
				;MPダメージの出力、大ボスでも一撃でMPが枯渇するほどだったので……

				;対象への攻撃
				CALL アビ汎用変数文字列リセット
				;闇の剣が当たった敵と同じ敵を狙う
				CALL アビ対象選択テンプレート_指定(攻撃対象一時保存)
				威力値_暗の剣 = RAND(威力値_暗の剣_最小, 威力値_暗の剣_最大 + 1)
				アビ汎用変数:効果割合 = 威力値_暗の剣
				CALL アビテンプレート_ＭＰ減少処理_現在値依存()

				;当たった場合のみ、自己回復
				IF !アビテンプレート用_回避フラグ
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					威力値_暗の剣 = RAND(威力値_暗の剣_最小, 威力値_暗の剣_最大 + 1)
					アビ汎用変数:効果割合 = 威力値_暗の剣
					CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
				ENDIF
			ELSE
				;闇の剣：単体にダメージ＋ＨＰ回復。
				;対象への攻撃
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_敵単体
				アビ汎用変数:効果割合 = 威力値_闇の剣
				CALL アビテンプレート_ダメージ処理_自属性

				;当たった場合のみ、自己回復
				IF !アビテンプレート用_回避フラグ
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					CALL バフ込み数値算出("攻撃力")
					アビ汎用変数:効果量 = RESULT * 威力値_闇の剣_回復力
					CALL アビテンプレート_回復処理_MAXでも回復
				ENDIF
			ENDIF
	ENDSELECT


;--------------------------------------------------
;〇強甲破点突き
;--------------------------------------------------
@固有アビ_2611_6(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 威力値_ブレイク加算
#DIM 威力値_総合威力
#DIM 状態異常_効果割合
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン
#DIM 攻撃対象一時保存
#DIM バフ・デバフ行数

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv1から習得なのでカット
;	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 1
;		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名   = 強甲破点突き＋＋
		威力値   = 240
		消費ＭＰ = 30
		威力値_ブレイク加算 = 160
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名   = 強甲破点突き＋
		威力値   = 220
		消費ＭＰ = 30
		威力値_ブレイク加算 = 130
	ELSE
		アビ名   = 強甲破点突き
		威力値   = 200
		消費ＭＰ = 30
		威力値_ブレイク加算 = 100
	ENDIF
	;ブレイク状態に使った時の威力加算が一番大きい。
	威力値_総合威力 = 威力値 + 威力値_ブレイク加算

	;全耐性マイナス20を発動。
	;付与率は、原作では100％……だが、まあ、落としておこっか。
	状態異常_効果割合 = 20
	状態異常_付与率   = 50
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>胴体防具ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>すでに胴体防具ブレイク効果が付与されている敵に使用した場合、威力が向上する。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>胴体防具ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>＞［{状態異常_効果割合}］の全耐性減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞すでに胴体防具ブレイク効果が付与されている敵に使用した場合、攻撃の威力が［攻撃力✕{威力値_総合威力}％］になる。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ対象選択テンプレート_敵単体
			攻撃対象一時保存 = アビテンプレート用_対象保存:0

			;対象の敵に「胴体防具ブレイク」という名前のデバフがかかっているかを見る
			バフ・デバフ行数 = デバフ番号取得_枠("胴体防具ブレイク", 攻撃対象一時保存)
			;かかっているなら威力向上
			IF バフ・デバフ行数 >= 0
				アビ汎用変数:効果割合 = 威力値_総合威力
			ELSE
				アビ汎用変数:効果割合 = 威力値
			ENDIF
			CALL アビテンプレート_ダメージ処理_自属性

			;胴体防具ブレイク部分
			;全耐性を20減らす……減らすための変数名、「割合」にしちゃった。まあいいか。
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 胴体防具ブレイク
			アビ汎用文字列:バフ・デバフ対象能力 = 全耐性
			アビ汎用変数:効果量                 = 状態異常_効果割合
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇星天爆撃打
;--------------------------------------------------
@固有アビ_2611_7(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 威力値_ブレイク加算
#DIM 威力値_総合威力
#DIM 状態異常_効果割合
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン
#DIM 攻撃対象一時保存
#DIM バフ・デバフ行数

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv10から習得
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 10
		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名   = 星天爆撃打＋＋
		威力値   = 220
		消費ＭＰ = 30
		威力値_ブレイク加算 = 130
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名   = 星天爆撃打＋
		威力値   = 200
		消費ＭＰ = 25
		威力値_ブレイク加算 = 100
	ELSE
		アビ名   = 星天爆撃打
		威力値   = 180
		消費ＭＰ = 20
		威力値_ブレイク加算 = 70
	ENDIF
	威力値_総合威力 = 威力値 + 威力値_ブレイク加算

	;命中率マイナス30％を発動。
	;付与率は、原作では100％……だが、まあ、落としておこっか。
	状態異常_効果割合 = 30
	状態異常_付与率   = 50
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>頭防具ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>すでに頭防具ブレイク効果が付与されている敵に使用した場合、威力が向上する。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>頭防具ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>＞［{状態異常_効果割合}％］の命中率減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞すでに頭防具ブレイク効果が付与されている敵に使用した場合、攻撃の威力が［攻撃力✕{威力値_総合威力}％］になる。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ対象選択テンプレート_敵単体
			攻撃対象一時保存 = アビテンプレート用_対象保存:0

			;対象の敵に「頭防具ブレイク」という名前のデバフがかかっているかを見る
			バフ・デバフ行数 = デバフ番号取得_枠("頭防具ブレイク", 攻撃対象一時保存)
			;かかっているなら威力向上
			IF バフ・デバフ行数 >= 0
				アビ汎用変数:効果割合 = 威力値_総合威力
			ELSE
				アビ汎用変数:効果割合 = 威力値
			ENDIF
			CALL アビテンプレート_ダメージ処理_自属性

			;胴体防具ブレイク部分
			;全耐性を20減らす……減らすための変数名、「割合」にしちゃった。まあいいか。
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 頭防具ブレイク
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:効果割合               = 状態異常_効果割合
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇冥界恐叫打
;--------------------------------------------------
@固有アビ_2611_8(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 威力値_ブレイク加算
#DIM 威力値_総合威力
#DIM 状態異常_効果割合
#DIM 状態異常_付与率
#DIM 状態異常_持続ターン
#DIM 攻撃対象一時保存
#DIM バフ・デバフ行数

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv20から習得
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 20
		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名   = 冥界恐叫打＋＋
		威力値   = 220
		消費ＭＰ = 35
		威力値_ブレイク加算 = 130
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
		アビ名   = 冥界恐叫打＋
		威力値   = 200
		消費ＭＰ = 30
		威力値_ブレイク加算 = 100
	ELSE
		アビ名   = 冥界恐叫打
		威力値   = 180
		消費ＭＰ = 25
		威力値_ブレイク加算 = 70
	ENDIF
	威力値_総合威力 = 威力値 + 威力値_ブレイク加算

	;攻撃力マイナス20％を発動
	;（攻撃力なのでまあ強い……と思いますんで消費とかデバフ率とか控えめ）
	;付与率は、原作では100％……だが、まあ、落としておこっか。
	状態異常_効果割合 = 20
	状態異常_付与率   = 50
	状態異常_持続ターン = 3

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>武器ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>すでに武器ブレイク効果が付与されている敵に使用した場合、威力が向上する。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>武器防具ブレイク効果（独自枠・{状態異常_持続ターン}ターン）を与える。成功率{状態異常_付与率}％。"
			詳細文文字列受け渡し変数 += @"<br>＞［{状態異常_効果割合}］の攻撃力減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞すでに武器ブレイク効果が付与されている敵に使用した場合、攻撃の威力が［攻撃力✕{威力値_総合威力}％］になる。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ対象選択テンプレート_敵単体
			攻撃対象一時保存 = アビテンプレート用_対象保存:0

			;対象の敵に「胴体防具ブレイク」という名前のデバフがかかっているかを見る
			バフ・デバフ行数 = デバフ番号取得_枠("武器ブレイク", 攻撃対象一時保存)
			;かかっているなら威力向上
			IF バフ・デバフ行数 >= 0
				アビ汎用変数:効果割合 = 威力値_総合威力
			ELSE
				アビ汎用変数:効果割合 = 威力値
			ENDIF
			CALL アビテンプレート_ダメージ処理_自属性

			;胴体防具ブレイク部分
			;全耐性を20減らす……減らすための変数名、「割合」にしちゃった。まあいいか。
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠       = 武器ブレイク
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合               = 状態異常_効果割合
			アビ汎用変数:持続ターン             = 状態異常_持続ターン
			アビ汎用変数:基礎成功確率           = 状態異常_付与率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇咬撃氷狼破
;--------------------------------------------------
@固有アビ_2611_9(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 威力値_下限
#DIM 威力値_ブレイク加算
#DIM 威力値_総合威力
#DIM バフ解除数
#DIM 敵バフ数
#DIM 敵バフ許容
#DIM 攻撃対象一時保存
#DIM バフ・デバフ行数

	IF 0
	;Lvが一定未満なら習得しない構文
	;Lv40から習得なのでカット
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 40
		アビ名 = 
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビ名   = 咬撃氷狼破＋＋
		威力値   = 350
		バフ解除数 = 3
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
		アビ名   = 咬撃氷狼破＋
		威力値   = 325
		バフ解除数 = 2
	ELSE
		アビ名   = 咬撃氷狼破
		威力値   = 300
		バフ解除数 = 1
	ENDIF

	消費ＭＰ = 60
	威力値_下限 = 50
	威力値_ブレイク加算 = 40
	;今回は後で計算する
	;（ここで計算するのは説明文詳細で例示するため）
	威力値_総合威力 = 威力値 - (威力値_ブレイク加算 * 3)
	SIF 威力値_総合威力 < 威力値_下限
		威力値_総合威力 = 威力値_下限

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>対象にかかっているバフの数が多いほど、威力が減少する。"
			詳細文文字列受け渡し変数 += @"<br>バフ効果を{バフ解除数}つ消去する。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>対象にかかっているバフの数１種につき、アビリティの威力が［{威力値_ブレイク加算}％］減少する。"
			詳細文文字列受け渡し変数 += @"<br>（例えば、三種のバフがかかっている敵に使用する場合、［攻撃力✕{威力値_総合威力}％］の自属性ダメージを与える）"
			詳細文文字列受け渡し変数 += @"<br>アビリティ威力の下限は［攻撃力✕{威力値_下限}％］。"
			詳細文文字列受け渡し変数 += @"<br>バフ効果を{バフ解除数}つ消去する。"
		CASE "アビ効果"
			;対象への攻撃
			CALL アビ対象選択テンプレート_敵単体
			攻撃対象一時保存 = アビテンプレート用_対象保存:0

			;対象の敵にかかっているバフの数をカウントし威力を強化
			敵バフ数 = 強化数算出(攻撃対象一時保存)
			威力値_総合威力 = 威力値 - (威力値_ブレイク加算 * 敵バフ数)
			;一応下限は設定
			SIF 威力値_総合威力 < 威力値_下限
				威力値_総合威力 = 威力値_下限
			アビ汎用変数:効果割合 = 威力値_総合威力

			CALL アビテンプレート_ダメージ処理_自属性

			;装飾品ブレイク部分
			;まあつまりバフ消去です
			CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, バフ解除数
				CALL アビテンプレート_不利効果_バフ消去
			NEXT
	ENDSELECT


;--------------------------------------------------
;〇拡散対象処理
;--------------------------------------------------
;敵の隊列について。
;通常はこう。
;　10　11
;　12　13
;　14　15
;　16　17
;　18　19
;大型エネミーは、「10～13」「14～17」などの位置を占有する。
;単体対象としては「10」「14」の位置にいる。
;　10　10
;　10　10
;　14　15
;　16　17
;　18　19
;/
;　10　10
;　10　10
;　14　14
;　14　14
;　18　19
;/
;　10　11
;　12　13
;　14　15
;　16　16
;　16　16
;……こういう配置になるハズ。
;--------------------------------------------------
;この関数でなにをしたいかというと、えーと……
;・通常敵13を狙った場合、上下である11・15を狙う
;・通常敵14を狙った場合、上下である12・16を狙う
;・通常敵11を狙った場合、下である13（上は存在しないのでゼロを返す）
;・通常敵19を狙った場合、上である17（下は存在しないのでゼロを返す）
;・ボス敵10を狙った場合、上下である14（上は存在しないのでゼロを返す）
;・ボス敵14を狙った場合、上下である12・18
;・ボス敵10がいる状態で通常敵14を狙った場合、10・16
;・ボス敵14がいる状態で通常敵12を狙った場合、10・14
;・ボス敵10.14がいる状態でボス敵14を狙った場合、10・18
;……こういうふうな挙動を実現してほしいわけだ。
;※ボスは必ず偶数列側にいるものとする。ボス10を狙って14、15かえした～い！ってなると若干面倒なため。
;--------------------------------------------------
;実挙動として
;・対象が不正でないかチェック
;・上または下の数値を仮入れ（14が入ってきたら、上なら12、下なら16を入れる）
;・そこに敵が存在するか、生きているかをチェック
;　・この際、ボスとしてそこに存在するかもチェックする
;ってなるかな……
;この辺できれば処理を共通化したかったけどちょっと俺には難しいぜ！
;こっから先は根性ゲーだが付いてこれるか、なッ！
;--------------------------------------------------
@雷神シド_攻撃拡散処理(拡散中心, 上下どちらか)
#FUNCTION
#DIM 拡散中心
#DIMS 上下どちらか
#DIM 拡散先番号
#DIM 拡散ターゲット
#DIM 帰る値


	;初期化
	拡散先番号 = 0
	帰る値 = 0
	;拡散ターゲットは「12番の位置にいる2番の敵」……の、「2番の敵」のほう。基本的にマイナス10すればよい。
	拡散ターゲット = 拡散中心 - 10

	IF 0
	;拡散中心が敵のマス位置でない場合は帰る
	ELSEIF !INRANGE(拡散中心, 10, 20)
		RETURNF 帰る値
	ELSEIF 上下どちらか == "上"
		;上が存在しえない場合も帰る
		IF 拡散中心 == 10 || 拡散中心 == 11
			RETURNF 帰る値
		;対象がボスだった場合
		;（対象10がボスだった場合はすでに弾いている）
		;たぶんここ、すなおにマイナス２するだけでいいはずだな……まあ明示ということで。
		ELSEIF 敵BATTLE_STATE:拡散ターゲット:ボスフラグ
			拡散先番号 = 拡散中心 - 2
		;対象が通常の敵だった場合
		;12だったら10、19だったら17……をひとまずセット
		ELSE
			拡散先番号 = 拡散中心 - 2
		ENDIF

		;拡散ターゲットを更新
		;「14番の位置の上を拾って12番の位置を得た」ので「じゃあ４番の敵から２番の敵を狙うね」の図。
		拡散ターゲット = 拡散先番号 - 10

		;拡散先に敵がいるかのチェック
		;んまあ拡散先番号に敵がいなければ基本ダメなんだけど……
		IF 敵BATTLE_STATE_STR:拡散ターゲット:エネミー名 == ""
			;仮の拡散先に敵はいないけど、ボスがそのマスを占有している場合……
			;　14狙って12に敵はいないけど10(11.12.13)にボスがいる
			;　15狙って13に敵はいないけど10(11.12.13)にボスがいる
			;……ようなパターンの時。
			IF 敵BATTLE_STATE:(拡散ターゲット - 4):ボスフラグ
				拡散先番号 = 拡散中心 - 4
			ELSEIF 敵BATTLE_STATE:(拡散ターゲット - 5):ボスフラグ
				拡散先番号 = 拡散中心 - 5
			;シンプルにそこになんもおらんときは帰る
			ELSE
				RETURNF 帰る値
			ENDIF
		ENDIF
	ELSEIF 上下どちらか == "下"
		;下が存在しえない場合は帰る
		IF 拡散中心 == 18 || 拡散中心 == 19
			RETURNF 帰る値
		;対象がボスだった場合
		;10狙ったら14を返す……処理だが、16.17.18.19を占有する場合は困る。20を返すことになるが20の位置などないため。
		ELSEIF 敵BATTLE_STATE:拡散ターゲット:ボスフラグ
			;というわけで16171819の位置にボスがいる場合は帰る。
			IF 拡散中心 == 16 || 拡散中心 == 17
				RETURNF 帰る値
			;そうでなければ＋４の値を返す。
			ELSE
				拡散先番号 = 拡散中心 + 4
			ENDIF
		;対象が通常の敵だった場合
		;10だったら12、17だったら19……をセット
		ELSE
			拡散先番号 = 拡散中心 + 2
		ENDIF

		;拡散ターゲットを更新
		;「14番の位置の上を拾って12番の位置を得た」ので「じゃあ４番の敵から２番の敵を狙うね」の図。
		拡散ターゲット = 拡散先番号 - 10

		;拡散先に敵がいるかのチェック
		IF 敵BATTLE_STATE_STR:拡散ターゲット:エネミー名 == ""
			;仮の拡散先に敵はいないけど、ボスがそのマスを占有している場合……
			;　13狙って15に敵はいないけど14(15.16.17)にボスがいる
			;　15狙って17に敵はいないけど16(17.18.19)にボスがいる
			;……ようなパターンの時。
			IF 敵BATTLE_STATE:(拡散ターゲット + 1):ボスフラグ
				拡散先番号 = 拡散中心 + 1
			;シンプルにそこになんもおらんときは帰る
			ELSE
				RETURNF 帰る値
			ENDIF
		ENDIF
	ENDIF

	;死んでたら拡散先番号はゼロ
	SIF 敵BATTLE_STATE:拡散ターゲット:ＨＰ < 1
		拡散先番号 = 帰る値

	RETURNF 拡散先番号


;--------------------------------------------------
;〇戦闘開始時処理
;--------------------------------------------------
;自身に独自バフをかける処理な。
;--------------------------------------------------
@固有戦闘開始時処理_キャラ_2611
#DIM 対象キャラ
#DIM 持続ターン
#DIM バフ効果割合_行動速度
#DIM バフ効果割合_光属性強化
#DIM バフ効果割合_光属性耐性
#DIM バフ効果割合_えくすかりぱー

	;本気かどうかを確認しエクスカリパーかエクスカリバーかで分岐
	対象キャラ = GETCHARA(2611)
	;いずれも対象は自分のみ。持続はまあほぼ永続。消去不可。
	CALL アビ対象選択テンプレート_自己のみ
	持続ターン = 99
	アビ汎用変数:消去不可オプション = 1

	;本気じじい
	IF 雷神シド_本気判定(対象キャラ)
		;バフの割合はいずれも50としておく
		;まあ原作の光（聖属性）耐性は無効率100％超えて吸収なんだけど……
		バフ効果割合_行動速度   = 50
		バフ効果割合_光属性強化 = 50
		バフ効果割合_光属性耐性 = 50

		;行動速度＆再行動バフ
		;再行動バフはターン終了時処理にて。
		アビテンプレート用_アビ名 = 永久ヘイスト
		アビ汎用文字列:バフ・デバフ枠 = 永久ヘイスト
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:持続ターン = 持続ターン
		アビ汎用変数:効果割合 = バフ効果割合_行動速度
		CALL アビテンプレート_有利効果_バフ効果セット

		;光強化
		アビテンプレート用_アビ名 = 聖剣強化
		アビ汎用文字列:バフ・デバフ枠 = 聖剣強化
		アビ汎用文字列:バフ・デバフ対象能力 = 光強化
		アビ汎用変数:持続ターン = 持続ターン
		アビ汎用変数:効果割合 = バフ効果割合_光属性強化
		CALL アビテンプレート_有利効果_バフ効果セット

		;光耐性
		;上述の通り原作だと無効を通り越して吸収だが、さすがにね。
		アビテンプレート用_アビ名 = 聖剣防護
		アビ汎用文字列:バフ・デバフ枠 = 聖剣防護
		アビ汎用文字列:バフ・デバフ対象能力 = 光耐性
		アビ汎用変数:持続ターン = 持続ターン
		アビ汎用変数:効果割合 = バフ効果割合_光属性耐性
		CALL アビテンプレート_有利効果_バフ効果セット

	;全然本気じゃない場合
	ELSE
		;えくすかりぱ～
		;Excalipoorのほう。
		;回避不能、攻撃力マイナス99％のクソデバフ
		アビテンプレート用_アビ名 = さいきょうのけん
		アビ汎用変数:弱体耐性貫通オプション = 1
		バフ効果割合_えくすかりぱー = 99

		アビ汎用文字列:バフ・デバフ枠 = さいきょうのけん
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 持続ターン
		アビ汎用変数:効果割合 = バフ効果割合_えくすかりぱー
		CALL アビテンプレート_不利効果_デバフ効果セット
	ENDIF





;--------------------------------------------------
;ターン終了時処理
;--------------------------------------------------
;・エクスカリバーの再行動バフ処理
;・乱命の宣告の処理
;--------------------------------------------------
@固有ターン終了時処理_キャラ_2611
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM LCOUNT
#DIM 敵の番号
#DIM 乱命の宣告カウント

	隊列番号 = キャラ隊列検索(GETCHARA(2611))

	;独自バフ「永久ヘイスト」がかかっているかをチェック
	バフ・デバフ行数 = バフ番号取得_枠("永久ヘイスト", 隊列番号)
	IF バフ・デバフ行数 >= 0
		;ターンカウントが３の倍数の場合、再行動
		IF 現在ターン数 % 3 == 0
			;自身に再行動バフを付与
			アビテンプレート用_アビ名 = 永久ヘイスト・カウントオーバー
			アビ汎用文字列:バフ・デバフ枠 = 永久ヘイスト_再行動
			アビ汎用文字列:バフ・デバフ対象能力 = 再行動
			アビ汎用変数:効果量 = 1
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
	ENDIF

	;乱命の宣告の処理
	;敵全員を見る
	FOR LCOUNT, 10, 20
		;LCOUNTは「敵の対列番号」なのでごっちゃにならんよう注意な
		敵の番号 = LCOUNT - 10
		;敵が居ないマスはスキップ
		SIF 敵BATTLE_STATE_STR:敵の番号:エネミー名 == ""
			CONTINUE
		;死んでたらスキップ
		SIF 敵BATTLE_STATE:敵の番号:ＨＰ < 1
			CONTINUE

		;生き残っている敵に「乱命の宣告」という名前のデバフがかかっているかを見る
		バフ・デバフ行数 = デバフ番号取得_枠("乱命の宣告", LCOUNT)

		;かかっている場合
		IF バフ・デバフ行数 > 0
			;変数「戦闘中カウント」の、「敵の番号」番に保存
			;（この都合で他に「戦闘中カウント」を使えないはず……カウントしたいなにかが増えたらERH使うしかないねえ）
			戦闘中カウント:隊列番号:敵の番号 += 1
			乱命の宣告カウント = 4 - 戦闘中カウント:隊列番号:敵の番号
			;なんかでゼロ未満にはならないようにしておくね
			SIF 乱命の宣告カウント < 0
				乱命の宣告カウント = 0
			KSTR:(K++) = %敵BATTLE_STATE_STR:敵の番号:エネミー名%の乱命の宣告が{乱命の宣告カウント}カウントになった！
			;ゼロカウントになったら即死処理
			IF 乱命の宣告カウント < 1
				;おまじない：リセット
				CALL アビ汎用変数文字列リセット
				;対象を直接指定
				CALL アビ対象選択テンプレート_指定(LCOUNT)
				;ただし：対象がアンデッドだった場合、全回復する。Oh...
				IF 敵BATTLE_STATE_STR:敵の番号:種族 == "アンデッド" || 敵BATTLE_STATE_STR:敵の番号:種族 == "霊魂"
					KSTR:(K++) = 不浄の生命である%敵BATTLE_STATE_STR:敵の番号:エネミー名%に負の生命力が満ちる……！
					;対象の最大HPを固定値に回復。
					アビ汎用変数:効果量 = 敵BATTLE_STATE:敵の番号:最大ＨＰ
					CALL アビテンプレート_回復処理_MAXでも回復
				;高命中率の即死。
				ELSE
					IF !RAND:2
						KSTR:(K++) = %敵BATTLE_STATE_STR:敵の番号:エネミー名%の死兆が成就する……！
					ELSE
						KSTR:(K++) = %敵BATTLE_STATE_STR:敵の番号:エネミー名%に抗えぬ死が追いついた……！
					ENDIF
					アビ汎用変数:基礎成功確率 = 100
					CALL アビテンプレート_不利効果_即死
				ENDIF

				;これで生きてたら乱命の宣告が解除される
				CALL アビテンプレート_有利効果_デバフ消去("乱命の宣告")
				;蘇生させたあとでなんかあっても困るのでカウントはここでリセット
				戦闘中カウント:隊列番号:敵の番号 = 0
			ENDIF
			;ここで止めたいWSTR
			WSTR:(K++) = 　
		ENDIF
	NEXT


;--------------------------------------------------
;本気判定
;--------------------------------------------------
;普通に再現すると頭おかしい強さになってしまうので、『本気を出すかどうか』の判定を行う。
;具体的には、ここで本気判定突破しないと持ち込む武器がエクスカリバーじゃなくてエクスカリパーになる。慈悲はない。
;--------------------------------------------------
@雷神シド_本気判定(キャラ番号)
#FUNCTION
#DIM キャラ番号

	;定住者になっていない場合は本気を出してくれない。まあ常連でもいいか……
	SIF !(TALENT:キャラ番号:従業員 || TALENT:キャラ番号:常連 || TALENT:キャラ番号:定住者 || TALENT:キャラ番号:店持ち || CFLAG:キャラ番号:滞在期間 == 999)
		RETURNF 0
	;陥落していない場合も本気を出してくれない
	SIF !陥落チェック(キャラ番号)
		RETURNF 0
	;レベル40以下でも本気を出してくれない
	SIF 基礎BATTLE_STATE:キャラ番号:Lv < 40
		RETURNF 0

	;定住していて、陥落していて、Lv40以上なら、本気。
	RETURNF 1


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_2611(対象キャラ)
#DIM 対象キャラ
	CALL 口上変数初期化

	;聖剣エクスカリバー
	;チートの中のチート、真のバランスブレイカーの剣。
	IF 雷神シド_本気判定(対象キャラ)
		KSTR:(K++) = □ 特性：『聖剣エクスカリバー』 □-------------------------------------------------------
		KSTR:(K++) = 　戦闘開始時、自身に［永久ヘイスト］（独自枠・99ターン・消去不可）効果を付与する。
		KSTR:(K++) = 　＞［50％］の行動速度上昇効果を与える。
		KSTR:(K++) = 　＞［50％］の光強化上昇効果を与える。
		KSTR:(K++) = 　＞［50％］の光耐性上昇効果を与える。
		KSTR:(K++) = 　＞３の倍数のターンの終了時、再行動効果（1回・1ターン）を与える。
		KSTR:(K++) = 　
		KSTR:(K++) = 　伝説の騎士剣。王の中の王、真の王者の剣。
	;Excalipoor
	;原作にも存在はするが、財宝扱いで装備できない。
	;また、その説明によれば、「切れ味は鋭い」らしい。
	ELSE
		KSTR:(K++) = □ 特性：『似非聖剣エクスカリパー』 □---------------------------------------------------
		KSTR:(K++) = 　戦闘開始時、自身に［さいきょうのけん］（独自枠・99ターン・消去不可）効果を付与する。
		KSTR:(K++) = 　＞［99％］の攻撃力減少効果を与える。
		KSTR:(K++) = 　
		KSTR:(K++) = 　――偽物だコレ！
		KSTR:(K++) = 　聖剣エクスカリバーのレプリカ。『カリBAー』ではなく、『カリPAー』のほう。
		KSTR:(K++) = 　相手を傷つけない魔法がかかった剣。
		KSTR:(K++) = 　リゾートに聖剣エクスカリバーを持ち込む気にはならなかったらしい。
	ENDIF
	KSTR:(K++) = 　

	CALL 口上変数表示(対象キャラ, 1)

