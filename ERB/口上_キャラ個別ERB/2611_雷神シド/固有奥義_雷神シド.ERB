;--------------------------------------------------
;●雷神シド口上＠erablue_resort用
;　――固有奥義
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれも雷神シド口上_readme.txt記載に従う
;--------------------------------------------------

@固有奥義_2611(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM 実奥義威力
#DIMS 奥義名
#DIM 攻撃回数

	IF 0
	ELSEIF 雷神シド_本気判定(キャラ番号)
		;バランスブレイク
		奥義名 = 衡平壊変
		実奥義威力 = 200
	ELSE
		;シンプル無双稲妻突きは通常アビリティで実装
		;天鼓雷音稲妻斬をモデルに「雷神斬」を捏造
		奥義名 = 無双稲妻雷神斬
		実奥義威力 = 150
	ENDIF
	;全体三連撃、という性能なのでいったんこれはゼロ
	奥義威力 = 0

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 = 奥義威力：{実奥義威力}％ ✕ ３
			詳細文文字列受け渡し変数 += @"<br>敵全体に自属性の奥義ダメージを３回与える。"
			詳細文文字列受け渡し変数 += @"<br>"
			IF 0
			ELSEIF 雷神シド_本気判定(キャラ番号)
				詳細文文字列受け渡し変数 += @"<br>雷神シドが秘奥義『衡平壊変＜バランスブレイク＞』。"
				詳細文文字列受け渡し変数 += @"<br>すなわち剣聖の一手、戦場の衡平を破壊する天変の連撃である。"
			ELSE
				;無双稲妻突きは原作的には非常に使いやすかったが、別に「オルランドゥの得意手」という設定はない。
				;ただ、『雷神』という異名からすると、おそらくそう間違ってもいないだろう。
				詳細文文字列受け渡し変数 += @"<br>雷神シドが得意手『無双稲妻突き』――その殲滅連撃。"
			ENDIF
			RETURN 奥義威力
		CASE "奥義前効果"
			;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
			;奥技前になんかやることはないのでここはカット

			;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
			;必要な場合を除きここでリセット
			CALL アビ汎用変数文字列リセット
		CASE "奥義追加効果"
			;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
			;複数回攻撃であるため、きちんと書きます。

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 実奥義威力
			;３連射。
			;FF14にてバランスブレイクが「両手＋尻尾」の剣による三連撃であったため。
			;聖剣＋暗黒剣＋剛剣の三連撃と言う意味でもあるのだろう。
			CALL アビテンプレート_ダメージ処理_自属性
			CALL アビテンプレート_ダメージ処理_自属性
			CALL アビテンプレート_ダメージ処理_自属性
	ENDSELECT




