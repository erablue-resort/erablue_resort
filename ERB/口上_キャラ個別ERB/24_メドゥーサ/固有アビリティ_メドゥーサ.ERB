
@固有アビ_24_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = サーペント・クワイタス+
	IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 150
	ENDIF
ELSE
	アビ名 = サーペント・クワイタス
	IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>対象に関わらず10回土属性［攻撃力×50％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>敵全体の強化効果を消去［1回］"
		詳細文文字列受け渡し変数 += @"<br>敵全体に劇毒効果［5ターン/回復不可］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		FOR LOCAL, 0, 10
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_ダメージ処理_土属性
			IF 戦闘中カウント:戦闘行動キャラ:2 == 1
				CALL アビテンプレート_ダメージ処理_土属性
			ENDIF
		NEXT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		CALL アビテンプレート_不利効果_バフ消去
		IF 戦闘中カウント:戦闘行動キャラ:2 == 1
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 劇毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:効果量 = 50
		アビ汎用変数:累積上限_固定値 = 500
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		IF 戦闘中カウント:戦闘行動キャラ:2 == 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有アビ_24_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号
#DIM 効果倍率
#DIM バフ・デバフ行数
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = インペイル・バイト+
	IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 100
	ENDIF
ELSE
	アビ名 = インペイル・バイト
	IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 150
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に土属性［攻撃力×300％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象に蛇の咬裂効果［個人固有枠/永続/回復不可］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 300
		CALL アビテンプレート_ダメージ処理_土属性
		IF 戦闘中カウント:戦闘行動キャラ:3 == 1
			CALL アビテンプレート_ダメージ処理_土属性
		ENDIF
		
		対象番号 = アビテンプレート用_対象保存:0
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂
		アビ汎用文字列:バフ・デバフ対象能力 = 蛇の咬裂
		アビ汎用変数:効果量 = 1
		アビ汎用変数:累積上限_固定値 = 3
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		IF 戦闘中カウント:戦闘行動キャラ:3 == 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		ENDIF

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:付随割合 = 5
		CALL アビテンプレート_不利効果_付随デバフ効果セット("蛇の咬裂")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:付随割合 = 5
		CALL アビテンプレート_不利効果_付随デバフ効果セット("蛇の咬裂")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂_弱体耐性
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:付随量 = 5
		CALL アビテンプレート_不利効果_付随デバフ効果セット("蛇の咬裂")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂_命中率
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		アビ汎用変数:付随量 = 5
		CALL アビテンプレート_不利効果_付随デバフ効果セット("蛇の咬裂")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 蛇の咬裂_被ダメ上昇
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:付随量 = 100
		CALL アビテンプレート_不利効果_付随デバフ効果セット("蛇の咬裂")
		
		IF 戦闘中カウント:戦闘行動キャラ:1 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:1 = 0
		ENDIF
ENDSELECT



@固有アビ_24_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM クールタイム
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = ディスイリュージョンアイ
	消費ＭＰ = 300
ENDIF

IF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:4
	クールタイム = MAX(0, 10 + 戦闘中カウント:隊列番号:4 - 現在ターン数)
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
ELSE
	クールタイム = 0
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体にディスイリュージョンアイ効果［1ターン/回復不可］
		詳細文文字列受け渡し変数 += @"<br>対象にDA率［100％］ダウン効果［個人枠/6ターン］"
		詳細文文字列受け渡し変数 += @"<br>対象にTA率［100％］ダウン効果［個人枠/6ターン］"
		詳細文文字列受け渡し変数 += @"<br>対象に命中率［30％］ダウン効果［個人枠/6ターン］"
		詳細文文字列受け渡し変数 += @"<br>発動時、10ターンのクールタイムが発生"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"（残り{クールタイム}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ディスイリュージョンアイ
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = DA率
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 6
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = TA率
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 6
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 命中率
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		アビ汎用変数:効果量 = 30
		アビ汎用変数:持続ターン = 6
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		戦闘中カウント:戦闘行動キャラ:4 = 現在ターン数
ENDSELECT



@固有戦闘開始時処理_キャラ_24
#DIM バフ・デバフ行数
#DIM メドゥ隊列
#DIM 対象番号
#DIM 星晶獣カウント

メドゥ隊列 = キャラ隊列検索(GETCHARA(24))
星晶獣カウント = 0

FOR 対象番号, 0, 4
	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:対象番号:ＨＰ < 1
		CONTINUE
	
	SIF TALENT:(BATTLE_STATE:対象番号:キャラ登録番号):種族 == 5
		星晶獣カウント += 1
NEXT

IF 星晶獣カウント > 0
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 星晶獣の矜持_攻撃力
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 30
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF
IF 星晶獣カウント > 1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 星晶獣の矜持_アビ与ダメ上昇
	アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
	アビ汎用変数:効果量 = 500
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF
IF 星晶獣カウント > 2
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 星晶獣の矜持_特殊反応
	アビ汎用文字列:バフ・デバフ対象能力 = 星晶獣の矜持_特殊反応
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF
IF 星晶獣カウント > 3
	戦闘中カウント:メドゥ隊列:2 = 1
	戦闘中カウント:メドゥ隊列:3 = 1
ENDIF

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = プライマル・レゾナンス
アビ汎用文字列:バフ・デバフ対象能力 = プライマル・レゾナンス
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
アビ汎用文字列:適用範囲 = 味方全体
CALL アビテンプレート_有利効果_バフ効果セット



@固有ターン終了時処理_キャラ_24
;ターン終了時処理関数から呼び出し
#DIM 大技判定
#DIM メドゥ隊列

大技判定 = 0
メドゥ隊列 = キャラ隊列検索(GETCHARA(24))

FOR LOCAL, 10, 20
	IF 行動種別フラグ:LOCAL:大技発動
		大技判定 = 1
		BREAK
	ENDIF
NEXT

IF バフ番号取得_枠("星晶獣の矜持_特殊反応", メドゥ隊列) > -1 && 大技判定 == 1
	WSTR:(K++) = メドゥーサの星晶獣の矜持！
	WSTR:(K++) = 次のダメージアビリティがMPと行動回数を消費しなくなった！
	
	戦闘中カウント:メドゥ隊列:0 = 1
	戦闘中カウント:メドゥ隊列:1 = 1
ENDIF



@固有奥義後処理_バフ_プライマル・レゾナンス(バフ・デバフ行数, 全滅フラグ)
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 全滅フラグ
#DIM メドゥ隊列
#DIM レゾナンス
#DIM 対象番号
#DIM バフ番号

SIF 全滅フラグ
	RETURN 0

メドゥ隊列 = キャラ隊列検索(GETCHARA(24))
バフ番号 = バフ番号取得_枠("レゾナンス", メドゥ隊列)

IF INRANGE(メドゥ隊列, 0, 3) && DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値") < 4
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(メドゥ隊列)
	アビ汎用文字列:バフ・デバフ枠 = レゾナンス
	アビ汎用文字列:バフ・デバフ対象能力 = レゾナンス
	アビ汎用変数:効果量 = 1
	アビ汎用変数:累積上限_固定値 = 4
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_有利効果_累積バフ効果セット
	
	レゾナンス = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
	
	IF レゾナンス == 4 && 探索中カウント:メドゥ隊列:5 == 0
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 9999
		CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
		探索中カウント:メドゥ隊列:5 = 1
	ENDIF
	
	SELECTCASE レゾナンス
		CASE 1
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = レゾナンス_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 30
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		CASE 2
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = レゾナンス_ダメージカット
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		CASE 3
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = レゾナンス_回避率
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		CASE 4
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = レゾナンス_ディスペルガード
			アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT
ENDIF



@探索欄表示_キャラ_24(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "レゾナンス:", 120, 90
GSETBRUSH レイヤー番号, 0xFFFF3377
GDRAWTEXT レイヤー番号, @"◆{DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("レゾナンス", 隊列番号), "効果量_固定値")}", 220, 90



@バフ・デバフ特殊表示_蛇の咬裂(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 蛇の咬裂
PRINTFORML Lvに応じて攻撃力・防御力・弱体耐性・命中率が減少/被ダメージが上昇している状態［回復不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



@バフ・デバフ特殊表示_ディスイリュージョンアイ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ディスイリュージョンアイ
PRINTFORML 行動を封じられた状態［回復不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_レゾナンス(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML レゾナンス
PRINTFORML Lvに応じて味方全体の防御性能が上昇している状態［消去不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_24(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『星晶獣の矜持』 □------------------------------------------------------------
KSTR:(K++) = 　メインメンバーの「星晶獣」キャラクターの数に応じて自身の攻撃性能が上昇
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『プライマル・レゾナンス』 □-------------------------------------------------
KSTR:(K++) = 　自身を除く「星晶獣」キャラクターが奥義発動時、自身のレゾナンスLvが上昇
KSTR:(K++) = 　探索中はじめてレゾナンスLvが4に到達時、一度だけ味方全体のMPを全て回復
KSTR:(K++) = 　◆レゾナンスLvに応じて味方全体の防御性能が上昇
CALL 口上変数表示(対象キャラ, 1)


