
@固有奥義_1025(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 隊列番号
#DIM バフ・デバフ行数

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	奥義名 = 天華繚乱・稲妻招雷+
	奥義威力 = 0
ELSE
	奥義名 = 天華繚乱・稲妻招雷
	奥義威力 = 0
ENDIF

;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 1025))
バフ・デバフ行数 = バフ番号取得_枠("神秘の光焔", 隊列番号)

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 = 奥義威力：300％<br>対象に麻痺効果［2ターン］
		ELSE
			詳細文文字列受け渡し変数 = 奥義威力：200％<br>対象に麻痺効果［1ターン］
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>◆この奥義は敵全体を対象とする"
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:ダメージ種類 = 奥義ダメージ
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:効果割合 = 300
		ELSE
			アビ汎用変数:効果割合 = 200
		ENDIF
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 麻痺
		アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
		アビ汎用変数:効果量 = 100
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:持続ターン = 2
		ELSE
			アビ汎用変数:持続ターン = 1
		ENDIF
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_奥義枠
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:累積上限_割合 = 200
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		IF バフ番号取得_枠("フィーバー", 隊列番号) < 0
			戦闘中カウント:隊列番号:1 += 10
			SIF 戦闘中カウント:隊列番号:1 > 100
				戦闘中カウント:隊列番号:1 = 100
		ENDIF
ENDSELECT
