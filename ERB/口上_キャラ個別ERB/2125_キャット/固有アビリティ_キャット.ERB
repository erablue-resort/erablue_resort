@固有通常攻撃_2125
#DIM 習得率
#DIM 敵レベル, 3
#DIM キャット隊列番号

キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))

IF クイック行動フラグ_TARGET >= 10
	CALL アビ対象選択テンプレート_指定(クイック行動フラグ_TARGET)
ELSE
	アビ汎用文字列:選択時メッセージ１ = 誰を攻撃しますか？
	CALL アビ対象選択テンプレート_敵単体
ENDIF

SIF アビテンプレート用_キャンセルフラグ == 1
	RETURN 0

敵レベル:0 = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):Lv
敵レベル:1 = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):Lv - 10
SIF 敵レベル:1 < 0
	敵レベル:1 = 0
敵レベル:2 = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):Lv - 15
SIF 敵レベル:2 < 0
	敵レベル:2 = 0

習得率 = RAND:100
;WSTR:(K++) = {習得率 + 敵レベル:0}
IF キャット習得技:0 == 0 && 連携中 == 0 &&習得率 + 敵レベル:0 > 60
	;フェイント習得
	キャット習得技:0 = 1
	標的指定スキップ_キャット = 1
	WSTR:(K++) = 💡キャットはフェイントを閃いた！
	PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
	CALL 固有アビ_2125_0("アビ名", キャット隊列番号)
	行動口上呼び出し用文字列 = KOJO_DUNGEON_固有アビ発動時_2125_0
	CALL 固有アビ_2125_0("アビ効果", キャット隊列番号)
	標的指定スキップ_キャット = 0
ELSEIF キャット習得技:2 == 0 && 連携中 == 0 && BATTLE_STATE:キャット隊列番号:Lv >= 10 && 習得率 + 敵レベル:1 > 70
	;サイドワインダー習得
	キャット習得技:2 = 1
	標的指定スキップ_キャット = 1
	WSTR:(K++) = 💡キャットはサイドワインダーを閃いた！
	WSTR:(K++) = キャットのサイドワインダー！
	PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
	;CALL メッセージ欄表示用関数()
	アビテンプレート用_アビ名表示済フラグ = 1
	CALL 固有アビ_2125_2("アビ名", キャット隊列番号)
	行動口上呼び出し用文字列 = KOJO_DUNGEON_固有アビ発動時_2125_2
	CALL 固有アビ_2125_2("アビ効果", キャット隊列番号)
	標的指定スキップ_キャット = 0
ELSEIF キャット習得技:3 == 0 && 連携中 == 0 && BATTLE_STATE:キャット隊列番号:Lv >= 15 && 習得率 + 敵レベル:2 > 80
	;スネークショット習得
	キャット習得技:3 = 1
	標的指定スキップ_キャット = 1
	WSTR:(K++) = 💡キャットはスネークショットを閃いた！
	WSTR:(K++) = キャットのスネークショット！
	PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
	アビテンプレート用_アビ名表示済フラグ = 1
	IF アビテンプレート用_対象保存:0 % 2
		IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 11):エネミー名 == ""
		ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 11):ＨＰ < 1
		ELSE
			アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 - 1
		ENDIF
	ELSE
		IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 9):エネミー名 == ""
		ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 9):ＨＰ < 1
		ELSE
			アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 + 1
		ENDIF
	ENDIF
	CALL 固有アビ_2125_3("アビ名", キャット隊列番号)
	行動口上呼び出し用文字列 = KOJO_DUNGEON_固有アビ発動時_2125_3
	CALL 固有アビ_2125_3("アビ効果", キャット隊列番号)
	標的指定スキップ_キャット = 0
ELSEIF キャット習得技:4 == 0 && 連携中 == 0 && BATTLE_STATE:キャット隊列番号:Lv >= 15 && 習得率 + 敵レベル:2 > 90
	;乱れ突き習得
	キャット習得技:4 = 1
	標的指定スキップ_キャット = 1
	WSTR:(K++) = 💡キャットは乱れ突きを閃いた！
	WSTR:(K++) = キャットの乱れ突き！
	PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
	アビテンプレート用_アビ名表示済フラグ = 1
	CALL 固有アビ_2125_4("アビ名", キャット隊列番号)
	行動口上呼び出し用文字列 = KOJO_DUNGEON_固有アビ発動時_2125_4
	CALL 固有アビ_2125_4("アビ効果", キャット隊列番号)
	標的指定スキップ_キャット = 0
ELSE
	;通常攻撃処理
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_通常攻撃処理
ENDIF

@固有アビ_2125_0(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF キャット習得技:0 == 1
	アビ名 = フェイント
	消費ＭＰ = 10
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体をスタンさせる。基礎成功率80％
		RETURN 消費ＭＰ
	CASE "アビ効果"
		SIF 標的指定スキップ_キャット == 0
			CALL アビ対象選択テンプレート_敵単体
		KSTR:(K++) = キャットのフェイント！
		;技を閃いた直後は必中
		IF RAND:10 < 8 || 標的指定スキップ_キャット == 1
			KSTR:(K++) = %敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 10):エネミー名%はスタンした！
			敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):行動済みフラグ = 1
			CALL メッセージ欄表示用関数()
			CALL 口上変数初期化
		ELSE
			KSTR:(K++) = しかし%敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 10):エネミー名%には効かなかった！
			CALL メッセージ欄表示用関数()
			CALL 口上変数初期化
		ENDIF
ENDSELECT

@固有アビ_2125_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名

IF キャット習得技:1 == 1
	アビ名 = マタドール
	消費ＭＰ = 30
ENDIF

アビ汎用文字列:実行時メッセージ１ = キャットは身構えた！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身の回避率を1ターン［+50％］する。
		詳細文文字列受け渡し変数 += @"<br>自身に1ターン回避時反撃［攻撃力✕240％］効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = マタドール
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 50
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = マタドール_特殊反応
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_回避時
		アビ汎用変数:効果量 = 240
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = -1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@固有アビ_2125_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 習得率
#DIM 敵レベル
#DIM キャット隊列番号

IF キャット習得技:2 == 1
	アビ名 = サイドワインダー
	消費ＭＰ = 20
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に自属性の［攻撃力✕170％］ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>敵の種族がカエルの場合、即死［100％］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 標的指定スキップ_キャット == 0
			CALL アビ対象選択テンプレート_敵単体
			SIF アビテンプレート用_キャンセルフラグ == 1
				RETURN 0
			キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))
			IF キャット習得技:3 == 0 && 連携中 == 0 && BATTLE_STATE:キャット隊列番号:Lv >= 15
				習得率 = RAND:100
				敵レベル = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):Lv - 15
				SIF 敵レベル < 0
					敵レベル = 0
				IF 習得率 + 敵レベル + 10 > 80
					;スネークショット習得
					キャット習得技:3 = 1
					標的指定スキップ_キャット = 1
					WSTR:(K++) = 💡キャットはスネークショットを閃いた！
					WSTR:(K++) = キャットのスネークショット！
					PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
					アビテンプレート用_アビ名表示済フラグ = 1
					IF アビテンプレート用_対象保存:0 % 2
						IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 11):エネミー名 == ""
						ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 11):ＨＰ < 1
						ELSE
							アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 - 1
						ENDIF
					ELSE
						IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 9):エネミー名 == ""
						ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 9):ＨＰ < 1
						ELSE
							アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 + 1
						ENDIF
					ENDIF
					CALL 固有アビ_2125_3("アビ名", キャット隊列番号)
					行動口上呼び出し用文字列 = KOJO_DUNGEON_固有アビ発動時_2125_0
					CALL 固有アビ_2125_3("アビ効果", キャット隊列番号)
					標的指定スキップ_キャット = 0
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
		アビ汎用変数:効果割合 = 170
		CALL アビテンプレート_ダメージ処理_自属性
		IF REGEXPMATCH(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "カエル|蛙") > 0
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_即死
		ENDIF
ENDSELECT

@固有アビ_2125_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名

IF キャット習得技:3 == 1
	アビ名 = スネークショット
	消費ＭＰ = 40
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵横一列に自属性の［攻撃力✕170％］ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>敵の種族がカエルの場合、即死［100％］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		SIF 標的指定スキップ_キャット == 0
			CALL アビ対象選択テンプレート_敵横列
		アビ汎用変数:効果割合 = 170
		CALL アビテンプレート_ダメージ処理_自属性
		IF REGEXPMATCH(敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 10):種族, "カエル|蛙") > 0
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_即死
		ENDIF
		IF アビテンプレート用_対象保存:1 >= 10
			IF REGEXPMATCH(敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:1 - 10):種族, "カエル|蛙") > 0
				アビ汎用変数:基礎成功確率 = 100
				CALL アビテンプレート_不利効果_即死
			ENDIF
		ENDIF
ENDSELECT

@固有アビ_2125_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名

IF キャット習得技:4 == 1
	アビ名 = 乱れ突き
	消費ＭＰ = 50
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に自属性の［攻撃力✕80％］ダメージを4回与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		SIF 標的指定スキップ_キャット == 0
			CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 80
		CALL アビテンプレート_ダメージ処理_自属性
		CALL アビテンプレート_ダメージ処理_自属性
		CALL アビテンプレート_ダメージ処理_自属性
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@固有アビ_2125_5(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名
#DIM キャット隊列番号
#DIM 現在ＨＰ補正

アビ名 = ふみつけ
消費ＭＰ = 60

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に自属性の［攻撃力✕290％］ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>技使用時のHPが多い程、強力なダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))
		現在ＨＰ補正 = BATTLE_STATE:キャット隊列番号:ＨＰ / 100
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 290 + 現在ＨＰ補正
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@固有戦闘開始時処理_キャラ_2125
#DIM キャット隊列番号
#DIM メンバー数

;テスト用
;CALL アビ対象選択テンプレート_自己のみ
;アビ汎用変数:効果量 = 100
;CALL アビテンプレート_有利効果_奥義ゲージUP

アビテンプレート用_アビ名表示済フラグ = 1
連携中 = 0
キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))

;抜け目ない子ネコ処理

IF 抜け目ない子ネコ取得Ｆ == 1
	アビテンプレート用_アビ名 = 抜け目ない子ネコ
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	CALL アビテンプレート_不利効果_トレハン
	CALL アビテンプレート_不利効果_トレハン
ENDIF

;フリーファイト－１処理

;メンバーカウント

メンバー数 = 0
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	メンバー数++
NEXT

IF キャット隊列番号 == 0 && メンバー数 == 4

	CALL フリーファイト－１

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:適用範囲 = 味方全体
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:適用範囲 = 敵全体
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_パッシブデバフ効果セット

ENDIF

;マタドール取得判定用

IF キャット習得技:1 == 0 && BATTLE_STATE:キャット隊列番号:Lv >= 5
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = マタドール取得判定
	アビ汎用文字列:バフ・デバフ対象能力 = マタドール取得判定
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF

@固有ターン開始時処理_キャラ_2125
#DIM キャット隊列番号
#DIM メンバー数

キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))
連携中 = 0
メンバー数 = 0
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	メンバー数++
NEXT

IF バフ番号取得_枠("陣形効果_キャット", キャット隊列番号) < 0 && キャット隊列番号 == 0 && メンバー数 == 4
	;交代時処理_陣形効果付与
	アビテンプレート用_アビ名表示済フラグ = 1

	CALL フリーファイト－１

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_デバフ効果セット

ELSEIF キャット隊列番号 == 0 && メンバー数 == 4
	;敵の仲間呼びに対応するためにデバフを張りなおす
	アビテンプレート用_アビ名表示済フラグ = 1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF


@固有行動直後処理_キャラ_2125
#DIM キャット隊列番号
#DIM メンバー数

キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))

メンバー数 = 0
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	メンバー数++
NEXT

IF バフ番号取得_枠("陣形効果_キャット", キャット隊列番号) < 0 && キャット隊列番号 == 0 && メンバー数 == 4
	;交代時処理_陣形効果付与
	アビテンプレート用_アビ名表示済フラグ = 1

	CALL フリーファイト－１

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_デバフ効果セット

ELSEIF キャット隊列番号 == 0 && メンバー数 == 4
	;敵の仲間呼びに対応するためにデバフを張りなおす
	アビテンプレート用_アビ名表示済フラグ = 1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
	アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF

@フリーファイト－１

アビテンプレート用_アビ名 = フリーファイト－１

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャットリーダー
アビ汎用文字列:バフ・デバフ対象能力 = フリーファイト－１
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット
	
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 陣形効果_敵対心
アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
アビ汎用変数:効果量 = -20
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
CALL アビテンプレート_有利効果_付随バフ効果セット("陣形効果_キャットリーダー")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 陣形効果_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
CALL アビテンプレート_有利効果_付随バフ効果セット("陣形効果_キャットリーダー")

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体_生者のみ_自己以外
アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャットメンバー
アビ汎用文字列:バフ・デバフ対象能力 = フリーファイト－１
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 陣形効果_行動速度
アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
アビ汎用変数:効果割合 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
CALL アビテンプレート_有利効果_付随バフ効果セット("陣形効果_キャットメンバー")

@バフ・デバフ特殊表示_陣形効果_キャットリーダー(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 陣形効果：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 攻撃力が10％上昇、敵対心が20低下
PRINTFORML 持続ターン：永続

@バフ・デバフ特殊表示_陣形効果_キャットメンバー(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 陣形効果：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 行動速度が10％上昇
PRINTFORML 持続ターン：永続

@固有行動直後処理_バフ・デバフ_陣形効果_キャット(効果番号)
#DIM 効果番号
#DIM メンバー数
#DIM CONST 先頭番号 = 0
#DIM 行動キャラ保存
#DIM 行動キャラ保存２

アビテンプレート用_アビ名表示済フラグ = 1

;先頭がキャットではない場合すべて消す
IF 先頭番号 != キャラ隊列検索(GETCHARA(2125))
	FOR LOCAL, 0, 4
		IF LOCAL == 先頭番号
			CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャットリーダー", 先頭番号))
		ELSE
			CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャットメンバー", LOCAL))
		ENDIF
		CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャット", LOCAL))
	NEXT
	KSTR:(K++) = 陣形効果が消滅した！
	FOR LOCAL, 10, 20
		SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
			CONTINUE
		CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャット", LOCAL - 10))
	NEXT
	RETURN 0
ENDIF

;一時的に戦闘行動キャラを先頭キャラに渡す
IF 戦闘行動キャラ != 0
	SWAP 戦闘行動キャラ, 行動キャラ保存
	戦闘行動キャラ = 先頭番号
ENDIF

;メンバーカウント
メンバー数 = 0
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	メンバー数++
	;メンバー交代時に付与
	IF バフ番号取得_枠("陣形効果_キャット", LOCAL) < 0 && LOCAL != 0
		SWAP 戦闘行動キャラ, 行動キャラ保存２
		戦闘行動キャラ = LOCAL
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャット
		アビ汎用文字列:バフ・デバフ対象能力 = 陣形効果_キャット
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = -1
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 陣形効果_キャットメンバー
		アビ汎用文字列:バフ・デバフ対象能力 = フリーファイト－１
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 陣形効果_行動速度
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_付随バフ効果セット("陣形効果_キャットメンバー")

		SWAP 戦闘行動キャラ, 行動キャラ保存２
	ENDIF
NEXT

;陣形効果削除
IF メンバー数 != 4 && バフ番号取得_枠("陣形効果_キャットリーダー", 先頭番号) > -1
	FOR LOCAL, 0, 4
		IF LOCAL == 先頭番号
			CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャットリーダー", 先頭番号))
		ELSE
			CALL 戦闘効果削除(バフ番号取得_枠("陣形効果_キャットメンバー", LOCAL))
		ENDIF
	NEXT
	KSTR:(K++) = 陣形効果が消滅した！
ENDIF

;陣形効果復活
IF メンバー数 == 4 && バフ番号取得_枠("陣形効果_キャットリーダー", 先頭番号) < 0
	CALL フリーファイト－１
ENDIF

IF 行動キャラ保存 != 0
	;戦闘行動キャラを戻す
	SWAP 戦闘行動キャラ, 行動キャラ保存
ENDIF

@特殊反応_回避時_マタドール_特殊反応(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = マタドール
アビ汎用変数:効果割合 = 240
アビ汎用変数:命中率補正値 = 200
CALL アビテンプレート_通常攻撃処理

@固有行動開始時処理_デバフ_マタドール取得判定(効果番号)
#DIM 効果番号
#DIM 習得率
#DIM 敵レベル
#DIM キャット隊列番号
#DIM 行動キャラ保存

キャット隊列番号 = キャラ隊列検索(GETCHARA(2125))

敵レベル = 敵BATTLE_STATE:(戦闘行動キャラ - 10):Lv - 5
SIF 敵レベル < 0
	敵レベル = 0

習得率 = RAND:100
IF キャット習得技:1 == 0 && 習得率 + 敵レベル > 50
	SWAP 戦闘行動キャラ, 行動キャラ保存
	戦闘行動キャラ = キャット隊列番号

	アビテンプレート用_アビ名表示済フラグ = 1
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 習マタドール
	アビ汎用文字列:バフ・デバフ対象能力 = 回避率
	アビ汎用変数:効果量 = 1000
	アビ汎用変数:持続ターン = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	アビ汎用文字列:バフ・デバフ枠 = 習マタドール_特殊反応
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_回避時
	アビ汎用変数:効果量 = 240
	アビ汎用変数:持続ターン = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	SWAP 戦闘行動キャラ, 行動キャラ保存
ENDIF

@特殊反応_回避時_習マタドール_特殊反応(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号

IF キャット習得技:1 == 0
	キャット習得技:1 = 1
	WSTR:(K++) = 💡キャットはマタドールを閃いた！
	PLAYSOUND "Inspiration08-3(Low-Delay).mp3"
ENDIF

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = マタドール
アビ汎用変数:効果割合 = 240
アビ汎用変数:命中率補正値 = 200
CALL アビテンプレート_通常攻撃処理

;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_2125(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『陣形 フリーファイト－１』 □--------------------------------------------------
KSTR:(K++) = 　キャットが隊列の先頭、バトルメンバーが４人かつ全員生存時のみ有効
KSTR:(K++) = 　キャット　　：攻撃力が10％上昇、敵対心が20低下
KSTR:(K++) = 　キャット以外：行動速度が10％上昇
KSTR:(K++) = 　
IF 抜け目ない子ネコ取得Ｆ == 1
	KSTR:(K++) = □ 特性：『抜け目ない子ネコ』 □---------------------------------------------------------
	KSTR:(K++) = 　戦闘開始時に敵全体ににドロップ率UP効果を2回与える
	KSTR:(K++) = 　
ENDIF

CALL 口上変数表示(対象キャラ, 1)