
@固有奥義_1895(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS 奥義名
#DIM 奥義威力
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

奥義名 = 不可視の獣顕現
奥義威力 = 0


アビ汎用文字列:実行時メッセージ１ = アズールの傍らに不可視の獣が生じ、吠え声をあげた。

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 不可視の獣を顕現させる。<br>顕現した獣はアズールの行動前にランダムな敵を攻撃する。<br>更にはアズールへの攻撃を庇いダメージを減少。<br>不可視の獣が受けたダメージが一定以上になると退去。
		RETURN 奥義威力
	CASE "奥義前効果"
	CASE "奥義追加効果"
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 不可視の獣
		アビ汎用変数:持続ターン = -1
		アビ汎用文字列:バフ・デバフ対象能力 = 不可視の獣
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 不可視の獣庇う
		アビ汎用変数:持続ターン = -1
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("不可視の獣")
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 不可視の獣ＨＰ
		アビ汎用変数:持続ターン = -1
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
		CALL アビテンプレート_有利効果_付随バフ効果セット("不可視の獣")
		
		IF バフ番号取得_枠("不可視の獣回復", 隊列番号) == -1
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 不可視の獣回復
			アビ汎用変数:持続ターン = -1
			アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被回復時
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@固有行動開始時処理_バフ_不可視の獣(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(1895))

CALL アビ対象選択テンプレート_敵ランダムＸ体(1)

SELECTCASE RAND:4
	CASE 0
		アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:((アビテンプレート用_対象保存:0) - 10):エネミー名%の背後から透明の獣が襲いかかった。
	CASE 1
		アビ汎用文字列:実行時メッセージ１ =	GRRRR！  不可視の獣が%敵BATTLE_STATE_STR:((アビテンプレート用_対象保存:0) - 10):エネミー名%の頭上から飛び掛かる。
	CASE 2
		アビ汎用文字列:実行時メッセージ１ =	%敵BATTLE_STATE_STR:((アビテンプレート用_対象保存:0) - 10):エネミー名%に斜めから襲いかかるは透明の獣！
	CASE 3
		アビ汎用文字列:実行時メッセージ１ =	「ゴアアアア！」横から襲いかかるのは透明の獣だ。
ENDSELECT

;魔力値の様なステータスが無いので最大MPをジツ方面のステータスとして参照
CALL バフ込み数値算出("最大ＭＰ", 隊列番号)
アビ汎用変数:効果割合 = RESULT / 2
;透明なので当然避け難い
アビ汎用変数:命中率補正値 = 200
アビ汎用変数:回避率補正値 = 50
アビ汎用変数:かばう無視オプション = 1
CALL アビテンプレート_通常攻撃処理

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 50
アビ汎用文字列:バフ・デバフ枠 = 隙
アビ汎用文字列:バフ・デバフ対象能力 = 回避率
アビ汎用変数:持続被ダメ回数 = 2
アビ汎用変数:基礎成功確率 = 80
アビ汎用変数:かばう無視オプション = 1
CALL アビテンプレート_不利効果_デバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 10
アビ汎用文字列:バフ・デバフ枠 = 隙_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:かばう無視オプション = 1
CALL アビテンプレート_不利効果_付随デバフ効果セット("隙")

BATTLE_STATE:隊列番号:奥義ゲージ = 0

@特殊反応_被ダメ時_不可視の獣ＨＰ(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIM バフ番号
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM 隊列番号
#DIM 不可視の獣のHP
#DIM 不可視の獣
#DIM 不可視の獣庇う
#DIM 不可視の獣ＨＰ


隊列番号 = キャラ隊列検索(GETCHARA(1895))

;ダメージ蓄積処理
戦闘中カウント:隊列番号:0 += 被ダメ時総ダメ:被ダメ記録順

SELECTCASE RAND:2
	CASE 0
		アビ汎用文字列:実行時メッセージ１ = 「ゴウオオオン！」透明の獣が立ちはだかり、リロード中のアズールを守る。
	CASE 1
		アビ汎用文字列:実行時メッセージ１ = アズールは透明の獣を操り、身をもって攻撃を防ぐ。そして目鼻から流れる血を拳で拭った。
ENDSELECT

アビ汎用文字列:実行時メッセージ２ = ""

;不可視の獣のHPを最大MPの2倍として計上(アズールが受けるダメージが半減されているので素の最大MP)
CALL バフ込み数値算出("最大ＭＰ", 隊列番号)
不可視の獣のHP = RESULT
IF 戦闘中カウント:隊列番号:0 > 不可視の獣のHP
	BATTLE_STATE:隊列番号:ＨＰ = 0
	SETBIT 戦闘中カウント:隊列番号:死亡時ダメ属性, 6
	アビ汎用文字列:実行時メッセージ３ = 不可視の獣の気配が消えると同時にアズールも気絶した。
	不可視の獣 = バフ番号取得_枠("不可視の獣", 隊列番号)
	CALL 戦闘効果削除(不可視の獣)
	不可視の獣庇う = バフ番号取得_枠("不可視の獣庇う", 隊列番号)
	CALL 戦闘効果削除(不可視の獣庇う)
	不可視の獣ＨＰ = バフ番号取得_枠("不可視の獣ＨＰ", 隊列番号)
	CALL 戦闘効果削除(不可視の獣ＨＰ)
	CALL 戦闘不能者登録(隊列番号, -1)
;最大HPの二割を切ると不可視の獣は撤退
ELSEIF 戦闘中カウント:隊列番号:0 > ((不可視の獣のHP * 80) / 100)
	アビ汎用文字列:実行時メッセージ３ = アズールが虚空になにか呟くと、不可視の獣の気配はフッと消失した。
	不可視の獣 = バフ番号取得_枠("不可視の獣", 隊列番号)
	CALL 戦闘効果削除(不可視の獣)
	不可視の獣庇う = バフ番号取得_枠("不可視の獣庇う", 隊列番号)
	CALL 戦闘効果削除(不可視の獣庇う)
	不可視の獣ＨＰ = バフ番号取得_枠("不可視の獣ＨＰ", 隊列番号)
	CALL 戦闘効果削除(不可視の獣ＨＰ)
ENDIF


@特殊反応_被回復時_不可視の獣回復(効果番号, 回復者隊列, 被回復記録順)
#DIM 効果番号
#DIM 被回復記録順
#DIM 回復者隊列
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(1895))

;ダメージ回復処理
戦闘中カウント:隊列番号:0 -= (被回復時最大量:被回復記録順 / 2)



@固有行動開始時処理_キャラ_1895
#DIM 隊列番号
#DIM 不可視の獣のHP
#DIM 自然回復量
#DIM 不可視の獣奥義ゲージ

隊列番号 = キャラ隊列検索(GETCHARA(1895))

IF バフ番号取得_枠("不可視の獣", 隊列番号) == -1
	CALL バフ込み数値算出("最大ＭＰ", 隊列番号)
	不可視の獣のHP = RESULT
	自然回復量 = (不可視の獣のHP / 10)  + ( (不可視の獣のHP / 100) * ((RAND:11) -5))
	戦闘中カウント:隊列番号:0 -= 自然回復量
	SIF 戦闘中カウント:隊列番号:0 < 0
		戦闘中カウント:隊列番号:0 = 0
	不可視の獣奥義ゲージ = 100 - (戦闘中カウント:隊列番号:0 / 不可視の獣のHP)
	SIF 不可視の獣奥義ゲージ < 0
		不可視の獣奥義ゲージ = 0
	BATTLE_STATE:隊列番号:奥義ゲージ = 不可視の獣奥義ゲージ
ELSE
	BATTLE_STATE:隊列番号:奥義ゲージ = 0
ENDIF


