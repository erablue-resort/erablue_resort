
@固有アビ_1895_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 連撃数
#DIM DA率計算値
#DIM TA率計算値
#DIM 連撃率加算値
#DIM 隊列番号
#DIM 銃弾ヒット数
#DIM 前回のヒット
#DIM 跳弾回数

隊列番号 = キャラ隊列検索(GETCHARA(1895))

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
 	アビ名 = 跳弾射撃＋＋
 	消費ＭＰ = 60
 	効果数値 = 5
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 13
 	アビ名 = 跳弾射撃＋
 	消費ＭＰ = 45
 	効果数値 = 4
ELSE
	アビ名 = 跳弾射撃
	消費ＭＰ = 30
	効果数値 = 3
ENDIF

アビ汎用文字列:実行時メッセージ１ = アズールは厳しい無表情を保ったまま、49マグナムをスピンさせ、引き金を引いた。BLAM！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>跳弾を利用し敵をランダムな回数攻撃する。
		詳細文文字列受け渡し変数 += "<br>最大ヒット回数{効果数値}回"
		RETURN 消費ＭＰ
	CASE "アビ効果"
	
		;攻撃回数決定
		LOCAL = RAND:100
		CALL バフ・デバフ測定(隊列番号, "DA率")
		DA率計算値 = RESULT + アビ汎用変数:DA率補正値
		CALL バフ・デバフ測定(隊列番号, "TA率")
		TA率計算値 = RESULT + アビ汎用変数:TA率補正値
		CALL バフ・デバフ測定(隊列番号, "連続攻撃率")
		連撃率加算値 = RESULT
		IF TA率計算値 + 連撃率加算値 > LOCAL
			連撃数 = 3
		 ELSE
			LOCAL = RAND:100
			IF DA率計算値 + 連撃率加算値 > LOCAL
				連撃数 = 2
			ELSE
				連撃数 = 1
			ENDIF
		ENDIF
		
		FOR LOCAL, 0, 連撃数
		
			銃弾ヒット数 = 0	
			戦闘中カウント:隊列番号:与ヒット数合計 = 0
			
			IF LOCAL == 0
				CALL アビ対象選択テンプレート_敵単体
			ELSE
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			ENDIF
			
			アビ汎用変数:効果割合 = 100
			CALL バフ込み数値算出("命中率", 隊列番号)
			アビ汎用変数:命中率補正値 = -(RESULT / 2)
			アビ汎用変数:TA率補正値 = -400
			アビ汎用変数:DA率補正値 = -400
			CALL アビテンプレート_通常攻撃処理
			
			SELECTCASE 連撃数
				CASE 3
				アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = トリプルアタック！
		 		CASE 2
				アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = ダブルアタック！
			ENDSELECT
			
			IF 戦闘中カウント:隊列番号:与ヒット数合計 > 0
				銃弾ヒット数 ++
				前回のヒット = アビテンプレート用_対象保存:0
			ENDIF
			戦闘中カウント:隊列番号:与ヒット数合計 = 0
			
			アビ汎用変数:命中率補正値 = 0
			アビ汎用変数:かばう無視オプション = 1
			
			FOR 跳弾回数, 0, (効果数値 * 2)
				IF RAND:効果数値 >= 銃弾ヒット数
					CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
					IF アビテンプレート用_対象保存:0 != 前回のヒット
						CALL アビテンプレート_通常攻撃処理
						IF 戦闘中カウント:隊列番号:与ヒット数合計 > 0
							銃弾ヒット数 ++
							前回のヒット = アビテンプレート用_対象保存:0
						ENDIF
						戦闘中カウント:隊列番号:与ヒット数合計 = 0
					ELSE
						前回のヒット = 0
					ENDIF
				ENDIF
			NEXT
		NEXT
		
ENDSELECT

@固有アビ_1895_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(1895))

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 33
 	アビ名 = 跳弾射撃(精密)＋＋
 	消費ＭＰ = 100
 	効果数値 = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
 	アビ名 = 跳弾射撃(精密)＋
 	消費ＭＰ = 75
 	効果数値 = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 跳弾射撃(精密)
	消費ＭＰ = 50
	効果数値 = 50
ENDIF

アビ汎用文字列:実行時メッセージ１ = アズールは、狙いを定めて49マグナムを撃った！　BLAMN！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>単体に{効果数値}％防御無視の通常攻撃ダメージを与える。
		詳細文文字列受け渡し変数 += "<br>命中補正200%(割合) 確定クリティカル" 
		RETURN 消費ＭＰ
	CASE "アビ効果"
	
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL バフ込み数値算出("命中率", 隊列番号)
		アビ汎用変数:命中率補正値 = -RESULT
		アビ汎用変数:TA率補正値 = -400
		アビ汎用変数:DA率補正値 = -400
		CALL アビテンプレート_通常攻撃処理
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 100
		CALL バフ込み数値算出("命中率", 隊列番号)
		アビ汎用変数:命中率補正値 = RESULT
		アビ汎用変数:クリティカルフラグ = 999
		アビ汎用変数:防御無視オプション_割合 = 効果数値
		CALL アビテンプレート_通常攻撃処理
		
ENDSELECT
