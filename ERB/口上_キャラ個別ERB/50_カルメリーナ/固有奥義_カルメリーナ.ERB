
;本家の最終上限開放に相当する条件が定まっていないため強化タイミングはけっこう適当です
;現状80は遠すぎると思うので50くらいに
@固有奥義_50(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 奥義ランク
#DIM 奥義実威力
#DIM 奥義威力補正
#DIM 奥義威力補正上限
#DIM 敵番号
#DIM 弱体数合計

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	奥義ランク = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	奥義ランク = 2
ELSE
	奥義ランク = 1
ENDIF

SELECTCASE 奥義ランク
	CASE IS >= 3
		奥義名 = アプローズ・レイン
		奥義威力 = 0
		奥義実威力 = 500
		奥義威力補正 = 20
		奥義威力補正上限 = 200
	CASE 2
		奥義名 = ドレッドリーカー＋
		奥義威力 = 0
		奥義実威力 = 450
		奥義威力補正 = 15
		奥義威力補正上限 = 150
	CASEELSE
		奥義名 = ドレッドリーカー
		奥義威力 = 0
		奥義実威力 = 400
		奥義威力補正 = 10
		奥義威力補正上限 = 100
ENDSELECT


;旧版はキルストリークと同様の処理（上限無し）でしたが、
;敵全体の弱体数を見るようにしたため補正上限を追加
;奥義ダメージ上限アップが存在しないので本家よりダメージ上昇率と上限を引き上げ
SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義実威力}％
		詳細文文字列受け渡し変数 += @"<br>敵全体のデバフ効果1つにつき、ダメージ倍率が［{奥義威力補正}％／最大{奥義威力補正上限}％］上昇する。"
		詳細文文字列受け渡し変数 += @"<br>味方全体に（通常枠・1回）の幻影効果を与える。"
			IF 奥義ランク >= 3
				;奥義ランク3の時点で陥落チェック不要のはずですが念のため
				IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
					詳細文文字列受け渡し変数 += @"<br>次に使用する『ウォーフープ＋』『オールセンセーション＋』が"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>次に使用する『ウォーフープ＋』『オールセンセーション』が"
				ENDIF
				詳細文文字列受け渡し変数 += @"<br>ＭＰを消費しなくなる。"
			ENDIF
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる
	CASE "奥義追加効果"

		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			弱体数合計 += 弱体数算出(敵番号)
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:ダメージ種類 = 奥義ダメージ
		アビ汎用変数:効果割合 = 奥義実威力 + MIN(奥義威力補正上限, 弱体数合計 * 奥義威力補正)
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 幻影
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		;1・3アビＭＰ消費無しフラグ
		;何か表示させてもよかったかもしれない
		IF 奥義ランク >= 3
			戦闘中カウント:戦闘行動キャラ:1 = 1
			戦闘中カウント:戦闘行動キャラ:3 = 1
		ENDIF
ENDSELECT
