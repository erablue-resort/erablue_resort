
;2025/5/25以前の仕様を基にしています


;連撃ダウン効果量100％は原作通りです
;調整してもよさそうですがとりあえずそのまま
@固有アビ_50_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM 防御ダウン値

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = ウォーフープ＋
	消費ＭＰ = 100
	威力値 = 325
	防御ダウン値 = 15
ELSE
	アビ名 = ウォーフープ
	消費ＭＰ = 100
	威力値 = 275
	防御ダウン値 = 15
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［{防御ダウン値}％］（B枠・3ターン）の防御力減少効果を与える。基礎成功率85％。"
		詳細文文字列受け渡し変数 += @"<br>［100％］（A枠・3ターン）の連続攻撃率減少効果を与える。基礎成功率85％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 防御ダウン値
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 85
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 85
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT



@固有アビ_50_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = カレイドスコープ＋
	消費ＭＰ = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = カレイドスコープ
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵単体に［1］（5ターン）の最大ＣＴ増加効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@固有アビ_50_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビ名 = オールセンセーション
	消費ＭＰ = 80
	威力値 = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［累積10％最大30％］（5ターン）の弱体耐性減少効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビテンプレート_不利効果_累積デバフ効果セット
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 弱体耐性_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
ENDSELECT




@固有行動開始時処理_キャラ_50
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(50))

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = ユニバーサルスター
;付随バフは非表示なので意味なし
;本当は何か表示させたくもあったので残してあります
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_付随バフ効果セット("幻影")

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = ユニバーサルスター_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 30
CALL アビテンプレート_有利効果_付随バフ効果セット("幻影")

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = ユニバーサルスター_DA率
アビ汎用文字列:バフ・デバフ対象能力 = DA率
アビ汎用変数:効果量 = 30
CALL アビテンプレート_有利効果_付随バフ効果セット("幻影")



;付随バフは非表示なので意味なし
@バフ・デバフ特殊表示_ユニバーサルスター(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ユニバーサルスター
PRINTFORML 自分に幻影効果が付与されている時、攻撃力［30％］・DA率［30％］アップしている状態



;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_50(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『ユニバーサルスター』 □------------------------------------------------------------
KSTR:(K++) = 　自分に幻影効果が付与されている時、攻撃力［30％］・DA率［30％］アップ
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『決めのピース☆』 □-------------------------------------------------
KSTR:(K++) = 　敵の弱体効果の数に応じて奥義ダメージアップ
CALL 口上変数表示(対象キャラ, 1)
