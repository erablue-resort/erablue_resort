;本家2025/5/26後の仕様に近づけました
;せめてこっちではのびのびとたたかってほしい
;威力と消費ＭＰなどは基準が確定次第修正します

;1アビ
;とりあえず＋では本家そのままの威力
;連撃ダウン効果量が最初から100％なのもおなじ
;複数ヒット時の表示が長くなりすぎるのはなんとかしたい
@固有アビ_50_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値
#DIM アビランク
#DIM 防御ダウン値
#DIM 追加威力値
#DIM 隊列番号
;#DIM アクロバティック行数

隊列番号 = キャラ隊列検索(キャラ番号)

;本家ではLV55で＋
;現状ちょっと遠すぎる気がするので若干下げました
;（この辺も基準ができれば修正します）
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビランク = 2
ELSE
	アビランク = 1
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = ウォーフープ＋
		消費ＭＰ = 110
		威力値 = 100
		防御ダウン値 = 25
	CASEELSE
		アビ名 = ウォーフープ
		消費ＭＰ = 110
		威力値 = 85
		防御ダウン値 = 20
ENDSELECT

;強化奥義後ならＭＰ消費なし
SIF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:1 == 1
	消費ＭＰ = 0

; アクロバティックLvによるダメアビ強化  本家仕様により近いバージョン
; 汎用性があった方がいいと思うので単純なアビダメ強化にしました
; こっちの仕様に戻すかもしれないので残しておきます
; アクロバティック行数 = バフ番号取得_枠("アクロバティック", 隊列番号)
; IF アクロバティック行数 > -1
; 	追加威力値 = DT_CELL_GET("戦闘効果データベース", アクロバティック行数, "効果量_固定値") * 15
; 	威力値 = 威力値 + 追加威力値
; ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>ターゲットに関わらず［攻撃力✕{威力値}％］の10回自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［{防御ダウン値}％］（B枠・3ターン）の防御力減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［100％］（A枠・3ターン）の連続攻撃率減少効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(10)
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 防御ダウン値
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット

		;ＭＰ消費無し解除
		SIF 戦闘中カウント:戦闘行動キャラ:1
			戦闘中カウント:戦闘行動キャラ:1 = 0
ENDSELECT


;2アビ
;旧仕様では、CT5という謎に早いグラビ（※持ち味云々の後遺症）を反映させたつもりの低燃費でしたが
;効果が盛られた今ではもうちょっと消費上げていいかも
;魅了の成功率は本家でどうなってるのかよくわからないので適当です
@固有アビ_50_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク
#DIM 魅了成功率

;本家ではLV75で＋、LV90で＋＋
;どちらも現状遠すぎると思うのでだいぶ下げました
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 60
	アビランク = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビランク = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 3
		アビ名 = カレイドスコープ＋＋
		消費ＭＰ = 35
		魅了成功率 = 85
	CASE 2
		アビ名 = カレイドスコープ＋
		消費ＭＰ = 35
		魅了成功率 = 85
	CASE 1
		アビ名 = カレイドスコープ
		消費ＭＰ = 40
		魅了成功率 = 75
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF アビランク >= 3
				;ツールチップやってみましたが色これでいいのだろうか
				IF OPTION変数:ツールチップ切り替え == 0 && ダンジョン表示モード == "戦闘画面"
					詳細文文字列受け渡し変数 += @"<br>敵単体に（3ターン・消去不可）の<font color='#%カラーパレット_HTML("紫")%'><nonbutton title='<br>[20％]の攻撃力と命中率減少効果<br>'>カレイドスコープ</nonbutton></font>効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>敵単体に（3ターン・消去不可）のカレイドスコープ効果を与える。"
				ENDIF
				詳細文文字列受け渡し変数 += @"<br>（通常枠・3ターン）の魅了効果を与える。成功率{魅了成功率}％。"
				詳細文文字列受け渡し変数 += @"<br>［1］（5ターン）の最大ＣＴ増加効果を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵単体に（通常枠・3ターン）の魅了効果を与える。成功率{魅了成功率}％。"
				詳細文文字列受け渡し変数 += @"<br>［1］（5ターン）の最大ＣＴ増加効果を与える。"
			ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF アビランク >= 3
			詳細文文字列受け渡し変数 += @"敵単体に（3ターン・消去不可）のカレイドスコープ効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>（通常枠・3ターン）の魅了効果を与える。成功率{魅了成功率}％。"
			詳細文文字列受け渡し変数 += @"<br>［1］（5ターン）の最大ＣＴ増加効果を与える。"
			詳細文文字列受け渡し変数 += @"<br><br>カレイドスコープ："
			詳細文文字列受け渡し変数 += @"<br>[20％]の攻撃力と命中率減少効果"
		ENDIF
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体

		IF アビランク >= 3
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_カレイドスコープ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット

			アビ汎用文字列:バフ・デバフ枠 = 命中率_カレイドスコープ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 魅了成功率
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


;3アビ
;特殊ダメージダウンは与ダメージ減少にしました（※デバフにダメージ補正が使えないため）
;しかし効果量がおまけすぎる
@固有アビ_50_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク
#DIM 威力値
#DIM 敵番号
#DIM 弱体数合計
#DIM 発動回数
#DIM 隊列番号
;#DIM アクロバティック行数

隊列番号 = キャラ隊列検索(キャラ番号)

;本家ではLV95で＋
;いくらなんでも現状遠すぎるので下げました  かわりに習得をやや遅めに
;敵の数が多いこちらでは2回発動の条件を厳しくしてもよかったかもしれない
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
	アビランク = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 2
		アビ名 = オールセンセーション＋
		消費ＭＰ = 130
		威力値 = 100
	CASE 1
		アビ名 = オールセンセーション
		消費ＭＰ = 120
		威力値 = 85
	CASEELSE
		アビ名 = 
ENDSELECT

; アクロバティックLvによるダメアビ強化  本家仕様により近いバージョン
; 汎用性があった方がいいと思うので単純なアビダメ強化にしました
; こっちの仕様に戻すかもしれないので残しておきます
; アクロバティック行数 = バフ番号取得_枠("アクロバティック", 隊列番号)
; IF アクロバティック行数 > -1
; 	追加威力値 = DT_CELL_GET("戦闘効果データベース", アクロバティック行数, "効果量_固定値") * 15
; 	威力値 = 威力値 + 追加威力値
; ENDIF

;強化奥義後ならＭＰ消費なし
SIF INRANGE(隊列番号, 0, 3) && 戦闘中カウント:隊列番号:3 == 1
	消費ＭＰ = 0

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{威力値}％］の4回自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［累積10％最大30％］（5ターン）の弱体耐性減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［累積5％最大15％］（5ターン）の与ダメージ減少効果を与える。"
			SIF アビランク >= 2
				詳細文文字列受け渡し変数 += @"<br>敵にデバフ効果が5個以上付与されている場合、2回発動する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;2回発動用のカウント
		IF アビランク >= 2
			FOR 敵番号, 10, 20
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				弱体数合計 += 弱体数算出(敵番号)
			NEXT

			IF 弱体数合計 >= 5
				発動回数 = 2
			ELSE
				発動回数 = 1
			ENDIF
		ELSE
			発動回数 = 1
		ENDIF

		FOR LOCAL, 0, 発動回数
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 威力値
			FOR LOCAL:1, 0, 4
				CALL アビテンプレート_ダメージ処理_自属性
			NEXT

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 弱体耐性_累積枠
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:累積上限_割合 = 30
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 与ダメージ_累積枠
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
			アビ汎用変数:効果割合 = 5
			アビ汎用変数:累積上限_割合 = 15
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		NEXT

		;ＭＰ消費無し解除
		SIF 戦闘中カウント:戦闘行動キャラ:3
			戦闘中カウント:戦闘行動キャラ:3 = 0
ENDSELECT


;4アビ
;敵対心上昇は計算方法の関係で本家の数値（+100％らしい）より雑に増やしました
;完全回避は回避率+1000％で代用するのが主流のようなのでそれに準拠
@固有アビ_50_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM アビランク
#DIM 敵対心上昇値

;本家ではLV100フェイトエピソード終了で習得 つまり最後
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビランク = 1
ELSE
	アビランク = 0
ENDIF

SELECTCASE アビランク
	CASE IS >= 1
		アビ名 = ワン・アンド・オンリー
		消費ＭＰ = 90
		敵対心上昇値 = 400
	CASEELSE
		アビ名 = 
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			IF OPTION変数:ツールチップ切り替え == 0 && ダンジョン表示モード == "戦闘画面"
				詳細文文字列受け渡し変数 += @"<br>自身に（5ターン）の<font color='#%カラーパレット_HTML("黄")%'><nonbutton title='<br>ダメージを受けなかったターンの終了時に<br>ランダムな敵のＣＴを［1］吸収し、味方全体の奥義ゲージを［20％］上昇させる。<br>'>ワン・アンド・オンリー</nonbutton></font>効果を与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>自身に（5ターン）のワン・アンド・オンリー効果を与える。"
			ENDIF
		詳細文文字列受け渡し変数 += @"<br>［{敵対心上昇値}％］（独自枠・1ターン）の敵対心上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（1回）の完全回避効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 += @"自身に（5ターン）のワン・アンド・オンリー効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［{敵対心上昇値}％］（独自枠・1ターン）の敵対心上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（1回）の完全回避効果を与える。"
		詳細文文字列受け渡し変数 += @"<br><br>ワン・アンド・オンリー："
		詳細文文字列受け渡し変数 += @"<br>ダメージを受けなかったターンの終了時に<br>ランダムな敵のＣＴを［1］吸収し、味方全体の奥義ゲージを［20％］上昇させる。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:特殊表示オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = ワン・アンド・オンリー
		アビ汎用文字列:バフ・デバフ対象能力 = ワン・アンド・オンリー
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 敵対心_ワン・アンド・オンリー
		アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
		アビ汎用変数:効果割合 = 敵対心上昇値
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 完全回避
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



;サポアビ『決めのピース☆』
;本家と同じく奥義ゲージ上昇量アップ（2*弱体数/Max20）
;これは非表示でいいとおもう
@固有行動開始時処理_キャラ_50
#DIM 隊列番号
#DIM 敵番号
#DIM 弱体数合計

隊列番号 = キャラ隊列検索(GETCHARA(50))

FOR 敵番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
		CONTINUE
	弱体数合計 += 弱体数算出(敵番号)
NEXT

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:特殊表示オプション = -1
アビ汎用文字列:バフ・デバフ枠 = 決めのピース☆
アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
アビ汎用変数:持続ターン = 1
アビ汎用変数:効果量 = MIN(20, 弱体数合計 * 2)
CALL アビテンプレート_有利効果_バフ効果セット


;サポアビ『ユニバーサルスター』
;本家ではアクロバティックLVに応じて以下のとおり上昇
;攻撃10/Max30％
;防御20/Max60％
;DA率20/Max60％
;TA率10/Max30％
;アビ倍率0.1/Max0.3倍
;アビ上限5/Max15％
;回避率10/Max30％
;→アビ倍率/上限はアビダメ補正（15/Max45％）に変更
;固有アビの倍率上昇に限定させるか悩んでとりあえず汎用性を選びました
;ついでに命中率補正を追加（10/Max30％）
;すげえ身体能力を発揮する的なバフなら命中率も上がっていいとおもいます（本家は基本的に必中なので…）
@固有ターン終了時処理_キャラ_50
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 被ダメ判定
#DIM ＣＴ吸収
#DIM ＣＴ吸収再確認
#DIM 敵番号
#DIM 敵ＣＴ

隊列番号 = キャラ隊列検索(GETCHARA(50))

被ダメ判定 = 0
IF 戦闘中カウント:隊列番号:被ダメ合計 > 戦闘中カウント:隊列番号:0
	被ダメ判定 = 1
ENDIF

IF 被ダメ判定 != 1
	;アクロバティックLV上昇
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:累積上限_固定値 = 3
	アビ汎用文字列:バフ・デバフ対象能力 = アクロバティック
	CALL アビテンプレート_刻印追加(1)
		
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:付随割合 = 10
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 防御力_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:付随割合 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = DA率_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = DA率
	アビ汎用変数:付随割合 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = TA率_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = TA率
	アビ汎用変数:付随割合 = 10
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	;枠名が長過ぎる  非表示だしいいか…
	アビ汎用文字列:バフ・デバフ枠 = ダメージ補正_アビリティダメージ_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
	アビ汎用変数:付随割合 = 15
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 命中率_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = 命中率
	アビ汎用変数:付随割合 = 10
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 回避率_アクロバティック
	アビ汎用文字列:バフ・デバフ対象能力 = 回避率
	アビ汎用変数:付随割合 = 10
	CALL アビテンプレート_有利効果_付随バフ効果セット("アクロバティック")

	;4アビのＣＴ吸収処理
	IF バフ番号取得_枠("ワン・アンド・オンリー", 隊列番号) > -1
		;まずＣＴ1以上の生きてる敵がいるか確認
		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):チャージターン > 0 && 敵BATTLE_STATE:(敵番号 - 10):ＨＰ > 0
				ＣＴ吸収 = 1 
		NEXT

		;1体でも吸収できる敵がいればランダムで吸う
		IF ＣＴ吸収 == 1
			CALL アビ汎用変数文字列リセット

			;ＣＴ1以上ある敵がランダム選択されるまでループ
			;ここから下はもうちょっとスマートに作れるのではとおもいます
			DO
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				敵ＣＴ = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン

				;無限ループ防止用の再確認
				;上のＣＴチェック後に何らかの理由でＣＴ1以上の敵がいなくなっていた場合にBREAK
				IF 敵ＣＴ < 1
					FOR 敵番号, 10, 20
						SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
						CONTINUE
						SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
						CONTINUE
						SIF 敵BATTLE_STATE:(敵番号 - 10):チャージターン > 0 && 敵BATTLE_STATE:(敵番号 - 10):ＨＰ > 0
						ＣＴ吸収再確認 = 1 
					NEXT

					SIF ＣＴ吸収再確認 != 1
						BREAK
				ENDIF
			LOOP 敵ＣＴ < 1

			;上でBREAKした場合、ＣＴ0に対して不発スロウを出したくないので改めて確認を入れる
			IF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン > 0
				;ＣＴ吸収はスロウと別の耐性を参照させたかったのですが今のところただのスロウ
				アビ汎用変数:効果量 = 1
				CALL アビテンプレート_不利効果_スロウ
			ENDIF

			;スロウに成功していればＣＴが減っているはず
			;スロウ前のＣＴが0じゃないかも念のため同時にチェック
			IF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン < 敵ＣＴ && 敵ＣＴ != 0
				CALL アビ対象選択テンプレート_味方全体_生者のみ
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果量 = 20
				CALL アビテンプレート_有利効果_奥義ゲージUP
			ENDIF
		ENDIF
	ENDIF
ENDIF

戦闘中カウント:隊列番号:0 = 戦闘中カウント:隊列番号:被ダメ合計




@バフ・デバフ特殊表示_アクロバティック(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL

LOCAL = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")

PRINTFORML アクロバティック
PRINTFORML アクロバティック効果［現在{LOCAL}／最大3］に応じて各能力が上昇している状態
PRINTFORML 上昇中能力：
PRINTFORML 攻撃力［{LOCAL * 10, 2}％］・防御力［{LOCAL * 20, 2}％］・DA率［{LOCAL * 20, 2}％］・TA率［{LOCAL * 10, 2}％］
PRINTFORML アビリティダメージ［{LOCAL * 15, 2}％］・命中率［{LOCAL * 10, 2}％］・回避率［{LOCAL * 10, 2}％］


@バフ・デバフ特殊表示_ワン・アンド・オンリー(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL

LOCAL:1 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")

PRINTFORML ワン・アンド・オンリー
PRINTFORML ダメージを受けなかったターンの終了時に
PRINTFORML ランダムな敵のＣＴを［1］吸収し、味方全体の奥義ゲージを［20％］上昇させる。
PRINTFORML 持続ターン：{LOCAL:1}


;サポアビ表示
;奥義ダメージ上限アップが存在しないため本家よりダメージ上昇率と最大値を引き上げ
@口上_ステータス画面_固有特性解説_50(対象キャラ)
#DIM 対象キャラ
#DIM 奥義ランク
#DIM 奥義威力補正
#DIM 奥義威力補正上限

CALL 口上変数初期化

IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 50
	奥義ランク = 3
ELSEIF 基礎BATTLE_STATE:対象キャラ:Lv >= 35
	奥義ランク = 2
ELSE
	奥義ランク = 1
ENDIF

SELECTCASE 奥義ランク
	CASE IS >= 3
		奥義威力補正 = 20
		奥義威力補正上限 = 200
	CASE 2
		奥義威力補正 = 15
		奥義威力補正上限 = 150
	CASEELSE
		奥義威力補正 = 10
		奥義威力補正上限 = 100
ENDSELECT

KSTR:(K++) = □ 特性：『ユニバーサルスター』 □-------------------------------------------------------
KSTR:(K++) = 　ダメージを受けなかったターンの終了時、
KSTR:(K++) = 　自身に［累積1最大3］（消去不可）のアクロバティック効果を与える。
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『決めのピース☆』 □-----------------------------------------------------------
KSTR:(K++) = 　敵全体のデバフ効果の数に応じて、自身の奥義ダメージ・ゲージ上昇効率がアップする。
KSTR:(K++) = 　奥義ダメージ：［デバフ数✕{奥義威力補正}％／最大{奥義威力補正上限}％］アップ
KSTR:(K++) = 　ゲージ上昇効率：［デバフ数✕2％／最大20％］アップ

CALL 口上変数表示(対象キャラ, 1)
