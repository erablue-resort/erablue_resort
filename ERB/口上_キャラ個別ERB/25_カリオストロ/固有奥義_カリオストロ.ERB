
@固有奥義_25(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM 効果数値
#DIMS 奥義名

;レベルなどの条件で奥義を強化する場合、奥義名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	IF  陥落チェック(キャラ番号)
		奥義名 = オプス・マグナ
	ELSE
		奥義名 = アルス・マグナ＋＋
	ENDIF
 	奥義威力 = 500
	効果数値 = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	奥義名 = アルス・マグナ＋
	奥義威力 = 500
	効果数値 = 20
ELSE
	奥義名 = アルス・マグナ
	奥義威力 = 500
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵単体に自属性の［攻撃力✕{奥義威力}％］のダメージを与える。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			詳細文文字列受け渡し変数 += @"<br>4ターンの間、味方全体の攻撃力を[{効果数値}％/B枠]増加させる。"
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
			詳細文文字列受け渡し変数 += @"<br>4ターンの間、自身の回復力を[30％/独自枠]上昇させ\@ 陥落チェック(キャラ番号)? 、ＭＰを[10％(最大100)]回復す# \@る。"
		RETURN 奥義威力
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;直接記述しても良いが、基本的にアビテンプレートの関数を使用すると便利で良い
		;テンプレート使用の例は下部分を参照
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 30
			アビ汎用文字列:バフ・デバフ枠 = 回復力_%TOHALF(奥義名)%
			アビ汎用文字列:バフ・デバフ対象能力 = 回復力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
			SIF 陥落チェック(キャラ番号)
				BATTLE_STATE:キャラ番号:ＭＰ = MIN(BATTLE_STATE:キャラ番号:ＭＰ + MIN(BATTLE_STATE:キャラ番号:ＭＰ / 10, 100), BATTLE_STATE:キャラ番号:最大ＭＰ)
		ENDIF
ENDSELECT
