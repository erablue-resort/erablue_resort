@固有アビ_2317_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

;Lv1初始技能
アビ名 = ドラゴンハート B
消費ＭＰ = 100

IF 隊列番号 >= 0
	判定値 = 戦闘中カウント:隊列番号:0 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に攻撃力［50％］（3ターン）および防御力［30％］（3ターン）を付与する。"
		詳細文文字列受け渡し変数 += @"<br>自身に再生［700］（3ターン）および奥義ゲージ［50］増加効果を付与する。"
		詳細文文字列受け渡し変数 += @"<br>本スキルは行動回数を消費せず、再使用まで［2ターン］。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値}回合）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ドラゴンハート_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ドラゴンハート_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ドラゴンハート_再生
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:効果量 = 700
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_奥義ゲージUP
		
		戦闘中カウント:隊列番号:0 = 現在ターン数 + 2
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_2317_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

;Lv10技能
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = ペリー・ダンサー B
	消費ＭＰ = 100
ELSE
	アビ名 = 
ENDIF

IF 隊列番号 >= 0
	判定値 = 戦闘中カウント:隊列番号:1 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身にクリティカル率［50％］（独立枠・3ターン）を付与する。"
		詳細文文字列受け渡し変数 += @"<br>自身にクリティカルダメージ［50％］（独立枠・3ターン）を付与する。"
		詳細文文字列受け渡し変数 += @"<br>本スキルは行動回数を消費せず、再使用まで［2ターン］。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値}回合）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ペリー・ダンサー_CT率
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 50
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ペリー・ダンサー_CT倍率
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:隊列番号:1 = 現在ターン数 + 2
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_2317_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM 隊列番号
#DIM 竜判定

隊列番号 = キャラ隊列検索(キャラ番号)
竜判定 = バフ番号取得_枠("境界を拓く、最後の竜", 隊列番号)

;Lv25技能
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	IF 竜判定 > -1
		;持有龙特性时
		アビ名 = レイ・ホライゾン A
		消費ＭＰ = 150
	ELSE
		;无龙特性时
		アビ名 = レイ・ホライゾン A
		消費ＭＰ = 150
	ENDIF
ELSE
	アビ名 = 
ENDIF

IF 隊列番号 >= 0
	判定値 = 戦闘中カウント:隊列番号:2 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		
		IF 竜判定 > -1
			;持有龙特性时的说明
			詳細文文字列受け渡し変数 += @"<br>奥義ダメージ［70％］（独立枠・永続）を付与する。"
			詳細文文字列受け渡し変数 += @"<br>回避率［100％］（独立枠・1ターン）を付与する。"
		ELSE
			;无龙特性时的说明
			詳細文文字列受け渡し変数 += @"<br>奥義ゲージ［100］増加効果を付与する。"
			詳細文文字列受け渡し変数 += @"<br>回避率［100％］（独立枠・1ターン）を付与する。"
			詳細文文字列受け渡し変数 += @"<br>自身に「境界を拓く、最後の竜」特性（99ターン）を付与する。"
		ENDIF
		
		詳細文文字列受け渡し変数 += @"<br>本スキルは行動回数を消費せず、再使用まで［2ターン］。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値}回合）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 竜判定 > -1
			;持有龙特性时的效果
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = レイ・ホライゾン_奥義威力
			アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
			アビ汎用変数:効果割合 = 70
			アビ汎用変数:持続ターン = -1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = レイ・ホライゾン_回避率
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ELSE
			;无龙特性时的效果
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 100
			CALL アビテンプレート_有利効果_奥義ゲージUP
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = レイ・ホライゾン_回避率
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 境界を拓く、最後の竜
			アビ汎用文字列:バフ・デバフ対象能力 = 境界を拓く、最後の竜
			アビ汎用変数:持続ターン = 99
			アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		戦闘中カウント:隊列番号:2 = 現在ターン数 + 2
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_2317_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM 隊列番号
#DIM 竜判定

隊列番号 = キャラ隊列検索(キャラ番号)
竜判定 = バフ番号取得_枠("境界を拓く、最後の竜", 隊列番号)

;Lv25陥落且持有龙特性
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25 && 陥落チェック(キャラ番号) && 竜判定 > -1
	アビ名 = 最強最美の妖精騎士
	消費ＭＰ = 150
ELSE
	アビ名 = 
ENDIF

IF 隊列番号 >= 0
	判定値 = 戦闘中カウント:隊列番号:3 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>「境界を拓く、最後の竜」特性を解除する。"
		詳細文文字列受け渡し変数 += @"<br>自身に「最強最美の妖精騎士」特性（99ターン）を付与する。"
		詳細文文字列受け渡し変数 += @"<br>奥義ゲージ［100］増加効果および回避率［100％］（1ターン）を付与する。"
		詳細文文字列受け渡し変数 += @"<br>本スキルは行動回数を消費せず、再使用まで［1ターン］"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値}回合）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;清除龙特性
		CALL 戦闘効果削除(竜判定)
		
		;赋予骑士特性
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 最強最美の妖精騎士
		アビ汎用文字列:バフ・デバフ対象能力 = 最強最美の妖精騎士
		アビ汎用変数:持続ターン = 99
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		;奥义槽增加
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		CALL アビテンプレート_有利効果_奥義ゲージUP
		
		;回避率提升
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 最強最美の妖精騎士_回避率
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:隊列番号:3 = 現在ターン数 + 1
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

;境界を拓く、最後の竜特性的特殊显示
@バフ・デバフ特殊表示_境界を拓く、最後の竜(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 境界を拓く、最後の竜
PRINTFORML ［最大ＭＰ✕10％］・20奥义
PRINTFORML 持续回合：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

;境界を拓く、最後の竜特性的每回合处理
@固有ターン終了時処理_バフ_境界を拓く、最後の竜(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果対象")

;MP回复
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(隊列番号)
アビテンプレート用_アビ名表示済フラグ = 1
アビ汎用変数:効果割合 = 10
アビ汎用文字列:回復側使用能力値 = 最大ＭＰ
CALL アビテンプレート_ＭＰ回復処理_MAXでも回復

;奥义槽回复
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(隊列番号)
アビテンプレート用_アビ名表示済フラグ = 1
アビ汎用変数:効果量 = 20
CALL アビテンプレート_有利効果_奥義ゲージUP

;最強最美の妖精騎士特性的特殊显示
@バフ・デバフ特殊表示_最強最美の妖精騎士(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 最強最美の妖精騎士
PRINTFORML ［最大ＭＰ✕10％］・50奥义槽
PRINTFORML 持续回合：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

;最強最美の妖精騎士特性的每回合处理
@固有ターン終了時処理_バフ_最強最美の妖精騎士(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果対象")

;MP回复
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(隊列番号)
アビテンプレート用_アビ名表示済フラグ = 1
アビ汎用変数:効果割合 = 10
アビ汎用文字列:回復側使用能力値 = 最大ＭＰ
CALL アビテンプレート_ＭＰ回復処理_MAXでも回復

;奥义槽回复
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(隊列番号)
アビテンプレート用_アビ名表示済フラグ = 1
アビ汎用変数:効果量 = 50
CALL アビテンプレート_有利効果_奥義ゲージUP

;战斗开始时赋予全体Aurora特性
@固有戦闘開始時処理_キャラ_2317
#DIM キャラ番号
#DIM 隊列番号

キャラ番号 = GETCHARA(2317)
隊列番号 = キャラ隊列検索(キャラ番号)

;不显示buff赋予信息
アビテンプレート用_アビ名表示済フラグ = 1

;赋予全体Aurora特性
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = オーロラ
アビ汎用文字列:バフ・デバフ対象能力 = オーロラ
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

;攻击力提升
CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = オーロラ_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 20
CALL アビテンプレート_有利効果_付随バフ効果セット("オーロラ")

;Aurora特性的每回合处理
@固有ターン終了時処理_バフ_オーロラ(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果対象")

;MP回复（从10%改为20%）
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(隊列番号)
アビテンプレート用_アビ名表示済フラグ = 1
アビ汎用変数:効果割合 = 10
アビ汎用文字列:回復側使用能力値 = 最大ＭＰ
CALL アビテンプレート_ＭＰ回復処理_MAXでも回復

;Aurora特性的特殊显示
@バフ・デバフ特殊表示_オーロラ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML オーロラ
PRINTFORML 全体攻击力提升20，［最大ＭＰ✕10％］回復

;战斗不能时清除Aurora特性
@固有戦闘不能時処理_キャラ_2317
#DIM 対象番号
#DIM オーロラ

FOR 対象番号, 0, 4
	オーロラ = バフ番号取得_枠("オーロラ", 対象番号)
	CALL 戦闘効果削除(オーロラ)
NEXT

;角色特性说明
@口上_ステータス画面_固有特性解説_2317(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『オーロラ』 □-----------------------------------------------------------------
KSTR:(K++) = 　味方全体の攻撃力20を付与する。，行動開始時自身のＭＰが［最大ＭＰ✕10％］回復
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)