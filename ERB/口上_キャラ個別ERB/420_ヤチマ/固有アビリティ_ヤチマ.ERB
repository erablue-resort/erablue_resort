
@固有アビ_420_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM α適応番号
#DIM α適応Lv

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = ディグレイド・スピネル+
	消費ＭＰ = 250
ELSE
	アビ名 = ディグレイド・スピネル
	消費ＭＰ = 300
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕75％］の水属性ダメージを6回与える。
		詳細文文字列受け渡し変数 += @"<br>［累積1Lv最大5Lv］（消去不可）のディグレイド・スピネル効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［1］のバフ消去を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［30％］（Ａ枠・3ターン）の追撃効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身にα適応効果が存在する場合、α適応効果の累積Lv分このアビリティのダメージ回数が増加する。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕75％］の水属性ダメージを6回与える。
		詳細文文字列受け渡し変数 += @"<br>［累積1Lv最大5Lv］（消去不可）のディグレイド・スピネル効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［ディグレイド・スピネルのLv✕4％］（独自枠）の防御力減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［ディグレイド・スピネルのLv✕2％］（独自枠）の被ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［1］のバフ消去を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［30％］（Ａ枠・3ターン）の追撃効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身にα適応効果が存在する場合、このアビリティのダメージ回数が［α適応効果の累積Lv］回増加する。"
	CASE "アビ効果"
		α適応番号 = バフ番号取得_枠("α適応", キャラ隊列検索(GETCHARA(420)))
		α適応Lv = DT_CELL_GET("戦闘効果データベース", α適応番号, "効果量_固定値")

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		FOR LOCAL, 0, 6 + α適応Lv
			アビ汎用変数:効果割合 = 75
			CALL アビテンプレート_ダメージ処理_水属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ディグレイド・スピネル
		アビ汎用文字列:バフ・デバフ対象能力 = ディグレイド・スピネル
		アビ汎用変数:効果量 = 1
		アビ汎用変数:累積上限_固定値 = 5
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = D・S_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:付随割合 = 4
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ディグレイド・スピネル")
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = D・S_被ダメ上昇
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:付随割合 = 2
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ディグレイド・スピネル")
		
		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_不利効果_バフ消去
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有アビ_420_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM δ適応番号
#DIM δ適応Lv

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = アナライズ・プロテクション+
	消費ＭＰ = 250
ELSE
	アビ名 = アナライズ・プロテクション
	消費ＭＰ = 300
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［50％］（Ａ枠・3ターン）の防御力上昇効果を与える。
		詳細文文字列受け渡し変数 += @"<br>［60％］（通常枠・1ターン）の被ダメージ減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（通常枠・1回）のディスペルガード効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身にβ適応効果が存在する場合、このアビリティの被ダメージ減少効果が［β適応効果の累積Lv✕10％］増加する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		δ適応番号 = バフ番号取得_枠("δ適応", キャラ隊列検索(GETCHARA(420)))
		δ適応Lv = DT_CELL_GET("戦闘効果データベース", δ適応番号, "効果量_固定値")

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 60 + (δ適応Lv * 10)
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
			
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
		アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有アビ_420_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM ヤチマ隊列
#DIM R・S判定
#DIM クールタイム
#DIM 攻撃対象一時退避
#DIM 行動キャラ保存

ヤチマ隊列 = キャラ隊列検索(GETCHARA(420))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = キネシス・アクティベート
	IF INRANGE(ヤチマ隊列, 0, 3) && 戦闘中カウント:ヤチマ隊列:3 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 450
	ENDIF
ENDIF

IF INRANGE(ヤチマ隊列, 0, 3) && 戦闘中カウント:ヤチマ隊列:0
	クールタイム = MAX(0, 10 + 戦闘中カウント:ヤチマ隊列:0 - 現在ターン数)
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
ELSE
	クールタイム = 0
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［100％］（通常枠・2ターン）の与ダメージ上昇効果を与える。
		詳細文文字列受け渡し変数 += @"<br>敵単体に通常攻撃を行う。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは［10ターン］のＣＴが発生する。"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"（残り{クールタイム}ターン）"
		詳細文文字列受け渡し変数 += @"<br>自身にレコデイション・シンク効果が存在する時、このアビリティは味方全体に対して発動する。"
		詳細文文字列受け渡し変数 += @"<br>自身にレコデイション・シンク効果が存在する時、このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		R・S判定 = バフ番号取得_枠("レコデイション・シンク", ヤチマ隊列)

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		攻撃対象一時退避 = アビテンプレート用_対象保存:0
		
		CALL アビ汎用変数文字列リセット
		IF R・S判定 > -1
			CALL アビ対象選択テンプレート_味方全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_自己のみ
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 与ダメージ上昇
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(攻撃対象一時退避)
		アビ汎用変数:効果割合 = 100
		
		IF R・S判定 > -1
			SWAP 戦闘行動キャラ, 行動キャラ保存
			
			;戦闘行動キャラを1～4番目のキャラに順に渡して通常攻撃
			FOR LOCAL, 0, 4
				SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
					CONTINUE
				SIF BATTLE_STATE:LOCAL:ＨＰ < 1
					CONTINUE
				
				戦闘行動キャラ = LOCAL
				CALL アビテンプレート_通常攻撃処理
			NEXT
			
			;戦闘行動キャラをヤチマに戻す
			SWAP 戦闘行動キャラ, 行動キャラ保存
		ELSE
			CALL アビテンプレート_通常攻撃処理
		ENDIF
		
		アビテンプレート用_行動消費無しフラグ = 1
		戦闘中カウント:戦闘行動キャラ:0 = 現在ターン数
		
		戦闘中カウント:ヤチマ隊列:3 = 0
ENDSELECT



@固有行動直後処理_キャラ_420
#DIM ヤチマ隊列
#DIM 付与回数
#DIM α適応番号
#DIM α適応Lv

ヤチマ隊列 = キャラ隊列検索(GETCHARA(420))

α適応番号 = バフ番号取得_枠("δ適応", ヤチマ隊列)
α適応Lv = DT_CELL_GET("戦闘効果データベース", α適応番号, "効果量_固定値")

SIF 戦闘中カウント:ヤチマ隊列:1 > 3
	RETURN 0

付与回数 = MIN(4, 戦闘中カウント:ヤチマ隊列:与ヒット数合計 / 15 - 戦闘中カウント:ヤチマ隊列:1)

IF 付与回数 > 0
	アビテンプレート用_アビ名表示済フラグ = 1
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = α適応
	アビ汎用文字列:バフ・デバフ対象能力 = α適応
	アビ汎用変数:効果量 = 1
	アビ汎用変数:累積上限_固定値 = 4
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	FOR LOCAL, 0, 付与回数
		CALL アビテンプレート_有利効果_累積バフ効果セット
	NEXT
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_クリ率
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
	アビ汎用変数:効果量 = 100
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_クリダメ率
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
	アビ汎用変数:付随量 = 5
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_追撃
	アビ汎用文字列:バフ・デバフ対象能力 = 追撃
	アビ汎用変数:付随割合 = 5
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")
	
	戦闘中カウント:ヤチマ隊列:1 += 付与回数
ENDIF



@固有被タゲ時処理_キャラ_420(全滅フラグ)
#DIM 全滅フラグ
#DIM ヤチマ隊列
#DIM δ適応番号
#DIM δ適応Lv

ヤチマ隊列 = キャラ隊列検索(GETCHARA(420))

δ適応番号 = バフ番号取得_枠("δ適応", ヤチマ隊列)
δ適応Lv = DT_CELL_GET("戦闘効果データベース", δ適応番号, "効果量_固定値")

SIF 全滅フラグ
	RETURN 0

SIF δ適応Lv == 4
	RETURN 0

;δ適応Lvは1ターンに1回のみ上昇
戦闘中カウント:ヤチマ隊列:2 = 1



@固有ターン終了時処理_キャラ_420
#DIM ヤチマ隊列
#DIM α適応番号
#DIM α適応Lv
#DIM δ適応番号
#DIM δ適応Lv
#DIM R・S判定

ヤチマ隊列 = キャラ隊列検索(GETCHARA(420))

α適応番号 = バフ番号取得_枠("α適応", ヤチマ隊列)
α適応Lv = DT_CELL_GET("戦闘効果データベース", α適応番号, "効果量_固定値")
δ適応番号 = バフ番号取得_枠("δ適応", ヤチマ隊列)
δ適応Lv = DT_CELL_GET("戦闘効果データベース", δ適応番号, "効果量_固定値")
R・S判定 = バフ番号取得_枠("レコデイション・シンク", ヤチマ隊列)

IF 戦闘中カウント:ヤチマ隊列:2 == 1 && δ適応Lv < 4
	アビテンプレート用_アビ名表示済フラグ = 1

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = δ適応
	アビ汎用文字列:バフ・デバフ対象能力 = δ適応
	アビ汎用変数:効果量 = 1
	アビ汎用変数:累積上限_固定値 = 4
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_有利効果_累積バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = δ適応_被ダメ軽減
	アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
	アビ汎用変数:付随量 = 5
	CALL アビテンプレート_有利効果_付随バフ効果セット("δ適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = δ適応_弱体耐性
	アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
	アビ汎用変数:付随量 = 25
	CALL アビテンプレート_有利効果_付随バフ効果セット("δ適応")
	
	戦闘中カウント:ヤチマ隊列:2 = 0
	
	
ENDIF

IF α適応Lv == 4 && δ適応Lv == 4 && R・S判定 < 0
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = レコデイション・シンク
	アビ汎用文字列:バフ・デバフ対象能力 = レコデイション・シンク
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = R・S_攻撃力
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 50
	CALL アビテンプレート_有利効果_付随バフ効果セット("レコデイション・シンク")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = R・S_防御力
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_有利効果_付随バフ効果セット("レコデイション・シンク")
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = R・S_絶対TA
	アビ汎用文字列:バフ・デバフ対象能力 = TA率
	アビ汎用変数:効果量 = 1000
	CALL アビテンプレート_有利効果_付随バフ効果セット("レコデイション・シンク")
	
	戦闘中カウント:ヤチマ隊列:0 = -10
	戦闘中カウント:ヤチマ隊列:3 = 1
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ_自己以外
	アビ汎用文字列:バフ・デバフ枠 = α適応
	アビ汎用文字列:バフ・デバフ対象能力 = α適応
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_クリ率
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
	アビ汎用変数:効果量 = 100
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_クリダメ率
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
	アビ汎用変数:効果量 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = α適応_追撃
	アビ汎用文字列:バフ・デバフ対象能力 = 追撃
	アビ汎用変数:効果割合 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("α適応")
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = δ適応
	アビ汎用文字列:バフ・デバフ対象能力 = δ適応
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = δ適応_被ダメ軽減
	アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
	アビ汎用変数:効果量 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("δ適応")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = δ適応_弱体耐性
	アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
	アビ汎用変数:効果量 = 100
	CALL アビテンプレート_有利効果_付随バフ効果セット("δ適応")
ENDIF



@固有通常攻撃後処理_キャラ_420(全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM ヤチマ隊列
#DIM R・S判定

SIF 全滅フラグ
	RETURN 0

ヤチマ隊列 = キャラ隊列検索(GETCHARA(420))
R・S判定 = バフ番号取得_枠("レコデイション・シンク", ヤチマ隊列)

IF R・S判定 > -1
	CALL アビテンプレート用_表示メッセージ変換処理
	アビテンプレート用_アビ名表示済フラグ = 0
	アビ汎用文字列:実行時メッセージ１ = ヤチマのレクリエイト・プライマル！
	CALL アビ汎用変数文字列リセット

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 50
	CALL アビテンプレート_ダメージ処理_火属性
	CALL アビテンプレート_ダメージ処理_水属性
	CALL アビテンプレート_ダメージ処理_土属性
	CALL アビテンプレート_ダメージ処理_風属性
	CALL アビテンプレート_ダメージ処理_光属性
	CALL アビテンプレート_ダメージ処理_闇属性
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 防御力_個人枠
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:効果割合 = 25
	アビ汎用変数:持続ターン = 2
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF



@探索欄表示_キャラ_420(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号
#DIM α適応判定
#DIM δ適応判定
#DIM R・S判定

α適応判定 = バフ番号取得_枠("α適応", 隊列番号)
δ適応判定 = バフ番号取得_枠("δ適応", 隊列番号)
R・S判定 = バフ番号取得_枠("レコデイション・シンク", 隊列番号)

IF α適応判定 > -1 && R・S判定 < 0
	GSETBRUSH レイヤー番号, 0xFFF72C2C
	GDRAWTEXT レイヤー番号, @"⁅α⁆{DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("α適応", 隊列番号), "効果量_固定値")}", 120, 90
ENDIF

IF δ適応判定 > -1 && R・S判定 < 0
	GSETBRUSH レイヤー番号, 0xFF2C2CF7
	GDRAWTEXT レイヤー番号, @"⁅δ⁆{DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("δ適応", 隊列番号), "効果量_固定値")}", 180, 90
ENDIF

IF R・S判定 > -1
	GSETBRUSH レイヤー番号, 0xFFFB2372
	GDRAWTEXT レイヤー番号, @"⁅⛜⁆", 120, 90
ENDIF



@バフ・デバフ特殊表示_ディグレイド・スピネル(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ディグレイド・スピネル
PRINTFORML Lvに応じて防御力が減少・被ダメージが上昇した状態［最大Lv5/回復不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



@バフ・デバフ特殊表示_α適応(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML α適応
PRINTFORML Lvに応じてクリティカル確率が上昇・通常攻撃時に追撃を行う状態［最大Lv4/消去不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



@バフ・デバフ特殊表示_δ適応(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML δ適応
PRINTFORML Lvに応じて被ダメージを軽減・弱体耐性が上昇した状態［最大Lv4/消去不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



@バフ・デバフ特殊表示_レコデイション・シンク(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML レコデイション・シンク
PRINTFORML 攻撃力・防御力が上昇/必ずトリプルアタック/α適応・δ適応効果を味方全体に適用する状態［消去不可］
PRINTFORML 持続ターン：永続



;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_420(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『幾百年の時を共に』 □------------------------------------------------------------
KSTR:(K++) = 　自身が15回攻撃を与える毎に自身のα適応Lvが1上昇［最大Lv4/永続/消去不可］
KSTR:(K++) = 　自身が敵の攻撃のターゲットになる毎に自身のδ適応Lvが1上昇［最大Lv4/永続/消去不可］
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『レコデイション・シンク』 □------------------------------------------------------
KSTR:(K++) = 　α適応Lv4かつδ適応Lv4のターン終了時、自身にレコデイション・シンク効果［永続/消去不可］
KSTR:(K++) = 　キネシス・アクティベートのクールタイムをリセット/次のキネシス・アクティベートがMPを消費しない
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『レクリエイト・プライマル』 □----------------------------------------------------
;属性補正弄るのは嫌な予感がするので一旦保留
;KSTR:(K++) = 　敵の属性を無視して弱点を突く
KSTR:(K++) = 　レコイデイション・シンク効果中、通常攻撃後に各属性［攻撃力✕50％］ダメージ/防御力［25％］ダウン効果［2ターン］
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)

