;アビリティ１：サポートはお任せください：味方全体の会心ダメージ率を9.1%増加
;アビリティ２：偵察データを共有します：味方1人に対して、会心値を27.2%増加。さらに会心ダメージ率を50.5%増加
;アビリティ３：完璧なエージェント：味方全体の会心率を11.3%増加、会心ダメージ率を32.8%増加/ さらに味方1人当たり、自身の「警護成功」を1個追加（最大15個まで）
;サポートアビリティ１：補給プランＢ：毎ターン開始時、味方1人に対して治癒力の129%分の回復
;サポートアビリティ２：作戦の成果：「警護成功」1個毎に、自身の攻撃力を3%増加

;----------------------------------------------------------------------------------
;アビリティ１処理
;----------------------------------------------------------------------------------	
@固有アビ_1906_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果ターン

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = サポートはお任せください+
	消費ＭＰ = 40
	効果数値 = 17
	効果ターン = 4
ELSE
	アビ名 = サポートはお任せください
	消費ＭＰ = 40
	効果数値 = 9
	効果ターン = 3
ENDIF

アビ汎用文字列:実行時メッセージ１ = 「私の合図で突撃してください。いいですね？」

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>{効果ターン}ターンの間、味方全体のクリティカルダメージを［{効果数値}％/独自枠］増加させる。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = ＣＴ倍率_サポートはお任せ
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ２処理
;----------------------------------------------------------------------------------	
@固有アビ_1906_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
#DIM 効果数値１
#DIM 効果数値２

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 偵察データを共有します+
	消費ＭＰ = 60
	効果数値１ = 39
	効果数値２ = 73
	効果ターン = 4
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 偵察データを共有します
	消費ＭＰ = 60
	効果数値１ = 27
	効果数値２ = 50
	効果ターン = 3
ENDIF

SELECTCASE RAND:3
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「後はお任せしますね」
CASE 1
	アビ汎用文字列:実行時メッセージ１ = 「ふふふ……準備は万全ですから」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「分析完了。弱点は把握済みです」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>{効果ターン}ターンの間、味方単体のクリティカル率を［{効果数値１}％/独自枠］、クリティカルダメージを［{効果数値２}％/独自枠］増加させる。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用文字列:バフ・デバフ枠 = ＣＴ率_偵察データ
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = ＣＴ倍率_偵察データ
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ３処理
;----------------------------------------------------------------------------------	
@固有アビ_1906_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
#DIM 効果数値１
#DIM 効果数値２
#DIM 味方番号
#DIM 対象数

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 完璧なエージェント+
	消費ＭＰ = 80
	効果数値１ = 19
	効果数値２ = 57
	効果ターン = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 完璧なエージェント
	消費ＭＰ = 80
	効果数値１ = 11
	効果数値２ = 32
	効果ターン = 4
ENDIF

SELECTCASE RAND:3
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「包囲網に隙などありません」
CASE 1
	アビ汎用文字列:実行時メッセージ１ = 「皆さん、確実に対処していきますよ」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「風紀委員会から逃れられると思っていたんですか？」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>{効果ターン}ターンの間、味方全体のクリティカル率を［{効果数値１}％/独自枠］、クリティカルダメージを［{効果数値２}％/独自枠］増加させる。
		詳細文文字列受け渡し変数 += @"<br>更にバフを掛けた味方１人当たり、自身に「警護成功」を１個追加（最大１５個まで）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;アビリティ発動時に生存している味方の数をカウント
		対象数 = 0
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
				CONTINUE
			対象数 ++
		NEXT
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用文字列:バフ・デバフ枠 = ＣＴ率_完璧
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = ＣＴ倍率_完璧
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 警護成功
		アビ汎用変数:累積上限_固定値 = 15
		CALL アビテンプレート_刻印追加(対象数)

ENDSELECT

;----------------------------------------------------------------------------------
;サポアビ処理
;----------------------------------------------------------------------------------
;サポアビ１『補給プランＢ』処理
@固有行動開始時処理_キャラ_1906
#DIM アコ番号
#DIM 隊列番号
#DIM 味方番号
#DIM ＭＡＸ数値
#DIM ＤＭＧキャラ

アコ番号 = GETCHARA(1906)
隊列番号 = キャラ隊列検索(GETCHARA(1906))

アビ汎用文字列:実行時メッセージ１ = 「風紀委員会からの支給品です」
アビテンプレート用_アビ名 = 補給プランＢ

ＭＡＸ数値 = 0

FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:ＨＰ < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:最大ＨＰ - BATTLE_STATE:味方番号:ＨＰ == 0
		;ＨＰが減っていないキャラの場合はその時点でループ終了
		CONTINUE

	;一番ダメージを受けているキャラクターを指定
	IF BATTLE_STATE:味方番号:最大ＨＰ - BATTLE_STATE:味方番号:ＨＰ > ＭＡＸ数値
		ＭＡＸ数値 = BATTLE_STATE:味方番号:最大ＨＰ - BATTLE_STATE:味方番号:ＨＰ
		ＤＭＧキャラ = 味方番号
	ENDIF
NEXT
IF 陥落チェック(アコ番号)
	;最もＨＰの少ないキャラクターを対象として回復。
	CALL アビ対象選択テンプレート_指定(ＤＭＧキャラ)
	アビ汎用変数:効果割合 = 245
	CALL アビテンプレート_回復処理_MAXでも回復
ELSE
	;最もＨＰの少ないキャラクターを対象として回復。
	CALL アビ対象選択テンプレート_指定(ＤＭＧキャラ)
	アビ汎用変数:効果割合 = 129
	CALL アビテンプレート_回復処理_MAXでも回復
ENDIF

;----------------------------------------------------------------------------------
;『警護成功』戦闘画面上表示処理
@探索欄表示_キャラ_1906(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "警護成功:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"◎{刻印現在数確認("警護成功", 隊列番号)}", 200, 90

;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1906(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『補給プランＢ』  □-----------------------------------------------------------------
IF 陥落チェック(対象キャラ)
	KSTR:(K++) = 　天雨アコの行動開始時、その時点で最もＨＰの少ない味方１人に対して［回復力✕２４５％］の回復
	KSTR:(K++) = 　
ELSE
	KSTR:(K++) = 　天雨アコの行動開始時、その時点で最もＨＰの少ない味方１人に対して［回復力✕１２９％］の回復
	KSTR:(K++) = 　
ENDIF

;未実装サポアビ
;IF 基礎BATTLE_STATE:対象キャラ:Lv >= 20
;	KSTR:(K++) = □ 特性：『作戦の成果』 □-----------------------------------------------------------------
;	IF 陥落チェック(対象キャラ)
;		KSTR:(K++) = 　「警護成功」1個毎に、自身の攻撃力を５％増加
;		KSTR:(K++) = 　
;	ELSE
;		KSTR:(K++) = 　「警護成功」1個毎に、自身の攻撃力を３％増加
;		KSTR:(K++) = 　
;	ENDIF
;ENDIF

CALL 口上変数表示(対象キャラ, 1)
