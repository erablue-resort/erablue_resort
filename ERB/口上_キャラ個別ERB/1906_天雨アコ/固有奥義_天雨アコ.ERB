;奥義：ターゲットは逃がしません！：敵1人の防御力を18.5%減少。その後ダメージ。自身の「警護成功」数によって与えるダメージが変更。

@固有奥義_1906(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値
#DIM 成功率
#DIM 効果ターン
#DIM 隊列番号
#DIM 警護成功数

隊列番号 = キャラ隊列検索(GETCHARA(1906))
IF 隊列番号 > -1
	警護成功数 = 刻印現在数確認("警護成功", 隊列番号)
ELSE
	警護成功数 = 0
ENDIF

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	奥義名 = ターゲットは逃がしません！+
	効果数値 = 24
	成功率 = 1000
	効果ターン = 4
	IF 警護成功数 >= 12
		;12個以上の場合
		奥義威力 = 1212
	ELSEIF 警護成功数 >= 8
		;8個以上の場合
		奥義威力 = 1111
	ELSEIF 警護成功数 >= 4
		;4個以上の場合
		奥義威力 = 1010
	ELSE
		;3個以下の場合
		奥義威力 = 942
	ENDIF
ELSE
	奥義名 = ターゲットは逃がしません！
	効果数値 = 18
	成功率 = 1000
	効果ターン = 3
	IF 警護成功数 >= 12
		;12個以上の場合
		奥義威力 = 638
	ELSEIF 警護成功数 >= 8
		;8個以上の場合
		奥義威力 = 584
	ELSEIF 警護成功数 >= 4
		;4個以上の場合
		奥義威力 = 531
	ELSE
		;3個以下の場合
		奥義威力 = 496
	ENDIF
ENDIF

アビ汎用文字列:実行時メッセージ１ = 「私の目から逃れることは出来ませんよ……！」

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>
		詳細文文字列受け渡し変数 += "敵単体の防御力を減少させた後に奥義ダメージ。威力は自身の「警護成功」数によって変化する。"
		RETURN 奥義威力
	CASE "奥義説明文_詳細"
		詳細文文字列受け渡し変数 += @"敵単体の防御力を［{効果数値}％／独自枠］減少させた後、奥義ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>奥義ダメージの威力は自身が保有する「警護成功」数によって上昇する。"
		詳細文文字列受け渡し変数 += @"<br>※「警護成功」は消費されない。"
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる

		アビ汎用文字列:バフ・デバフ枠 = 防御力_ターゲット
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 成功率
		CALL アビテンプレート_不利効果_デバフ効果セット

		;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
		;必要な場合を除きここでリセット
		CALL アビ汎用変数文字列リセット
ENDSELECT
