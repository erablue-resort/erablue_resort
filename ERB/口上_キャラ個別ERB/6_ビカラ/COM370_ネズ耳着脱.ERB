
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_6
#FUNCTION
LOCAL = GETCHARA(6)
IF CSTR:LOCAL:スタイル管理文字列 == "[北の守護神]ビカラ（陰）"
	TSTR:コマンド名受渡 = ネズ耳を付ける
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	TSTR:コマンド名受渡 = ネズ耳をはずす
ENDIF
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_370_6
LOCALS = <br>■ネズ耳をはずす<br>
LOCALS += "ネズ耳を外して陰ビカラに変化させるコマンド。<br>ネズ耳が外れている時、[ネズ耳を付ける]に変化する。<br>複数の素質と名前、グラフィックが変化する。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_6
LOCAL = GETCHARA(6)
IF CSTR:LOCAL:スタイル管理文字列 == "[北の守護神]ビカラ（陰）"
	CSTR:LOCAL:スタイル管理文字列 = 
	CALL スタイル変更_6(LOCAL, "変更後処理")
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	CSTR:LOCAL:スタイル管理文字列 = [北の守護神]ビカラ（陰）
	CALL スタイル変更_6(LOCAL, "変更後処理")
ENDIF

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_6
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
LOCAL = GETCHARA(6)
SIF TARGET != LOCAL
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_6
LOCAL = GETCHARA(6)
IF CSTR:LOCAL:スタイル管理文字列 == "[北の守護神]ビカラ（陰）"
	PRINTFORML %CALLNAME:PLAYER%はビカラのネズ耳を外した。
	PRINTFORML 
	PRINTFORML 「%TEXTR("はわわわ……どうして外すんですか/あわわわ……付け耳が、外さないでくださ～い/どうして取っちゃうんですかぁ……意地悪しないで～")%……」
	PRINTFORML 
	PRINTFORMW %CALLNAME:TARGET%の髪が瞬く間に黒く染まっていく……
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	PRINTFORML %CALLNAME:PLAYER%はビカラにネズ耳を返した。
	PRINTFORML 
	PRINTFORML 「%TEXTR("ハハッ、ぼくはビッキィ！/盛り上がってきたねぇ♪　今日も元気にラッキークッキービッキィ♪/十二神将ビッキィ♪　華麗に参上っ♪")%」
	PRINTFORML 
	PRINTFORMW %CALLNAME:TARGET%の髪が瞬く間に白く染まっていく……
ENDIF



@スタイル変更_6(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード
;この関数はいくつ別スタイル作ってもキャラ毎に一個

SELECTCASE 動作モード
	CASE "スタイル名取得"
		VARSET RESULTS
		;RESULTS:Xに肩書を、RESULTS:X+10にリミテッドや水着など通称(？)を入れる
		RESULTS:0 = %NAME:対象キャラ%
		RESULTS:10 = 陽

		RESULTS:1 = [北の守護神]ビカラ（陰）
		RESULTS:11 = 陰

	CASE @"スタイル解説_%NAME:対象キャラ%"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += @"■%NAME:対象キャラ%<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "ネズ耳を付けたビッキィスタイル。<br>"
		TSTR:スタイル解説 += "明るく楽しく、今日も元気にラッキークッキービッキィ♪<br>"
		TSTR:スタイル解説 += "ネズ耳切り替えコマンド・ボタンでも切り替えが可能。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
	CASE "スタイル解説_[北の守護神]ビカラ（陰）"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += "■[北の守護神]ビカラ（陰）<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "ネズ耳を外した素のビカラスタイル。<br>"
		TSTR:スタイル解説 += "あ……ダメダメネズミですみません……。<br>"
		TSTR:スタイル解説 += "ネズ耳切り替えコマンド・ボタンでも切り替えが可能。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[北の守護神]ビカラ（陰）")
	CASE "変更後処理"
		;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
		;一人称や二人称、素質の変化などを想定
		SELECTCASE CSTR:対象キャラ:スタイル管理文字列
			CASE ""
				TALENT:対象キャラ:陽気／陰気 = 1
				TALENT:対象キャラ:目立ちたがり = 1
				TALENT:対象キャラ:度胸 = 0
				TALENT:対象キャラ:戦闘好き = 0
				TALENT:対象キャラ:会話資質 = 1
				TALENT:対象キャラ:羞恥心 = -1
				TALENT:対象キャラ:自慰しやすい = 0
				TALENT:対象キャラ:楽観的 = 1
				CALLNAME:対象キャラ = ビカラ
				CSTR:対象キャラ:フォルダ判定キー '= CALLNAME:対象キャラ
				CSTR:対象キャラ:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(対象キャラ)
			CASE "[北の守護神]ビカラ（陰）"
				TALENT:対象キャラ:陽気／陰気 = -1
				TALENT:対象キャラ:目立ちたがり = 0
				TALENT:対象キャラ:度胸 = -1
				TALENT:対象キャラ:戦闘好き = -1
				TALENT:対象キャラ:羞恥心 = 1
				TALENT:対象キャラ:自慰しやすい = 1
				TALENT:対象キャラ:会話資質 = -1
				TALENT:対象キャラ:楽観的 = -1
				CALLNAME:対象キャラ = ビカラ（陰）
				CSTR:対象キャラ:フォルダ判定キー '= CALLNAME:対象キャラ
				CSTR:対象キャラ:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(対象キャラ)
			;CASE RESULTS:2
			;CASE RESULTS:3
		ENDSELECT
ENDSELECT