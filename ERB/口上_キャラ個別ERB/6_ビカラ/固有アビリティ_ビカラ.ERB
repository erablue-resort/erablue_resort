
@固有アビ_6_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = グリーティング・ドーマウス+
	消費ＭＰ = 100
ELSE
	アビ名 = グリーティング・ドーマウス
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕100％］の闇属性ダメージを5回与える。
		詳細文文字列受け渡し変数 += @"<br>ドーちゃんをけしかける。"
		詳細文文字列受け渡し変数 += @"<br>◆敵が大技発動時、トラップが起動"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕100％］の闇属性ダメージを5回与える。
		詳細文文字列受け渡し変数 += @"<br>（独自枠・1回・消去不可）のドーちゃん効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（ドーちゃん："
		詳細文文字列受け渡し変数 += @"<br>　効果を受けた敵が大技を発動した時、敵全体に［999］の無属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>　［100％］（独自枠・3ターン）の麻痺効果を与える。成功率120％）"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 5
			CALL アビテンプレート_ダメージ処理_闇属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ドーちゃん
		アビ汎用文字列:バフ・デバフ対象能力 = ドーちゃん
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT



@固有アビ_6_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号
#DIM バフ行数
#DIM テンション判定
#DIM テンション割合

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	IF 展開中フィールド名格納 == "ゆ～とぴあ"
		アビ名 = エキセントリックパレード
	ELSE
		アビ名 = エンチャンテッド・ドリーム++
	ENDIF
	消費ＭＰ = 175
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = エンチャンテッド・ドリーム+
	消費ＭＰ = 175
ELSE
	アビ名 = エンチャンテッド・ドリーム
	消費ＭＰ = 200
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 展開中フィールド名格納 == "ゆ～とぴあ" && 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［20％］（Ｃ枠・1回）の追撃効果を与える。
			詳細文文字列受け渡し変数 += @"<br>対象に被ダメージ［220］減少効果［1ターン］"
			詳細文文字列受け渡し変数 += @"<br>対象のHPを［220］回復"
			詳細文文字列受け渡し変数 += @"<br>［220］（通常枠・1ターン）の被ダメージ減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［220］のＨＰ回復を与える。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>さぁ、夢とキラキラのゆ～とぴあへ！
			IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
				詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
				詳細文文字列受け渡し変数 += @"<br>このアビリティはゆ～とぴあ展開中、『エキセントリックパレード』に変化する。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 展開中フィールド名格納 == "ゆ～とぴあ" && 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			;パレードは詳細なし
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>（5ターン）のゆ～とぴあフィールドを展開する。
			詳細文文字列受け渡し変数 += "<br>＞自身に［1000％］のTA率上昇効果を与える。"
			詳細文文字列受け渡し変数 += "<br>＞［300％］の敵対心上昇効果を与える。"
			詳細文文字列受け渡し変数 += "<br>＞［50％］の回避率上昇効果を与える。"
			詳細文文字列受け渡し変数 += "<br>味方全体に［4］（通常枠・1ターン）のテンション効果を与える。"
			IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
				詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
				詳細文文字列受け渡し変数 += @"<br>このアビリティはゆ～とぴあ展開中、『エキセントリックパレード』に変化する。"
			ENDIF
		ENDIF
	CASE "アビ効果"
		IF 展開中フィールド名格納 == "ゆ～とぴあ" && 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 追撃C
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果量 = 220
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 220
			CALL アビテンプレート_回復処理_MAXでも回復
		ELSE
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:フィールド枠 = ゆ～とぴあ
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_フィールド効果セット
			
			CALL アビ対象選択テンプレート_自己のみ

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 子神宮の主_絶対TA
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:効果量 = 1000
			CALL アビテンプレート_有利効果_付随バフ効果セット("ゆ～とぴあ")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 子神宮の主_敵対心
			アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
			アビ汎用変数:効果量 = 300
			CALL アビテンプレート_有利効果_付随バフ効果セット("ゆ～とぴあ")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 子神宮の主_回避率
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 50
			CALL アビテンプレート_有利効果_付随バフ効果セット("ゆ～とぴあ")

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 4
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_テンション

			; FOR 対象番号, 0, 4
			; 	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
			; 		CONTINUE
			; 	SIF BATTLE_STATE:対象番号:ＨＰ < 1
			; 		CONTINUE
				
			; 	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
				
			; 	IF バフ行数 < 0
			; 		テンション判定 = RAND:100 + 1
			; 		IF テンション判定 < 6
			; 			テンション割合 = 100
			; 		ELSEIF テンション判定 < 21
			; 			テンション割合 = 60
			; 		ELSEIF テンション判定 < 51
			; 			テンション割合 = 30
			; 		ELSE
			; 			テンション割合 = 10
			; 		ENDIF
					
			; 		CALL アビ汎用変数文字列リセット
			; 		CALL アビ対象選択テンプレート_指定(対象番号)
			; 		アビ汎用文字列:バフ・デバフ枠 = テンション
			; 		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			; 		アビ汎用変数:効果割合 = テンション割合
			; 		アビ汎用変数:持続ターン = 1
			; 		アビ汎用変数:特殊表示オプション = 1
			; 		CALL アビテンプレート_有利効果_バフ効果セット
			; 	ELSE
			; 		テンション割合 = DT_CELL_GET("戦闘効果データベース", バフ行数, "効果量_割合")
					
			; 		IF テンション割合 == 60
			; 			テンション割合 = 100
			; 		ELSEIF テンション割合 == 30
			; 			テンション割合 = 60
			; 		ELSEIF テンション割合 == 10
			; 			テンション割合 = 30
			; 		ENDIF
					
			; 		DT_CELL_SET "戦闘効果データベース", バフ行数, "効果量_割合", テンション割合
			; 	ENDIF
			; NEXT
			
			IF !陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = ゆ～とぴあ_攻撃力
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果割合 = 20
				アビ汎用文字列:適用範囲 = 敵全体
				アビ汎用文字列:付随先_隊列設定 = 戦闘行動キャラ
				CALL アビテンプレート_有利効果_付随バフ効果セット("ゆ～とぴあ")
			ENDIF

			SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
				アビテンプレート用_行動消費無しフラグ = 1
		ENDIF
ENDSELECT



@固有アビ_6_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 一子之夢
	消費ＭＰ = 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（独自枠・2被ダメ・消去不可）の一子之夢効果を与える。
		詳細文文字列受け渡し変数 += "<br>＞［50％］のゲージ上昇効率上昇効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［10％］の奥義ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［30％］の回避率上昇効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 一子之夢
		アビ汎用文字列:バフ・デバフ対象能力 = 一子之夢
		アビ汎用変数:持続被ダメ回数 = 2
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 一子之夢_ゲージ上昇効率
		アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("一子之夢")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 一子之夢_奥義与ダメ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:効果割合 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("一子之夢")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 一子之夢_回避率
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 30
		CALL アビテンプレート_有利効果_付随バフ効果セット("一子之夢")
ENDSELECT



@固有アビ_6_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 生存キャラ数
#DIM 対象番号
#DIM バフ行数
#DIM テンション判定

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 陽鼠虹城
	消費ＭＰ = 300
ENDIF

生存キャラ数 = 0
テンション判定 = 0

; FOR 対象番号, 0, 4
; 	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
; 		CONTINUE
; 	SIF BATTLE_STATE:対象番号:ＨＰ < 1
; 		CONTINUE
	
; 	生存キャラ数 += 1
; 	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
	
; 	IF DT_CELL_GET("戦闘効果データベース", バフ行数, "効果量_割合") == 100
; 		テンション判定 += 1
; 	ENDIF
; NEXT

バフ行数 = バフ番号取得_枠("テンション", 対象番号)
IF DT_CELL_GET("戦闘効果データベース", バフ行数, "効果量_固定値") == 4
	アビテンプレート用_アビ封印用フラグ = 0
ELSE
	アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［1000％］（独自枠・1回）の回避率上昇効果を与える。
		詳細文文字列受け渡し変数 += @"<br>（1回）の再行動効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>◆味方全体のテンションLvが4の時にのみ発動可能"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 完全回避
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有ターン終了時処理_キャラ_6
#DIM 対象番号
#DIM トラップ起動
#DIM ドーちゃん

FOR LOCAL, 10, 20
	IF 行動種別フラグ:LOCAL:大技発動 && デバフ番号取得_枠("ドーちゃん", LOCAL) > -1
		トラップ起動 = 1
		対象番号 = LOCAL
		ドーちゃん = デバフ番号取得_枠("ドーちゃん", LOCAL)
		BREAK
	ENDIF
NEXT

IF トラップ起動
	アビテンプレート用_アビ名 = ドーちゃん
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果量 = 999
	CALL アビテンプレート_ダメージ処理_無属性
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 捕獲
	アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
	アビ汎用変数:効果割合 = 100
	アビ汎用変数:基礎成功確率 = 120
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	CALL 戦闘効果削除(ドーちゃん)
ENDIF



@固有回避時処理_キャラ_6(全滅フラグ)
#DIM 全滅フラグ
#DIM バフ行数
#DIM ビカラ番号
#DIM ビカラ隊列
#DIM 対象番号
#DIM 効果ターン

SIF 全滅フラグ
	RETURN 0

ビカラ番号 = GETCHARA(6)
ビカラ隊列 = キャラ隊列検索(ビカラ番号)

IF CSTR:ビカラ番号:スタイル管理文字列 == ""
	KSTR:(K++) = 「いぇいっ！」
ELSE
	KSTR:(K++) = 「ひぃ…！」
ENDIF
WSTR:(K++) = ビカラのビート・ザ・マウス！

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵全体_生者のみ
アビ汎用変数:効果割合 = 500
CALL アビテンプレート_ダメージ処理_闇属性
; SIF DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("テンション", ビカラ隊列), "効果量_割合")
; 	CALL アビテンプレート_ダメージ処理_闇属性

CALL アビ汎用変数文字列リセット
CALL アビテンプレート_不利効果_バフ消去
; SIF DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("テンション", ビカラ隊列), "効果量_割合")
; 	CALL アビテンプレート_不利効果_バフ消去

IF 陥落チェック(ビカラ番号) && 基礎BATTLE_STATE:ビカラ番号:Lv >= 70
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果量 = 1
	CALL アビテンプレート_有利効果_バフ効果延長("テンション")
	; FOR 対象番号, 0, 4
	; 	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
	; 		CONTINUE
	; 	SIF BATTLE_STATE:対象番号:ＨＰ < 1
	; 		CONTINUE
		
	; 	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
		
	; 	IF バフ行数 > -1
	; 		効果ターン = DT_CELL_GET("戦闘効果データベース", バフ行数, "持続ターン")
	; 		DT_CELL_SET "戦闘効果データベース", バフ行数, "持続ターン", 効果ターン + 1
	; 		SIF DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("テンション", ビカラ隊列), "効果量_割合")
	; 			DT_CELL_SET "戦闘効果データベース", バフ行数, "持続ターン", 効果ターン + 1
	; 	ENDIF
	; NEXT
ENDIF



@固有ターン開始時処理_フィールド_ゆ～とぴあ
#DIM バフ行数
#DIM 隊列番号
#DIM ビカラ番号
#DIM ビカラ隊列
#DIM 対象番号
#DIM テンション判定
#DIM テンション割合

ビカラ番号 = GETCHARA(6)

CALL アビ対象選択テンプレート_味方全体_生者のみ
CALL アビ汎用変数文字列リセット
アビ汎用変数:効果量 = 4
アビ汎用変数:持続ターン = 1
CALL アビテンプレート_有利効果_テンション

; FOR 対象番号, 0, 4
; 	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
; 		CONTINUE
; 	SIF BATTLE_STATE:対象番号:ＨＰ < 1
; 		CONTINUE
	
; 	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
	
; 	IF バフ行数 < 0
; 		テンション判定 = RAND:100 + 1
; 		IF テンション判定 < 6
; 			テンション割合 = 100
; 		ELSEIF テンション判定 < 21
; 			テンション割合 = 60
; 		ELSEIF テンション判定 < 51
; 			テンション割合 = 30
; 		ELSE
; 			テンション割合 = 10
; 		ENDIF
		
; 		CALL アビ汎用変数文字列リセット
; 		CALL アビ対象選択テンプレート_指定(対象番号)
; 		アビ汎用文字列:バフ・デバフ枠 = テンション
; 		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
; 		アビ汎用変数:効果割合 = テンション割合
; 		アビ汎用変数:持続ターン = 1
; 		アビ汎用変数:特殊表示オプション = 1
; 		CALL アビテンプレート_有利効果_バフ効果セット
; 	ELSE
; 		テンション割合 = DT_CELL_GET("戦闘効果データベース", バフ行数, "効果量_割合")
		
; 		IF テンション割合 == 60
; 			テンション割合 = 100
; 		ELSEIF テンション割合 == 30
; 			テンション割合 = 60
; 		ELSEIF テンション割合 == 10
; 			テンション割合 = 30
; 		ENDIF
		
; 		DT_CELL_SET "戦闘効果データベース", バフ行数, "効果量_割合", テンション割合
; 	ENDIF
; NEXT


; @バフ・デバフ特殊表示_テンション(隊列番号, バフ・デバフ行数)
; #DIM 隊列番号
; #DIM バフ・デバフ行数
; #DIM テンションLv
; VARSET LOCALS

; IF DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合") == 100
; 	テンションLv = 4
; ELSEIF DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合") == 60
; 	テンションLv = 3
; ELSEIF DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合") == 30
; 	テンションLv = 2
; ELSEIF DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合") == 10
; 	テンションLv = 1
; ENDIF

; PRINTFORML テンション
; PRINTFORML Lvに応じて攻撃力が上昇する状態
; PRINTFORML Lv：{テンションLv}　　　　　持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_ドーちゃん(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ドーちゃん
PRINTFORML 大技発動時、無属性ダメージ/捕獲効果を受ける状態［消去不可］
PRINTFORML 持続回数：1



@バフ・デバフ特殊表示_捕獲(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 捕獲
PRINTFORML 攻撃行動を封じられた状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_一子之夢(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 一子之夢
PRINTFORML 奥義ゲージ上昇効率・奥義与ダメージ・回避率が上昇した状態［消去不可］
PRINTFORML 持続回数：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続被ダメ回数")}



@フィールド説明文_ゆ～とぴあ
#DIM ビカラ番号
#DIM 配列番号

ビカラ番号 = GETCHARA(6)

LOCALS = ゆ～とぴあ<br>
IF 陥落チェック(ビカラ番号) && 基礎BATTLE_STATE:ビカラ番号:Lv >= 75
	LOCALS += "ターン開始時、味方全体にテンション効果<br>"
ELSE
	LOCALS += "ターン開始時、味方全体にテンション効果/敵全体の攻撃力が上昇<br>"
ENDIF

配列番号 = バフ・デバフ効果検索("フィールド", "枠", "ゆ～とぴあ", -1)
LOCALS += @"効果ターン：{DT_CELL_GET("戦闘効果データベース", 配列番号, "持続ターン")}ターン<br>"

詳細文文字列受け渡し変数 = %LOCALS%

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_6(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『子神宮の主』 □---------------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　ゆ～とぴあ展開中、自身が必ずTA/敵対心・回避率が上昇/奥義発動時、味方全体にテンション効果・ディスペルガード効果
ELSE
	KSTR:(K++) = 　ゆ～とぴあ展開中、自身が必ずTA/敵対心・回避率が上昇/奥義発動時、味方全体にテンション効果
ENDIF
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『ビート・ザ・マウス』 □------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　攻撃回避時、敵全体に闇属性ダメージ/強化効果を消去/味方全体のテンション効果を延長
	KSTR:(K++) = 　◆テンションLv4の時、2回発動
ELSE
	KSTR:(K++) = 　攻撃回避時、敵全体に闇属性ダメージ/強化効果を消去
ENDIF

CALL 口上変数表示(対象キャラ, 1)



