@口上変数初期化
VARSET KSTR
VARSET WSTR
VARSET NSTR
VARSET WNSTR
K = 0
VARSET K_CALL
VARSET KCOLOR,-1

VARSET 切り替え顔グラリソース
VARSET 切り替え名前
TSTR:メッセージ欄文字列受け渡し_下側 = %メッセージ欄スクロール表示(, , 0)%</div>
メッセージ欄追加数カウント = 0

;KSTR:(K++) = 
;WSTR:(K++) = 
;NSTR:(K++) = 

;K_CALL:K = 
;KCOLOR:K = 


@口上変数表示,ARG = -1,ARG:1 = 0
;非表示フラグがある時は何もしない
SIF RFLAG:口上非表示フラグ
	RETURN 0
;SKIPDISP中もなんもしない
SIF ISSKIP()
	RETURN 0
SIF !口上有無確認(0,1)
	RETURN 0
SIF ARG == -1
	ARG = TARGET
FOR LOCAL,0,1000
	IF !(K_CALL:LOCAL == "")
		CALLFORM %K_CALL:LOCAL%
		SIF LOCAL > 0
			CALL WSTR書き込み,LOCAL - 1
	ENDIF
	SIF !口上有無確認(LOCAL,1)
		BREAK
	SIF KCOLOR:LOCAL > -1
		SETCOLOR KCOLOR:LOCAL
	IF !(WSTR:LOCAL == "")
		CALL PRINT_STRW(WSTR:LOCAL,ARG)
	ELSEIF !(KSTR:LOCAL == "")
		CALL PRINT_STRL(KSTR:LOCAL,ARG)
	ELSEIF !(NSTR:LOCAL == "")
		CALL PRINT_STR(NSTR:LOCAL,ARG)
	ELSEIF !(WNSTR:LOCAL == "")
		CALL PRINT_STRWN(WNSTR:LOCAL,ARG)
	ENDIF
NEXT
RESETCOLOR
SIF WSTR:(LOCAL - 2) == "" && ARG:1 == 0
	WAIT
RETURN 1

@WSTR書き込み,ARG
IF !(WSTR:ARG == "")
ELSEIF !(KSTR:ARG == "")
	WSTR:ARG = %KSTR:ARG%
ELSEIF !(NSTR:ARG == "")
	WSTR:ARG = %NSTR:ARG%
ENDIF

@口上有無確認,ARG,ARG:1 = 0
#FUNCTION
;ARG:1　0だとKを口上のない番号に移動
IF ARG:1 == 0
	FOR LOCAL,0,1000
		SIF !(WSTR:LOCAL == "")
			CONTINUE
		SIF !(KSTR:LOCAL == "")
			CONTINUE
		SIF !(NSTR:LOCAL == "")
			CONTINUE
		SIF !(WNSTR:LOCAL == "")
			CONTINUE
		K = LOCAL
		BREAK
	NEXT
ENDIF
SIF !(WSTR:ARG == "")
	RETURNF 1
SIF !(KSTR:ARG == "")
	RETURNF 1
SIF !(NSTR:ARG == "")
	RETURNF 1
SIF !(WNSTR:ARG == "")
	RETURNF 1
RETURNF 0


@コメント文頭処理(対象キャラ = -1, 処理モード = 0)
#DIM 対象キャラ
#DIM 処理モード
#DIM 文字数カウント
#DIMS 一時保存文字列
#DIM 一時カウント
#DIM 区切り位置, 2

SIF 対象キャラ == -1
	対象キャラ = TARGET
LOCAL:2 = 1
LOCALS '= "│"
SIF 処理モード == 1
	LOCALS '= " "
SIF 処理モード == 2
	LOCALS '= "　　　　　　　"

;IF 処理モード == 2 && GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
;	LOCALS '= "　　　　　　　"
;ELSEIF 処理モード == 2
;	RETURN 0
;ENDIF
文字数カウント = 0
FOR LOCAL, 0, 1000
	IF !(NSTR:LOCAL == "")
		一時カウント = 文字数カウント + STRLENS(NSTR:LOCAL)
		IF 文字数カウント + STRLENS(NSTR:LOCAL) > 66
			FOR LOCAL:1, 0, (文字数カウント + STRLENS(NSTR:LOCAL)) / 66
				区切り位置 = (LOCAL:1 + 1) * 66 + (LOCAL:1 * (15 + STRLENS(LOCALS))) - 文字数カウント
				NSTR:LOCAL '= SUBSTRING(NSTR:LOCAL, 0, 区切り位置) + "_改行_　　" + LOCALS + " 　　" + SUBSTRING(NSTR:LOCAL, 区切り位置, -1)
			NEXT
			文字数カウント = 一時カウント % 66
		ENDIF
		IF LOCAL:2
			NSTR:LOCAL '= "　　" + LOCALS + " 　　" + NSTR:LOCAL
		ENDIF
		LOCAL:2 = 0
	ELSEIF !(WSTR:LOCAL == "")
		IF 文字数カウント + STRLENS(WSTR:LOCAL) > 66
			FOR LOCAL:1, 0, (文字数カウント + STRLENS(WSTR:LOCAL)) / 66
				区切り位置 = (LOCAL:1 + 1) * 66 + (LOCAL:1 * (15 + STRLENS(LOCALS))) - 文字数カウント
				WSTR:LOCAL '= SUBSTRING(WSTR:LOCAL, 0, 区切り位置) + "_改行_　　" + LOCALS + " 　　" + SUBSTRING(WSTR:LOCAL, 区切り位置, -1)
			NEXT
		ENDIF
		IF LOCAL:2
			WSTR:LOCAL '= "　　" + LOCALS + " 　　" + WSTR:LOCAL
		ENDIF
		文字数カウント = 0
		LOCAL:2 = 1
	ELSEIF !(KSTR:LOCAL == "")
		IF 文字数カウント + STRLENS(KSTR:LOCAL) > 66
			FOR LOCAL:1, 0, (文字数カウント + STRLENS(KSTR:LOCAL)) / 66
				区切り位置 = (LOCAL:1 + 1) * 66 + (LOCAL:1 * (15 + STRLENS(LOCALS))) - 文字数カウント
				KSTR:LOCAL '= SUBSTRING(KSTR:LOCAL, 0, 区切り位置) + "_改行_　　" + LOCALS + " 　　" + SUBSTRING(KSTR:LOCAL, 区切り位置, -1)
			NEXT
		ENDIF
		IF LOCAL:2
			KSTR:LOCAL '= "　　" + LOCALS + " 　　" + KSTR:LOCAL
		ENDIF
		文字数カウント = 0
		LOCAL:2 = 1
	ELSE
		BREAK
	ENDIF
NEXT

@口上_キャラ解説表示(対象キャラ)
#DIM 対象キャラ
SIF !EXISTFUNCTION(@"口上_ステータス画面_キャラ解説_{NO:対象キャラ}")
	RETURN 0

; 口上変数の初期化
CALL 口上変数初期化
; キャラごとの解説取得(口上変数を更新)
CALLFORM 口上_ステータス画面_キャラ解説_{NO:対象キャラ}(対象キャラ)
; キャラ解説表示
CALL キャラ解説表示用(対象キャラ)
; 表示行数を返す
RETURN RESULT

@PRINT_STATEコメント表示(対象キャラ = -1, 処理モード = 0)
#DIM 対象キャラ
#DIM 処理モード
#DIM 縦幅
#DIM LCOUNT
#DIMS 文章保存
#DIMS 文頭文字列
#DIMS 表示文字列
#DIM 表示位置
#DIM 横幅

文頭文字列 '= "│"
横幅 = 3380
SELECTCASE 処理モード
	CASE 0
		表示位置 = 600
	CASE 1
		表示位置 = 600
	CASE 2
		表示位置 = 1200
		横幅 = 4100
ENDSELECT

文頭文字列 '= "　　" + 文頭文字列 + " 　　"

;非表示フラグがある時は何もしない
SIF RFLAG:口上非表示フラグ
	RETURN 0
;SKIPDISP中もなんもしない
SIF ISSKIP()
	RETURN 0
SIF !口上有無確認(0,1)
	RETURN 0
SIF 対象キャラ == -1
	対象キャラ = TARGET
文章保存 = 
FOR LOCAL,0,1000
	SIF !口上有無確認(LOCAL,1)
		BREAK
	IF !(WSTR:LOCAL == "")
		CALL PRINT_STATEコメント表示_実処理(WSTR:LOCAL,対象キャラ)
		RESULTS += "<br>"
	ELSEIF !(KSTR:LOCAL == "")
		CALL PRINT_STATEコメント表示_実処理(KSTR:LOCAL,対象キャラ)
		RESULTS += "<br>"
	ELSEIF !(NSTR:LOCAL == "")
		CALL PRINT_STATEコメント表示_実処理(NSTR:LOCAL,対象キャラ)
	ELSEIF !(WNSTR:LOCAL == "")
		CALL PRINT_STATEコメント表示_実処理(WNSTR:LOCAL,対象キャラ)
		RESULTS += "<br>"
	ENDIF
	文章保存 += RESULTS
NEXT

縦幅 = HTML_STRINGLINES(文章保存, 66)
;文頭表示
表示文字列 = 
IF 処理モード == 0
	表示文字列 += @"<div rect='0,0,600,{縦幅 * 200}'>"
	FOR LCOUNT, 0, 縦幅
		表示文字列 += @"%文頭文字列%<br>"
	NEXT
	表示文字列 += "</div>"
ENDIF
;文字列表示
表示文字列 += @"<div rect='{表示位置},0,{横幅},{縦幅 * 200}'>"
表示文字列 += @"%文章保存%"
表示文字列 += "</div>"

HTML_PRINT 表示文字列, 1
DEBUGPRINTFORML %表示文字列%
FOR LCOUNT, 0, 縦幅
	PRINTL
NEXT

RETURN 1

@PRINT_STATEコメント表示_実処理(処理文章, 対象キャラ)
#DIM LCOUNT
#DIM 返し数
#DIM 対象キャラ
#DIMS 返し文
#DIM 文字色
#DIM 書式
#DIM 改行数
#DIMS 文章保存
#DIMS 処理文章

SIF 対象キャラ == -1
	対象キャラ = TARGET
VARSET LOCALS

;文字列を分解する
LOCAL = 0
LOCAL:1 = 0
文章保存 = 
SPLIT 処理文章, "_", LOCALS
FOR LCOUNT, 0, RESULT
	;特殊な操作
	SELECTCASE LOCALS:LCOUNT
	CASE "CALLNAME:TARGET","調教対象","被調教者"
		文章保存 += CALLNAME:TARGET
		CONTINUE
	CASE "CALLNAME:PLAYER","調教者"
		文章保存 += CALLNAME:PLAYER
		CONTINUE
	CASE "CALLNAME:MASTER","主人","主人公"
		文章保存 += CALLNAME:MASTER
		CONTINUE
	CASE "一人称","1st"
		文章保存 += CSTR:対象キャラ:一人称
		CONTINUE
	CASE "二人称","2nd"
		文章保存 += CSTR:対象キャラ:二人称
		CONTINUE
	CASE "呼び名","CALLNAME"
		文章保存 += CALLNAME:対象キャラ
		CONTINUE
	CASE "名前","NAME"
		文章保存 += NAME表示(対象キャラ)
		CONTINUE
	CASE "CALL"
		;次の文字をCALLで呼び出す(引数なし)
		LCOUNT += 1
		CALLFORM %LOCALS:LCOUNT%
		CONTINUE
	CASE "COLOR"
		;次の文字の色を変更する
		LCOUNT += 1
		文章保存 += @"<font color='#%LOCALS:LCOUNT%'>"
		LCOUNT += 1
		文章保存 += @"%LOCALS:LCOUNT%</font>"
		CONTINUE
	CASE "-"
		;次の文字に打消し線を引く
		LCOUNT += 1
		文章保存 += @"<s>%LOCALS:LCOUNT%</s>"
		CONTINUE
	CASE "B"
		;次の文字を太字にする
		LCOUNT += 1
		文章保存 += @"<b>%LOCALS:LCOUNT%</b>"
		CONTINUE
	CASE "I"
		;次の文字を斜体にする
		LCOUNT += 1
		文章保存 += @"<i>%LOCALS:LCOUNT%</i>"
		CONTINUE
	CASE "LINE"
		;次の文字に下線を引く
		LCOUNT += 1
		文章保存 += @"<u>%LOCALS:LCOUNT%</u>"
		CONTINUE
	CASE "FS"
		;次の文字の書式を変更する(Bit 0:太字 1:斜体 2:打ち消し線 3:下線)
		LCOUNT += 1
		書式 = TOINT(LOCALS:LCOUNT)
		FONTSTYLE 書式
		CONTINUE
	CASE "L","改行"
		文章保存 += "<br>"
		CONTINUE
	CASE "H"
		文章保存 += "<img src='えっちハート'>"
		CONTINUE
	CASE "A"
		文章保存 += "<img src='怒りマーク'>"
		CONTINUE
	CASEELSE
		文章保存 += LOCALS:LCOUNT
	ENDSELECT
NEXT
RESULTS '= 文章保存


