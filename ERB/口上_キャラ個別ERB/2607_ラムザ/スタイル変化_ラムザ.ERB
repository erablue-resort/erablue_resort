;--------------------------------------------------
;●ラムザ口上＠erablue_resort用
;　――スタイル変更処理
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもラムザ口上_readme.txt記載に従う
;--------------------------------------------------



;--------------------------------------------------
;スタイル変更基礎関数
;--------------------------------------------------
;必須関数。
;スタイル変更を追加する場合、必ず作成すること！
;ただしいくつ別スタイル作ってもキャラ毎に一個でよい
;--------------------------------------------------
@スタイル変更_2607(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード

	SELECTCASE 動作モード
		CASE "スタイル名取得"
			;ここは触らない
			VARSET RESULTS
			RESULTS:0 = %NAME:対象キャラ%
			RESULTS:10 = ノーマル

			;RESULTS:Xに名前全体を、RESULTS:X+10にリミテッドや水着など通称(？)を入れる
			;複数スタイルを作りたい場合はRESULTS:2、RESULTS:12のように数字をふやしていく
			;スタイル変更の条件を付けたい場合、RESULTSを条件分岐の中にいれる。
			;例：
			; IF 陥落チェック(対象キャラ)
			; 		RESULTS:1 = [強化状態二つ名]テスト太郎
			; 		RESULTS:11 = 強化スタイル
			; ENDIF

			;ただし：今回は「つくる」だけで、自由に変更はできないというか……
			IF 1
				RESULTS:1 = [ウォークライコマンダー]ラムザ・ベオルブ
				RESULTS:11 = 強化スタイル
			ENDIF
;			IF 0
;				;半分ネタメモ。これはチャプター４あたり想定のラムザ。運用シンプルな感じにしたいよね……
;				;あと「地烈斬一文字」とか合体アビリティでもぶっ放すか～？
;				RESULTS:2 = [影の英雄]ラムザ・ベオルブ
;				RESULTS:12 = リミテッド
;			ENDIF


		CASE @"スタイル解説_%NAME:対象キャラ%"
			TSTR:スタイル解説 = 
			TSTR:スタイル解説 += @"■%NAME:対象キャラ%"
			TSTR:スタイル解説 += "<br>"
			TSTR:スタイル解説 += "<br>他者強化のスタイル。"
			TSTR:スタイル解説 += "<br>戦闘開始時、または固有アビリティの他者へのバフを使用すると、このスタイルに変化する。"
			TSTR:スタイル解説 += "<br>ステータスは防御的。"
			TSTR:スタイル解説 += "<br>バフはいずれも単体対象だが、効果は高い。"
			TSTR:スタイル解説 += "<br>"
			;このCALLはそのままで良い
			CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
		CASE "スタイル解説_[ウォークライコマンダー]ラムザ・ベオルブ"
			TSTR:スタイル解説 = 
			TSTR:スタイル解説 += @"■[ウォークライコマンダー]ラムザ・ベオルブ"
			TSTR:スタイル解説 += "<br>"
			TSTR:スタイル解説 += "<br>自己強化のスタイル。"
			TSTR:スタイル解説 += "<br>戦闘中、固有アビリティ「さけぶ」を使用すると変化する。"
			TSTR:スタイル解説 += "<br>ステータスが攻撃的なものに変化する。"
			TSTR:スタイル解説 += "<br>固有アビリティが攻撃的なものに変化し、奥技の威力が上昇する。"
			TSTR:スタイル解説 += "<br>固有アビリティで他者へバフをかけるとスタイルが変化する。"
			TSTR:スタイル解説 += "<br>※戦闘開始時、通常スタイルに戻るため、ここで変更しても意味はない※"
			TSTR:スタイル解説 += "<br>"
			;このCALLはそのままで良い、戦闘アビが変動しない場合は消しても良い
			CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[ウォークライコマンダー]ラムザ・ベオルブ")
		CASE "変更後処理"
			;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
			;一人称や二人称、素質の変化などの調教側ステータスの変更を想定
			;戦闘ステータスは別の関数
			SELECTCASE CSTR:対象キャラ:スタイル管理文字列
				CASE ""
					;通常時に戻す処理
				CASE "[ウォークライコマンダー]ラムザ・ベオルブ"
					;スタイル変更時の処理
			ENDSELECT
	ENDSELECT


;--------------------------------------------------
;スタイル変更時、戦闘ステータス変動
;--------------------------------------------------
;関数名に[]が使えないため、しょうがなく[]排除時の文字列をキーに
;例えば[強化状態二つ名]テスト太郎というスタイルなら、
;@スタイルステータス補正_強化状態二つ名_テスト太郎　という関数名にすること
;--------------------------------------------------
;装備再計算時にこの関数は呼ばれる
;補正の順番は「スタイル＞指輪＞耳飾り＞料理など＞パーティキャラ補正」
;補正可能なステータスは「装備ステータス補正保存.ERD」を参照のこと
;--------------------------------------------------
@スタイルステータス補正_ウォークライコマンダー_ラムザ・ベオルブ(対象キャラ)
#DIM 対象キャラ
#DIM 現在レベル

	;現在のレベルを取っておく
	現在レベル = 基礎BATTLE_STATE:対象キャラ:Lv

	;ラムザの成長はサポート……に近い成長率に設定している
	;ここでは「成長率が変動した状態で現在レベルまで成長した」数値に設定
	;このスタイルチェンジは「戦闘中に発生するもの」を想定しているので、HP、MPは変動しない
	;かなり消費の大きなアビリティを使用してのスーパーモードなので（しかも解除される可能性がある）んまあ多少強くてもええやろ……という精神
	;/
	;　最大ＨＰ ：42→42
	;　最大ＭＰ ：26→26
	;　攻撃力   ：19→24
	;　防御力   ：20→18
	;　回復力   ：20→18
	;　行動速度 ：23→26
;	装備ステータス補正保存:対象キャラ:最大ＨＰ += 現在レベル * 5
;	装備ステータス補正保存:対象キャラ:最大ＭＰ -= 現在レベル * 1
	装備ステータス補正保存:対象キャラ:攻撃力   += 現在レベル * 5
	装備ステータス補正保存:対象キャラ:防御力   -= 現在レベル * 2
	装備ステータス補正保存:対象キャラ:回復力   -= 現在レベル * 2
	装備ステータス補正保存:対象キャラ:行動速度 += 現在レベル * 3

	;奥義ゲージ蓄積率、敵対心も上昇する。叫んでるので。
	装備ステータス補正保存:対象キャラ:ゲージ上昇効率 = 110
	装備ステータス補正保存:対象キャラ:ゲージ上昇効率_被ダメ時 = 110
	装備ステータス補正保存:対象キャラ:敵対心 = 150

	;特定のレベルを境に上昇するステータス
	;命中・回避は変わらないが、DA・TA率が大上昇
	IF 現在レベル >= 20
		装備ステータス補正保存:対象キャラ:DA率 += 4
		装備ステータス補正保存:対象キャラ:TA率 += 2
	ENDIF
	IF 現在レベル >= 40
		装備ステータス補正保存:対象キャラ:DA率 += 4
		装備ステータス補正保存:対象キャラ:TA率 += 2
	ENDIF
	IF 現在レベル >= 60
		装備ステータス補正保存:対象キャラ:DA率 += 6
		装備ステータス補正保存:対象キャラ:TA率 += 3
	ENDIF
	IF 現在レベル >= 80
		装備ステータス補正保存:対象キャラ:DA率 += 6
		装備ステータス補正保存:対象キャラ:TA率 += 3
	ENDIF



;--------------------------------------------------
;スタイル変更時、使用可能アビリティの変動
;--------------------------------------------------
;テンプレート「固有アビリティ_キャラ名.ERB」に記述する
;「@固有アビ_2607_任意の文字列(ARGS, キャラ番号)」の上部にある分岐を修正する

; IF スタイル判定関数(キャラ番号, "デフォルト") == 0
; を以下のように修正
; IF スタイル判定関数(キャラ番号, "<スタイル名>") == 0

;こうすることで、そのアビは<スタイル名>になっている時のみ使用可能という意味になる


;--------------------------------------------------
;スタイル変更時、使用可能奥義の変動
;--------------------------------------------------
;奥義はアビのように複数種類を使うことを想定していないため、別関数を呼ぶことを推奨。
;奥義テンプレにある以下の分岐のうち、任意のスタイル名の部分を編集した上で
;奥義テンプレをコピーして作成した関数に貼り付け、中身を記述する。

; IF CSTR:キャラ番号:スタイル管理文字列 == "任意のスタイル名"
; 	CALL 任意のスタイル名固有奥義(ARGS, キャラ番号)
; 	RETURN RESULT
; ENDIF
