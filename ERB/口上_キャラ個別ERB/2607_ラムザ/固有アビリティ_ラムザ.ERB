;--------------------------------------------------
;●ラムザ口上＠erablue_resort用
;　――固有アビリティ
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもラムザ口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●スキルに関するメモ
;--------------------------------------------------
;戦闘中の表示について：
;　アビリティ名の文字数は全角で12文字くらいまで。
;　固有アビに使える行は３行
;　１行に表示するアビリティは３つ
;　ただし「じゃあ最大９アビまで表示が崩れないのか？」というとちょっと違って
;　/
;　[100] 通常攻撃
;　[110] なまえのなが～いアビリティいち～～～[111] アビリティ２
;　[112] アビリティ３
;　[120] アビリティ４　[121] アビリティ５
;　（証アビ行）
;　/
;　……と、本来は[110]と同じ行に表示されるべき[112]が改行されて表示されちゃう場合がある。
;--------------------------------------------------

;--------------------------------------------------
;●通常スタイルの仕様
;--------------------------------------------------
;ふたつのモードを切り替えることができる（スタイルチェンジでなく）
;・自己バフ「さけぶ」を使用：「ウォークライコマンダー」モード……攻撃的なステータス・アビリティに変化
;・他者バフ「おうえん」を使用：「ベオルブ家末弟」モード……「エール」「はげます」などを使用できる
;弱点は「アタッカーをやるにはさけぶ必要があるが、さけぶの消費がクソ重い」「バッファーとしてもだいぶ悠長」
;ためる、はげます、エールは重ね掛け前提の数値。
;--------------------------------------------------
;・アビ１：さけぶ
;　自分対象。バフ。消費MPは「最大値の15％」
;　自身のスタイルを[ウォークライコマンダー]に変更する。
;　（攻撃、行動速度、DA率、TA率、奥義ゲージ蓄積率が上昇。狙われやすくなる。防御力、回復力がダウン）
;　独自バフ「BraveUP」を付与する。持続５ターン。解除不可。
;　（BraveUP：攻撃力、行動速度、DA率、TA率が上昇する）
;　/
;　原作では：自身の攻撃力・魔力・Speed・Braveをいっぺんに、戦闘中永続で強化する。FFT最強のアビリティのひとつ。
;　攻撃力も魔力も直で上昇させる手段はあんまりないがあるにはある。これがそもそも強力。
;　Speed強化がかなり重要で、FFシリーズ伝統のアクティブタイムバトルなので、これが上昇すると敵が１行動する間に２、３回行動できるようになる。
;　上げる手段がロクになく、他の上げる手段もラムザのみが持つアビリティだったりするので、めちゃくちゃに強力。
;　Braveはカウンター系アビリティの発動率であるほか、特に強力な武器種の攻撃力に関わる。さらにこのBraveUPは、戦闘後に一部引き継がれる。
;　無消費で効果が大きく強化も永続と非常に強力。２、３回さけんだラムザはラスボスとタイマンを張れるほどお強い。いやまあラスボス弱いから仕方ないんだけど。
;　DFFACでは一定時間（25秒）だがまあ仕方ないところか。
;・アビ２：おうえん
;　味方単体対象。
;　使用すると、自身のスタイルを[ベオルブ家末弟]に戻し、独自バフ「BraveUP」を解除する。
;　攻撃力、防御力、行動速度を上昇させ、再生と高揚を付与する。
;　それぞれ効果はそれなりに高い。効果は３～５ターン。
;　/
;　これはDFFACから。原作に「おうえん」はないが、DFFACのこれは「はげます＋エール」って感じ、ある。
;・アビ３：体当たり/剣閃
;　自属性単体攻撃。
;　命中率が高い（※実は原作でもそう。必中で、リアクションアビリティを無視する）。
;　原作的にはノックバック効果もあるけど困るね～
;　〇さけぶ状態の場合、「剣閃」に変化
;　　→命中補正はなくなるが、威力が向上し、縦列範囲攻撃になる（DFFでの「薙ぎ払い」にも対応）
;・アビ４：おまじない
;　自分以外の単体対象。
;　自分のHPを、最大値の20％消費し（１は残る）、消費分の２倍回復を行う。
;　ターンを消費しない。
;　/
;　おまじないはラムザの固有アビリティのひとつで、HPを20％消費し、その倍の量、対象のHPを回復する。
;　まあぶっちゃけめちゃくちゃ弱い効果。
;・アビ５：はげます/ためる
;　自分以外の単体対象。
;　対象の攻撃力、DA率、TA率をわずかに上昇させる(５ターン)。重ね掛け可能。
;　〇さけぶ状態の場合、「ためる」に変化
;　　→自分の攻撃力を累積で上昇させる（99ターン）。
;　/
;　原作的には「はげます」は、上述「さけぶ」の効果のひとつ「BraveUP」効果を持つ。
;　他人にかけられるうえに無消費・有射程・即時発動・必中で、わりと序盤のうちから使えるため、非常に強力で重要なアビリティなんだけど
;　攻撃力上げるほうはともかく反撃率はキャラによっては無意味だし拾うのも大変なので、DA・TA率バフにしておく
;　/
;　同じく原作的には、「ためる」はその戦闘中永続で自身の攻撃力を上昇させる効果を持つ。
;　誰でも使えて、一回一回の効果は大きくないが、カンストまで累積可能で、その気になれば最序盤からカンストダメージをぶち込むことも可能。
;　序盤から覚えられて、無消費・即時発動・必中なので、経験値稼ぎによく使われる。
;　攻撃力上昇効果は攻撃力マイナス効果で打ち消すしかなかったりするし、実際のところかなり強力。
;・アビ６：投石→投石六連弾/地烈斬
;　土属性単体攻撃。
;　レベルアップで強化され、地烈斬になると横列攻撃になる。
;　/
;　原作的には「投石」は、無消費・有射程・即時発動の遠距離攻撃。
;　ただし非常に低威力。
;　通常攻撃が最序盤でまあ30とか40とか、終盤になると250くらいは出るところ、投石は序盤10～20くらいで終盤でも20～30程度しか出ない。
;　ノックバック効果こそあるがあくまで序盤用。あるいは、「眠ったキャラを物理で起こす」「攻撃を受けたらカウンターで自分にバフをかける」系のやつを味方側から発動するとかの用途。
;　/
;　投石六連弾はFFRKってソシャゲで実装されたやつ。ガンガン投げる。
;　最後の一発はなんかもう岩。
;　/
;　地烈斬はモンクの強力なアビリティ。
;　長射程の一直線攻撃。障害物貫通、無消費・有射程・即時発動・必中（ただし浮いている敵には当たらない）
;　地属性かつ地属性強化アイテムの入手が容易であるため、めちゃくちゃに強い。
;　浮いている敵に当たらない……のは、さすがにチェックがしんどい。ごめんな。
;・アビ７：エール/ランダムブレイク
;　自分以外の単体対象。
;　対象の行動速度を上昇させる(５ターン)。重ね掛け可能。
;　たま～に再行動も付与する。
;　〇さけぶ状態の場合、「ランダムブレイク」→「トリプルブレイク」に変化
;　　→敵の攻撃・防御・速度をダウンさせる効果のある自属性攻撃。
;　/
;　エールは上述「さけぶ」の効果のひとつ「SpeedUP」を他人にかけられる。
;　無消費・有射程・即時発動・必中で、中盤あたりから使えるため、非常に強力で重要なアビリティ。主人公なのでアタッカーをやらせたくなるがサポーターをやってるほうが強い。
;　/
;　トリプルブレイクはDFFACより。
;　本来は「防御ダウン→攻撃ダウン→移動速度ダウン」の三連撃。
;　原作でのナイトのアビリティ「戦技」をモデルにしていると思われる。
;・アビ８：算術ホーリー
;　光属性単体攻撃。命中補正が高い。
;　また、条件に応じて、味方のHP回復、敵へ光属性の追撃を行う。
;　通常時は「速度ｘ」……原作的にはCTを想定……行動速度の値依存。
;　行動速度は素数を取る計算も面倒なので「下二桁」だけを見るようにした。
;　さけぶ状態の場合は「レベルｘ」、ラムザのレベルと対象のレベルに依存して対象が決定される。
;　速度は1～99、レベルは1～80を見るので、レベルのほうが当たりやすく、強力になる、はず。
;　/
;　ホーリー。聖属性単体攻撃。
;　FFTのホーリーはFFシリーズでも特に強い。
;　算術はFFTのやべーアビリティ。
;　詳細は省くが今回は「敵単体と自分を必ず巻き込む算術ホーリー」にしている。
;　原作との仕様とはだいぶ異なる。
;・アルテマ
;　通常奥義。原作的には無属性・必中・小範囲・魔法攻撃。
;　原作的には「さけぶ」は魔力も上昇させるので、さけぶ状態だと威力が向上する。
;・塵地螺鈿飾剣・乱の炎
;　陥落奥義。原作的には無属性・必中・中範囲・魔法攻撃。
;　原作的には「さけぶ」は魔力も上昇させるので、さけぶ状態だと威力が向上する。
;
;・反撃タックル
;　パッシブスキル。
;　物理攻撃を受けた際、一定確率で反撃する。
;　さけぶ状態だと反撃率が向上する。Braveが上がるので。
;--------------------------------------------------


;--------------------------------------------------
;〇さけぶ
;--------------------------------------------------
@固有アビ_2607_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM ＭＰ消費率
#DIM 攻撃バフ
#DIM 行動速度バフ
#DIM DA率バフ
#DIM TA率バフ
#DIM バフ持続

;	;スタイル判定関数を呼び出す
;	;スタイル変更が無い場合はここは何も触らなくて良い
;	IF 0
;	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
;		アビ名 = 
;		RETURN 0
;	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
;		アビ名 = 
;		RETURN 0
;	ENDIF

	;さすがにこれは没っておくね
;	アビ名 = さけぶジャ
;	アビ名 = さけぶガ
;	アビ名 = さけぶラ
;	アビ名 = さけぶ

	;ネタメモ
	;さけぶの口上のひとつには「アルマァアアーッ！！！」を入れる

	;Lv1から習得
	IF 0
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
		アビ名 = ガッツをさけぶ
		攻撃バフ = 25
		行動速度バフ = 25
		DA率バフ = 15
		TA率バフ = 10
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
		アビ名 = さけぶ＋＋
		攻撃バフ = 20
		行動速度バフ = 20
		DA率バフ = 10
		TA率バフ = 5
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名 = さけぶ＋
		攻撃バフ = 20
		行動速度バフ = 15
		DA率バフ = 10
		TA率バフ = 5
	ELSE
		アビ名 = さけぶ
		攻撃バフ = 15
		行動速度バフ = 15
		DA率バフ = 10
		TA率バフ = 5
	ENDIF

	;消費量・BraveUP持続ターンは固定。
	ＭＰ消費率 = 15
	消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ * ＭＰ消費率 / 100
	バフ持続 = 5

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}（最大ＭＰの{ＭＰ消費率}％）
			詳細文文字列受け渡し変数 += @"<br>自身のスタイルを［ウォークライコマンダー］に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身に、BraveUPバフ（独自枠・{バフ持続}ターン・消去不可）を与える。"
			詳細文文字列受け渡し変数 += @"<br>スタイル変更、BraveUPバフは、自身のアビリティによって消去されることがある。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}（最大ＭＰの{ＭＰ消費率}％）
			詳細文文字列受け渡し変数 += @"<br>自身のスタイルを［ウォークライコマンダー］に変更する。"
			詳細文文字列受け渡し変数 += @"<br>このスタイル変更は、自身のアビリティによって消去される（［ベオルブ家末弟］スタイルに再変更される）ことがある。"
			詳細文文字列受け渡し変数 += @"<br>自身に、BraveUPバフ（独自枠・{バフ持続}ターン・消去不可）を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[{攻撃バフ}％]の攻撃力上昇効果"
			詳細文文字列受け渡し変数 += @"<br>＞[{行動速度バフ}％]の行動速度上昇効果"
			詳細文文字列受け渡し変数 += @"<br>＞[{DA率バフ}]のDA率上昇効果"
			詳細文文字列受け渡し変数 += @"<br>＞[{TA率バフ}]のTA率上昇効果"
			詳細文文字列受け渡し変数 += @"<br>＞BraveUPバフは自身のアビリティによって消去されることがある。"
		CASE "アビ効果"
			;スタイル変更
			CALL ラムザ_スタイル変更(キャラ番号, "ウォークライコマンダー")

			;「さけぶ」の効果はすべて自己対象・3T・消去不可
			CALL アビ対象選択テンプレート_自己のみ

			;ってことで以下各バフ
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = BraveUP_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 攻撃バフ
			アビ汎用変数:持続ターン = バフ持続
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = BraveUP_行動速度
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 行動速度バフ
			アビ汎用変数:持続ターン = バフ持続
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = BraveUP_DA率
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果量 = DA率バフ
			アビ汎用変数:持続ターン = バフ持続
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = BraveUP_TA率
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:効果量 = TA率バフ
			アビ汎用変数:持続ターン = バフ持続
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇おうえん
;--------------------------------------------------
@固有アビ_2607_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM バフ持続
#DIM 攻撃バフ
#DIM 防御バフ
#DIM 行動速度バフ
#DIM 再生バフ
#DIM 高揚バフ
#DIM ラムザ隊列位置
#DIM BraveUP判定


;	;スタイル判定関数を呼び出す
;	;スタイル変更が無い場合はここは何も触らなくて良い
;	IF 0
;	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
;		アビ名 = 
;		RETURN 0
;	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
;		アビ名 = 
;		RETURN 0
;	ENDIF

	;Lv1から習得
	IF 0
		;この辺は没
;		アビ名 = おうえジャ
;		アビ名 = おうえガ
;		アビ名 = おうえラ
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
		;名称はFFBEより
		アビ名 = 士気策励＋
		消費ＭＰ = 50
		バフ持続 = 5
		攻撃バフ = 25
		防御バフ = 25
		行動速度バフ = 20
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
		;名称はFFBEより
		アビ名 = 士気策励
		消費ＭＰ = 40
		バフ持続 = 4
		攻撃バフ = 25
		防御バフ = 25
		行動速度バフ = 20
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
		アビ名 = おうえん＋＋
		消費ＭＰ = 35
		バフ持続 = 4
		攻撃バフ = 20
		防御バフ = 20
		行動速度バフ = 20
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名 = おうえん＋
		消費ＭＰ = 30
		バフ持続 = 3
		攻撃バフ = 20
		防御バフ = 20
		行動速度バフ = 20
	ELSE
		アビ名 = おうえん
		消費ＭＰ = 25
		バフ持続 = 3
		攻撃バフ = 15
		防御バフ = 15
		行動速度バフ = 15
	ENDIF

	;再生と高揚はいったん固定
	再生バフ = 10
	高揚バフ = 5

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>自身のスタイルを［ベオルブ家末弟］に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身にBraveUPバフが付与されている場合、消去する。"
			詳細文文字列受け渡し変数 += @"<br>味方単体に、おうえん効果（独自枠・{バフ持続}ターン）を与える。"
			詳細文文字列受け渡し変数 += @"<br>スタイル変更は、自身のアビリティによって消去されることがある。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>自身のスタイルを［ベオルブ家末弟］に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身にBraveUPバフが付与されている場合、消去する。"
			詳細文文字列受け渡し変数 += @"<br>このスタイル変更は、自身のアビリティによって消去される（［ウォークライコマンダー］スタイルに再変更される）ことがある。"
			詳細文文字列受け渡し変数 += @"<br>味方単体に、おうえん効果（独自枠・{バフ持続}ターン）を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[{攻撃バフ}％]の攻撃力上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[{防御バフ}％]の防御力上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[{行動速度バフ}％]の行動速度上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[回復力✕{再生バフ}]の再生効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞[{高揚バフ}]の高揚効果を与える。"
		CASE "アビ効果"
			;スタイル変更
			CALL ラムザ_スタイル変更(キャラ番号, "ベオルブ家末弟")

			;対象は味方単体
			;一応自分にも使えるがさけんだほうがいいかなという具合
			CALL アビ対象選択テンプレート_味方単体_生者のみ

			;ってことで以下各バフ
			アビ汎用文字列:バフ・デバフ枠 = おうえん_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 攻撃バフ
			アビ汎用変数:持続ターン = バフ持続
			CALL アビテンプレート_有利効果_バフ効果セット

			アビ汎用文字列:バフ・デバフ枠 = おうえん_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 防御バフ
			アビ汎用変数:持続ターン = バフ持続
			CALL アビテンプレート_有利効果_バフ効果セット

			アビ汎用文字列:バフ・デバフ枠 = おうえん_行動速度
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 行動速度バフ
			アビ汎用変数:持続ターン = バフ持続
			CALL アビテンプレート_有利効果_バフ効果セット

			;再生
			;さけぶ状態だと回復力が落ちるので、再生の効力が落ちる
			アビ汎用文字列:バフ・デバフ枠 = おうえん_再生
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 再生バフ / 100
			アビ汎用変数:持続ターン = バフ持続
			CALL アビテンプレート_有利効果_バフ効果セット

			;高揚
			アビ汎用文字列:バフ・デバフ枠 = おうえん_高揚
			アビ汎用文字列:バフ・デバフ対象能力 = 高揚
			アビ汎用変数:効果量 = 高揚バフ
			アビ汎用変数:持続ターン = バフ持続
			CALL アビテンプレート_有利効果_バフ効果セット
	ENDSELECT


;--------------------------------------------------
;〇体当たり/剣閃
;--------------------------------------------------
@固有アビ_2607_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 攻撃威力
#DIM 体当たりダメージ乱数
#DIM 命中補正

	;スタイルによって大きく分岐するので（マジで大きく分岐するので）ざっとここで分けちゃう
	;Lv1から習得。アビ１、アビ２がバフ系だからね～。
	IF 0
	;ベオルブ家末弟
	;「体当たり」
	;低威力だが消費は軽く命中率が高い……という具合で。
	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
		;Lv1から習得
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
			アビ名 = 空中体当たり
			消費ＭＰ = 15
			攻撃威力 = 160
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			アビ名 = 体当たり＋＋
			消費ＭＰ = 10
			攻撃威力 = 140
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			アビ名 = 体当たり＋
			消費ＭＰ = 5
			攻撃威力 = 120
		ELSE
			アビ名 = 体当たり
			消費ＭＰ = 3
			攻撃威力 = 100
		ENDIF

		命中補正 = 100

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の自属性のダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>ダメージの乱数幅が広い。命中率が高い。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の自属性のダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>ダメージの乱数幅が広い。命中率が高い。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ウォークライコマンダー］である場合、「剣閃」にアビリティが変化する。"
			CASE "アビ効果"
				CALL アビ対象選択テンプレート_敵単体
				アビ汎用変数:命中率補正値 = 命中補正
				;原作ではダメージが「攻撃力×（１、２、３、４）」と非常に乱数幅が広かったためその再現
				;さすがに33％、66％、100％、133％、とするのは幅が広すぎるので、60～120％で変動するように（ちょっと下振れしやすいように、ゲーム側での実装とはちょっと別の形式で）
				体当たりダメージ乱数 = RAND:60 + 60
				攻撃威力 = 攻撃威力 * 体当たりダメージ乱数 / 100
				アビ汎用変数:効果割合 = 攻撃威力
				CALL アビテンプレート_ダメージ処理_自属性
		ENDSELECT
	;ウォークライコマンダー
	;「剣閃」
	;縦列への攻撃。DFFACより。
	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
		;Lv1から習得
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
			アビ名 = 剣閃＋＋
			消費ＭＰ = 25
			攻撃威力 = 200
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			アビ名 = 剣閃＋
			消費ＭＰ = 20
			攻撃威力 = 160
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			アビ名 = 剣閃
			消費ＭＰ = 15
			攻撃威力 = 130
		ELSE
			アビ名 = 薙ぎ払い
			消費ＭＰ = 10
			攻撃威力 = 100
		ENDIF

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵縦列に［攻撃力✕{攻撃威力}％］の自属性のダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵縦列に［攻撃力✕{攻撃威力}％］の自属性のダメージを与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ベオルブ家末弟］である場合、「体当たり」にアビリティが変化する。"
			CASE "アビ効果"
				CALL アビ対象選択テンプレート_敵縦列
				アビ汎用変数:効果割合 = 攻撃威力
				CALL アビテンプレート_ダメージ処理_自属性
		ENDSELECT
	ENDIF


;--------------------------------------------------
;〇おまじない
;--------------------------------------------------
@固有アビ_2607_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM ＨＰ消費率
#DIM 消費ＨＰ
#DIM ＨＰ回復量

	;Lv1から習得。
	;両スタイルで共有。オヤジから教わったやつであるため。
	アビ名 = おまじない
	消費ＭＰ = 0
	ＨＰ消費率 = 20
	消費ＨＰ = 基礎BATTLE_STATE:キャラ番号:最大ＨＰ * ＨＰ消費率 / 100
	ＨＰ回復量 = 消費ＨＰ * 2

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ} ／ 消費ＨＰ：{消費ＨＰ}
			詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体に［{ＨＰ回復量}］のＨＰ回復を与える。"
			詳細文文字列受け渡し変数 += @"<br>使用時、自身のＨＰを［{消費ＨＰ}］消費する。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ} ／ 消費ＨＰ：{消費ＨＰ}
			詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体に［{ＨＰ回復量}］（消費したＨＰの２倍）のＨＰ回復を与える。"
			詳細文文字列受け渡し変数 += @"<br>使用時、自身のＨＰを［{消費ＨＰ}］消費する（最大ＨＰの{ＨＰ消費率}％）"
			詳細文文字列受け渡し変数 += @"<br>現在ＨＰが消費量に満たなかった場合、ＨＰを１まで消費し、その分の効果を発揮する。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		CASE "アビ効果"
			;行動を消費しない、まあHP20％消費するんでそうそう何度も使えないだろうが……
			アビテンプレート用_行動消費無しフラグ = 1
			;対象は自分以外の生存者のみ
			CALL アビ対象選択テンプレート_味方単体_生者のみ_自己以外

			;現在HPが、消費HP未満である場合
			;まー例えば200消費のところ150しかないとかっすね
			;その場合は「149消費、298回復」になる
			IF 消費ＨＰ > 基礎BATTLE_STATE:キャラ番号:ＨＰ
				;現在HPから１だけ低い数値を消費予定とする
				消費ＨＰ = 基礎BATTLE_STATE:キャラ番号:ＨＰ - 1
				;HP回復量も取り直す
				ＨＰ回復量 = 消費ＨＰ * 2
			ENDIF

			;効果量は固定
			アビ汎用変数:効果量 = ＨＰ回復量
			;回復量の乱数幅を％で設定する。数値が大きいほど回復量にブレが出る。未設定時は5
			;……のやつだが、今回はゼロ％。きっちり固定値回復する。
			アビ汎用変数:数値乱数幅 = 0
			CALL アビテンプレート_回復処理

			;消費というか反動ダメージ部分
			;対象がマイナス１でない場合（＝対象を取れた場合）HPを消費
			SIF アビテンプレート用_対象保存:0 != -1
				基礎BATTLE_STATE:キャラ番号:ＨＰ -= 消費ＨＰ
	ENDSELECT


;--------------------------------------------------
;〇はげます/ためる
;--------------------------------------------------
@固有アビ_2607_5(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 攻撃バフ
#DIM DA率バフ
#DIM TA率バフ
#DIM バフ持続
#DIM 累積上限_攻撃バフ
#DIM 累積上限_DA率バフ
#DIM 累積上限_TA率バフ

	;スタイルによって大きく分岐するので（マジで大きく分岐するので）ざっとここで分けちゃう
	IF 0
	;Lv10から習得……なので、レベル10未満のときはここで分岐させてカット
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 10
		アビ名 = 
		;この辺いるのかな？　まあ一応置いておく
		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				RETURN 消費ＭＰ
			CASE "アビ効果"
		ENDSELECT
	;ベオルブ家末弟
	;「はげます」
	;DA、TA率バフ。
	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			アビ名 = ガッツではげます
			消費ＭＰ = 15
			攻撃バフ = 10
			DA率バフ = 5
			TA率バフ = 3
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ名 = はげます＋＋
			消費ＭＰ = 15
			攻撃バフ = 7
			DA率バフ = 4
			TA率バフ = 2
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
			アビ名 = はげます＋
			消費ＭＰ = 10
			攻撃バフ = 5
			DA率バフ = 3
			TA率バフ = 2
		ELSE
			アビ名 = はげます
			消費ＭＰ = 5
			攻撃バフ = 3
			DA率バフ = 2
			TA率バフ = 1
		ENDIF

		バフ持続 = 5

		;5回まで累積可能
		累積上限_攻撃バフ = 攻撃バフ * 5
		累積上限_DA率バフ = DA率バフ * 5
		累積上限_TA率バフ = TA率バフ * 5

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体にはげます効果（独自枠・累積・{バフ持続}ターン）を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体にはげます効果（独自枠・累積・{バフ持続}ターン）を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［累積{攻撃バフ}％最大{累積上限_攻撃バフ}％］の攻撃力上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［累積{DA率バフ}最大{累積上限_DA率バフ}］のDA率上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［累積{TA率バフ}最大{累積上限_TA率バフ}］のTA率上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ウォークライコマンダー］である場合、「ためる」にアビリティが変化する。"
			CASE "アビ効果"
				;対象は自分以外（原作的には自分にも撃てたが……）
				CALL アビ対象選択テンプレート_味方単体_生者のみ_自己以外

				アビ汎用文字列:バフ・デバフ枠 = はげます_攻撃力
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果割合 = 攻撃バフ
				アビ汎用変数:累積上限_割合 = 累積上限_攻撃バフ
				アビ汎用変数:持続ターン = バフ持続
				CALL アビテンプレート_有利効果_累積バフ効果セット

				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = はげます_DA率バフ
				アビ汎用文字列:バフ・デバフ対象能力 = DA率
				アビ汎用変数:効果量 = DA率バフ
				アビ汎用変数:累積上限_固定値 = 累積上限_DA率バフ
				アビ汎用変数:持続ターン = バフ持続
				CALL アビテンプレート_有利効果_累積バフ効果セット

				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = はげます_TA率バフ
				アビ汎用文字列:バフ・デバフ対象能力 = TA率
				アビ汎用変数:効果量 = TA率バフ
				アビ汎用変数:累積上限_固定値 = 累積上限_TA率バフ
				アビ汎用変数:持続ターン = バフ持続
				CALL アビテンプレート_有利効果_累積バフ効果セット
		ENDSELECT
	;ウォークライコマンダー
	;「ためる」
	;自分への攻撃バフ。累積かつ解除不可で、スタイルを戻してもこれは持続。
	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
			アビ名 = ガッツをためる
			消費ＭＰ = 20
			攻撃バフ = 10
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			アビ名 = ためる＋＋
			消費ＭＰ = 20
			攻撃バフ = 7
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			アビ名 = ためる＋
			消費ＭＰ = 15
			攻撃バフ = 5
		ELSE
			アビ名 = ためる
			消費ＭＰ = 10
			攻撃バフ = 3
		ENDIF

		バフ持続 = 99
		;10回まで累積可能
		累積上限_攻撃バフ = 攻撃バフ * 10

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自身に［累積{攻撃バフ}％最大{累積上限_攻撃バフ}％］（独自枠・{バフ持続}ターン）の攻撃力上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自身に［累積{攻撃バフ}％最大{累積上限_攻撃バフ}％］（独自枠・{バフ持続}ターン）の攻撃力上昇効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ベオルブ家末弟］である場合、「はげます」にアビリティが変化する。"
			CASE "アビ効果"
				;累積攻撃バフ
				CALL アビ対象選択テンプレート_自己のみ
				アビ汎用文字列:バフ・デバフ枠 = ためる_攻撃力
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果割合 = 攻撃バフ
				アビ汎用変数:累積上限_割合 = 累積上限_攻撃バフ
				アビ汎用変数:持続ターン = バフ持続
				CALL アビテンプレート_有利効果_累積バフ効果セット
		ENDSELECT
	ENDIF


;--------------------------------------------------
;〇投石→投石六連弾/地烈斬
;--------------------------------------------------
@固有アビ_2607_6(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 攻撃威力
#DIM 大投石威力
#DIM 命中補正

	;レベルが上がると、スタイルによって分岐する
	IF 0
	;Lv15から習得……なので、習得レベル未満のときはここで分岐させてカット
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 15
		アビ名 = 
		;この辺いるのかな？　まあ一応置いておく
		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				RETURN 消費ＭＰ
			CASE "アビ効果"
		ENDSELECT
	;Lv30未満なら共通
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 30
		アビ名 = 投石
		消費ＭＰ = 10
		攻撃威力 = 150

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の土属性のダメージを与える。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の土属性のダメージを与える。"
			CASE "アビ効果"
				CALL アビ対象選択テンプレート_敵単体
				アビ汎用変数:効果割合 = 攻撃威力
				CALL アビテンプレート_ダメージ処理_土属性
		ENDSELECT
	;ベオルブ家末弟
	;投石→投石六連弾
	;消費のわりに威力がちょっと高いがランダムターゲットになっている
	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 70
			アビ名 = 投石六連弾＋＋
			消費ＭＰ = 15
			攻撃威力 = 40
			大投石威力 = 140
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 50
			アビ名 = 投石六連弾＋
			消費ＭＰ = 15
			攻撃威力 = 35
			大投石威力 = 120
		ELSE
			アビ名 = 投石六連弾
			消費ＭＰ = 15
			攻撃威力 = 30
			大投石威力 = 100
		ENDIF

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に［攻撃力✕{攻撃威力}％］の土属性ダメージを５回与える。"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に［攻撃力✕{大投石威力}％］の土属性ダメージを１回与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に［攻撃力✕{攻撃威力}％］の土属性ダメージを５回与える。"
				詳細文文字列受け渡し変数 += @"<br>ランダムな敵に［攻撃力✕{大投石威力}％］の土属性ダメージを１回与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ウォークライコマンダー］である場合、「地烈斬」にアビリティが変化する。"
			CASE "アビ効果"
				;１～５回目は低威力
				FOR LOCAL, 0, 6
					;敵対心は無視「しない」と明示（ヘイトに影響される）
					アビ汎用変数:敵対心無視オプション = 0
					CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
					アビ汎用変数:効果割合 = 攻撃威力
					CALL アビテンプレート_ダメージ処理_土属性
				NEXT
				;最後の一発はダメージが高い
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 大投石威力
				CALL アビテンプレート_ダメージ処理_土属性
		ENDSELECT
	;ウォークライコマンダー
	;投石→地烈斬
	;高い威力の横列攻撃。命中率が高い。
	;浮遊している敵に当たるのは……DFFACでは当たってたからな、許してくれ。
	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 70
			;疾風は原作の詠唱から。別に風属性は追加されない
			アビ名 = 疾風・地烈斬
			消費ＭＰ = 25
			攻撃威力 = 300
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 50
			アビ名 = 地烈斬＋
			消費ＭＰ = 20
			攻撃威力 = 250
		ELSE
			アビ名 = 地烈斬
			消費ＭＰ = 15
			攻撃威力 = 200
		ENDIF
		命中補正 = 50

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵横列に［攻撃力✕{攻撃威力}％］の土属性ダメージを与える。命中率が高い。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵横列に［攻撃力✕{攻撃威力}％］の土属性ダメージを与える。命中率が高い。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ベオルブ家末弟］である場合、「投石六連弾」にアビリティが変化する。"
			CASE "アビ効果"
				CALL アビ対象選択テンプレート_敵横列
				アビ汎用変数:効果割合 = 攻撃威力
				アビ汎用変数:命中率補正値 = 命中補正
				CALL アビテンプレート_ダメージ処理_土属性
		ENDSELECT
	ENDIF


;--------------------------------------------------
;〇エール/ランダムブレイク
;--------------------------------------------------
@固有アビ_2607_7(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 攻撃威力
#DIM 行動速度バフ
#DIM 累積上限_行動速度バフ
#DIM 再行動付与率
#DIM 再行動効果量
#DIM 再行動持続
#DIM バフ持続
#DIM ブレイク種類
#DIM 攻撃デバフ
#DIM 防御デバフ
#DIM 行動速度デバフ
#DIM デバフ付与率

	;レベルが上がると、スタイルによって分岐する
	IF 0
	;Lv25から習得……なので、習得レベル未満のときはここで分岐させてカット
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 25
		アビ名 = 
		;この辺いるのかな？　まあ一応置いておく
		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				RETURN 消費ＭＰ
			CASE "アビ効果"
		ENDSELECT
	;ベオルブ家末弟
	;エール
	;行動速度がいっぱい上がり、たま～に再行動する
	ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 70
			アビ名 = ガッツのエール
			消費ＭＰ = 45
			行動速度バフ = 40
			再行動付与率 = 30
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 50
			アビ名 = エール＋
			消費ＭＰ = 30
			行動速度バフ = 35
			再行動付与率 = 20
		ELSE
			アビ名 = エール
			消費ＭＰ = 15
			行動速度バフ = 30
			再行動付与率 = 10
		ENDIF
		バフ持続 = 5

		;５回まで累積可能
		累積上限_行動速度バフ = 行動速度バフ * 5

		;再行動は効果も持続も少なく。さすがにね。
		再行動効果量 = 1
		再行動持続 = 1

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体にエール効果（独自枠・累積・{バフ持続}ターン）を与える。"
				詳細文文字列受け渡し変数 += @"<br>再行動効果（{再行動効果量}回・{再行動持続}ターン）を与える。成功率{再行動付与率}％。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>自分以外の味方単体にエール効果（独自枠・累積・{バフ持続}ターン）を与える。"
				詳細文文字列受け渡し変数 += @"<br>再行動効果（{再行動効果量}回・{再行動持続}ターン）を与える。成功率{再行動付与率}％。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ウォークライコマンダー］である場合、「ランダムブレイク」にアビリティが変化する。"
			CASE "アビ効果"
				;対象は自分以外
				CALL アビ対象選択テンプレート_味方単体_生者のみ_自己以外

				アビ汎用文字列:バフ・デバフ枠 = エール_行動速度
				アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
				アビ汎用変数:持続ターン = バフ持続
				アビ汎用変数:効果割合 = 行動速度バフ
				アビ汎用変数:累積上限_割合 = 累積上限_行動速度バフ
				CALL アビテンプレート_有利効果_累積バフ効果セット

				;低めの確率で再行動付与
				IF RAND:100 < 再行動付与率
					アビ汎用文字列:バフ・デバフ枠 = 再行動
					アビ汎用文字列:バフ・デバフ対象能力 = 再行動
					アビ汎用変数:効果量 = 再行動効果量
					アビ汎用変数:持続ターン = 再行動持続
					CALL アビテンプレート_有利効果_バフ効果セット
				ENDIF
		ENDSELECT
	;ウォークライコマンダー
	;ランダムブレイク
	;単体攻撃。攻撃・防御・行動速度をダウンさせる。
	ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 70
			アビ名 = トリプルブレイク
			消費ＭＰ = 25
			攻撃威力 = 300
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 50
			アビ名 = ダブルブレイク
			消費ＭＰ = 20
			攻撃威力 = 250
		ELSE
			アビ名 = ランダムブレイク
			消費ＭＰ = 15
			攻撃威力 = 200
		ENDIF

		攻撃デバフ = 15
		防御デバフ = 15
		行動速度デバフ = 15
		バフ持続 = 3
		デバフ付与率 = 100

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の自属性ダメージを与える。"
				IF 0
				ELSEIF アビ名 == "トリプルブレイク"
					詳細文文字列受け渡し変数 += @"<br>攻撃命中時、攻撃、防御、行動速度減少効果を与える。"
				ELSEIF アビ名 == "ダブルブレイク"
					詳細文文字列受け渡し変数 += @"<br>攻撃命中時、攻撃、防御、行動速度のうち二種の減少効果を与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>攻撃命中時、攻撃、防御、行動速度のうち一種の減少効果を与える。"
				ENDIF
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルによってアビリティが変化する。"
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の自属性ダメージを与える。"
				IF 0
				ELSEIF アビ名 == "トリプルブレイク"
					詳細文文字列受け渡し変数 += @"<br>攻撃が命中した際、下記能力減少効果を与える。"
				ELSEIF アビ名 == "ダブルブレイク"
					詳細文文字列受け渡し変数 += @"<br>攻撃が命中した際、下記能力減少効果のうちふたつをランダムで与える。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>攻撃が命中した際、下記能力減少効果のうちひとつをランダムで与える。"
				ENDIF
				詳細文文字列受け渡し変数 += @"<br>＞［{攻撃デバフ}％］（Ａ枠・{バフ持続}ターン）の攻撃減少効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［{防御デバフ}％］（Ａ枠・{バフ持続}ターン）の防御減少効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>＞［{行動速度デバフ}％］（Ａ枠・{バフ持続}ターン）の行動速度減少効果を与える。"
				詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ベオルブ家末弟］である場合、「エール」にアビリティが変化する。"
			CASE "アビ効果"
				;敵単体にダメージ部。まあフツーの攻撃。DFFAC的には３連撃だが……
				CALL アビ対象選択テンプレート_敵単体
				アビ汎用変数:効果割合 = 攻撃威力
				CALL アビテンプレート_ダメージ処理_自属性

				;おまじないでリセットしておく
				ブレイク種類 = 0

				IF 0
				;攻撃がヒットしていなければデバフもナシ
				ELSEIF アビテンプレート用_回避フラグ
					ブレイク種類 = 0
				ELSEIF アビ名 == "トリプルブレイク"
					SETBIT ブレイク種類, 1
					SETBIT ブレイク種類, 2
					SETBIT ブレイク種類, 3
				ELSEIF アビ名 == "ダブルブレイク"
					IF 0
					ELSEIF !RAND:3
						SETBIT ブレイク種類, 1
						SETBIT ブレイク種類, 2
					ELSEIF !RAND:2
						SETBIT ブレイク種類, 1
						SETBIT ブレイク種類, 3
					ELSE
						SETBIT ブレイク種類, 2
						SETBIT ブレイク種類, 3
					ENDIF
				ELSE
					IF 0
					ELSEIF !RAND:3
						SETBIT ブレイク種類, 3
					ELSEIF !RAND:2
						SETBIT ブレイク種類, 2
					ELSE
						SETBIT ブレイク種類, 1
					ENDIF
				ENDIF

				;攻撃デバフ
				IF GETBIT(ブレイク種類, 1)
					CALL アビ汎用変数文字列リセット
					アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
					アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
					アビ汎用変数:効果割合 = 攻撃デバフ
					アビ汎用変数:持続ターン = バフ持続
					アビ汎用変数:基礎成功確率 = デバフ付与率
					CALL アビテンプレート_不利効果_デバフ効果セット
				ENDIF
				;防御デバフ
				IF GETBIT(ブレイク種類, 2)
					CALL アビ汎用変数文字列リセット
					アビ汎用文字列:バフ・デバフ枠 = 防御力A
					アビ汎用文字列:バフ・デバフ対象能力 = 防御力
					アビ汎用変数:効果割合 = 防御デバフ
					アビ汎用変数:持続ターン = バフ持続
					アビ汎用変数:基礎成功確率 = デバフ付与率
					CALL アビテンプレート_不利効果_デバフ効果セット
				ENDIF
				;行動速度デバフ
				IF GETBIT(ブレイク種類, 3)
					CALL アビ汎用変数文字列リセット
					アビ汎用文字列:バフ・デバフ枠 = 行動速度A
					アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
					アビ汎用変数:効果割合 = 行動速度デバフ
					アビ汎用変数:持続ターン = バフ持続
					アビ汎用変数:基礎成功確率 = デバフ付与率
					CALL アビテンプレート_不利効果_デバフ効果セット
				ENDIF
		ENDSELECT
	ENDIF


;--------------------------------------------------
;〇ホーリー/算術速度xホーリー/算術LvXホーリー
;--------------------------------------------------
@固有アビ_2607_8(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 攻撃威力
#DIM 命中補正
#DIMS 算術対象
#DIM 算術X
#DIM LCOUNT
#DIM 敵の番号
#DIM 算出攻撃力
#DIM チェック値

	;レベルが上がると、スタイルによって分岐する
	IF 0
	;Lv30から習得……なので、習得レベル未満のときはここで分岐させてカット
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv < 30
		アビ名 = 
		;この辺いるのかな？　まあ一応置いておく
		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				RETURN 消費ＭＰ
			CASE "アビ効果"
		ENDSELECT
	;Lv30以上で（算術）ホーリーを習得
	ELSE
		;算術チェック
		IF 0
		;ベオルブ家末弟
		ELSEIF スタイル判定関数(キャラ番号, "デフォルト") == 1
			算術対象 '= "速度"
			チェック値 = 基礎BATTLE_STATE:キャラ番号:行動速度 % 100
		;ウォークライコマンダー
		ELSEIF スタイル判定関数(キャラ番号, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
			算術対象 '= "レベル"
			チェック値 = 基礎BATTLE_STATE:キャラ番号:Lv % 100
		ENDIF


		;算術に使う行動速度/レベルを算出する。
		;値は３、４、５、素数、算術不可、である。
		算術X = 0
		IF 0
		ELSEIF チェック値 % 3 == 0
			算術X = 3
		ELSEIF チェック値 % 4 == 0
			算術X = 4
		ELSEIF チェック値 % 5 == 0
			算術X = 5
		;素数の場合。80以下で一番大きな素数を仮に入れておく
		ELSEIF ラムザ_算術_素数チェック(チェック値)
			算術X = 79
		;そのいずれでもない場合（７×７＝49、７×11＝77、など）は算術効果を発揮しない。
		ELSE
			算術X = 0
		ENDIF

		;名称・消費・威力・命中補正をセット
		IF 0
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 70
			アビ名 = ホーリー＋＋
			消費ＭＰ = 50
			攻撃威力 = 300
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv > 50
			アビ名 = ホーリー＋
			消費ＭＰ = 40
			攻撃威力 = 275
		ELSE
			アビ名 = ホーリー
			消費ＭＰ = 35
			攻撃威力 = 250
		ENDIF

		命中補正 = 150

		;アビ名を算術名称へ上書き
		IF 算術対象 == "速度"
			IF 算術X == 79
				アビ名 = 算術・速度素数ホーリー
			ELSEIF GROUPMATCH(算術X, 3, 4, 5)
				アビ名 '= @"算術・速度{算術X}ホーリー"
			ENDIF
		ELSEIF 算術対象 == "レベル"
			IF 算術X == 79
				アビ名 = 算術・Lv素数ホーリー
			ELSEIF GROUPMATCH(算術X, 3, 4, 5)
				アビ名 '= @"算術・Lv{算術X}ホーリー"
			ENDIF
		ELSE
			;算術対象の行動速度/レベルでなければなんもしない
		ENDIF

		SELECTCASE ARGS
			CASE "アビ名"
				TSTR:コマンド名受渡 = %アビ名%
			CASE "アビ説明文"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。命中率が高い。"
				IF 0
				;算術対象：行動速度
				ELSEIF 算術対象 == "速度"
					IF 0
					;行動速度が３、４、５の倍数の場合
					ELSEIF GROUPMATCH(算術X, 3, 4, 5)
						詳細文文字列受け渡し変数 += @"<br>行動速度の下二桁が{算術X}の倍数の敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
					;行動速度が素数の場合
					ELSEIF 算術X == 79
						詳細文文字列受け渡し変数 += @"<br>行動速度の下二桁が素数である敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
					;算術の対象が３４５倍数でも素数でもない場合
					;……つまりただのホーリーの場合。
					ELSE
						;なんも言わない
					ENDIF
					詳細文文字列受け渡し変数 += @"<br>自身のスタイルと行動速度によってアビリティが変化する。"
				;算術対象：レベル
				ELSEIF 算術対象 == "レベル"
					IF 0
					;レベルが３、４、５の倍数の場合
					ELSEIF GROUPMATCH(算術X, 3, 4, 5)
						詳細文文字列受け渡し変数 += @"<br>レベルが{算術X}の倍数の敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
					;レベルが素数の場合
					ELSEIF 算術X == 79
						詳細文文字列受け渡し変数 += @"<br>レベルが素数である敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
					;算術の対象が３４５倍数でも素数でもない場合
					;……つまりただのホーリーの場合。
					ELSE
						;なんも言わない
					ENDIF
					詳細文文字列受け渡し変数 += @"<br>自身のスタイルと現在レベルによってアビリティが変化する。"
				ENDIF
				RETURN 消費ＭＰ
			CASE "アビ説明文_詳細"
				詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
				詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。命中率が高い。"
				IF 0
				;算術対象：行動速度
				ELSEIF 算術対象 == "速度"
					IF 0
					;行動速度が３、４、５の倍数の場合
					ELSEIF GROUPMATCH(算術X, 3, 4, 5)
						詳細文文字列受け渡し変数 += @"<br>行動速度の下二桁が{算術X}の倍数の敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
						詳細文文字列受け渡し変数 += @"<br>自身の現在の行動速度が「３、４、５の倍数である」「素数である」「どちらでもない」のいずれであるかによってアビリティが変化する。"
					;行動速度が素数の場合
					ELSEIF 算術X == 79
						詳細文文字列受け渡し変数 += @"<br>行動速度の下二桁が素数である敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
						詳細文文字列受け渡し変数 += @"<br>自身の現在の行動速度が「３、４、５の倍数である」「素数である」「どちらでもない」のいずれであるかによってアビリティが変化する。"
					;算術の対象が３４５倍数でも素数でもない場合
					;……つまりただのホーリーの場合。
					ELSE
						詳細文文字列受け渡し変数 += @"<br>自身の現在の行動速度が「３、４、５の倍数」「素数」である場合、算術が発動する。"
					ENDIF
					詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ウォークライコマンダー］である場合、算術対象がレベルに変化する。"
				;算術対象：レベル
				ELSEIF 算術対象 == "レベル"
					IF 0
					;レベルが３、４、５の倍数の場合
					ELSEIF GROUPMATCH(算術X, 3, 4, 5)
						詳細文文字列受け渡し変数 += @"<br>レベルが{算術X}の倍数の敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
						詳細文文字列受け渡し変数 += @"<br>自身の現在レベルが「３、４、５の倍数である」「素数である」「どちらでもない」のいずれであるかによってアビリティが変化する。"
					;レベルが素数の場合
					ELSEIF 算術X == 79
						詳細文文字列受け渡し変数 += @"<br>レベルが素数である敵味方を対象にする。"
						詳細文文字列受け渡し変数 += @"<br>対象の味方に、［攻撃力✕{攻撃威力}％］のＨＰ回復を与える。"
						詳細文文字列受け渡し変数 += @"<br>対象の敵に、［攻撃力✕{攻撃威力}％］の光属性ダメージを与える。"
						詳細文文字列受け渡し変数 += @"<br>自身の現在レベルが「３、４、５の倍数である」「素数である」「どちらでもない」のいずれであるかによってアビリティが変化する。"
					;算術の対象が３４５倍数でも素数でもない場合
					;……つまりただのホーリーの場合。
					ELSE
						詳細文文字列受け渡し変数 += @"<br>自身の現在レベルが「３、４、５の倍数」「素数」である場合、算術が発動する。"
					ENDIF
					詳細文文字列受け渡し変数 += @"<br>自身のスタイルが［ベオルブ家末弟］である場合、算術対象が行動速度に変化する。"
				ENDIF
			CASE "アビ効果"
				;敵単体にダメージ部
				CALL アビ対象選択テンプレート_敵単体
				アビ汎用変数:効果割合 = 攻撃威力
				アビ汎用変数:命中率補正値 = 命中補正
				CALL アビテンプレート_ダメージ処理_光属性

				;算術Lvがセットされてるならこの先へ
				;この辺、回数系のバフを一瞬で消費するが、まあ使えりゃ強いから許せ。
				IF 算術X > 0
					;味方全員を見る
					;当たった全員が回復するのは……カメレオンローブを急いで着せてるんだよきっと。
					FOR LCOUNT, 0, 4
						;「基礎BATTLE_STATE」は、キャラ全員が持っているステータスの数値
						;「BATTLE_STATE」は、ダンジョン中の計算用に一時保存している装備込みの数値
						;今回は「味方を見る」ので、基礎じゃないほうのばとるすてーと。

						;戦闘員が居ないマスはスキップ
						SIF BATTLE_STATE:LCOUNT:キャラ登録番号 == 0
							CONTINUE
						;死んでたらスキップ
						SIF BATTLE_STATE:LCOUNT:ＨＰ < 1
							CONTINUE

						;チェック対象の値を取る
						SIF 算術対象 == "速度"
							チェック値 = BATTLE_STATE:LCOUNT:行動速度 % 100
						SIF 算術対象 == "レベル"
							チェック値 = BATTLE_STATE:LCOUNT:Lv % 100

						IF 0
						;素数である場合
						ELSEIF 算術X == 79 && ラムザ_算術_素数チェック(チェック値)
							;いったんなにもしない
						;算術LVの倍数である場合
						ELSEIF チェック値 % 算術X == 0
							;いったんなにもしない
						;対象のレベルが素数でも倍数でもないのなら次
						ELSE
							CONTINUE
						ENDIF

						CALL アビ汎用変数文字列リセット
						;対象を直接指定
						CALL アビ対象選択テンプレート_指定(LCOUNT)

						CALL バフ込み数値算出("攻撃力")
						算出攻撃力 = RESULT
						アビ汎用変数:効果量 = 算出攻撃力 * 攻撃威力 / 100
						CALL アビテンプレート_回復処理_MAXでも回復
					NEXT
					;敵全員を見る
					;こっちは聖属性吸収エネミーを無視している。ごめんね。
					FOR LCOUNT, 10, 20
						敵の番号 = LCOUNT - 10
						;敵が居ないマスはスキップ
						SIF 敵BATTLE_STATE_STR:敵の番号:エネミー名 == ""
							CONTINUE
						;死んでたらスキップ
						SIF 敵BATTLE_STATE:敵の番号:ＨＰ < 1
							CONTINUE

						;チェック対象の値を取る
						SIF 算術対象 == "速度"
							チェック値 = 敵BATTLE_STATE:敵の番号:行動速度 % 100
						SIF 算術対象 == "レベル"
							チェック値 = 敵BATTLE_STATE:敵の番号:Lv % 100

						IF 0
						;素数である場合
						ELSEIF 算術X == 79 && ラムザ_算術_素数チェック(チェック値)
							;いったんなにもしない
						;算術LVの倍数である場合
						ELSEIF チェック値 % 算術X == 0
							;いったんなにもしない
						;対象のレベルが素数でも倍数でもないのなら次
						ELSE
							CONTINUE
						ENDIF

						CALL アビ汎用変数文字列リセット
						;対象を直接指定
						CALL アビ対象選択テンプレート_指定(LCOUNT)
						アビ汎用変数:効果割合 = 攻撃威力
						アビ汎用変数:命中率補正値 = 命中補正
						CALL アビテンプレート_ダメージ処理_光属性
					NEXT
				ENDIF
		ENDSELECT
	ENDIF


;--------------------------------------------------
;〇算術用素数チェック処理
;--------------------------------------------------
@ラムザ_算術_素数チェック(チェック値)
#FUNCTION
#DIM チェック値

	;なんかの事故で対象の値が0未満だったら0にしておく
	SIF チェック値 < 0
		チェック値 = 0
	;対象の値が101以上であるなら、100で割った余りを取り出す（＝下二桁を取り出す）（1297/100＝12と余り97、の余り97を取り出す）
	;次の素数チェックがちょっと力業になりすぎるため。
	SIF チェック値 > 100
		チェック値 = チェック値 % 100

	;チェック値が素数であるかを確認し、素数であるなら１を返す
	;チェックする素数は2～97まででいいように値を加工している
	SIF GROUPMATCH(チェック値, 2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31, 37, 41, 43, 47, 53, 59, 61, 67, 71, 73, 79, 83, 89, 97)
		RETURNF 1

	RETURNF 0


;--------------------------------------------------
;〇戦闘開始時処理
;--------------------------------------------------
;・通常スタイルに戻す処理
;・リアクションアビリティ：反撃タックルをセットする処理
;--------------------------------------------------
@固有戦闘開始時処理_キャラ_2607
#DIM 対象キャラ
#DIM カウンター威力倍率
#DIM 持続ターン
#DIMS てすとん

	;通常スタイルに変更
	対象キャラ = GETCHARA(2607)

	CALL ラムザ_スタイル変更(対象キャラ, "ベオルブ家末弟")

	;反撃タックルの設定
	カウンター威力倍率 = 100
	持続ターン = 99

	;カウンターバフ付与
	CALL アビ対象選択テンプレート_自己のみ
	アビテンプレート用_アビ名 = 反撃タックル
	アビ汎用文字列:バフ・デバフ枠 = 反撃タックル
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:効果割合 = カウンター威力倍率
	アビ汎用変数:持続ターン = 持続ターン
	CALL アビテンプレート_有利効果_バフ効果セット


;--------------------------------------------------
;・反撃タックル（カウンター部分）
;　汎用アビ、リベンジウォールからコピペ……した、パチュリーのSリフレクターからコピペ
;--------------------------------------------------
@特殊反応_被ダメ時_反撃タックル(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIM バフ番号
#DIM 攻撃者隊列
#DIM 被ダメ記録順
#DIM 対象キャラ
#DIM Brave値

	対象キャラ = GETCHARA(2607)
	IF 0
	ELSEIF スタイル判定関数(対象キャラ, "[ベオルブ家末弟]ラムザ・ベオルブ") == 1
		Brave値 = 40
	ELSEIF スタイル判定関数(対象キャラ, "[ウォークライコマンダー]ラムザ・ベオルブ") == 1
		Brave値 = 80
	ELSE
		Brave値 = 0
	ENDIF

	;RANDがBraveの値以下なら発動。叫んでBrave上げてたら80％で発動ってことだナ
	IF RAND:100 < Brave値
		CALL アビ対象選択テンプレート_指定(攻撃者隊列)
		アビテンプレート用_アビ名 = 反撃タックル
		アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
		アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
		CALL アビテンプレート_ダメージ処理_自属性
	ENDIF


;--------------------------------------------------
;戦闘中スタイル変更処理
;--------------------------------------------------
@ラムザ_スタイル変更(対象キャラ, 変更後スタイル名)
#DIM 対象キャラ
#DIMS 変更後スタイル名
#DIM ラムザ隊列位置

	IF 0
	ELSEIF 変更後スタイル名 == ""
		;なんも入ってこなかったら怒る
		PRINTFORML ラムザのスタイル変更でエラー吐いてるで：１
		RETURN 0
	ELSEIF 変更後スタイル名 == "ウォークライコマンダー"
		;正式名称をセット
		変更後スタイル名 '= "[ウォークライコマンダー]ラムザ・ベオルブ"
	ELSEIF 変更後スタイル名 == "ベオルブ家末弟"
		;通常スタイルに戻すためにスタイル名を空欄にセット
		変更後スタイル名 '= ""

		;独自枠のバフ、BraveUPが付いてたら消す
		ラムザ隊列位置 = キャラ隊列検索(GETCHARA(2607))
		CALL ラムザ_BraveUP削除処理(ラムザ隊列位置, "BraveUP_攻撃力")
		CALL ラムザ_BraveUP削除処理(ラムザ隊列位置, "BraveUP_行動速度")
		CALL ラムザ_BraveUP削除処理(ラムザ隊列位置, "BraveUP_DA率")
		CALL ラムザ_BraveUP削除処理(ラムザ隊列位置, "BraveUP_TA率")

	ELSE
		;なんも入ってこなかったら怒る
		PRINTFORML ラムザのスタイル変更でエラー吐いてるで：２
		RETURN 0
	ENDIF

	;スタイル名変更
	CSTR:対象キャラ:スタイル管理文字列 '= 変更後スタイル名

	CALL 装備再計算処理(対象キャラ)
	CALL 戦闘初期処理_味方(0)


;--------------------------------------------------
;独自バフ消去処理
;--------------------------------------------------
@ラムザ_BraveUP削除処理(ラムザ隊列位置, 消去先のバフ名)
#DIM ラムザ隊列位置
#DIMS 消去先のバフ名
#DIM BraveUP判定

	BraveUP判定 = バフ番号取得_枠(消去先のバフ名, ラムザ隊列位置)
	;おまじない：バフが付与されてる場合のみ消す
	SIF BraveUP判定 > -1
		CALL 戦闘効果削除(BraveUP判定)


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_2607(対象キャラ)
#DIM 対象キャラ
	CALL 口上変数初期化

	;KSTR:(K++) = □ 特性：『なにかを叫べる青年』 □-------------------------------------------------------
	;KSTR:(K++) = □ 特性：『アルマァアアーッ』 □---------------------------------------------------------
	KSTR:(K++) = □ 特性：『ウォークライコマンダー』 □---------------------------------------------------
	KSTR:(K++) = 　一部の固有アビリティを使用するとスタイルが変化し、ステータス・アビリティも変化する
	KSTR:(K++) = 　・スタイル『ベオルブ家末弟』
	KSTR:(K++) = 　　他者強化型のスタイル
	KSTR:(K++) = 　　戦闘開始直後はこのスタイルであるほか、固有アビリティ『おうえん』を使用すると変化する
	KSTR:(K++) = 　・スタイル『ウォークライコマンダー』
	KSTR:(K++) = 　　自己強化・攻撃型のスタイル
	KSTR:(K++) = 　　固有アビリティ『さけぶ』を使用すると、この戦闘中のみ変化する
	KSTR:(K++) = 　
	KSTR:(K++) = □ 特性：『リアクションアビリティ 反撃タックル』 □--------------------------------------
	KSTR:(K++) = 　攻撃を受けた際自動的に反撃する
	KSTR:(K++) = 　スタイルによって反撃率は変化し、『ベオルブ家末弟』では40％、『ウォークライコマンダー』では80％で発動する
	KSTR:(K++) = 　

	CALL 口上変数表示(対象キャラ, 1)
