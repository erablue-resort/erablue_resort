;シーカトラスⅡ
@固有アビ_46_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIMS アビ名
;燃費のいい単体攻撃に
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = シーカトラスⅡ＋
	消費ＭＰ = 70
	効果数値 = 350
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = シーカトラスⅡ
	消費ＭＰ = 60
	効果数値 = 300
ELSE
	アビ名 = シーカトラス
	消費ＭＰ = 50
	効果数値 = 200
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力×{効果数値}％］の水属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>味方全体に［30％］の奥義ゲージ増加を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;攻撃部分
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性
		
		;奥義ゲージ部分
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 30
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

;タービュラントショット
@固有アビ_46_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果数値２


IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = タービュラントショット＋＋
	効果数値 = 250
	消費ＭＰ = 120
	効果数値２ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = タービュラントショット＋
	効果数値 = 200
	消費ＭＰ = 100
	効果数値２ = 65
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
	アビ名 = タービュラントショット
	効果数値 = 150
	消費ＭＰ = 80
	効果数値２ = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕{効果数値}＋（攻撃対象のチャージ✕{効果数値２}）％］の水属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>味方全体に［攻撃対象のチャージ✕10％］（Ａ枠・3ターン）の命中率上昇効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		LOCAL = 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン
		アビ汎用変数:効果割合 = 100 + (LOCAL * 効果数値２)
		CALL アビテンプレート_ダメージ処理_水属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 命中率A
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		アビ汎用変数:効果量 = LOCAL * 10
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

;オーシャンセンス
@固有アビ_46_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIMS アビ名
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	アビ名 = オーシャンセンス＋
	消費ＭＰ = 150
	効果数値 = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = オーシャンセンス
	消費ＭＰ = 100
	効果数値 = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>味方全体に［{効果数値}％］（通常枠・3ターン）の水属性攻撃上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値}％］（Ａ枠・3ターン）の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		;弱体・攻撃部分
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ

		アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_水
		アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_水
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:基礎成功確率 = 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT
;---------------------------

@固有バフ・デバフ処理_キャラ_46_与ダメージ(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象の種族に水棲があるなら
IF 戦闘行動対象 >= 10 && STRFINDU(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "水棲") >= 0
	;ダメージ１．３倍
	RETURN ダメージ値 * 13 / 10
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値

@バフ・デバフ特殊表示_ドゥルガ漁団頭領(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ドゥルガ漁団頭領
PRINTFORML 水棲系エネミーに対して与えるダメージが1.3倍になる
PRINTFORML 持続ターン：永続

;--------------------------------------------------
@口上_ステータス画面_固有特性解説_46(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『ドゥルガ漁団頭領』 □-------------------------------------------------------------------------------
KSTR:(K++) = 水棲系エネミーに対して与えるダメージが1.3倍になる
