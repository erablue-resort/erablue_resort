;ダブル・ドラゴン・トビゲリ：ニンジャスレイヤー*ユカノ
@固有チェインバースト処理_1206(対象キャラ, 隊列番号)
#DIM 対象キャラ
#DIM 隊列番号
#DIM チェンバ威力
#DIMS 奥義名

;2回以上奥義を撃っていた場合、敵全体にダメージ
;まず条件分岐を入れる。条件を満たしていない場合、RETURN 0で通常のチェンバ処理へ戻る

;チェインバーストに参加しているキャラを取る時、は「チェンバ基準キャラ保存:0~4」に隊列番号が入っている
;この処理はチェンバに参加したキャラすべてを見るが、「ターン内で奥義を撃った順」に見ていき、
;固有チェンバが見つかった時点で終わる。そのため早く奥義を撃ったキャラで固有チェンバを満たすと後ろは発動しないことに注意すること

;特定のキャラたちの組み合わせを見たい場合は「チェンバ組み合わせ確認()」という関数がある
;以下のような条件文にすれば良い
;IF チェンバ組み合わせ確認("[千年を探す者]ユエル", "[千年を継ぎし者]ソシエ")

IF チェンバ組み合わせ確認("[ナラク・ウィズイン]ニンジャスレイヤー", "[リライジング・ドラゴン]ドラゴン・ユカノ") == 0
	RETURN 0
ENDIF

;以下、通常のチェインバーストの処理をテンプレートとして記載
;必要部分を適宜変更のこと

;チェインバースト時の口上
TRYCALLFORM KOJO_DUNGEON_チェンバ発動時_{NO:(BATTLE_STATE:隊列番号:キャラ登録番号)}(BATTLE_STATE:隊列番号:キャラ登録番号)
CALL アビテンプレ変数リセット

;処理のために戦闘行動キャラへ代入
戦闘行動キャラ = 隊列番号

;ここでダメージに使うステータス数値を算出
;攻撃、防御、回復のうち一番高いステータスが採用される
CALL バフ込み数値算出("攻撃力")
LOCAL = RESULT
LOCALS = 攻撃力
CALL バフ込み数値算出("防御力")
IF LOCAL < RESULT
	LOCAL = RESULT
	LOCALS = 防御力
ENDIF
CALL バフ込み数値算出("回復力")
IF LOCAL < RESULT
	LOCAL = RESULT
	LOCALS = 回復力
ENDIF

;ダメージ処理
CALL アビ対象選択テンプレート_敵全体_生者のみ
アビ汎用文字列:ダメージ種類 = チェンバダメージ
アビ汎用文字列:攻撃側使用能力値 = %LOCALS%
;威力
アビ汎用変数:効果割合 = 200 + ターン中奥義回数保存 * 10
;発動時メッセージ
アビ汎用文字列:実行時メッセージ１ = 「「イイイイヤアアアアーーーーッ！！」」
アビ汎用文字列:実行時メッセージ２ = 赤い二重螺旋軌道を描き、ダブル・ドラゴン・トビゲリが放たれた！！
;その他攻撃オプション
SETBIT アビ汎用変数:かばう無視オプション, 0
SETBIT アビ汎用変数:かばう無視オプション, 1
アビ汎用変数:命中率補正値 = 200
CALL アビテンプレート_ダメージ処理_自属性

;ここでカットイン処理、特殊画像を出さないなら触らなくてヨシ
CALL カットイン呼出("チェインバースト")
IF RESULT != -1
	WAIT
	CALL カットイン終了
ENDIF

RETURN 1
