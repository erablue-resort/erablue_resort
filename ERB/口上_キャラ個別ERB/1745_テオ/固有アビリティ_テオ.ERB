
@固有アビ_1745_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM テオ隊列
#DIM 効果数値1
#DIM 効果数値2

テオ隊列 = キャラ隊列検索(GETCHARA(1745))
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = みんなが笑える日まで
	消費ＭＰ = 20
	効果数値1 = 50
	効果数値2 = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	アビ名 = 獅子族の力＋＋
	消費ＭＰ = 20
	効果数値1 = 50
	効果数値2 = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
	アビ名 = 獅子族の力＋
	消費ＭＰ = 20
	効果数値1 = 30
	効果数値2 = 50
ELSE
	アビ名 = 獅子族の力
	消費ＭＰ = 20
	効果数値1 = 30
	効果数値2 = 33
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自分の攻撃力を{効果数値1}％/５Ｔ上昇させる。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv <= 35
				詳細文文字列受け渡し変数 += @"<br>【デメリット】自分のＨＰを{効果数値2}％消費する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 獅子族の力
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			アビ汎用文字列:バフ・デバフ枠 = みんなが笑える日まで
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 基礎BATTLE_STATE:キャラ番号:Lv <= 35
			IF BATTLE_STATE:テオ隊列:ＨＰ <= BATTLE_STATE:テオ隊列:ＨＰ * 効果数値2 / 100
				BATTLE_STATE:テオ隊列:ＨＰ = 1
			ELSE
				BATTLE_STATE:テオ隊列:ＨＰ = BATTLE_STATE:テオ隊列:ＨＰ * 効果数値2 / 100
			ENDIF
		ENDIF
ENDSELECT

@固有アビ_1745_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM テオ隊列
#DIM 効果数値1
#DIM 効果数値2

テオ隊列 = キャラ隊列検索(GETCHARA(1745))
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 27
	アビ名 = 百獣の王鍵＋＋
	消費ＭＰ = 50
	効果数値1 = 33
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 百獣の王鍵＋
	消費ＭＰ = 75
	効果数値1 = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 7
	アビ名 = 百獣の王鍵
	消費ＭＰ = 75
	効果数値1 = 10
ELSE
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体の攻撃力と防御力を{効果数値1}％/３Ｔ上昇させる。

		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値1
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値1
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

ENDSELECT

@固有アビ_1745_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM テオ隊列
#DIM 効果数値1
#DIM 効果数値2

テオ隊列 = キャラ隊列検索(GETCHARA(1745))
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 34
	アビ名 = 優秀な付き人
	消費ＭＰ = 35
	効果数値1 = 33
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 21
	アビ名 = 付き人＋
	消費ＭＰ = 45
	効果数値1 = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 13
	アビ名 = 付き人
	消費ＭＰ = 45
	効果数値1 = 10
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 34
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自分と自分以外の味方一人の連続攻撃確率を{効果数値1}％/５Ｔ上昇させる。
	ELSE
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自分以外の味方一人の連続攻撃確率を{効果数値1}％/５Ｔ上昇させる。
	ENDIF

	RETURN 消費ＭＰ
	CASE "アビ効果"

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ_自己以外
		アビ汎用変数:効果量 = 効果数値1
		アビ汎用文字列:バフ・デバフ枠 = DA率A
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		アビ汎用変数:効果量 = 効果数値1
		アビ汎用文字列:バフ・デバフ枠 = TA率A
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		IF BATTLE_STATE:テオ隊列:Lv >= 34
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 効果数値1
			アビ汎用文字列:バフ・デバフ枠 = DA率A
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 効果数値1
			アビ汎用文字列:バフ・デバフ枠 = TA率A
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF

ENDSELECT

@固有通常攻撃後処理_キャラ_1745(全滅フラグ, 連撃数)
#DIM キャラ番号
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM テオ隊列
#DIM リーダー特性
#DIM マナボ１
#DIM マナボ２
#DIM パワーフリップ性能増加

SIF 全滅フラグ
	戦闘中カウント:テオ隊列:与ヒット数合計 = 0
SIF 全滅フラグ
	RETURN 0

テオ隊列 = キャラ隊列検索(GETCHARA(1745))

IF 連撃数 == 3
	CALL アビテンプレート用_表示メッセージ変換処理
	アビテンプレート用_アビ名表示済フラグ = 0

	アビ汎用文字列:実行時メッセージ１ = 「てやあっ！」
	アビ汎用文字列:実行時メッセージ２ = テオのパワーフリップ！
	CALL アビ汎用変数文字列リセット
	IF バフ存在判定_枠("キミを見つけたよ",テオ隊列)
		アビ汎用変数:効果割合 = 250 + (戦闘中カウント:テオ隊列:0 * 150)
	ELSE
		アビ汎用変数:効果割合 = 150 + (戦闘中カウント:テオ隊列:0 * 150)
	ENDIF
	;LS適用されているかどうか。テオリーダーなら100%強化の250%、そうでなければ150%が基礎威力になる。
	SIF テオ隊列 == 0
		リーダー特性 = MIN(60, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 3)
		;リーダー特性適用で１５ヒット毎に３％加算
	SIF BATTLE_STATE:テオ隊列:Lv >= 30
		マナボ１ = MIN(40, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 2)
		;マナボ１解放で１５ヒット毎に３％加算
	IF BATTLE_STATE:テオ隊列:Lv >= 80
		マナボ２ = MIN(60, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 3)
		;パワフリの匠
	ELSEIF BATTLE_STATE:テオ隊列:Lv >= 70
		マナボ２ = MIN(40, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 2)
		;パワフリの鉄人
	ELSEIF BATTLE_STATE:テオ隊列:Lv >= 60
		マナボ２ = MIN(20, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 1)
		;パワフリの達人
	ELSE
		;Lv６０いってなければマナボ２の補正は無し。
	ENDIF
	;ここまでパッシブによるパワフリ補正処理。一番しんどい！！きくうしさん本当にありがとうございます。
	アビ汎用変数:効果割合 = アビ汎用変数:効果割合 * (リーダー特性 + マナボ１ + マナボ２ + 100) / 100
	;ここでダメージ計算おわり。長い！！
	戦闘中カウント:テオ隊列:0 += 1
	CALL アビテンプレート_ダメージ処理_光属性
	
	;ここで連続パワフリ数を増やしている。
ELSEIF 連撃数 < 3
	戦闘中カウント:テオ隊列:0 = 0
ENDIF

@固有戦闘開始時処理_キャラ_1745
#DIM パワーフリップ性能増加
#DIM テオ隊列
#DIM 対象番号
#DIM 現在属性

;リーダー特性

テオ隊列 = キャラ隊列検索(GETCHARA(1745))

IF テオ隊列 == 0
	FOR LOCAL, 0, 4
		SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
			CONTINUE
		;生者のみ
		SIF BATTLE_STATE:LOCAL:ＨＰ < 1
			CONTINUE
		;対象属性（今回は光）かどうか
		IF BATTLE_STATE:LOCAL:属性 == 4
			CALL アビ対象選択テンプレート_指定(LOCAL, 0)
			アビ汎用文字列:バフ・デバフ枠 = キミを見つけたよ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 30
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
	NEXT
ENDIF
CALL アビテンプレ変数リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = パワーフリップ性能増加
アビ汎用文字列:バフ・デバフ対象能力 = パワーフリップ性能増加
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット
SIF BATTLE_STATE:テオ隊列:Lv >= 80
	CALL アビテンプレート_有利効果_ウェポンバースト
;メッセージ出さない用に保存文字列をリセット
CALL アビテンプレ変数リセット

@バフ・デバフ特殊表示_キミを見つけたよ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM テオ隊列
テオ隊列 = キャラ隊列検索(GETCHARA(1745))
VARSET LOCALS

PRINTFORML キミを見つけたよ
PRINTFORML 攻撃力＋３０％

@バフ・デバフ特殊表示_パワーフリップ性能増加(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM テオ隊列
#DIM リーダー特性
#DIM マナボ１
#DIM マナボ２
VARSET LOCALS

テオ隊列 = キャラ隊列検索(GETCHARA(1745))

SIF テオ隊列 == 0
	リーダー特性 = MIN(60, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 3)
	;リーダー特性適用で１５ヒット毎に３％加算
SIF BATTLE_STATE:テオ隊列:Lv >= 30
	マナボ１ = MIN(40, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 2)
	;マナボ１解放で１５ヒット毎に３％加算
IF BATTLE_STATE:テオ隊列:Lv >= 80
	マナボ２ = MIN(60, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 3)
	;パワフリの匠
ELSEIF BATTLE_STATE:テオ隊列:Lv >= 70
	マナボ２ = MIN(40, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 2)
	;パワフリの鉄人
ELSEIF BATTLE_STATE:テオ隊列:Lv >= 60
	マナボ２ = MIN(20, 戦闘中カウント:テオ隊列:与ヒット数合計 / 15 * 1)
	;パワフリの達人
ELSE
	;Lv６０いってなければマナボ２の補正は無し。
ENDIF
;補正値取得

PRINTFORML パワーフリップ性能アップ
IF テオ隊列 == 0
	PRINTFORML ・キミを見つけたよ
	PRINTFORML 自身のパワーフリップダメージ＋１００％
	PRINTFORML 与ヒット数合計１５毎に、自身のパワーフリップダメージ＋３％（最大６０％） 現在{リーダー特性}％
ELSE
ENDIF
;リーダー特性表示
	PRINTFORML 
IF BATTLE_STATE:テオ隊列:Lv >= 30
	PRINTFORML ・百獣の王冠
	PRINTFORML 与ヒット数合計１５毎に、自身のパワーフリップダメージ＋２％（最大４０％） 現在{マナボ１}％
ELSE
ENDIF
;マナボ１表示
	PRINTFORML 
IF BATTLE_STATE:テオ隊列:Lv >= 80
	PRINTFORML ・パワフリの匠
	PRINTFORML 与ヒット数合計１５毎に、自身のパワーフリップダメージ＋３％（最大６０％） 現在{マナボ２}％
ELSEIF BATTLE_STATE:テオ隊列:Lv >= 70
	PRINTFORML ・パワフリの鉄人
	PRINTFORML 与ヒット数合計１５毎に、自身のパワーフリップダメージ＋２％（最大４０％） 現在{マナボ２}％
ELSEIF BATTLE_STATE:テオ隊列:Lv >= 60
	PRINTFORML ・パワフリの達人
	PRINTFORML 与ヒット数合計１５毎に、自身のパワーフリップダメージ＋１％（最大２０％） 現在{マナボ２}％
ELSE
ENDIF
;マナボ２表示


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1745(対象キャラ)
#DIM 対象キャラ

CALL 口上変数初期化
KSTR:(K++) = □ リーダー特性：『キミを見つけたよ』 □----------------------------------------------------------
KSTR:(K++) = 　◆1番目に編成時、光属性の味方の攻撃力が上昇/パワーフリップの性能アップ
KSTR:(K++) = 　◆1番目に編成時、与ヒット数合計15毎に、自身のパワーフリップダメージ＋３％（最大６０％）
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『クロスブルーフリッパー：格闘』 □-------------------------------------------
KSTR:(K++) = 　トリプルアタック・奥義発動時、パワーフリップ：格闘が発動
KSTR:(K++) = 　◆連続でトリプルアタック時、パワーフリップの性能アップ
KSTR:(K++) = 　
IF 基礎BATTLE_STATE:対象キャラ:Lv >= 30
KSTR:(K++) = □ 特性：『百獣の王冠』 □-------------------------------------------
KSTR:(K++) = 　与ヒット数合計１５毎に、自身のパワーフリップダメージ＋２％（最大４０％）
SIF 基礎BATTLE_STATE:対象キャラ:Lv >= 80
KSTR:(K++) = 　戦闘開始時、自身の奥義ゲージを１００％にする。
KSTR:(K++) = 　
ELSE
ENDIF
IF 基礎BATTLE_STATE:対象キャラ:Lv >= 80
	KSTR:(K++) = □ 特性：『パワフリの匠』 □-------------------------------------------
	KSTR:(K++) = 　与ヒット数合計１５毎に、自身のパワーフリップダメージ＋３％（最大６０％）
	KSTR:(K++) = 　
ELSEIF 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = □ 特性：『パワフリの鉄人』 □-------------------------------------------
	KSTR:(K++) = 　与ヒット数合計１５毎に、自身のパワーフリップダメージ＋２％（最大４０％）
	KSTR:(K++) = 　
ELSEIF 基礎BATTLE_STATE:対象キャラ:Lv >= 60
	KSTR:(K++) = □ 特性：『パワフリの達人』 □-------------------------------------------
	KSTR:(K++) = 　与ヒット数合計１５毎に、自身のパワーフリップダメージ＋１％（最大２０％）
	KSTR:(K++) = 　
ELSE
ENDIF

CALL 口上変数表示(対象キャラ, 1)


;このへん雑多メモ

;優秀な付き人、百獣の王鍵（全体両面バフ）、獅子族の力（デメリット付き攻撃バフ）
;

;12/26
;うお～～～～～アビ名悩む～～～～～～～
;折角のテオきゅんだから凝ったものにしてあげてえ！効果も凝りてえ！
;こちとらテオきゅん実装と同時にワーフリ復帰してそこから終末まで見届けたからな
;限定テオきゅんもしっかりゲットしてLv100にしたんだからな！！

;12/28
;モンエナキメすぎて無気力になっちゃった

;12/29
;きくうしさんのお陰でパワフリが滅茶苦茶整いました
