
@固有アビ_417_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 劇毒番号
#DIM 劇毒判定
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(417))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 流刀蛇尾+
	IF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ELSE
	アビ名 = 流刀蛇尾
	IF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ENDIF

FOR LOCAL, 10, 20
	劇毒番号 = デバフ番号取得_枠("劇毒", LOCAL)
	IF DT_CELL_GET("戦闘効果データベース", 劇毒番号, "効果量_固定値") > 6
		劇毒判定 = 1
		BREAK
	ENDIF
NEXT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に闇属性［攻撃力×300％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象にDA率［10％］累積ダウン効果［累積枠/最大40％/5ターン］"
		詳細文文字列受け渡し変数 += @"<br>対象にTA率［10％］累積ダウン効果［累積枠/最大40％/5ターン］"
		詳細文文字列受け渡し変数 += @"<br>対象の劇毒Lvが1上昇［最大Lv10/5ターン/回復不可］"
		詳細文文字列受け渡し変数 += @"<br>◆劇毒Lv7以上の敵がいる時、2回発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体
		アビ汎用変数:効果割合 = 300
		CALL アビテンプレート_ダメージ処理_闇属性
		SIF 劇毒判定 == 1
			CALL アビテンプレート_ダメージ処理_闇属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = DA率_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		SIF 劇毒判定 == 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = TA率_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		SIF 劇毒判定 == 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 劇毒
		アビ汎用文字列:バフ・デバフ対象能力 = 劇毒
		アビ汎用変数:効果量 = 1
		アビ汎用変数:累積上限_固定値 = 10
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		SIF 劇毒判定 == 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 劇毒_毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:付随量 = 50
		CALL アビテンプレート_不利効果_付随デバフ効果セット("劇毒")
		
		IF バフ番号取得_枠("不休活期", キャラ隊列検索(GETCHARA(417))) > -1
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体
			アビ汎用変数:効果割合 = 300
			CALL アビテンプレート_ダメージ処理_闇属性
			SIF 劇毒判定 == 1
				CALL アビテンプレート_ダメージ処理_闇属性
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = DA率_累積枠
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:累積上限_割合 = 40
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット
			SIF 劇毒判定 == 1
				CALL アビテンプレート_不利効果_累積デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = TA率_累積枠
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:累積上限_割合 = 40
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_累積デバフ効果セット
			SIF 劇毒判定 == 1
				CALL アビテンプレート_不利効果_累積デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 劇毒
			アビ汎用文字列:バフ・デバフ対象能力 = 劇毒
			アビ汎用変数:効果量 = 1
			アビ汎用変数:累積上限_固定値 = 10
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_不利効果_累積デバフ効果セット
			SIF 劇毒判定 == 1
				CALL アビテンプレート_不利効果_累積デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 劇毒_毒
			アビ汎用文字列:バフ・デバフ対象能力 = 毒
			アビ汎用変数:付随量 = 50
			CALL アビテンプレート_不利効果_付随デバフ効果セット("劇毒")
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有アビ_417_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 劇毒番号
#DIM 劇毒判定
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(417))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = 蜿蜒長蛇+
	IF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 250
	ENDIF
ELSE
	アビ名 = 蜿蜒長蛇
	IF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ENDIF

FOR LOCAL, 10, 20
	劇毒番号 = デバフ番号取得_枠("劇毒", LOCAL)
	IF DT_CELL_GET("戦闘効果データベース", 劇毒番号, "効果量_固定値") > 6
		劇毒判定 = 1
		BREAK
	ENDIF
NEXT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ターゲットに関わらず4回闇属性［攻撃力×100％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージ［10％］アップ"
		詳細文文字列受け渡し変数 += @"<br>◆劇毒Lv7以上の敵がいる時、ダメージ回数2倍"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		IF 劇毒判定 == 1
			FOR LOCAL, 0, 8
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 100
				CALL アビテンプレート_ダメージ処理_闇属性
			NEXT
		ELSE
			FOR LOCAL, 0, 4
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 100
				CALL アビテンプレート_ダメージ処理_闇属性
			NEXT
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 10
		CALL アビテンプレート_有利効果_奥義ゲージUP
		
		IF バフ番号取得_枠("不休活期", キャラ隊列検索(GETCHARA(417))) > -1
			CALL アビ汎用変数文字列リセット
			IF 劇毒判定 == 1
				FOR LOCAL, 0, 8
					CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
					アビ汎用変数:効果割合 = 100
					CALL アビテンプレート_ダメージ処理_闇属性
				NEXT
			ELSE
				FOR LOCAL, 0, 4
					CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
					アビ汎用変数:効果割合 = 100
					CALL アビテンプレート_ダメージ処理_闇属性
				NEXT
			ENDIF
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有アビ_417_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号
#DIM 不休活期番号
#DIM 短縮後効果ターン
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(417))


;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 常山蛇勢
	消費ＭＰ = 300
ENDIF

;不休活期効果が残り3ターン以下の場合発動不可
IF 隊列番号 > -1
	不休活期番号 = バフ番号取得_枠("不休活期", 隊列番号)
	IF 不休活期番号 > -1 && DT_CELL_GET("戦闘効果データベース", 不休活期番号, "持続ターン") > 3
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に常山蛇勢効果［5ターン/消去不可］
		詳細文文字列受け渡し変数 += @"<br>自身が即時奥義発動可能"
		詳細文文字列受け渡し変数 += @"<br>◆自身の不休活期効果が3ターン短縮/このアビリティは行動回数を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 常山蛇勢
		アビ汎用文字列:バフ・デバフ対象能力 = 常山蛇勢
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 常山蛇勢_アビ与ダメ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果割合 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("常山蛇勢")
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		
		短縮後効果ターン = DT_CELL_GET("戦闘効果データベース", 不休活期番号, "持続ターン") - 3
		DT_CELL_SET "戦闘効果データベース", 不休活期番号, "持続ターン", 短縮後効果ターン
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有戦闘開始時処理_キャラ_417

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 巳神宮の主
アビ汎用文字列:バフ・デバフ対象能力 = 状態異常耐性_毒
アビ汎用変数:効果量 = 1000
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不休活期
アビ汎用文字列:バフ・デバフ対象能力 = 不休活期
アビ汎用変数:持続ターン = 12
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不休活期_弱体無効
アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
CALL アビテンプレート_有利効果_付随バフ効果セット("不休活期")

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 夜刀守巳
アビ汎用文字列:バフ・デバフ対象能力 = 夜刀守巳
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビテンプレ変数リセット



@固有行動直後処理_バフ・デバフ_夜刀守巳(効果番号)
#DIM 効果番号
#DIM 行動キャラ保存
#DIM 隊列番号
#DIMS 夜刀守巳

;その行動で劇毒を付けてないなら戻す
SIF STRFIND(アビテンプレート用_付与デバフ保存, "劇毒") < 0
    RETURN 0

;インダラが前衛にいないなら戻す
隊列番号 = キャラ隊列検索(キャラ検索("[南南東の守護神]インダラ"))
SIF 隊列番号 < 0 || 隊列番号 > 3
    RETURN 0

;一時的に戦闘行動キャラをインダラに渡す
SWAP 戦闘行動キャラ, 行動キャラ保存
戦闘行動キャラ = 隊列番号

;凶疫付与処理
アビテンプレート用_アビ名 = 夜刀守巳

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
アビ汎用文字列:バフ・デバフ枠 = 凶疫
アビ汎用文字列:バフ・デバフ対象能力 = 凶疫
アビ汎用変数:効果量 = 1
アビ汎用変数:累積上限_固定値 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_不利効果_累積デバフ効果セット
		
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 凶疫_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:付随割合 = 2
CALL アビテンプレート_不利効果_付随デバフ効果セット("凶疫")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 凶疫_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:付随割合 = 2
CALL アビテンプレート_不利効果_付随デバフ効果セット("凶疫")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 凶疫_被ダメ上昇
アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
アビ汎用変数:付随量 = 100
CALL アビテンプレート_不利効果_付随デバフ効果セット("凶疫")

;戦闘行動キャラを戻す
SWAP 戦闘行動キャラ, 行動キャラ保存



@固有ターン終了時処理_バフ_不休活期(効果番号)
#DIM 効果番号
#DIM 対象番号

IF DT_CELL_GET("戦闘効果データベース", 効果番号, "持続ターン") == 1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 睡眠
	アビ汎用文字列:バフ・デバフ対象能力 = 睡眠
	アビ汎用変数:持続ターン = 2
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:弱体耐性貫通オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 睡眠_封印
	アビ汎用文字列:バフ・デバフ対象能力 = 封印
	アビ汎用変数:効果割合 = 100
	アビ汎用変数:弱体耐性貫通オプション = 1
	CALL アビテンプレート_不利効果_付随デバフ効果セット("睡眠")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 睡眠_被ダメ上昇
	アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
	アビ汎用変数:効果割合 = 25
	アビ汎用変数:弱体耐性貫通オプション = 1
	CALL アビテンプレート_不利効果_付随デバフ効果セット("睡眠")
ENDIF



@固有ターン終了時処理_デバフ_凶疫(効果番号)
#DIM 効果番号
#DIM 対象番号
#DIM バフ番号
#DIM 短縮後効果ターン

IF DT_CELL_GET("戦闘効果データベース", 効果番号, "効果量_固定値") == 10
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		;バフでないならスキップ
		SIF DT_CELL_GETS("戦闘効果データベース", バフ番号, "バフ・デバフフラグ") != "バフ"
			CONTINUE
		;消去不可バフならスキップ
		SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "消去不可フラグ") == 1
			CONTINUE
		;残り効果ターンが1ターン以下ならスキップ
		SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") < 2
			CONTINUE
		
		短縮後効果ターン = DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") - 1
		DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", 短縮後効果ターン
	NEXT
ENDIF



@バフ・デバフ特殊表示_不休活期(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 不休活期
PRINTFORML 弱体効果を無効化/流刀蛇尾と蜿蜒長蛇が2回発動/効果終了時、自身に睡眠効果を付与する状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_睡眠(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 睡眠
PRINTFORML 行動が封じられ、被ダメージが増加する状態［回復不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_劇毒(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 劇毒
PRINTFORML 毎ターンダメージを受ける状態［最大Lv10/回復不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_常山蛇勢(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 常山蛇勢
PRINTFORML アビリティの与ダメージが上昇する状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_凶疫(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 凶疫
PRINTFORML Lvに応じて攻撃力・防御力が減少、被ダメージが増加する状態［最大Lv10/回復不可］
PRINTFORML Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")}　　　　　持続ターン：永続



;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_417(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『巳神宮の主』 □------------------------------------------------------------
KSTR:(K++) = 　弱体効果「毒」を無効化
KSTR:(K++) = 　バトル開始時、自身に不休活期効果［12ターン/消去不可］
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『夜刀守巳』 □-------------------------------------------------
KSTR:(K++) = 　味方が劇毒Lvを上昇させた時、対象の凶疫Lvが1上昇［最大Lv10/永続/回復不可］
KSTR:(K++) = 　◆凶疫Lvが10の時、その敵の消去可能な強化効果を毎ターン短縮

CALL 口上変数表示(対象キャラ, 1)

