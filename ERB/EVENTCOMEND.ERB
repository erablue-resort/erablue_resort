@EVENTCOM
;ターン終了の処理


@EVENTCOMEND0_通常モード
#DIM 食事回復量
#DIM 料理ランク一時保存
#DIM 料理属性一時保存, 10
#DIM モード中キャラカウント
#DIM 回数保存
#DIM 時間保存
#DIM 表示候補, 1000
#DIM TARGET保存
#DIM PLAYER保存
#DIM 訪問キャラ保存

IF TFLAG:オートコマンドフラグ || TFLAG:オートコマンド連続発動
ELSE
	;処理の都合上、身体に触れる派生は履歴に載せない
	SIF SELECTCOM == 306
		RSTR:SELECTCOM_S受け渡し = 

	;コマンド履歴の保存
	コマンド履歴:19 = コマンド履歴:18
	コマンド履歴:18 = コマンド履歴:17
	コマンド履歴:17 = コマンド履歴:16
	コマンド履歴:16 = コマンド履歴:15
	コマンド履歴:15 = CFLAG:MASTER:現在マップ種別
	コマンド履歴:14 = コマンド履歴:13
	コマンド履歴:13 = コマンド履歴:12
	コマンド履歴:12 = コマンド履歴:11
	コマンド履歴:11 = コマンド履歴:10
	コマンド履歴:10 = CFLAG:MASTER:現在位置
	コマンド履歴:9 = コマンド履歴:8
	コマンド履歴:8 = コマンド履歴:7
	コマンド履歴:7 = コマンド履歴:6
	コマンド履歴:6 = コマンド履歴:5
	コマンド履歴:5 = TARGET
	コマンド履歴:4 = コマンド履歴:3
	コマンド履歴:3 = コマンド履歴:2
	コマンド履歴:2 = コマンド履歴:1
	コマンド履歴:1 = コマンド履歴:0
	IF RSTR:SELECTCOM_S受け渡し == ""
		コマンド履歴:0 = SELECTCOM
	ELSE
		コマンド履歴:0 = -1
	ENDIF
	派生コマンド履歴:4 = %派生コマンド履歴:3%
	派生コマンド履歴:3 = %派生コマンド履歴:2%
	派生コマンド履歴:2 = %派生コマンド履歴:1%
	派生コマンド履歴:1 = %派生コマンド履歴:0%
	派生コマンド履歴:0 = %RSTR:SELECTCOM_S受け渡し%
	
	SELECTCOM_NAME履歴:4 = %SELECTCOM_NAME履歴:3%
	SELECTCOM_NAME履歴:3 = %SELECTCOM_NAME履歴:2%
	SELECTCOM_NAME履歴:2 = %SELECTCOM_NAME履歴:1%
	SELECTCOM_NAME履歴:1 = %SELECTCOM_NAME履歴:0%
	SELECTCOM_NAME履歴:0 = %SELECTCOM_NAME%

	;連続記録、とりあえずここにベタ置き
	IF SELECTCOM != 325 || (SELECTCOM == 325 && RFLAG:コマンド結果受渡し変数４ == -1)
		TFLAG:乳首当て連続記録 = 0
	ENDIF

	;TARGET回数記録
	回数保存 = 0
	IF GETBIT(FLAG:モード管理, モードビット_指示)
		;指示モード時は基本記録しないが、TARGETがMASTERの場合のみPLAYER側の回数を増加させる
		IF TARGET == MASTER
			回数保存 = CFLAG:PLAYER:コマンドタゲ回数記録
			CFLAG:PLAYER:コマンドタゲ回数記録 ++
			CALL コマンド回数履歴記録(PLAYER, 回数保存)
		ENDIF
	ENDIF
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		;同時モード時は全員分の回数を増加させる
		FOR LOCAL, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:LOCAL < 0
				BREAK
			回数保存 = CFLAG:(同時モード_選択キャラ:LOCAL):コマンドタゲ回数記録
			CFLAG:(同時モード_選択キャラ:LOCAL):コマンドタゲ回数記録 ++
			CALL コマンド回数履歴記録(同時モード_選択キャラ:LOCAL, 回数保存)
		NEXT
	ELSEIF TARGET > 0
		;通常時はTARGETがいる場合回数を増加させる
		回数保存 = CFLAG:TARGET:コマンドタゲ回数記録
		CFLAG:TARGET:コマンドタゲ回数記録 ++
		CALL コマンド回数履歴記録(TARGET, 回数保存)
	ENDIF
ENDIF

;射精場所記録
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF TCVAR:LOCAL:現ターン被射精場所
		TCVAR:LOCAL:前ターン被射精場所 = TCVAR:LOCAL:現ターン被射精場所
	ELSE
		;オートコマンド時、前ターンフラグの方は初期化しない
		SIF !TFLAG:オートコマンドフラグ && !TFLAG:オートコマンド連続発動
			TCVAR:LOCAL:前ターン被射精場所 = 0
	ENDIF
	TCVAR:LOCAL:現ターン被射精場所 = 0
NEXT

訪問キャラ保存 = 0
IF 訪問先場所番号
	訪問キャラ保存 = AUTOCOM_実行番号保存
ENDIF

;リセット前に次ターン自動行動をするか判定
IF TFLAG:オートコマンドフラグ
	TFLAG:前ターンオートフラグ = TFLAG:オートコマンドフラグ + (AUTOCOM_対象番号保存 * 100000)
ELSE
	TFLAG:前ターンオートフラグ = 0
ENDIF
AUTOCOM_実行番号保存 = -1
AUTOCOM_対象番号保存 = -1
;オート判定
FLAG:オートコマンド判定中フラグ = 1
;一旦保存
TARGET保存 = TARGET
PLAYER保存 = PLAYER
CALL BEFORE_AUTOCOM
;判定後戻す
SWAP TARGET保存, TARGET
SWAP PLAYER保存, PLAYER
TFLAG:オートコマンドフラグ = RESULT
FLAG:オートコマンド判定中フラグ = 0


;キャラごとの特殊処理を呼び出す
TRYCALLFORM TURNEND_CHARAEVENT_{NO:TARGET}

;初うふふ突入時処理
IF フレーバー素質:TARGET:素質表示設定 == 3 && TCVAR:TARGET:初うふふフラグ
	PRINTL
	PRINTW 初うふふ突入のため、フレーバー素質設定画面を開きます。
	CALL フレーバー素質個別設定画面(TARGET)
ENDIF

;諸々リセット
CALL TURN_RESET
COM_RAND = RAND:1000

;自室訪問時、遊びに行く際の処理
;リセットの後に置くことで、前ターン位置記録の後に処理している
IF 訪問先場所番号
	CFLAG:訪問キャラ保存:現在位置 = 訪問先場所番号
	CFLAG:PLAYER:現在位置 = 訪問先場所番号
	訪問先場所番号 = 0
ENDIF


;パンツ収集要素……とりあえず封印
;パンツ没収、収集
;IF TFLAG:調教モード < 2
;	IF TARGET > 0 && !CFLAG:睡眠
;		IF STRLENS(CSTR:MASTER:12) && RAND:2
;			PRINTL 
;			PRINTFORMW %CALLNAME:TARGET%に見とがめられ身体検査をされた…
;			PRINTFORMW 
;			PRINTFORMW 
;			PRINTFORMW 
;			PRINTFORMW くすねたパンティを没収された
;			SIF !GETBIT (CFLAG:10 ,0)
;				PRINTFORMW %CALLNAME:TARGET%に弱みを握られた
;			SETBIT CFLAG:10 ,0
;			CSTR:MASTER:12 =
;		ENDIF
;	ENDIF
;ENDIF

;設定している食事メニューによって回復量設定
食事回復量 = 400
料理ランク一時保存 = 0
VARSET 料理属性一時保存
FOR LOCAL, 1, 6
	IF 現在設定メニュー:LOCAL
		料理ランク一時保存 += DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:LOCAL, "料理ランク", 1)
		料理属性一時保存:LOCAL = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:LOCAL, "料理属性", 1) + 100
	ENDIF
NEXT
食事回復量 += 料理ランク一時保存 * 10


FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;知識系素質の取得処理
	CALL 知識系素質の取得(LOCAL)
	;体力の回復
	IF スケジュール状態:LOCAL:状況 == スケジュール_実行中 && !CFLAG:LOCAL:うふふ
		SELECTCASE 移動スケジュール文字列:LOCAL:(スケジュール状態:LOCAL:現在スケジュール):スケジュール
			CASE "食事", "自炊"
				IF TIME < 1020 && !GETBIT(TCVAR:LOCAL:食事済みフラグ, 1)
					;17時前は昼食
					SELECTCASE MATCH(料理属性一時保存, 基礎BATTLE_STATE:LOCAL:属性 + 100)
						CASE 3
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 12 / 10)
						CASE 4
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 14 / 10)
						CASE 5
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 16 / 10)
						CASEELSE
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)))
					ENDSELECT
					SETBIT TCVAR:LOCAL:食事済みフラグ, 1
				ELSEIF TIME >= 1020 && !GETBIT(TCVAR:LOCAL:食事済みフラグ, 2)
					;17時以降は夕食
					SELECTCASE MATCH(料理属性一時保存, 基礎BATTLE_STATE:LOCAL:属性 + 100)
						CASE 3
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 12 / 10)
						CASE 4
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 14 / 10)
						CASE 5
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)) * 16 / 10)
						CASEELSE
							BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + (食事回復量 * 3 / (3 - TALENT:LOCAL:回復速度)))
					ENDSELECT
					SETBIT TCVAR:LOCAL:食事済みフラグ, 2
				ENDIF
			CASE "お風呂", "自室入浴"
				;回復早い、回復遅い
				BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + ((TIME - TIME:2) * 30 / (3 - TALENT:LOCAL:回復速度)))
		ENDSELECT
	ENDIF
NEXT

IF FLAG:お出かけ先フラグ > 0 && TIME > 1260
	TFLAG:オートコマンドフラグ = 0

	PRINTW （そろそろ帰宅する時間だ…　一日を終了します）
	;二日酔い処理のため、酩酊が残ってたら絶対値を負に
	BASE:MASTER:酩酊 = BASE:MASTER:酩酊 * -1

	CFLAG:MASTER:睡眠 = 1
	CFLAG:MASTER:就寝時刻 = DAY * 1440 + TIME
	CFLAG:MASTER:現在位置 = CFLAG:MASTER:自室位置
	CFLAG:MASTER:現在マップ種別 = 0
	IF CFLAG:TARGET:デート == MASTER + 100
		CALL デート採点処理(TARGET)
	ENDIF
	CALL うふふ終了処理("外出より帰宅")
	CALL 飲み会終了処理
	FLAG:一日終了 = 1
ELSEIF CFLAG:MASTER:酔いすぎ == 1
	TFLAG:オートコマンドフラグ = 0

	PRINTW （酔いつぶれてしまいました…　一日を終了します）
	;二日酔い処理のため、酩酊が残ってたら絶対値を負に
	BASE:MASTER:酩酊 = BASE:MASTER:酩酊 * -1

	CFLAG:MASTER:睡眠 = 1
	CFLAG:MASTER:就寝時刻 = DAY * 1440 + TIME
	IF TARGET > 0 && TALENT:恋慕 && BASE:体力 >= 1
		IF FLAG:お出かけ先フラグ > 0
			PRINTFORMW なんとかリゾートまで帰り着くことが出来た……
		ELSE
			PRINTFORMW %CALLNAME%が%CALLNAME:MASTER%を私室へと運んでくれた…
			CFLAG:現在位置 = CFLAG:MASTER:自室位置
			CFLAG:現在マップ種別 = 0
		ENDIF
		CFLAG:MASTER:現在位置 = CFLAG:MASTER:自室位置
		CFLAG:MASTER:現在マップ種別 = 0
		IF CFLAG:TARGET:デート == MASTER + 100
			CALL デート採点処理(TARGET)
		ENDIF
	ENDIF
	CALL うふふ終了処理("あなた泥酔")
	CALL 飲み会終了処理
	FLAG:一日終了 = 1
ELSEIF CFLAG:MASTER:起床予定時刻 > TIME && CFLAG:MASTER:起床予定時刻 - 120 < TIME
	TFLAG:オートコマンドフラグ = 0

	PRINTW （眠気が限界だ…　一日を終了します）
	;二日酔い処理のため、酩酊が残ってたら絶対値を負に
	BASE:MASTER:酩酊 = BASE:MASTER:酩酊 * -1

	CFLAG:MASTER:睡眠 = 1
	CFLAG:MASTER:就寝時刻 = DAY * 1440 + TIME
	IF TARGET > 0 && TALENT:恋慕 && BASE:体力 >= 1
		IF FLAG:お出かけ先フラグ > 0
			PRINTFORMW なんとかリゾートまで帰り着くことが出来た……
		ELSE
			PRINTFORMW %CALLNAME%が%CALLNAME:MASTER%を私室へと運んでくれた…
			CFLAG:現在位置 = CFLAG:MASTER:自室位置
			CFLAG:現在マップ種別 = 0
		ENDIF
		CFLAG:MASTER:現在位置 = CFLAG:MASTER:自室位置
		CFLAG:MASTER:現在マップ種別 = 0
		IF CFLAG:TARGET:デート == MASTER + 100
			CALL デート採点処理(TARGET)
		ENDIF
	ENDIF
	CALL うふふ終了処理("眠気限界")
	CALL 飲み会終了処理
	FLAG:一日終了 = 1
ENDIF
IF TARGET && RCVAR:不機嫌
	PRINTFORMW %CALLNAME:TARGET%を怒らせてしまった…
	IF AUTOCOM_実行番号保存 == TARGET
		TFLAG:オートコマンドフラグ = 0
		AUTOCOM_実行番号保存 = -1
		AUTOCOM_対象番号保存 = -1
		自慰ターゲット保存 = -1
	ENDIF
	CALL うふふ終了処理("不機嫌")
	CALL 飲み会終了処理
ENDIF
モード中キャラカウント = 0
FOR LOCAL, 1, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	IF BASE:(TARGET:LOCAL):体力 < 1 && TCVAR:(TARGET:LOCAL):食いしばりフラグ > 1
		BASE:(TARGET:LOCAL):体力 = 1
		TCVAR:(TARGET:LOCAL):食いしばりフラグ --
	ELSEIF BASE:(TARGET:LOCAL):体力 < 1 && !CFLAG:(TARGET:LOCAL):体力限界
		CALL 体力限界時画像表示処理(TARGET:LOCAL)
		PRINTFORML （%CALLNAME:(TARGET:LOCAL)%の体力が限界に来ています）
		TRYCALLFORM KOJO_EVENT_うふふ体力限界_{NO:(TARGET:LOCAL)}(TARGET:LOCAL)
		FORCEWAIT
		IF CFLAG:(TARGET:LOCAL):デート == MASTER + 100
			CALL デート採点処理(TARGET:LOCAL)
		ENDIF
		TCVAR:(TARGET:LOCAL):食いしばりフラグ = 0
		CFLAG:(TARGET:LOCAL):体力限界 = 1
		IF CFLAG:(TARGET:LOCAL):うふふ == 0 && CFLAG:(TARGET:LOCAL):飲み会 == 0
			CFLAG:(TARGET:LOCAL):一緒に遊ぶ = 0
			CFLAG:(TARGET:LOCAL):同行 = 0
			CFLAG:(TARGET:LOCAL):デート = 0
			CFLAG:(TARGET:LOCAL):隠密 = 0
		ENDIF
		EXP:(TARGET:LOCAL):ダウン経験 += 1
		;うふふ時間で履歴登録
		時間保存 = (TIME + 1440 * DAY) - CFLAG:(TARGET:LOCAL):うふふ開始時間記録
		IF 時間保存 > 300 && CFLAG:(TARGET:LOCAL):うふふ開始時間記録 > 0
			CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:(TARGET:LOCAL)%と%TOFULL(TOSTR(時間保存 / 60))%時間%TOFULL(TOSTR(時間保存 % 60))%分えっちした</font><img src='えっちハート'>","うふふ")
			CALL 履歴データベース登録(CFLAG:(TARGET:LOCAL):人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:MASTER%と%TOFULL(TOSTR(時間保存 / 60))%時間%TOFULL(TOSTR(時間保存 % 60))%分えっちした</font><img src='えっちハート'>","うふふ")
		ENDIF
		CFLAG:(TARGET:LOCAL):うふふ開始時間記録 = 0
		;経験取得表示スキップ用
		TCVAR:(TARGET:LOCAL):436 = EXP:(TARGET:LOCAL):ダウン経験
		;CALL CHARA_SLEEP, TARGET:LOCAL
		;CFLAG:(TARGET:LOCAL):うふふ = 0
		;CFLAG:(TARGET:LOCAL):飲み会 = 0
		;TCVAR:(TARGET:LOCAL):初うふふフラグ = 0
		;CALL 性欲ゲージ減少処理(TARGET:LOCAL)
		IF AUTOCOM_実行番号保存 == TARGET:LOCAL
			TFLAG:オートコマンドフラグ = 0
			AUTOCOM_実行番号保存 = -1
			AUTOCOM_対象番号保存 = -1
			自慰ターゲット保存 = -1
		ENDIF
		;オナホ時は自室に運ぶために現在地更新
		IF 追従中キャラ(TARGET:LOCAL)
			CFLAG:(TARGET:LOCAL):現在位置 = CFLAG:PLAYER:現在位置
			CFLAG:(TARGET:LOCAL):現在マップ種別 = CFLAG:PLAYER:現在マップ種別
		ENDIF
		;モードもリセット
		CALL 特定キャラモードリセット(TARGET:LOCAL)
		FLAG:モード管理 = 0
		TFLAG:あなたターゲット用 = 0
		VARSET 同時モード_選択キャラ, -1
		同時モード_選択数 = 0
		連れ込みパターン = 
	ENDIF
	SIF !CFLAG:(TARGET:LOCAL):体力限界
		モード中キャラカウント += 1
NEXT
IF CFLAG:MASTER:うふふ && モード中キャラカウント == 0
	CALL 呼び出す候補キャラ登録(表示候補)
	IF RESULT > 0 && BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !CFLAG:MASTER:隠密
		PRINTL
		PRINTFORML %CALLNAME:MASTER%の相手が全員ダウンしてしまった……
		PRINTFORML 新しい相手を呼び出しますか？
		PRINTBUTTON "[0] はい", 0
		PRINTL
		PRINTBUTTON "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			CALL COM210
			IF RESULT == -1
				CALL うふふ終了処理("全員ダウン")
			ELSE
				;呼び出した相手をTARGETに
				TFLAG:TARGET変更用 = RFLAG:コマンド結果受渡し変数
			ENDIF
		ELSE
			CALL うふふ終了処理("全員ダウン")
		ENDIF
	ELSE
		CALL うふふ終了処理("全員ダウン")
	ENDIF
ENDIF
モード中キャラカウント = 0
FOR LOCAL, 1, CHARANUM
	SIF 飲み会TARGET:LOCAL < 1
		BREAK
	IF BASE:(飲み会TARGET:LOCAL):体力 < 1 && TCVAR:(飲み会TARGET:LOCAL):食いしばりフラグ > 1
		BASE:(飲み会TARGET:LOCAL):体力 = 1
		TCVAR:(飲み会TARGET:LOCAL):食いしばりフラグ --
	ELSEIF BASE:(飲み会TARGET:LOCAL):体力 < 1
		CALL 体力限界時画像表示処理(飲み会TARGET:LOCAL)
		PRINTFORML （%CALLNAME:(飲み会TARGET:LOCAL)%の体力が限界に来ています）
		FORCEWAIT
		IF CFLAG:(飲み会TARGET:LOCAL):デート == MASTER + 100
			CALL デート採点処理(飲み会TARGET:LOCAL)
		ENDIF
		TCVAR:(飲み会TARGET:LOCAL):食いしばりフラグ = 0
		CFLAG:(飲み会TARGET:LOCAL):体力限界 = 1
		EXP:(飲み会TARGET:LOCAL):ダウン経験 += 1
		;経験取得表示スキップ用
		TCVAR:(飲み会TARGET:LOCAL):436 = EXP:(飲み会TARGET:LOCAL):ダウン経験
		CALL CHARA_SLEEP, 飲み会TARGET:LOCAL
		CFLAG:(飲み会TARGET:LOCAL):うふふ = 0
		CALL 性欲ゲージ減少処理(飲み会TARGET:LOCAL)
		CFLAG:(飲み会TARGET:LOCAL):飲み会 = 0
		TCVAR:(飲み会TARGET:LOCAL):初うふふフラグ = 0
		IF AUTOCOM_実行番号保存 == 飲み会TARGET:LOCAL
			TFLAG:オートコマンドフラグ = 0
			AUTOCOM_実行番号保存 = -1
			AUTOCOM_対象番号保存 = -1
			自慰ターゲット保存 = -1
		ENDIF
		;モードもリセット
		CALL 特定キャラモードリセット(飲み会TARGET:LOCAL)
		FLAG:モード管理 = 0
		TFLAG:あなたターゲット用 = 0
		VARSET 同時モード_選択キャラ, -1
		同時モード_選択数 = 0
	ENDIF
	SIF CFLAG:(飲み会TARGET:LOCAL):飲み会
		モード中キャラカウント += 1
NEXT
IF CFLAG:MASTER:飲み会 && モード中キャラカウント == 0
	CALL 飲み会終了処理
ENDIF
;非同室キャラのダウンを判定
FOR LOCAL, 1, CHARANUM
	SIF MATCH(TARGET, LOCAL) > 0
		CONTINUE
	SIF MATCH(飲み会TARGET, LOCAL) > 0
		CONTINUE
	SIF CFLAG:LOCAL:滞在期間 < 1
		CONTINUE
	IF BASE:LOCAL:体力 < 1 && TCVAR:LOCAL:食いしばりフラグ > 1
		BASE:LOCAL:体力 = 1
		TCVAR:LOCAL:食いしばりフラグ --
	ELSEIF BASE:LOCAL:体力 < 1 && !CFLAG:LOCAL:体力限界
		TCVAR:LOCAL:食いしばりフラグ = 0
		CFLAG:LOCAL:体力限界 = 1
		IF CFLAG:LOCAL:うふふ == 0 && CFLAG:LOCAL:飲み会 == 0
			CFLAG:LOCAL:一緒に遊ぶ = 0
			CFLAG:LOCAL:同行 = 0
			CFLAG:LOCAL:デート = 0
			CFLAG:LOCAL:隠密 = 0
		ENDIF
		EXP:LOCAL:ダウン経験 += 1
		CFLAG:LOCAL:うふふ開始時間記録 = 0
		;経験取得表示スキップ用
		TCVAR:LOCAL:436 = EXP:LOCAL:ダウン経験
		IF AUTOCOM_実行番号保存 == LOCAL
			TFLAG:オートコマンドフラグ = 0
			AUTOCOM_実行番号保存 = -1
			AUTOCOM_対象番号保存 = -1
			自慰ターゲット保存 = -1
		ENDIF
		;モードもリセット
		CALL 特定キャラモードリセット(LOCAL)
	ENDIF
NEXT

LOCAL = TIME - TIME:2
IF LOCAL > 0
	PRINTL
	PRINTFORML \@ LOCAL / 60 > 0?{LOCAL / 60}時間# \@{LOCAL % 60}分 経過
ENDIF
TIME:2 = TIME > 1440 ? TIME - 1440 # TIME

WAIT




@TURN_RESET
#DIM 同室番号
#DIM 登録番号
#DIM 分身番号
#DIM TARGET保存
#DIM COM保存
#DIMS キャラ衣装識別
#DIMS 母乳種別

SIF !TFLAG:オートコマンドフラグ
	自慰ターゲット保存 = -1

FOR LOCAL,0,CHARANUM
	CALL 処女童貞適用処理(LOCAL)

	;連れ込み満足
	IF RCVAR:LOCAL:連れ込み満足フラグ
		連れ込みパターン = 
		TCVAR:LOCAL:連れ込み済フラグ = 1
	ENDIF
	
	;妊娠率補正加算
	TRYCALLFORM 妊娠率加算_固有補正_{NO:LOCAL}(LOCAL)
	TCVAR:LOCAL:妊娠率補正 += RCVAR:LOCAL:妊娠率補正_加算値

	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE

	;コマンド初回フラグの保存
	コマンド初回フラグ:LOCAL:通常コマンド += コマンド初回フラグ_一時保存:LOCAL:0
	コマンド初回フラグ:LOCAL:通常コマンド_一日単位 += コマンド初回フラグ_一時保存:LOCAL:0
	コマンド初回フラグ_一時保存:LOCAL:0 = 
	コマンド初回フラグ_PLAYER:LOCAL:通常コマンド += コマンド初回フラグ_PLAYER_一時保存:LOCAL:0
	コマンド初回フラグ_PLAYER:LOCAL:通常コマンド_一日単位 += コマンド初回フラグ_PLAYER_一時保存:LOCAL:0
	コマンド初回フラグ_PLAYER_一時保存:LOCAL:0 = 
	IF モード初回フラグ_一時保存:LOCAL:0 != ""
		VARSET RESULTS
		SPLIT モード初回フラグ_一時保存:LOCAL:0, "_", RESULTS
		FOR 登録番号, 0, RESULT
			SIF RESULTS:登録番号 == ""
				BREAK
			LOCALS = %GETVARS(@"コマンド初回フラグ:LOCAL:%RESULTS:登録番号%")%
			SETVAR @"コマンド初回フラグ:LOCAL:%RESULTS:登録番号%", @"%LOCALS%-%RESULTS:(登録番号 + 1)%-"
			登録番号 ++
		NEXT
	ENDIF
	モード初回フラグ_一時保存:LOCAL:0 = 
	IF モード初回フラグ_PLAYER_一時保存:LOCAL:0 != ""
		VARSET RESULTS
		SPLIT モード初回フラグ_PLAYER_一時保存:LOCAL:0, "_", RESULTS
		FOR 登録番号, 0, RESULT
			SIF RESULTS:登録番号 == ""
				BREAK
			LOCALS = %GETVARS(@"コマンド初回フラグ_PLAYER:LOCAL:%RESULTS:登録番号%")%
			SETVAR @"コマンド初回フラグ_PLAYER:LOCAL:%RESULTS:登録番号%", @"%LOCALS%-%RESULTS:(登録番号 + 1)%-"
			登録番号 ++
		NEXT
	ENDIF
	モード初回フラグ_PLAYER_一時保存:LOCAL:0 = 

	;満腹残り時間の減少
	TCVAR:LOCAL:満腹 = MAX(TCVAR:LOCAL:満腹 - TIME_PROGRESS(TFLAG:行動前時刻), 0)

	IF TFLAG:オートコマンドフラグ
		;次回がオートコマンドである
	ELSE
		;次回がオートコマンドでない
		IF TCVAR:LOCAL:前ターン絶頂 && NOWEX:LOCAL:多重絶頂
			TCVAR:LOCAL:連続絶頂記録 += 1
		ELSE
			TCVAR:LOCAL:連続絶頂記録 = 0
		ENDIF
	ENDIF

	IF TFLAG:前ターンオートフラグ
		;今回がオートコマンドである
		TCVAR:LOCAL:前ターン絶頂 = MAX(TCVAR:LOCAL:前ターン絶頂, NOWEX:LOCAL:多重絶頂)
		TCVAR:LOCAL:前ターンＣ絶頂 += NOWEX:LOCAL:Ｃ絶頂
		TCVAR:LOCAL:前ターンＢ絶頂 += NOWEX:LOCAL:Ｂ絶頂
		TCVAR:LOCAL:前ターンＶ絶頂 += NOWEX:LOCAL:Ｖ絶頂
		TCVAR:LOCAL:前ターンＡ絶頂 += NOWEX:LOCAL:Ａ絶頂
		TCVAR:LOCAL:前ターンＳ絶頂 += NOWEX:LOCAL:Ｓ絶頂
		SIF TCVAR:LOCAL:被射精位置 > 0
			TCVAR:LOCAL:前ターン被射精位置 = TCVAR:LOCAL:被射精位置

		TCVAR:LOCAL:ターンスコア += NOWEX:LOCAL:スコア
	ELSE
		;今回がオートコマンドでない
		TCVAR:LOCAL:前ターン絶頂 = NOWEX:LOCAL:多重絶頂
		TCVAR:LOCAL:前ターンＣ絶頂 = NOWEX:LOCAL:Ｃ絶頂
		TCVAR:LOCAL:前ターンＢ絶頂 = NOWEX:LOCAL:Ｂ絶頂
		TCVAR:LOCAL:前ターンＶ絶頂 = NOWEX:LOCAL:Ｖ絶頂
		TCVAR:LOCAL:前ターンＡ絶頂 = NOWEX:LOCAL:Ａ絶頂
		TCVAR:LOCAL:前ターンＳ絶頂 = NOWEX:LOCAL:Ｓ絶頂
		TCVAR:LOCAL:前ターン被射精位置 = TCVAR:LOCAL:被射精位置

		TCVAR:LOCAL:ターンスコア = NOWEX:LOCAL:スコア
	ENDIF

	IF TFLAG:オートコマンドフラグ
		;次回がオートコマンドである
	ELSE
		;次回がオートコマンドでない
		TCVAR:LOCAL:被射精位置 = 0

		;スコアペナルティによるスコアの減少
		IF TCVAR:LOCAL:スコアペナルティ > TCVAR:LOCAL:ターンスコア
			TCVAR:LOCAL:ターンスコア = 0
			TCVAR:LOCAL:スコアペナルティ -= TCVAR:LOCAL:ターンスコア
		ELSEIF TCVAR:LOCAL:スコアペナルティ > 0
			TCVAR:LOCAL:ターンスコア -= TCVAR:LOCAL:スコアペナルティ
			TCVAR:LOCAL:スコアペナルティ = 0
		ENDIF
		;連続絶頂ボーナスによるスコアの増加
		IF TCVAR:LOCAL:連続絶頂記録 > 9
			TCVAR:LOCAL:ターンスコア = TCVAR:LOCAL:ターンスコア * (19 + SQRT(TCVAR:LOCAL:連続絶頂記録 - 9)) / 10
			EX:LOCAL:スコア += TCVAR:LOCAL:前ターンスコア / 2
		ELSEIF TCVAR:LOCAL:連続絶頂記録 > 0
			TCVAR:LOCAL:ターンスコア = TCVAR:LOCAL:ターンスコア * (10 + TCVAR:LOCAL:連続絶頂記録) / 10
			EX:LOCAL:スコア += TCVAR:LOCAL:前ターンスコア / 2
		ENDIF
		;今日のスコアに加算
		EX:LOCAL:スコア += TCVAR:LOCAL:ターンスコア
		;リセット
		TCVAR:LOCAL:前ターンスコア = TCVAR:LOCAL:ターンスコア
		TCVAR:LOCAL:ターンスコア = 0
	ENDIF

	IF CFLAG:LOCAL:うふふ && LOCAL != PLAYER

		;絶頂累積
		TCVAR:LOCAL:うふふ中Ｃ絶頂累積 += NOWEX:LOCAL:Ｃ絶頂
		TCVAR:LOCAL:うふふ中Ｂ絶頂累積 += NOWEX:LOCAL:Ｂ絶頂
		TCVAR:LOCAL:うふふ中Ｖ絶頂累積 += NOWEX:LOCAL:Ｖ絶頂
		TCVAR:LOCAL:うふふ中Ａ絶頂累積 += NOWEX:LOCAL:Ａ絶頂
		TCVAR:LOCAL:うふふ中Ｓ絶頂累積 += NOWEX:LOCAL:Ｓ絶頂

		;複数プレイ中なら関係性上昇(何もしない実行時を除く)
		FOR 同室番号, 1, CHARANUM
			SIF TARGET:同室番号 < 1
				BREAK
			SIF TARGET:同室番号 == LOCAL
				CONTINUE
			SIF CFLAG:(TARGET:同室番号):うふふ == 0
				CONTINUE
			SIF CFLAG:(TARGET:同室番号):隠密 != CFLAG:LOCAL:隠密
				CONTINUE
			SIF TALENT:(TARGET:同室番号):分身
				CONTINUE

			CALL ADD_RELATION(LOCAL, TARGET:同室番号, RELATION_うふふ時補正(LOCAL, TARGET:同室番号, MAX(15, TIME - TIME:2) / 15))
		NEXT
	ELSEIF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
		;絶頂累積
		TCVAR:LOCAL:うふふ中Ｃ絶頂累積 += NOWEX:LOCAL:Ｃ絶頂
		TCVAR:LOCAL:うふふ中Ｂ絶頂累積 += NOWEX:LOCAL:Ｂ絶頂
		TCVAR:LOCAL:うふふ中Ｖ絶頂累積 += NOWEX:LOCAL:Ｖ絶頂
		TCVAR:LOCAL:うふふ中Ａ絶頂累積 += NOWEX:LOCAL:Ａ絶頂
		TCVAR:LOCAL:うふふ中Ｓ絶頂累積 += NOWEX:LOCAL:Ｓ絶頂
	ENDIF

	;母乳の収集
	IF TALENT:LOCAL:母乳体質 && NOWEX:LOCAL:Ｂ絶頂 && MODE_確認_ターゲット側("搾乳機", LOCAL) > -1
		TCVAR:LOCAL:母乳カウント += 300
		SIF TCVAR:LOCAL:母乳マッサージ
			TCVAR:LOCAL:母乳カウント += 150
	ENDIF
	;母乳のアイテム化
	IF TCVAR:LOCAL:母乳カウント > 5000
		キャラ衣装識別 '= ""

		キャラ衣装識別 '= REPLACE(CSTR:LOCAL:顔グラ表示切り替え用文字列, "顔_", "")
		SIF キャラ衣装識別 == ""
			キャラ衣装識別 '= REPLACE(CSTR:LOCAL:短冊グラ表示切り替え用文字列, "短冊_", "")
		SIF キャラ衣装識別 == ""
			キャラ衣装識別 '= CSTR:LOCAL:表示グラフィックカテゴリ
		SIF キャラ衣装識別 == ""
			キャラ衣装識別 = デフォルト
		;分身だった場合は本体のミルク扱いになる
		TARGET保存 = -1
		IF CFLAG:LOCAL:分身
			FOR 分身番号, 0, 分身最大数
				IF 分身登録:分身番号:0 == LOCAL
					TARGET保存 = 分身登録:分身番号:1
					BREAK
				ENDIF
			NEXT
		ENDIF
		SIF TARGET保存 < 0
			TARGET保存 = LOCAL
		SELECTCASE RAND:100 + TCVAR:TARGET保存:ミルク濃厚率補正
			CASE IS < 70
				母乳種別 = 通常
			CASE IS < 110
				母乳種別 = 濃厚
			CASEELSE
				母乳種別 = 特濃
		ENDSELECT
		;基礎情報を突っ込む
		DT_ROW_ADD "ミルクデータベース", "生産元No", NO:TARGET保存, "生産元名称", NAME:TARGET保存, "生産日", DAY, "効果基礎値", 100, "状態", 母乳種別, "グラフィック保存", @"ミルク生産者表示_%GRAP_COM_RAND(TARGET保存, "ミルク生産者表示_", キャラ衣装識別, "")%"
		DRAWLINE
		SETCOLOR カラーパレット("えっちな色")
		IF 母乳種別 == "通常"
			PRINTFORML %CALLNAME:TARGET保存%の母乳が溜まったため、「%CALLNAME:TARGET保存%のミルク」を入手した。
		ELSE
			PRINTFORML %CALLNAME:TARGET保存%の母乳が溜まったため、「%CALLNAME:TARGET保存%の%母乳種別%ミルク」を入手した。
		ENDIF
		PRINTW ※消費カロリー：100kcal
		TCVAR:LOCAL:摂取カロリー -= 100
		RESETCOLOR
		TCVAR:LOCAL:母乳カウント = 0
	ENDIF

	;CUP等のリセット
	VARSET CUP:LOCAL:0, 0
	VARSET CDOWN:LOCAL:0, 0
	VARSET DOWNBASE:LOCAL:0, 0
	TCVAR:LOCAL:野外オナニー_絶頂 = 0
	TCVAR:LOCAL:あなた快楽上昇 = 0
	;NOWEXのリセット
	VARSET NOWEX:LOCAL:0, 0
	CFLAG:LOCAL:快楽計算フラグ = 0
	TCVAR:LOCAL:弱点コマンドフラグ = 0
	TCVAR:LOCAL:弱点シチュフラグ = 0

	;経験値取表示用
	;現在位置の保存
	CFLAG:LOCAL:前ターン位置 = CFLAG:LOCAL:現在位置
	CFLAG:LOCAL:前マップ種別 = CFLAG:LOCAL:現在マップ種別
	CALL EXP変動記録処理(LOCAL)

	;発情期フラグの更新
	IF 発情期判定(LOCAL)
		IF TALENT:LOCAL:性欲 == -2
			CFLAG:LOCAL:発情期フラグ = 0
		ELSE
			IF CFLAG:LOCAL:発情期フラグ < 0
				CFLAG:LOCAL:発情期フラグ = RISKY_DAY(LOCAL) * -1
				IF CFLAG:LOCAL:発情期フラグ > -1
					;発情期が終わった時、性欲を大きく減らす
					BASE:LOCAL:性欲 = MAX(0, BASE:LOCAL:性欲 - 1500)
				ENDIF
			ELSE
				CFLAG:LOCAL:発情期フラグ = RISKY_DAY(LOCAL) * -1
			ENDIF
		ENDIF
		;ドラフorドラフまぜまぜは発情期に母乳体質化＆発情期でなくなったら母乳を止める
		;発情期に母乳体質化するオプションがあるキャラも処理する
		IF 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
			IF (TALENT:LOCAL:種族 == 3 || GETBIT(TALENT:LOCAL:発情期あり, 2) ||キャラ一致チェック(LOCAL, "[六竜の『金』]ガレヲン")) && TALENT:LOCAL:母乳体質 != 3 && (GETBIT(TALENT:LOCAL:性別, 0) || OPTION変数:男ドラフ母乳)
				IF CFLAG:LOCAL:発情期フラグ < 0
					SIF TALENT:LOCAL:母乳体質 == 0
						TALENT:LOCAL:母乳体質 = 2
				ELSE
					SIF TALENT:LOCAL:母乳体質 == 2
						TALENT:LOCAL:母乳体質 = 0
				ENDIF
			ENDIF
		ENDIF
		;発情期にふたなり化するキャラを処理
		IF GETBIT(TALENT:LOCAL:発情期あり, 3) && 素質切り替えOPTION記憶:ふたなり_女性 == 0
			IF CFLAG:LOCAL:発情期フラグ < 0
				TALENT:LOCAL:性別 = 3
			ELSE
				;女性器ないキャラがふたなり化妊娠中の時は戻さない
				IF GETBIT(CFLAG:LOCAL:元性別保存, 0) != 0 || TALENT:LOCAL:妊娠 == 0
					TALENT:LOCAL:性別 = CFLAG:LOCAL:元性別保存
				ENDIF
			ENDIF
		ELSEIF GETBIT(TALENT:LOCAL:発情期あり, 8)
			IF CFLAG:LOCAL:発情期フラグ < 0
				TALENT:LOCAL:性別 = 1
			ELSE
				;女性器ないキャラが女体化妊娠中の時は戻さない
				IF GETBIT(CFLAG:LOCAL:元性別保存, 0) != 0 || TALENT:LOCAL:妊娠 == 0
					TALENT:LOCAL:性別 = CFLAG:LOCAL:元性別保存
				ENDIF
			ENDIF
		ENDIF
	ELSE
		CFLAG:LOCAL:発情期フラグ = 0
	ENDIF
NEXT

;告白成功
SIF SELECTCOM == 352 && RFLAG:コマンド結果受渡し変数 && SAVESTR:ゲームフェイズ管理 == "通常モード"
	SETBIT CFLAG:1 , 0

;脱衣コマンド
IF RFLAG:オートコマンド脱衣用フラグ == 0 && SAVESTR:ゲームフェイズ管理 == "通常モード"
	SELECTCASE SELECTCOM
		;上半身鎧脱衣
		CASE 200
			TEQUIP:上半身鎧あり = 0
		;上半身服脱衣
		CASE 201
			TEQUIP:上半身服あり = 0
			TEQUIP:脇出し = 0
			TEQUIP:へそ出し = 0
			TEQUIP:はだけ可 = 0
			TEQUIP:手差し込み可 = 0
			TEQUIP:パイズリ可 = 0
			TEQUIP:上半身下着抜き取り可 = 0
			;上下一体の場合ここで下も脱がす
			IF TEQUIP:上下一体服
				TEQUIP:下半身服あり = 0
				TEQUIP:スカートめくり可 = 0
				TEQUIP:下半身下着抜き取り可 = 0
				TEQUIP:ずらし可 = 0
				TEQUIP:上下一体服 = 0
			ENDIF
		;上半身タイツ脱衣
		CASE 202
			IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
				TEQUIP:上タイツ破れ = 1
				SIF TEQUIP:上下一体タイツ
					TEQUIP:下タイツ破れ = 1
			ELSE
				TEQUIP:上タイツあり = 0
				TEQUIP:上タイツ破れ = 0
				;上下一体の場合ここで下も脱がす
				IF TEQUIP:上下一体タイツ
					TEQUIP:下タイツあり = 0
					TEQUIP:下タイツ破れ = 0
					TEQUIP:上下一体タイツ = 0
				ENDIF
			ENDIF
		;下半身鎧脱衣、前と後ろは一気に脱がす
		CASE 203
			TEQUIP:下腹部後鎧あり = 0
			TEQUIP:下腹部前鎧あり = 0
		;下半身服脱衣
		CASE 204
			TEQUIP:下半身服あり = 0
			TEQUIP:スカートめくり可 = 0
			TEQUIP:下半身下着抜き取り可 = 0
			TEQUIP:ずらし可 = 0
		;下半身タイツ脱衣
		CASE 205
			IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
				TEQUIP:下タイツ破れ = 1
			ELSE
				TEQUIP:下タイツあり = 0
				TEQUIP:下タイツ破れ = 0
			ENDIF
		;ブラ脱衣
		CASE 206
			TEQUIP:上半身下着あり = 0
		;下着脱衣
		CASE 207
			TEQUIP:下半身下着あり = 0
		;全裸にする
		CASE 208
			IF GETBIT(FLAG:モード管理, モードビット_同時)
				FOR LOCAL, 0, 同時モード_選択数
					CALL 全裸化処理(同時モード_選択キャラ:LOCAL)
				NEXT
			ELSE
				CALL 全裸化処理(TARGET)
			ENDIF
		CASE 211
			;むりやりうふふごっこ
			LOCALS '= CSTR:TARGET:コスプレ前の服
			LOCALS:1 '= CSTR:TARGET:着せ替え服
			TARGET保存 = TARGET
			;ゲームフェイズ変更でリセットされるのでSELECTCOM保存
			SWAP SELECTCOM, COM保存
			CALL ゲームフェイズ変更("むりやりうふふモード")
			;SELECTCOM復帰
			SWAP SELECTCOM, COM保存
			;食いしばり時など、むりやりうふふに入らないパターンがあるため分岐
			IF SAVESTR:ゲームフェイズ管理 == "むりやりうふふモード"
				TARGET = TARGET保存
				CSTR:TARGET:コスプレ前の服 '= LOCALS
				CALL CLOTHES_CHANGE(TARGET, LOCALS:1)
				;うふふに移行
				CALL うふふ開始キャラ処理(TARGET, "むりやりうふふごっこ")
				TSTR:むりやりごっこ時シチュ保存 '= CSTR:TARGET:うふふシチュ記録
				CFLAG:PLAYER:うふふ = 1
			ENDIF
	ENDSELECT
ENDIF

;全裸散歩フラグ
IF RFLAG:全裸散歩フラグ
	FLAG:モード管理 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		IF !MODE_存在判定_ターゲット側("全裸散歩", TARGET:LOCAL)
			CFLAG:(TARGET:LOCAL):うふふ = 0
			CFLAG:(TARGET:LOCAL):うふふ開始時間記録 = 0
			CALL 性欲ゲージ減少処理(TARGET:LOCAL)
			TCVAR:(TARGET:LOCAL):初うふふフラグ = 0
			;モードもリセット
			CALL 特定キャラモードリセット(TARGET:LOCAL)
			IF CSTR:(TARGET:LOCAL):コスプレ前の服 != ""
				CALL CLOTHES_RESET(TARGET:LOCAL)
				CSTR:(TARGET:LOCAL):コスプレ前の服 '= ""
			ENDIF
			;体力限界時処理
			IF CFLAG:(TARGET:LOCAL):体力限界
				CALL CHARA_SLEEP, TARGET:LOCAL
				CFLAG:(TARGET:LOCAL):飲み会 = 0
			ENDIF
			;散歩してない分身を消す
			CALL 分身指定削除(TARGET:LOCAL)
		ENDIF
	NEXT
	;オートコマンドもリセット
	TFLAG:オートコマンドフラグ = 0
	AUTOCOM_実行番号保存 = -1
	AUTOCOM_対象番号保存 = -1
	自慰ターゲット保存 = -1
	CALL SORT_CHARALIST()
	CALL TARGET_SET()
ENDIF

;追跡情報を戻しておく
IF RFLAG:追跡フラグ
	あなた追跡キャラ = RFLAG:追跡フラグ
	サイド描画表示種類 = %RSTR:追跡時サイド記録%
ENDIF

;フラグのリセット
CALL R系列フラグリセット
TFLAG:調教者絶頂強度 = 0
TFLAG:オートコマンド連続発動 = MAX(TFLAG:オートコマンド連続発動 - 1, 0)
IF TFLAG:オートコマンド連続発動 < 1
	;オプションのフラグを全部リセット
	IF TFLAG:オートコマンド連続発動_逆オナホ
		CALL 逆オナホ解除()
	ENDIF
	TFLAG:オートコマンド連続発動_なすがまま = 0
ENDIF

PREVCOM = SELECTCOM


@R系列フラグリセット

#DIM 対象キャラ

IF RFLAG:入替えコマンドフラグ == 1
	SWAP TARGET, PLAYER
	RFLAG:入替えコマンドフラグ = 0
ENDIF

SELECTCOM_NAME = 
VARSET RFLAG
VARSET RSTR
FOR 対象キャラ,0,CHARANUM
	VARSET RCVAR:対象キャラ:0
NEXT

@EXP変動記録処理(対象キャラ)
#DIM 対象キャラ
#DIM EXP一時保存

FOR LOCAL,0,200
	SELECTCASE LOCAL
		CASE GETNUM(EXP, "Ｃ絶頂経験") TO GETNUM(EXP, "Ｓ絶頂経験")
			EXP一時保存 = CSVEXP(NO:対象キャラ, LOCAL)
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 && TCVAR:対象キャラ:(400 + LOCAL) == EXP一時保存
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に初めて[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 10 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 10
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した回数が１０回を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した回数が５０回を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した回数が１００回を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した回数が５００回を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%REPLACE(GET_EXPNAME(LOCAL, 対象キャラ), "経験", "")%]した回数が９９９回を超えた</font>","うふふ")
		CASE GETNUM(EXP, "肉体熟知")
			EXP一時保存 = 0
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) <= EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%の[肉体熟知]が５０を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) <= EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%の[肉体熟知]が１００を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) <= EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%の[肉体熟知]が５００を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) <= EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%の[肉体熟知]が９９９を超えた</font>","うふふ")
		CASE GETNUM(EXP, "乳首開発") TO GETNUM(EXP, "尻尾開発")
			EXP一時保存 = CSVEXP(NO:対象キャラ, LOCAL)
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 && TCVAR:対象キャラ:(400 + LOCAL) == EXP一時保存
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中、初めて[%GET_EXPNAME(LOCAL, 対象キャラ)%]を進めた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 10 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 10
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%GET_EXPNAME(LOCAL, 対象キャラ)%]の進行率が１０を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%GET_EXPNAME(LOCAL, 対象キャラ)%]の進行率が５０を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%GET_EXPNAME(LOCAL, 対象キャラ)%]の進行率が１００を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%GET_EXPNAME(LOCAL, 対象キャラ)%]の進行率が５００を超えた</font>","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>ゲーム中に[%GET_EXPNAME(LOCAL, 対象キャラ)%]の進行率が９９９を超えた</font>","うふふ")
		CASE GETNUM(EXP, "ダウン経験")
			EXP一時保存 = CSVEXP(NO:対象キャラ, LOCAL)
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 && TCVAR:対象キャラ:(400 + LOCAL) == EXP一時保存
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"初めて体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 10 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 10
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"１０回体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"５０回体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"１００回体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 300 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 300
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"３００回体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"５００回体力が０になった","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"９９９回体力が０になった","うふふ")
		CASE GETNUM(EXP, "釣り経験")
			EXP一時保存 = CSVEXP(NO:対象キャラ, LOCAL)
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 && TCVAR:対象キャラ:(400 + LOCAL) == EXP一時保存
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"初めて魚を釣り上げた","リゾート")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 10 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 10
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"魚を１０匹釣り上げた","リゾート")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"魚を５０匹釣り上げた","リゾート")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"魚を１００匹釣り上げた","リゾート")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"魚を５００匹釣り上げた","リゾート")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"魚を９９９匹釣り上げた","リゾート")
		CASE GETNUM(EXP, "妊娠経験")
			EXP一時保存 = 0
			SIF EXP:対象キャラ:(LOCAL) > EXP一時保存 && TCVAR:対象キャラ:(400 + LOCAL) == EXP一時保存
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"初めて妊娠した","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 10 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 10
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"１０回妊娠した","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 50 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 50
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"５０回妊娠した","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 100 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 100
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"１００回妊娠した","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 500 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 500
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"５００回妊娠した","うふふ")
			SIF EXP:対象キャラ:(LOCAL) >= EXP一時保存 + 999 && TCVAR:対象キャラ:(400 + LOCAL) < EXP一時保存 + 999
				CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"９９９回妊娠した","うふふ")
	ENDSELECT
	TCVAR:対象キャラ:(400 + LOCAL) = EXP:対象キャラ:(LOCAL)
NEXT

;自動回復の回復量を返す
;最大値からあふれる場合はそのぶん減らす
@REGENE_CALC(ARG)
#FUNCTION
#DIM 回復量
回復量 = MAXBASE:ARG:体力 * (3 + TALENT:ARG:回復速度 * 2) / 100
回復量 = MIN(MAXBASE:ARG:体力 - BASE:ARG:体力, 回復量)
RETURNF 回復量

@コマンド回数履歴記録(対象キャラ, 回数保存)
#DIM 対象キャラ
#DIM 回数保存

SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 0 && 回数保存 <= 0
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中に初めてコマンドの対象になった","日常_実績")
SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 100 && 回数保存 <= 100
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中にコマンドの対象になった回数が１００回を超えた","日常_実績")
SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 500 && 回数保存 <= 500
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中にコマンドの対象になった回数が５００回を超えた","日常_実績")
SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 1000 && 回数保存 <= 1000
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中にコマンドの対象になった回数が１０００回を超えた","日常_実績")
SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 5000 && 回数保存 <= 5000
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中にコマンドの対象になった回数が５０００回を超えた","日常_実績")
SIF CFLAG:対象キャラ:コマンドタゲ回数記録 > 9999 && 回数保存 <= 9999
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"ゲーム中にコマンドの対象になった回数が９９９９回を超えた","日常_実績")


@処女童貞適用処理(対象キャラ)
#DIM 対象キャラ
#DIMS 道具記録
#DIMS 相手記録

;処女喪失
IF RCVAR:対象キャラ:破瓜
	TALENT:対象キャラ:処女 = 0
	SELECTCASE RCVAR:対象キャラ:破瓜
		CASE 1
			IF RCVAR:対象キャラ:破瓜_相手番号 < 0
				相手記録 = 誰とも知らない相手のペニス
			ELSE
				相手記録 = %CALLNAME:(RCVAR:対象キャラ:破瓜_相手番号)%のペニス
				道具記録 = ペニス
			ENDIF
		CASE 2
			相手記録 = バイブ
			道具記録 = バイブ
		CASE 4
			IF RCVAR:対象キャラ:破瓜_相手番号 < 0
				相手記録 = 誰とも知らない相手の触手
			ELSE
				相手記録 = %CALLNAME:(RCVAR:対象キャラ:破瓜_相手番号)%の触手
			ENDIF
			道具記録 = 触手
		CASE 5
			相手記録 = %CALLNAME:(RCVAR:対象キャラ:破瓜_相手番号)%のペニスバンド
			道具記録 = ペニスバンド
	ENDSELECT
	IF RCVAR:対象キャラ:破瓜_相手番号 > -1
		CALL 履歴データベース登録(CFLAG:(RCVAR:対象キャラ:破瓜_相手番号):人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%道具記録%で%CALLNAME:対象キャラ%のＶ処女を奪った</font><img src='えっちハート'>","うふふ実績")
	ENDIF
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%相手記録%にＶ処女を捧げた</font><img src='えっちハート'>","うふふ実績")
ENDIF
;Ａ処女喪失
IF RCVAR:対象キャラ:Ａ破瓜
	TALENT:対象キャラ:Ａ処女 = 0
	SELECTCASE RCVAR:対象キャラ:Ａ破瓜
		CASE 1
			IF RCVAR:対象キャラ:Ａ破瓜_相手番号 < 0
				相手記録 = 誰とも知らない相手のペニス
			ELSE
				相手記録 = %CALLNAME:(RCVAR:対象キャラ:Ａ破瓜_相手番号)%のペニス
				道具記録 = ペニス
			ENDIF
		CASE 2
			相手記録 = バイブ
			道具記録 = バイブ
		CASE 4
			IF RCVAR:対象キャラ:Ａ破瓜_相手番号 < 0
				相手記録 = 誰とも知らない相手の触手
			ELSE
				相手記録 = %CALLNAME:(RCVAR:対象キャラ:Ａ破瓜_相手番号)%の触手
			ENDIF
			道具記録 = 触手
		CASE 5
			相手記録 = %CALLNAME:(RCVAR:対象キャラ:Ａ破瓜_相手番号)%のペニスバンド
			道具記録 = ペニスバンド
	ENDSELECT
	IF RCVAR:対象キャラ:Ａ破瓜_相手番号 > -1
		CALL 履歴データベース登録(CFLAG:(RCVAR:対象キャラ:Ａ破瓜_相手番号):人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%道具記録%で%CALLNAME:対象キャラ%のＡ処女を奪った</font><img src='えっちハート'>","うふふ実績")
	ENDIF
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%相手記録%にＡ処女を捧げた</font><img src='えっちハート'>","うふふ実績")
ENDIF
;童貞喪失
IF RCVAR:対象キャラ:童貞喪失
	SETBIT TALENT:対象キャラ:非童貞, 0
	IF RCVAR:対象キャラ:童貞喪失_相手番号 < 0
		相手記録 = 誰とも知らない相手
	ELSE
		相手記録 = %CALLNAME:(RCVAR:対象キャラ:童貞喪失_相手番号)%
		CALL 履歴データベース登録(CFLAG:(RCVAR:対象キャラ:童貞喪失_相手番号):人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%のＶ童貞を奪った</font><img src='えっちハート'>","うふふ実績")
	ENDIF
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%相手記録%にＶ童貞を捧げた</font><img src='えっちハート'>","うふふ実績")
ENDIF
;Ａ童貞喪失
IF RCVAR:対象キャラ:Ａ童貞喪失
	SETBIT TALENT:対象キャラ:非童貞, 1
	IF RCVAR:対象キャラ:Ａ童貞喪失_相手番号 < 0
		相手記録 = 誰とも知らない相手
	ELSE
		相手記録 = %CALLNAME:(RCVAR:対象キャラ:Ａ童貞喪失_相手番号)%
		CALL 履歴データベース登録(CFLAG:(RCVAR:対象キャラ:Ａ童貞喪失_相手番号):人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:対象キャラ%のＡ童貞を奪った</font><img src='えっちハート'>","うふふ実績")
	ENDIF
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%相手記録%にＡ童貞を捧げた</font><img src='えっちハート'>","うふふ実績")
ENDIF

;変数リセット
RCVAR:対象キャラ:破瓜 = 0
RCVAR:対象キャラ:Ａ破瓜 = 0
RCVAR:対象キャラ:童貞喪失 = 0
RCVAR:対象キャラ:Ａ童貞喪失 = 0
RCVAR:対象キャラ:破瓜_相手番号 = 0
RCVAR:対象キャラ:Ａ破瓜_相手番号 = 0
RCVAR:対象キャラ:童貞喪失_相手番号 = 0
RCVAR:対象キャラ:Ａ童貞喪失_相手番号 = 0


@全裸化処理(対象キャラ)
#DIM 対象キャラ

;脱衣不可キャラのコスプレ対策で関数化

IF TALENT:対象キャラ:脱衣不可 && CSTR:対象キャラ:着せ替え服 != ""
	;コスプレ解除
	CALL CLOTHES_RESET(対象キャラ)
ELSE
	TEQUIP:対象キャラ:上半身鎧あり = 0
	TEQUIP:対象キャラ:上半身服あり = 0
	TEQUIP:対象キャラ:脇出し = 0
	TEQUIP:対象キャラ:へそ出し = 0
	TEQUIP:対象キャラ:はだけ可 = 0
	TEQUIP:対象キャラ:手差し込み可 = 0
	TEQUIP:対象キャラ:パイズリ可 = 0
	TEQUIP:対象キャラ:上タイツあり = 0
	TEQUIP:対象キャラ:上半身下着あり = 0
	TEQUIP:対象キャラ:上半身下着抜き取り可 = 0
	TEQUIP:対象キャラ:下腹部後鎧あり = 0
	TEQUIP:対象キャラ:下腹部前鎧あり = 0
	TEQUIP:対象キャラ:下半身服あり = 0
	TEQUIP:対象キャラ:スカートめくり可 = 0
	TEQUIP:対象キャラ:ずらし可 = 0
	TEQUIP:対象キャラ:下タイツあり = 0
	TEQUIP:対象キャラ:下半身下着あり = 0
	TEQUIP:対象キャラ:下半身下着抜き取り可 = 0
	TEQUIP:対象キャラ:上下一体服 = 0
	TEQUIP:対象キャラ:上下一体タイツ = 0
	TEQUIP:対象キャラ:上タイツ破れ = 0
	TEQUIP:対象キャラ:下タイツ破れ = 0
ENDIF
